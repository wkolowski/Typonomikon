(** * D1: Indukcja i rekursja *)

(** W poprzednim rozdziale dowiedzieliÅ›my siÄ™ juÅ¼ co nieco o typach, a
    takÅ¼e spotkaliÅ›my kilka z nich oraz kilka sposobÃ³w tworzenia nowych
    typÃ³w ze starych (takich jak np. koniunkcja; pamiÄ™taj, Å¼e zdania sÄ…
    typami). W tym rozdziale dowiemy siÄ™, jak definiowaÄ‡ nowe typy przy
    pomocy indukcji oraz jak uÅ¼yÄ‡ rekursji do definiowania funkcji, ktÃ³re
    operujÄ… na tych typach. *)

(** * Typy induktywne *)

(** W Coqu sÄ… trzy gÅ‚Ã³wne rodzaje typÃ³w: produkt zaleÅ¼ny, typy induktywne
    i typy koinduktywne. Z pierwszym z nich juÅ¼ siÄ™ zetknÄ™liÅ›my, drugi
    poznamy w tym rozdziale, trzeci na razie pominiemy.

    Typ induktywny definiuje siÄ™ przy pomocy zbioru konstruktorÃ³w, ktÃ³re
    sÅ‚uÅ¼Ä…, jak sama nazwa wskazuje, do budowania termÃ³w tego typu.
    Konstruktory te sÄ… funkcjami (byÄ‡ moÅ¼e zaleÅ¼nymi), ktÃ³rych
    przeciwdziedzinÄ… jest definiowany typ, ale niczego nie
    obliczajÄ… â€” nadajÄ… jedynie termom ich "ksztaÅ‚t". W szczegÃ³lnoÅ›ci, nie
    majÄ… nic wspÃ³lnego z konstruktorami w takich jÄ™zykach jak C++ lub Java
    â€” nie mogÄ… przetwarzaÄ‡ swoich argumentÃ³w, alokowaÄ‡ pamiÄ™ci, dokonywaÄ‡
    operacji wejÅ›cia/wyjÅ›cia etc.

    Tym, co jest waÅ¼ne w przypadku konstruktorÃ³w, jest ich iloÅ›Ä‡, nazwy
    oraz iloÅ›Ä‡ i typy przyjmowanych argumentÃ³w. To te cztery rzeczy decydujÄ…
    o tym, jakie "ksztaÅ‚ty" bÄ™dÄ… miaÅ‚y termy danego typu, a wiÄ™c i czym
    bÄ™dzie sam typ. W ogolnoÅ›ci kaÅ¼dy term jest skoÅ„czonym, ukorzenionym
    drzewem, ktÃ³rego ksztaÅ‚t zaleÅ¼y od charakterystyki konstruktorÃ³w tak:
    - kaÅ¼dy konstruktor to inny rodzaj wÄ™zÅ‚a (nazwa konstruktora to nazwa
      wÄ™zÅ‚a)
    - konstruktory nierekurencyjne to liÅ›cie, a rekurencyjne â€” wÄ™zÅ‚y
      wewnÄ™trzne
    - argumenty konstruktorÃ³w to dane przechowywane w danym wÄ™Åºle *)

(** Typ induktywny moÅ¼na wyobraÅ¼aÄ‡ sobie jako przestrzeÅ„ zawierajÄ…cÄ…
    te i tylko te drzewa, ktÃ³re moÅ¼na zrobiÄ‡ przy pomocy jego
    konstruktorÃ³w. Nie przejmuj siÄ™, jeÅ¼eli opis ten wydaje ci siÄ™
    dziwny â€” sposÃ³b definiowania typÃ³w induktywnych i ich wartoÅ›ci
    w Coqu jest diametralnie rÃ³Å¼ny od sposobu definiowania klas i
    obiektÃ³w w jÄ™zykach imperatywnych i wymaga przyzwyczajenia siÄ™
    do niego. Zobaczmy, jak powyÅ¼szy opis ma siÄ™ do konkretnych
    przykÅ‚adÃ³w. *)

(** ** Enumeracje *)

(** Najprostszym rodzajem typÃ³w induktywnych sÄ… enumeracje, czyli typy,
    ktÃ³rych wszystkie konstruktory sÄ… staÅ‚ymi. *)

Inductive bool : Set :=
    | true : bool
    | false : bool.

(** Definicja typu induktywnego ma nastÄ™pujÄ…cÄ… postaÄ‡: najpierw wystÄ™puje
    sÅ‚owo kluczowe [Inductive], nastÄ™pnie nazwa typu, a po dwukropku sort
    ([Set], [Prop] lub [Type]). NastÄ™pnie wymieniamy konstruktory typu â€”
    dla czytelnoÅ›ci kaÅ¼dy w osobnej linii. MajÄ… one swoje unikalne nazwy i
    sÄ… funkcjami, ktÃ³rych przeciwdziedzinÄ… jest definiowany typ. W naszym
    przypadku mamy 2 konstruktory, zwane [true] oraz [false], ktÃ³re sÄ…
    funkcjami zeroargumentowymi.

    DefinicjÄ™ tÄ™ moÅ¼emy udczytaÄ‡ nastÄ™pujÄ…co: "[true] jest typu [bool],
    [false] jest typu [bool] i nie ma Å¼adnych wiÄ™cej wartoÅ›ci typu
    [bool]".

    Uwaga: naleÅ¼y odrÃ³Å¼niÄ‡ symbole [:=] oraz [=]. Pierwszy z nich sÅ‚uÅ¼y
    do definiowania, a drugi do zapisywania rÃ³wnoÅ›ci.

    Zapis [name := term] oznacza "niech od teraz [name] bÄ™dzie innÄ… nazwÄ…
    dla [term]". Jest to komenda, a nie zdanie logiczne. Od teraz jeÅ¼eli
    natkniemy siÄ™ na nazwÄ™ [name], bÄ™dziemy mogli odwinÄ…Ä‡ jej definicjÄ™ i
    wstawiÄ‡ w jej miejsce [term]. PrzykÅ‚ad: [Definition five := 5].
    AntyprzykÅ‚ad: [2 := 5] (bÅ‚Ä…d skÅ‚adni).

    Zapis [a = b] oznacza "[a] jest rÃ³wne [b]". Jest to zdanie logiczne,
    a nie komenda. Zdanie to rzecz jasna nie musi byÄ‡ prawdziwe. PrzykÅ‚ad:
    [2 = 5]. AntyprzykÅ‚ad: [five = 5] (jeÅ¼eli [five] nie jest zdefiniowane,
    to dostajemy komunikat w stylu "nie znaleziono nazwy [five]"). *)

Definition negb (b : bool) : bool :=
match b with
    | true => false
    | false => true
end.

(** Definicja funkcji wyglÄ…da tak: najpierw mamy sÅ‚owo kluczowe [Definition]
    (jeÅ¼eli funkcja nie jest rekurencyjna), nastÄ™pnie argumenty funkcji
    w postaci [(name : type)]; po dwukropku przeciwdziedzina, a po symbolu
    [:=] ciaÅ‚o funkcji.

    Podstawowym narzÄ™dziem sÅ‚uÅ¼Ä…cym do definiowania funkcji jest
    dopasowanie do wzorca (ang. pattern matching). Pozwala ono sprawdziÄ‡,
    ktÃ³rego konstruktora uÅ¼yto do zrobienia dopasowywanej wartoÅ›ci.
    Podobnym tworem wystÄ™pujÄ…cym w jÄ™zykach imperatywnych jest instrukcja
    switch, ale dopasowanie do wzorca jest od niej duÅ¼o potÄ™Å¼niejsze.

    Nasza funkcja dziaÅ‚a nastÄ™pujÄ…co: sprawdzamy, ktÃ³rym konstruktorem
    zrobiono argument [b] â€” jeÅ¼eli byÅ‚o to [true], zwracamy [false], a
    gdy byÅ‚o to [false], zwracamy [true]. *)

(** **** Ä†wiczenie ([andb] i [orb]) *)

(** Zdefiniuj funkcje [andb] (koniunkcja boolowska) i [orb] (alternatywa
    boolowska). *)

(* begin hide *)
Definition andb (b1 b2 : bool) : bool :=
match b1 with
    | true => b2
    | false => false
end.

Definition orb (b1 b2 : bool) : bool :=
match b1 with
    | true => true
    | false => b2
end.
(* end hide *)

(** Zanim odpalimy naszÄ… funkcjÄ™, powinniÅ›my zadaÄ‡ sobie pytanie: w jaki
    sposÃ³b programy sÄ… wykonywane? W celu lepszego zrozumienia tego
    zagadnienia porÃ³wnamy ewaluacjÄ™ programÃ³w napisanych w jÄ™zykach
    imperatywnych oraz funkcyjnych.

    RozwaÅ¼my bardzo uproszczony model: interpreter wykonuje program,
    ktÃ³ry nie dokonuje operacji wejÅ›cia/wyjÅ›cia, napisany w jakimÅ›
    jÄ™zyku imperatywnym. W tej sytuacji dziaÅ‚anie interpretera
    sprowadza siÄ™ do tego, Å¼e czyta on kod programu instrukcja po
    instrukcji, a nastÄ™pnie w zaleÅ¼noÅ›ci od przeczytanej instrukcji
    odpowiednio zmienia swÃ³j stan.

    W jÄ™zyku czysto funkcyjnym taki model ewaluacji jest niemoÅ¼liwy,
    gdyÅ¼ nie dysponujemy globalnym stanem. Zamiast tego, w Coqu
    wykonanie programu polega na jego redukcji. O co chodzi?
    Przypomnijmy najpierw, Å¼e program to term, ktÃ³rego typem jest
    specyfikacja, czyli typ sortu [Set]. Termy mogÄ… byÄ‡ redukowane,
    czyli zamieniane na rÃ³wnowaÅ¼ne, ale prostsze, przy uÅ¼yciu
    reguÅ‚ redukcji. PrzeÅ›ledÅºmy wykonanie programu [negb true]
    krok po kroku. *)

Eval cbv delta in negb true.
(* ===> = (fun b : bool => match b with
                   | true => false
                   | false => true
                   end) true
        : bool *)

(** Redukcja delta odwija definicje. Å»eby uÅ¼yÄ‡ jakiejÅ› redukcji, uÅ¼ywamy
    komendy [Eval cbv redukcje in term]. *)

Eval cbv delta beta in negb true.
(* ===> = match true with
          | true => false
          | false => true
          end
        : bool *)

(** Redukcja beta podstawia argument do funkcji. *)

Eval cbv delta beta iota in negb true.
(* === = false : bool *)

(** Redukcja jota dopasowuje term do wzorca i zamienia go na term
    znajdujÄ…cy siÄ™ po prawej stronie [=>] dla dopasowanego przypadku. *)

Eval cbv in negb true.
(* ===> = false : bool *)

(** Å»eby zredukowaÄ‡ term za jednym zamachem, moÅ¼emy pominÄ…Ä‡ nazwy
    redukcji wystÄ™pujÄ…ce po [cbv] â€” w taki wypadku zostanÄ… zaaplikowane
    wszystkie moÅ¼liwe redukcje, czyli program zostanie wykonany. Do jego
    wykonania moÅ¼emy teÅ¼ uÅ¼yÄ‡ komend [Eval cbn] oraz [Eval compute] (a
    takÅ¼e [Eval simpl], ale taktyka [simpl] jest przestarzaÅ‚a, wiÄ™c nie
    polecam). *)

(** **** Ä†wiczenie (redukcja) *)

(** Zredukuj "rÄ™cznie" programy [andb false false] oraz [orb false true]. *)

(** Jak widaÄ‡, wykonanie programu w Coqu jest doÅ›Ä‡ toporne. Wynika to
    z faktu, Å¼e Coq nie zostaÅ‚ stworzony do wykonywania programÃ³w, a
    jedynie do ich definiowania i dowodzenia ich poprawnoÅ›ci. Aby uÅ¼yÄ‡
    programu napisanego w Coqu w Å›wiecie rzeczywistym, stosuje siÄ™
    zazwyczaj mechanizm ekstrakcji, ktÃ³ry pozwala z programu napisanego
    w Coqu automatycznie uzyskaÄ‡ program w Scheme, OCamlu lub Haskellu,
    ktÃ³re sÄ… od Coqa duÅ¼o szybsze i duÅ¼o powszechniej uÅ¼ywane. My jednak
    nie bÄ™dziemy siÄ™ tym przejmowaÄ‡. 

    Zdefiniowawszy naszÄ… funkcjÄ™, moÅ¼emy zadaÄ‡ sobie pytanie:
    czy definicja jest poprawna? GdybyÅ›my pisali w jednym z
    jÄ™zykÃ³w imperatywnych, moglibyÅ›my napisaÄ‡ dla niej testy
    jednostkowe, polegajÄ…ce np. na tym, Å¼e generujemy losowo
    wejÅ›cie funkcji i sprawdzamy, czy wynik posiada Å¼Ä…danÄ… przez
    nas wÅ‚aÅ›ciwoÅ›Ä‡. Coq umoÅ¼liwia nam coÅ› duÅ¼o silniejszego:
    moÅ¼emy wyraziÄ‡ przy pomocy twierdzenia, Å¼e funkcja posiada
    interesujÄ…cÄ… nas wÅ‚asnoÅ›Ä‡, a nastÄ™pnie sprÃ³bowaÄ‡ je udowodniÄ‡.
    JeÅ¼eli nam siÄ™ powiedzie, mamy caÅ‚kowitÄ… pewnoÅ›Ä‡, Å¼e funkcja
    rzeczywiÅ›cie posiada Å¼Ä…danÄ… wÅ‚asnoÅ›Ä‡. *)

Theorem negb_involutive :
  forall b : bool, negb (negb b) = b.
Proof.
  intros. destruct b.
    cbn. reflexivity.
    cbn. reflexivity.
Qed.

(** Nasze twierdzenie gÅ‚osi, Å¼e funkcja [negb] jest inwolucjÄ…,
    tzn. Å¼e dwukrotne jej zaaplikowanie do danego argumentu
    nie zmienia go, lub teÅ¼, innymi sÅ‚owy, Å¼e [negb] jest
    swojÄ… wÅ‚asnÄ… odwrotnoÅ›ciÄ….

    DowÃ³d przebiega w nastÄ™pujÄ…cy sposÃ³b: taktykÄ… [intro]
    wprowadzamy zmiennÄ… [b] do kontekstu, a nastÄ™pnie
    uÅ¼ywamy taktyki [destruct], aby sprawdziÄ‡, ktÃ³rym
    konstruktorem zostaÅ‚a zrobiona. PoniewaÅ¼ typ [bool] ma
    dwa konstruktory, taktyka ta generuje nam dwa podcele:
    musimy udowodniÄ‡ twierdzenie osobno dla przypadku, gdy
    [b = true] oraz dla [b = false]. Potem przy pomocy
    taktyki [cbn] redukujemy (czyli wykonujemy) programy
    [negb (negb true)] i [negb (negb false)]. ZauwaÅ¼, Å¼e
    byÅ‚oby to niemoÅ¼liwe, gdyby argument byÅ‚ postaci [b]
    (nie moÅ¼na wtedy zaaplikowaÄ‡ Å¼adnej redukcji), ale jest
    jak najbardziej moÅ¼liwe, gdy jest on postaci [true] albo
    [false] (wtedy redukcja przebiega jak w przykÅ‚adzie). Na
    koniec uÅ¼ywamy taktyki [reflexivity], ktÃ³ra potrafi udowodniÄ‡
    cel postaci [a = a].

    [destruct] jest taktykowym odpowiednikiem dopasowania do wzorca
    i bardzo czÄ™sto jest uÅ¼ywany, gdy mamy do czynienia z czymÅ›,
    co zostaÅ‚o za jego pomocÄ… zdefiniowane.

    ByÄ‡ moÅ¼e poczuÅ‚eÅ› dyskomfort spowodowany tym, Å¼e cel postaci
    [a = a] moÅ¼na udowodniÄ‡ dwoma rÃ³Å¼nymi taktykami ([reflexivity]
    oraz [trivial]) albo Å¼e termy moÅ¼na redukowaÄ‡ na cztery rÃ³Å¼ne
    sposoby ([Eval cbn], [Eval cbv], [Eval compute], [Eval simpl]).
    Niestety, bÄ™dziesz musiaÅ‚ siÄ™ do tego przyzwyczaiÄ‡ â€” jÄ™zyk
    taktyka Coqa jest strasznie zabaÅ‚aganiony i niesie ze sobÄ…
    spory bagaÅ¼ swej mrocznej przeszÅ‚oÅ›ci. Na szczÄ™Å›cie od niedawna
    trwajÄ… prace nad jego ucywilizowaniem, czego pierwsze efekty
    widaÄ‡ juÅ¼ od wersji 8.5. W chwilach desperacji uratowaÄ‡ moÅ¼e
    ciÄ™ jedynie dokumentacja:
    - https://coq.inria.fr/refman/coq-tacindex.html
    - https://coq.inria.fr/refman/proof-engine/ltac.html *)

Theorem negb_involutive' :
  forall b : bool, negb (negb b) = b.
Proof.
  destruct b; cbn; reflexivity.
Qed.

(** ZauwaÅ¼my, Å¼e nie musimy uÅ¼ywaÄ‡ taktyki [intro], Å¼eby wprowadziÄ‡
    [b] do kontekstu: taktyka [destruct] sama jest w stanie wykryÄ‡,
    Å¼e nie ma go w kontekÅ›cie i wprowadziÄ‡ je tam przed rozbiciem
    go na konstruktory. ZauwaÅ¼my teÅ¼, Å¼e oba podcele rozwiÄ…zaliÅ›my
    w ten sam sposÃ³b, wiÄ™c moÅ¼emy uÅ¼yÄ‡ kombinatora [;] (Å›rednika),
    Å¼eby rozwiÄ…zaÄ‡ je oba za jednym zamachem. *)

(** **** Ä†wiczenie (logika boolowska) *)

(** Udowodnij poniÅ¼sze twierdzenia. *)

Theorem andb_assoc :
  forall b1 b2 b3 : bool,
    andb b1 (andb b2 b3) = andb (andb b1 b2) b3.
(* begin hide *)
Proof.
  destruct b1, b2, b3; cbn; trivial.
Qed.
(* end hide *)

Theorem andb_comm :
  forall b1 b2 : bool,
    andb b1 b2 = andb b2 b1.
(* begin hide *)
Proof.
  destruct b1, b2; cbn; trivial.
Qed.
(* end hide *)

Theorem orb_assoc :
  forall b1 b2 b3 : bool,
    orb b1 (orb b2 b3) = orb (orb b1 b2) b3.
(* begin hide *)
Proof.
  destruct b1, b2, b3; cbn; trivial.
Qed.
(* end hide *)

Theorem orb_comm :
  forall b1 b2 : bool,
    orb b1 b2 = orb b2 b1.
(* begin hide *)
Proof.
  destruct b1, b2; cbn; reflexivity.
Qed.
(* end hide *)

Theorem andb_true_elim :
  forall b1 b2 : bool,
    andb b1 b2 = true -> b1 = true /\ b2 = true.
(* begin hide *)
Proof.
  destruct b1, b2; cbn; split; auto.
Qed.
(* end hide *)

(** **** Ä†wiczenie (rÃ³Å¼a kierunkÃ³w) *)

Module Directions.

(** Zdefiniuj typ opisujÄ…cy kierunki podstawowe (pÃ³Å‚noc, poÅ‚udnie, wschÃ³d,
    zachÃ³d - dodatkowe punkty za nadanie im sensownych nazw). *)

(* begin hide *)
Inductive D : Type :=
    | N : D
    | S : D
    | W : D
    | E : D.
(* end hide *)

(** Zdefiniuj funkcje [turnL] i [turnR], ktÃ³re reprezentujÄ… obrÃ³t o 90
    stopni przeciwnie/zgodnie z ruchem wskazÃ³wek zegara. SformuÅ‚uj i
    udowodnij twierdzenia mÃ³wiÄ…ce, Å¼e:
    - obrÃ³t cztery razy w lewo/prawo niczego nie zmienia
    - obrÃ³t trzy razy w prawo to tak naprawdÄ™ obrÃ³t w lewo (jest to tzw.
      pierwsze twierdzenie korwinizmu)
    - obrÃ³t trzy razy w lewo to obrÃ³t w prawo (jest to tzw. drugie
      twierdzenie korwinizmu)
    - obrÃ³t w prawo, a potem w lewo niczego nie zmienia
    - obrÃ³t w lewo, a potem w prawo niczego nie zmienia
    - kaÅ¼dy kierunek to pÃ³Å‚noc, poÅ‚udnie, wschÃ³d lub zachÃ³d (tzn. nie ma
      innych kierunkÃ³w) *)

(* begin hide *)
Definition turnL (d : D) : D :=
match d with
    | N => W
    | W => S
    | S => E
    | E => N
end.

Definition turnR (d : D) : D :=
match d with
    | N => E
    | E => S
    | S => W
    | W => N
end.

Lemma turnL4 :
  forall d : D, turnL (turnL (turnL (turnL d))) = d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma turnR4 :
  forall d : D, turnR (turnR (turnR (turnR d))) = d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma turnL3 :
  forall d : D, turnL (turnL (turnL d)) = turnR d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma turnR3 :
  forall d : D, turnR (turnR (turnR d)) = turnL d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma turnL_turnR :
  forall d : D, turnL (turnR d) = d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma turnR_turnL :
  forall d : D, turnR (turnL d) = d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma D_uniqueness :
  forall d : D, d = N \/ d = S \/ d = W \/ d = E.
Proof.
  destruct d; repeat (left + right); trivial; fail.
Qed.
(* end hide *)

(** Zdefiniuj funkcjÄ™ [opposite], ktÃ³re danemu kierunkowi przyporzÄ…dkowuje
    kierunek do niego przeciwny (czyli pÃ³Å‚nocy przyporzÄ…dkowuje poÅ‚udnie
    etc.). WymyÅ›l i udowodnij jakÄ…Å› ciekawÄ™ specyfikacjÄ™ dla tej funkcji
    (wskazÃ³wka: powiÄ…Å¼ jÄ… z [turnL] i [turnR]). *)

(* begin hide *)
Definition opposite (d : D) : D :=
match d with
    | N => S
    | S => N
    | W => E
    | E => W
end.

Lemma opposite_involutive :
  forall d : D, opposite (opposite d) = d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma opposite_turnL :
  forall d : D, opposite (turnL d) = turnR d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma opposite_turnR :
  forall d : D, opposite (turnR d) = turnL d.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma opposite_turnL_comm :
  forall d : D, opposite (turnL d) = turnL (opposite d).
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma opposite_turnR_comm :
  forall d : D, opposite (turnR d) = turnR (opposite d).
Proof.
  destruct d; cbn; reflexivity.
Qed.
(* end hide *)

(** Zdefiniuj funkcjÄ™ [is_opposite], ktÃ³ra bierze dwa kierunki i zwraca
    [true], gdy sÄ… one przeciwne oraz [false] w przeciwnym wypadku. WymyÅ›l
    i udowodnij jakÄ…Å› specyfikacjÄ™ dla tej funkcji. WskazÃ³wka: jakie sÄ… jej
    zwiÄ…zku z [turnL], [turnR] i [opposite]? *)

(* begin hide *)
Definition is_opposite (d1 d2 : D) : bool :=
match d1, d2 with
    | N, S => true
    | S, N => true
    | W, E => true
    | E, W => true
    | _, _ => false
end.

Lemma is_opposite_turnL :
  forall d1 d2 : D,
    is_opposite (turnL d1) (turnL d2) = is_opposite d1 d2.
Proof.
  destruct d1, d2; cbn; reflexivity.
Qed.

Lemma is_opposite_turnR :
  forall d1 d2 : D,
    is_opposite (turnR d1) (turnR d2) = is_opposite d1 d2.
Proof.
  destruct d1, d2; cbn; reflexivity.
Qed.

Lemma is_opposite_opposite_l :
  forall d : D, is_opposite (opposite d) d = true.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma is_opposite_opposite_r :
  forall d : D, is_opposite d (opposite d) = true.
Proof.
  destruct d; cbn; reflexivity.
Qed.

Lemma is_opposite_comm :
  forall d1 d2 : D,
    is_opposite d1 d2 = is_opposite d2 d1.
Proof.
  destruct d1, d2; cbn; reflexivity.
Qed.
(* end hide *)

(** PokaÅ¼, Å¼e funkcje [turnL], [turnR] oraz [opposite] sÄ… injekcjami i
    surjekcjami (co to dokÅ‚adnie znacz, dowiemy siÄ™ pÃ³Åºniej). Uwaga: to
    zadanie wymaga uÅ¼yci taktyki [inversion], ktÃ³ra jest opisana w
    podrozdziale o polimorfizmie. *)

Lemma turnL_inj :
  forall x y : D, turnL x = turnL y -> x = y.
(* begin hide *)
Proof.
  destruct x, y; cbn; intros; inversion H; reflexivity.
Qed.
(* end hide *)

Lemma turnR_inj :
  forall x y : D, turnR x = turnR y -> x = y.
(* begin hide *)
Proof.
  destruct x, y; cbn; intros; inversion H; reflexivity.
Qed.
(* end hide *)

Lemma opposite_inj :
  forall x y : D, opposite x = opposite y -> x = y.
(* begin hide *)
Proof.
  destruct x, y; cbn; intros; inversion H; reflexivity.
Qed.
(* end hide *)

Lemma turnL_sur :
  forall y : D, exists x : D, turnL x = y.
(* begin hide *)
Proof.
  intro. exists (turnR y). apply turnL_turnR.
Qed.
(* end hide *)

Lemma turnR_sur :
  forall y : D, exists x : D, turnR x = y.
(* begin hide *)
Proof.
  intro. exists (turnL y). apply turnR_turnL.
Qed.
(* end hide *)

Lemma opposite_sur :
  forall y : D, exists x : D, opposite x = y.
(* begin hide *)
Proof.
  intro. exists (opposite y). apply opposite_involutive.
Qed.
(* end hide *)

End Directions.

(** **** Ä†wiczenie (rÃ³Å¼ne enumeracje) *)

(** Zdefiniuj typy induktywne reprezentujÄ…ce:
    - dni tygodnia
    - miesiÄ…ce
    - kolory podstawowe systemu RGB *)

(* begin hide *)
Inductive Day : Type :=
    | Mon : Day
    | Tue : Day
    | Wed : Day
    | Thu : Day
    | Fri : Day
    | Sat : Day
    | Sun : Day.

Inductive Month : Type :=
    | Jan : Month
    | Feb : Month
    | Mar : Month
    | Apr : Month
    | May : Month
    | Jun : Month
    | Jul : Month
    | Aug : Month
    | Sep : Month
    | Oct : Month
    | Nov : Month
    | Dec : Month.

Inductive Color : Type :=
    | R : Color
    | G : Color
    | B : Color.
(* end hide *)

(** WymyÅ›l do nich jakieÅ› ciekawe funkcje i twierdzenia. *)

(* begin hide *)

(** TODO *)

Definition nextDay (d : Day) : Day :=
match d with
    | Mon => Tue
    | Tue => Wed
    | Wed => Thu
    | Thu => Fri
    | Fri => Sat
    | Sat => Sun
    | Sun => Mon
end.

Definition prevDay (d : Day) : Day :=
match d with
    | Mon => Sun
    | Tue => Mon
    | Wed => Tue
    | Thu => Wed
    | Fri => Thu
    | Sat => Fri
    | Sun => Sat
end.
(* end hide *)

(** ** Konstruktory rekurencyjne *)

(** PowiedzieliÅ›my, Å¼e termy typÃ³w induktywnych sÄ… drzewami. W przypadku
    enumeracji jest to sÅ‚abo widoczne, gdyÅ¼ drzewa te sÄ… zdegenerowane â€”
    sÄ… to po prostu punkciki oznaczone nazwami konstruktorÃ³w. Efekt
    rozgaÅ‚Ä™zienia moÅ¼emy uzyskaÄ‡, gdy jeden z konstruktorÃ³w bÄ™dzie
    rekurencyjny, tzn. gdy jako argument bÄ™dzie przyjmowaÅ‚ term typu,
    ktÃ³ry wÅ‚aÅ›nie definiujemy. Naszym przykÅ‚adem bÄ™dÄ… liczby naturalne
    (choÄ‡ i tutaj rozgaÅ‚Ä™zienie bÄ™dzie nieco zdegenerowane  ­- kaÅ¼dy term
    bÄ™dzie mÃ³gÅ‚ mieÄ‡ co najwyÅ¼ej jedno). *)

Module NatDef.

(** Mechanizm moduÅ‚Ã³w jest podobny do mechanizmu sekcji i na razie nie
    bÄ™dzie nas interesowaÅ‚ â€” uÅ¼yjemy go, Å¼eby nie zaÅ›miecaÄ‡ gÅ‚Ã³wnej
    przestrzeni nazw (mechanizm sekcji w tym przypadku by nie pomÃ³gÅ‚). *)

Inductive nat : Set :=
    | O : nat
    | S : nat -> nat.

Notation "0" := O.

(** Uwaga: nazwa pierwszego konstruktora to duÅ¼a litera O, a nie cyfra 0
    â€” cyfry nie mogÄ… byÄ‡ nazwami. Å»eby obejÅ›Ä‡ tÄ™ niedogodnoÅ›Ä‡, musimy
    posÅ‚uÅ¼yÄ‡ siÄ™ mechanizmem notacji â€” dziÄ™ki temu bÄ™dziemy mogli pisaÄ‡
    0 zamiast O.

    DefinicjÄ™ tÄ™ moÅ¼emy odczytaÄ‡ w nastÄ™pujÄ…cy sposÃ³b: "[0] jest liczbÄ…
    naturalnÄ…; jeÅ¼eli [n] jest liczbÄ… naturalnÄ…, to [S n] rÃ³wnieÅ¼ jest
    liczbÄ… naturalnÄ…". Konstruktor [S] odpowiada tutaj dodawaniu jedynki:
    [S 0] to 1, [S (S 0)] to 2, [S (S (S 0))] to 3 i tak dalej. *)

Check (S (S (S 0))).
(* ===> S (S (S 0)) : nat *)

End NatDef.

Check S (S (S 0)).
(* ===> 3 : nat *)

(** RÄ™czne liczenie iloÅ›ci [S] w kaÅ¼dej liczbie szybko staje siÄ™ trudne
    nawet dla maÅ‚ych liczb. Na szczÄ™Å›cie standardowa biblioteka Coqa
    udostÄ™pnia notacje, ktÃ³re pozwalajÄ… nam zapisywaÄ‡ liczby naturalne
    przy pomocy dobrze znanych nam cyfr arabskich. Å»eby uzyskaÄ‡ do nich
    dostÄ™p, musimy opuÅ›ciÄ‡ zdefiniowany przez nas moduÅ‚ [NatDef], Å¼eby
    nasza definicja [nat] nie przysÅ‚aniaÅ‚a tej bibliotecznej. Zaczniemy
    za to nowy moduÅ‚, Å¼ebyÅ›my mogli swobodnie zredefiniowaÄ‡ dziaÅ‚ania
    na liczbach naturalnych z biblioteki standardowej. *)

Module NatOps.

Fixpoint plus (n m : nat) : nat :=
match n with
    | 0 => m
    | S n' => S (plus n' m)
end.

(* begin hide *)
(*Notation "n + m" := (plus n m). DziÄ™ki notacjom bÄ™dziemy teÅ¼ mogli pisaÄ‡
    [n + m] zamiast [plus n m].*)
(* end hide *)

(** W zapisie unarnym liczby naturalne moÅ¼emy wyobraÅ¼aÄ‡ sobie jako kupki
    [S]-Ã³w, wiÄ™c dodawanie dwÃ³ch liczb sprowadza siÄ™ do przerzucenia [S]-Ã³w
    z jednej kupki na drugÄ….

    Definiowanie funkcji dla typÃ³w z konstruktorami rekurencyjnymi
    wyglÄ…da podobnie jak dla enumeracji, ale wystÄ™pujÄ… drobne rÃ³Å¼nice:
    jeÅ¼eli bÄ™dziemy uÅ¼ywaÄ‡ rekurencji, musimy zaznaczyÄ‡ to za pomocÄ…
    sÅ‚owa kluczowego [Fixpoint] (zamiast wczeÅ›niejszego [Definition]).
    ZauwaÅ¼my teÅ¼, Å¼e jeÅ¼eli funkcja ma wiele argumentÃ³w tego samego typu,
    moÅ¼emy napisaÄ‡ [(arg1 ... argN : type)] zamiast dÅ‚uÅ¼szego [(arg1 : type)
    ... (argN : type)].

    JeÅ¼eli konstruktor typu induktywnego bierze jakieÅ› argumenty, to wzorce,
    ktÃ³re go dopasowujÄ…, stajÄ… siÄ™ nieco bardziej skomplikowane: poza nazwÄ…
    konstruktora muszÄ… teÅ¼ dopasowywaÄ‡ argumenty â€” w naszym przypadku uÅ¼ywamy
    zmiennej o nazwie [n'], ktÃ³ra istnieje tylko lokalnie (tylko we wzorcu
    dopasowania oraz po prawej stronie strzaÅ‚ki [=>]).

    NaszÄ… funkcjÄ™ zdefiniowaliÅ›my przy pomocy najbardziej elementarnego
    rodzaju rekursji, jaki jest dostÄ™pny w Coqu: rekursji strukturalnej.
    W przypadku takiej rekursji wywoÅ‚ania rekurencyjne mogÄ… odbywaÄ‡ siÄ™
    jedynie na termach strukturalnie mniejszych, niÅ¼ obecny argument
    gÅ‚Ã³wny rekurencji. W naszym przypadku argumentem gÅ‚Ã³wnym jest [n]
    (bo on jest dopasowywany), zaÅ› rekurencyjnych wywoÅ‚aÅ„ dokonujemy na
    [n'], gdzie [n = S n']. [n'] jest strukturalnie mniejszy od [S n'],
    gdyÅ¼ skÅ‚ada siÄ™ z jednego [S] mniej. JeÅ¼eli wyobrazimy sobie nasze
    liczby jako kupki [S]-Ã³w, to wywoÅ‚ania rekurencyjne moÅ¼emy robiÄ‡
    jedynie po zdjÄ™ciu z kupki co najmniej jednego [S]. *)

(** **** Ä†wiczenie (rekursja niestrukturalna) *)

(** WymyÅ›l funkcjÄ™ z liczb naturalnych w liczby naturalne,
    ktÃ³ra jest rekurencyjna, ale nie jest strukturalnie rekurencyjna.
    Precyzyjniej piszÄ…c: pÃ³Åºniej okaÅ¼e siÄ™, Å¼e wszystkie formy
    rekurencji to tak naprawdÄ™ rekursja strukturalna pod przykrywkÄ….
    WymyÅ›l takÄ… definicjÄ™, ktÃ³ra na pierwszy rzut oka nie jest
    strukturalnie rekurencyjna. *)

(* begin hide *)
(** OdpowiedÅº: dzielenie. *)
(* end hide *)

(** Podobnie jak w przypadku funkcji [negb], tak i tym razem w celu
    sprawdzenia poprawnoÅ›ci naszej definicji sprÃ³bujemy udowodniÄ‡, Å¼e
    posiada ona wÅ‚aÅ›ciwoÅ›ci, ktÃ³rych siÄ™ spodziewamy. *)

Theorem plus_O_n :
  forall n : nat, plus 0 n = n.
Proof.
  intro. cbn. trivial.
Qed.

(** Tak prosty dowÃ³d nie powinien nas dziwiÄ‡ â€” wszakÅ¼e twierdzenie to
    wynika wprost z definicji (sprÃ³buj zredukowaÄ‡ "rÄ™cznie" wyraÅ¼enie
    [0 + n]). *)

Theorem plus_n_O_try1 :
  forall n : nat, plus n 0 = n.
Proof.
  intro. destruct n.
    trivial.
    cbn. f_equal.
Abort.

(** Tym razem nie jest juÅ¼ tak prosto â€” [n + 0 = n] nie wynika z definicji
    przez prostÄ… redukcjÄ™, gdyÅ¼ argumentem gÅ‚Ã³wnym funkcji [plus] jest
    jej pierwszy argument, czyli [n]. Å»eby mÃ³c zredukowaÄ‡ to wyraÅ¼enie,
    musimy rozbiÄ‡ [n]. Pokazanie, Å¼e [0 + 0 = 0] jest trywialne, ale
    drugi przypadek jest juÅ¼ beznadziejny. PoniewaÅ¼ funkcje zachowujÄ…
    rÃ³wnoÅ›Ä‡ (czyli [a = b] implikuje [f a = f b]), to aby pokazaÄ‡, Å¼e
    [f a = f b], wystarczyÄ‡ pokazaÄ‡, Å¼e [a = b] â€” w ten wÅ‚aÅ›nie sposÃ³b
    dziaÅ‚a taktyka [f_equal]. Nie pomogÅ‚a nam ona jednak â€” po jej
    uÅ¼yciu mamy do pokazania to samo, co na poczÄ…tku, czyli [n + 0 = n]. *)

Theorem plus_n_O :
  forall n : nat, plus n 0 = n.
Proof.
  intro. induction n.
    trivial.
    cbn. f_equal. assumption.
Qed.

(** Do udowodnienia tego twierdzenia musimy posÅ‚uÅ¼yÄ‡ siÄ™ indukcjÄ….
    Indukcja jest sposobem dowodzenia wÅ‚aÅ›ciwoÅ›ci typÃ³w induktywnych
    i funkcji rekurencyjnych, ktÃ³ry dziaÅ‚a mniej wiÄ™cej tak: Å¼eby
    udowodniÄ‡, Å¼e kaÅ¼dy term typu [A] posiada wÅ‚asnoÅ›Ä‡ [P], pokazujemy
    najpierw, Å¼e konstruktory nierekurencyjne posiadajÄ… tÄ™ wÅ‚asnoÅ›Ä‡
    dla dowolnych argumentÃ³w, a nastÄ™pnie, Å¼e konstruktory rekurencyjne
    zachowujÄ… tÄ™ wÅ‚asnoÅ›Ä‡.

    W przypadku liczb naturalnych indukcja wyglÄ…da tak: Å¼eby pokazaÄ‡,
    Å¼e kaÅ¼da liczba naturalna ma wÅ‚asnoÅ›Ä‡ [P], najpierw naleÅ¼y
    pokazaÄ‡, Å¼e zachodzi [P 0], a nastÄ™pnie, Å¼e jeÅ¼eli zachodzi [P n],
    to zachodzi takÅ¼e [P (S n)]. Z tych dwÃ³ch reguÅ‚ moÅ¼emy zbudowaÄ‡
    dowÃ³d na to, Å¼e [P n] zachodzi dla dowolnego [n].

    Ten sposÃ³b rozumowania moÅ¼emy zrealizowaÄ‡ w Coqu przy pomocy
    taktyki [induction]. DziaÅ‚a ona podobnie do [destruct], czyli
    rozbija podany term na konstruktory, ale w przypadku konstruktorÃ³w
    rekurencyjnych robi coÅ› jeszcze â€” daje nam zaÅ‚oÅ¼enie indukcyjne,
    ktÃ³re mÃ³wi, Å¼e dowodzone przez nas twierdzenie zachodzi dla
    rekurencyjnych argumentÃ³w konstruktora. WÅ‚aÅ›nie tego byÅ‚o nam
    trzeba: zaÅ‚oÅ¼enie indukcyjne pozwala nam dokoÅ„czyÄ‡ dowÃ³d. *)

Theorem plus_comm :
  forall n m : nat, plus n m = plus m n.
Proof.
  induction n as [| n']; cbn; intros.
    rewrite plus_n_O. trivial.
    induction m as [| m'].
      cbn. rewrite plus_n_O. trivial.
      cbn. rewrite IHn'. rewrite <- IHm'. cbn. rewrite IHn'.
        trivial.
Qed.

(** Pojedyncza indukcja nie zawsze wystarcza, co obrazuje powyÅ¼szy przypadek.
    ZauwaÅ¼my, Å¼e przed uÅ¼yciem [induction] nie musimy wprowadzaÄ‡ zmiennych
    do kontekstu â€” taktyka ta robi to sama, podobnie jak [destruct].
    RÃ³wnieÅ¼ podobnie jak [destruct], moÅ¼emy przekazaÄ‡ jej wzorzec, ktÃ³rym
    nadajemy nazwy argumentom konstruktorÃ³w, na ktÃ³re rozbijany jest term.
    
    W ogÃ³lnoÅ›ci wzorzec ma postaÄ‡ [[a11 .. a1n | ... | am1 .. amk]]. Pionowa
    kreska oddziela argumenty poszczegÃ³lnych konstruktorÃ³w: [a11 .. a1n]
    to argumenty pierwszego konstruktora, zaÅ› [am1 .. amk] to argumenty
    m-tego konstruktora. [nat] ma dwa konstruktory, z czego pierwszy nie
    bierze argumentÃ³w, a drugi bierze jeden, wiÄ™c nasz wzorzec ma postaÄ‡
    [[| n']]. DziÄ™ki temu nie musimy polegaÄ‡ na domyÅ›lnych nazwach nadawanych
    argumentom przez Coqa, ktÃ³re czÄ™sto wprowadzajÄ… zamÄ™t.

    JeÅ¼eli damy taktyce [rewrite] nazwÄ™ hipotezy lub twierdzenia, ktÃ³rego
    konkluzjÄ… jest [a = b], to zamienia ona w obecnym podcelu wszystkie
    wystÄ…pienia [a] na [b] oraz generuje tyle podcelÃ³w, ile przesÅ‚anek ma
    uÅ¼yta hipoteza lub twierdzenie. W naszym przypadku uÅ¼yliÅ›my udowodnionego
    uprzednio twierdzenia [plus_n_O], ktÃ³re nie ma przesÅ‚anek, czego efektem
    byÅ‚o po prostu przepisanie [plus m 0] na [m].

    PrzepisywaÄ‡ moÅ¼emy teÅ¼ w drugÄ… stronÄ™ piszÄ…c [rewrite <-]. Wtedy jeÅ¼eli
    konkluzjÄ… danego [rewrite] twierdzenia lub hipotezy jest [a = b], to
    w celu wszystkie [b] zostanÄ… zastÄ…pione przez [a]. *)

(** **** Ä†wiczenie (mnoÅ¼enie) *)

(** Zdefiniuj mnoÅ¼enie i udowodnij jego wÅ‚aÅ›ciwoÅ›ci. *)

(* begin hide *)
Fixpoint mult (n m : nat) : nat :=
match n with
    | 0 => 0
    | S n' => plus m (mult n' m)
end.
(* end hide *)

Theorem mult_0_l :
  forall n : nat, mult 0 n = 0.
(* begin hide *)
Proof.
  induction n; trivial.
Qed.
(* end hide *)

Theorem mult_0_r :
  forall n : nat, mult n 0 = 0.
(* begin hide *)
Proof.
  induction n as [| n'].
    cbn. trivial.
    cbn. assumption.
Restart.
  induction n; trivial.
Qed.
(* end hide *)

Theorem mult_1_l :
  forall n : nat, mult 1 n = n.
(* begin hide *)
Proof.
  destruct n as [| n'].
    cbn. trivial.
    cbn. rewrite plus_n_O. trivial.
Restart.
  destruct n; cbn; try rewrite plus_n_O; trivial.
Qed.
(* end hide*)

Theorem mult_1_r :
  forall n : nat, mult n 1 = n.
(* begin hide *)
Proof.
  induction n.
    cbn. trivial.
    cbn. rewrite IHn. trivial.
Restart.
  induction n; cbn; try rewrite IHn; trivial.
Qed.
(* end hide *)

(** JeÅ¼eli Ä‡wiczenie byÅ‚o za proste i czytaÅ‚eÅ› podrozdziaÅ‚ o kombinatorach
    taktyk, to sprÃ³buj udowodniÄ‡:
    - dwa pierwsze twierdzenia uÅ¼ywajÄ…c nie wiÄ™cej niÅ¼ 2 taktyk
    - trzecie bez uÅ¼ycia indukcji, uÅ¼ywajÄ…c nie wiÄ™cej niÅ¼ 4 taktyk
    - czwarte uÅ¼ywajÄ…c nie wiÄ™cej niÅ¼ 4 taktyk *)

(** Wszystkie dowody powinny byÄ‡ nie dÅ‚uÅ¼sze niÅ¼ pÃ³Å‚ linijki. *)

(** **** Ä†wiczenie (inne dodawanie) *)

(** Dodawanie moÅ¼na alternatywnie zdefiniowaÄ‡ takÅ¼e w sposÃ³b przedstawiony
    poniÅ¼ej. Udowodnij, Å¼e ta definicja jest rÃ³wnowaÅ¼na poprzedniej. *)

Fixpoint plus' (n m : nat) : nat :=
match m with
    | 0 => n
    | S m' => plus' (S n) m'
end.

Theorem plus'_n_0 :
  forall n : nat, plus' n 0 = n.
(* begin hide *)
Proof. reflexivity. Qed.
(* end hide *)

Theorem plus'_S :
  forall n m : nat, plus' (S n) m = S (plus' n m).
(* begin hide *)
Proof.
  intros. generalize dependent n.
  induction m as [| m']; cbn; intros.
    reflexivity.
    rewrite IHm'. reflexivity.
Qed.
(* end hide *)

Theorem plus'_0_n :
  forall n : nat, plus' 0 n = n.
(* begin hide *)
Proof.
  induction n as [| n'].
    trivial.
    cbn. rewrite plus'_S. rewrite IHn'. trivial.
Qed.
(* end hide *)

Theorem plus'_comm :
  forall n m : nat, plus' n m = plus' m n.
(* begin hide *)
Proof.
  intros. generalize dependent n. induction m as [| m']; cbn; intros.
    rewrite plus'_0_n. trivial.
    rewrite IHm'. cbn. trivial.
Qed.
(* end hide *)

Theorem plus'_is_plus :
  forall n m : nat, plus' n m = plus n m.
(* begin hide *)
Proof.
  induction n as [| n']; cbn; intro.
    apply plus'_0_n.
    rewrite <- IHn'. rewrite plus'_S. trivial.
Qed.
(* end hide *)

End NatOps.

(** ** Typy polimorficzne i wÅ‚aÅ›ciwoÅ›ci konstruktorÃ³w *)

(** Przy pomocy komendy [Inductive] moÅ¼emy definiowaÄ‡ nie tylko typy
    induktywne, ale takÅ¼e rodziny typÃ³w induktywnych. JeÅ¼eli taka
    rodzina parametryzowana jest typem, to mamy do czynienia z
    polimorfizmem. *)

Inductive option (A : Type) : Type :=
    | Some : A -> option A
    | None : option A.

(** [option] jest rodzinÄ… typÃ³w, zaÅ› samo [option A] dla ustalonego [A]
    jest typem, ktÃ³ry reprezentuje moÅ¼liwoÅ›Ä‡ istnienia wartoÅ›ci typu [A]
    (konstruktor [Some]) albo i nie (konstruktor [None]). *)

Check Some.
(* ===> Some forall A : Type, A -> option A *)

Check Some nat 5.
(* ===> Some nat 5 *)

Check None.
(* ===> None forall A : Type, option A *)

Arguments Some [A] _.
Arguments None [A].

(** Jak widaÄ‡ typ [A], bÄ™dÄ…cy parametrem [option], jest teÅ¼ pierwszym
    argumentem kaÅ¼dego z konstruktorÃ³w.
    Pisanie go bywa uciÄ…Å¼liwe, ale na szczÄ™Å›cie Coq moÅ¼e sam wywnioskowaÄ‡
    jego wartoÅ›Ä‡, jeÅ¼eli mu kaÅ¼emy. Komenda [Arguments] pozwala nam
    okreÅ›liÄ‡, ktÃ³re argumenty majÄ… byÄ‡ domyÅ›lne â€” chcemy, aby argument [A]
    byÅ‚ domyÅ›lny, gdyÅ¼ w przypadku konstruktura [Some] moÅ¼e byÄ‡ wywnioskowany
    z drugiego argumentu, a w przypadku [None] â€” zazwyczaj z kontekstu.

    Konstruktory typÃ³w induktywnych majÄ… kilka wÅ‚aÅ›ciwoÅ›ci, o ktÃ³rch
    warto wiedzieÄ‡. Po pierwsze, wartoÅ›ci zrobione za pomocÄ… rÃ³Å¼nych
    konstruktorÃ³w sÄ… rÃ³Å¼ne. Jest to konieczne, gdyÅ¼ za pomocÄ… dopasowania
    do wzorca moÅ¼emy rozrÃ³Å¼niÄ‡ rÃ³Å¼ne konstruktory â€” gdyby byÅ‚y one
    rÃ³wne, uzyskalibyÅ›my sprzecznoÅ›Ä‡. *)

Definition isSome {A : Type} (a : option A) : Prop :=
match a with
    | Some _ => True
    | None => False
end.

(** Pomocnicza funkcja [isSome] ma za zadanie sprawdziÄ‡, ktÃ³rym
    konstruktorem zrobiono wartoÅ›Ä‡ typu [option A]. Zapis [{A : Type}]
    oznacza, Å¼e [A] jest argumentem domyÅ›lnym funkcji â€” Coq moÅ¼e go
    wywnioskowaÄ‡, gdyÅ¼ zna typ argumentu [a] (jest nim [option A]).
    ZauwaÅ¼ teÅ¼, Å¼e funkcja ta zwraca zdania logiczne, a nie wartoÅ›ci
    boolowskie. *)

Theorem some_not_none :
  forall (A : Type) (a : A), Some a <> None.
Proof.
  unfold not; intros. change False with (isSome (@None A)).
  rewrite <- H. cbn. trivial.
Qed.

(** MoÅ¼emy uÅ¼yÄ‡ tej pomocniczej funkcji, aby udowodniÄ‡, Å¼e konstruktory
    [Some] i [None] tworzÄ… rÃ³Å¼ne wartoÅ›ci. Taktyka [change t1 with t2]
    pozwala nam zamieniÄ‡ term [t1] na [t2] pod warunkiem, Å¼e sÄ… one
    konwertowalne (czyli jeden z nich redukuje siÄ™ do drugiego). W naszym
    wypadku chcemy zastÄ…piÄ‡ [False] przez [isSome (@None A)], ktÃ³ry
    redukuje siÄ™ do [False] (sprÃ³buj zredukowaÄ‡ to wyraÅ¼enie rÄ™cznie).

    UÅ¼ycie symbolu [@] pozwala nam dla danego wyraÅ¼enia zrezygnowaÄ‡ z
    prÃ³by automatycznego wywnioskowania argumentÃ³w domyÅ›lnych â€” w tym
    przypadku Coq nie potrafiÅ‚by wywnioskowaÄ‡ argumentu dla konstruktora
    [None], wiÄ™c musimy podaÄ‡ ten argument rÄ™cznie. 

    NastÄ™pnie moÅ¼emy skorzystaÄ‡ z rÃ³wnania [Some a = None], Å¼eby
    uzyskaÄ‡ cel postaci [isSome (Some a)]. Cel ten redukuje siÄ™
    do [True], ktÃ³rego udowodnienie jest trywialne. *)

Theorem some_not_none' :
  forall (A : Type) (a : A), Some a <> None.
Proof. inversion 1. Qed.

(** CaÅ‚a procedura jest doÅ›Ä‡ skomplikowana â€” w szczegÃ³lnoÅ›ci wymaga
    napisania funkcji pomocniczej. Na szczÄ™Å›cie Coq jest w stanie
    sam wywnioskowaÄ‡, Å¼e konstruktory sÄ… rÃ³Å¼ne. MoÅ¼emy zrobiÄ‡ to
    przy pomocy znanej nam z poprzedniego rozdziaÅ‚u taktyki [inversion].
    Zapis [inversion 1] oznacza: wprowadÅº zmienne zwiÄ…zane przez
    kwantyfikacjÄ™ uniwersanÄ… do kontekstu i uÅ¼yj taktyki [inversion]
    na pierwszej przesÅ‚ance implikacji. W naszym przypadku implikacja
    jest ukryta w definicji negacji: [Some a <> None] to tak naprawdÄ™
    [Some a = None -> False]. *)

Theorem some_inj :
  forall (A : Type) (x y : A),
    Some x = Some y -> x = y.
Proof.
  intros. injection H. trivial.
Qed.

(** KolejnÄ… wÅ‚aÅ›ciwoÅ›ciÄ… konstruktorÃ³w jest fakt, Å¼e sÄ… one injekcjami,
    tzn. jeÅ¼eli dwa termy zrobione tymi samymi konstruktorami sÄ… rÃ³wne,
    to argumenty tych konstruktorÃ³w teÅ¼ sÄ… rÃ³wne.

    Aby skorzystaÄ‡ z tej wÅ‚aÅ›ciwoÅ›ci w dowodzie, moÅ¼emy uÅ¼yÄ‡ taktyki
    [injection], podajÄ…c jej jako argument nazwÄ™ hipotezy. JeÅ¼eli
    hipoteza jest postaci [C x1 ... xn = C y1 ... yn], to nasz cel [G]
    zostanie zastÄ…piony przez implikacjÄ™ [x1 = y1 -> ... -> xn = yn -> G].
    Po wprowadzeniu hipotez do kontekstu moÅ¼emy uÅ¼yÄ‡ ich do udowodnienia
    [G], zazwyczaj przy pomocy taktyki [rewrite].

    W naszym przypadku [H] miaÅ‚o postaÄ‡ [Some x = Some y], a cel [x = y],
    wiÄ™c [injection H] przeksztaÅ‚ciÅ‚o cel do postaci [x = y -> x = y],
    ktÃ³ry jest trywialny. *)

Theorem some_inj' :
  forall (A : Type) (x y : A), Some x = Some y -> x = y.
Proof.
  inversion 1. trivial.
Qed.

(** Taktyka [inversion] moÅ¼e nam pomÃ³c rÃ³wnieÅ¼ wtedy, kiedy chcemy skorzystaÄ‡
    z injektywnoÅ›ci konstruktorÃ³w. W zasadzie jest ona nawet bardziej
    przydatna â€” dziaÅ‚a ona tak jak [injection], ale zamiast zostawiaÄ‡ cel w
    postaci [x1 = y1 -> ... -> G], wprowadza ona wygenerowane hipotezy do
    kontekstu, a nastÄ™pnie przepisuje w celu wszystkie, ktÃ³rych przepisanie
    jest moÅ¼liwe. W ten sposÃ³b oszczÄ™dza nam ona nieco pisania.

    W naszym przypadku [inverson 1] dodaÅ‚a do kontekstu hipotezÄ™ [x = y],
    a nastÄ™pnie przepisaÅ‚a jÄ… w celu (ktÃ³ry miaÅ‚ postaÄ‡ [x = y]), dajÄ…c
    cel postaci [y = y]. *)

Theorem some_inj'' :
  forall (A : Type) (x y : A), Some x = Some y -> x = y.
Proof.
  injection 1. intro. subst. trivial.
Qed.

(** TaktykÄ… uÅ‚atwiajÄ…cÄ… pracÄ™ z [injection] oraz [inversion] jest [subst].
    Taktyka ta wyszukuje w kontekÅ›cie hipotezy postaci [a = b],
    przepisuje je we wszystkich hipotezach w kontekÅ›cie i celu, w ktÃ³rych
    jest to moÅ¼liwe, a nastÄ™pnie usuwa. SzczegÃ³lnie czÄ™sto spotykana
    jest kombinacja [inversion H; subst], gdyÅ¼ [inversion] czÄ™sto
    generuje sporÄ… iloÅ›Ä‡ hipotez postaci [a = b], ktÃ³re [subst] nastÄ™pnie
    "sprzÄ…ta".

    W naszym przypadku hipoteza [H0 : x = y] zostaÅ‚a przepisana nie tylko
    w celu, dajÄ…c [y = y], ale takÅ¼e w hipotezie [H], dajÄ…c
    [H : Some y = Some y]. *)

(** **** Ä†wiczenie (zero i jeden) *)

(** Udowodnij poniÅ¼sze twierdzenie bez uÅ¼ywania taktyki [inversion].
    Å»eby byÅ‚o trudniej, nie pisz osobnej funkcji pomocniczej â€” zdefiniuj
    swojÄ… funkcjÄ™ bezpoÅ›rednio w miejscu, w ktÃ³rym chcesz jej uÅ¼yÄ‡.  *)

Theorem zero_not_one : 0 <> 1.
(* begin hide *)
Proof.
  intro. change False with
  ((fun n : nat =>
  match n with
      | 0 => False
      | _ => True
  end) 0).
  rewrite H. trivial.
Qed.
(* end hide *)

(** Dwie opisane wÅ‚aÅ›ciwoÅ›ci, choÄ‡ pozornie niewinne, a nawet przydatne,
    majÄ… bardzo istotne i daleko idÄ…ce konsekwencje. PowodujÄ™ one na
    przykÅ‚ad, Å¼e nie istniejÄ… typy ilorazowe. DokÅ‚adne znaczenie tego
    faktu omÃ³wimy pÃ³Åºniej, zaÅ› teraz musimy zadowoliÄ‡ siÄ™ jedynie
    prostym przykÅ‚adem w formie Ä‡wiczenia. *)

Module rational.

Inductive rational : Set :=
    | mk_rational :
      forall (sign : bool) (numerator denominator : nat),
        denominator <> 0 -> rational.

Axiom rational_eq :
  forall (s s' : bool) (p p' q q' : nat)
    (H : q <> 0) (H' : q' <> 0), p * q' = p' * q ->
      mk_rational s p q H = mk_rational s' p' q' H'.

(** Typ [rational] ma reprezentowaÄ‡ liczby wymierne. Znak jest typu
    [bool] â€” moÅ¼emy interpretowaÄ‡, Å¼e [true] oznacza obecnoÅ›Ä‡ znaku
    minus, a [false] brak znaku. Dwie liczby naturalne bÄ™dÄ… oznaczaÄ‡
    kolejno licznik i mianownik, a na koÅ„cu Å¼Ä…damy jeszcze dowodu na
    to, Å¼e mianownik nie jest zerem.

    OczywiÅ›cie typ ten sam w sobie niewiele ma wspÃ³lnego z liczbami
    wymiernymi â€” jest to po prostu trÃ³jka elementÃ³w o typach [bool,
    nat, nat], z ktÃ³rych ostatni nie jest zerem. Å»eby rzeczywiÅ›cie
    reprezentowaÅ‚ liczby wymierne musimy zapewniÄ‡, Å¼e termy, ktÃ³re
    reprezentujÄ… te same wartoÅ›ci, sÄ… rÃ³wne, np. 1/2 musi byÄ‡ rÃ³wna
    2/4.

    W tym celu postulujemy aksjomat, ktÃ³ry zapewni nam poÅ¼Ä…dane
    wÅ‚aÅ›ciwoÅ›ci relacji rÃ³wnoÅ›ci. Komenda [Axiom] pozwala nam
    wymusiÄ‡ istnienie termu poÅ¼Ä…danego typu i nadaÄ‡ mu nazwÄ™,
    jednak jest szalenie niebezpieczna â€” jeÅ¼eli zapostulujemy
    aksjomat, ktÃ³ry jest sprzeczny, jesteÅ›my zgubieni.

    W takiej sytuacji caÅ‚e nasze dowodzenie idzie na marne, gdyÅ¼
    ze sprzecznego aksjomatu moÅ¼emy wywnioskowaÄ‡ [False], z
    [False] zaÅ› moÅ¼emy wywnioskowaÄ‡ cokolwiek, o czym przekonaliÅ›my
    siÄ™ w rozdziale pierwszym. Tak teÅ¼ jest w tym przypadku â€”
    aksjomat [rational_eq] jest sprzeczny, gdyÅ¼ Å‚amie zasadÄ™
    injektywnoÅ›ci konstruktorÃ³w. *)

(** **** Ä†wiczenie (niedobry aksjomat) *)

(** Udowodnij, Å¼e aksjomat [rational_eq] jest sprzeczny. WskazÃ³wka: znajdÅº
    dwie liczby wymierne, ktÃ³re sÄ… rÃ³wne na mocy tego aksjomatu, ale ktÃ³re
    moÅ¼na rozrÃ³Å¼niÄ‡ za pomocÄ… dopasowania do wzorca. *)

(* begin hide *)
Definition q_1_2 : rational :=
  mk_rational true 1 2 ltac:(inversion 1).

Definition q_2_4 : rational :=
  mk_rational true 2 4 ltac:(inversion 1).

Lemma q_1_2_eq_q_2_4 : q_1_2 = q_2_4.
Proof.
  apply rational_eq. reflexivity.
Qed.
(* end hide *)

Theorem rational_eq_inconsistent : False.
(* begin hide *)
Proof.
  change False with
  ((fun q : rational =>
  match q with
      | mk_rational true 1 2 _ => False
      | _ => True
  end) q_1_2).
  rewrite q_1_2_eq_q_2_4. cbn. trivial.
Qed.
(* end hide *)

End rational.

(** ** Listy, czyli parametry + rekursja *)

(** PoÅ‚Ä…czenie funkcji zaleÅ¼nych, konstruktorÃ³w rekurencyjnych i
    polimorfizmu pozwala nam na opisywanie (prawie) dowolnych typÃ³w.
    Jednym z najbardziej podstawowych i najbardziej przydatnych
    narzÄ™dzi w programowaniu funkcyjnym (i w ogÃ³le w Å¼yciu) sÄ… listy. *)

Module MyList.

Inductive list (A : Type) : Type :=
    | nil : list A
    | cons : A -> list A -> list A.

(** Lista przechowuje wartoÅ›ci pewnego ustalonego typu [A] (a wiÄ™c nie
    moÅ¼na np. trzymaÄ‡ w jednej liÅ›cie jednoczeÅ›nie wartoÅ›ci typu [bool] i
    [nat]) i moÅ¼e mieÄ‡ jednÄ… z dwÃ³ch postaci: moÅ¼e byÄ‡ pusta (konstruktor
    [nil]) albo skÅ‚adaÄ‡ siÄ™ z gÅ‚owy i ogona (konstruktor [cons]). GÅ‚owa
    listy to wartoÅ›Ä‡ typu [A], zaÅ› jej ogon to inna lista przechowujÄ…ca
    wartoÅ›ci typu [A]. *)

Check nil.
(* ===> nil : forall A : Type, list A *)

Check cons.
(* ===> cons : forall A : Type, A -> list A -> list A *)

Arguments nil [A].
Arguments cons [A] _ _.

(** Jak juÅ¼ wspomnieliÅ›my, jeÅ¼eli typ induktywny ma argument (w naszym
    przypadku [A : Type]), to argument ten jest teÅ¼ pierwszym argumentem
    kaÅ¼dego z konstruktorÃ³w. W przypadku konstruktora [cons] podawanie
    argumentu [A] jest zbÄ™dne, gdyÅ¼ kolejnym jego argumentem jest wartoÅ›Ä‡
    tego typu. Wobec tego Coq moÅ¼e sam go wywnioskowaÄ‡, jeÅ¼eli mu kaÅ¼emy.

    Robimy to za pomocÄ… komendy [Arguments konstruktor argumenty].
    Argumenty w nawiasach kwadratowych Coq bÄ™dzie traktowaÅ‚ jako domyÅ›lne,
    a te oznaczone podkreÅ›lnikiem trzeba bÄ™dzie zawsze podawaÄ‡ rÄ™cznie.
    Nazwa argumentu domyÅ›lnego musi byÄ‡ taka sama jak w definicji typu
    (w naszym przypadku w definicji [list] argument nazywaÅ‚ siÄ™ [A],
    wiÄ™c tak teÅ¼ musimy go nazwaÄ‡ uÅ¼ywajÄ…c komendy [Arguments]). Musimy
    wypisaÄ‡ wszystkie argumenty danego konstruktora â€” ich iloÅ›Ä‡ moÅ¼emy
    sprawdziÄ‡ np. komendÄ… [Check].

    Warto w tym momencie zauwaÅ¼yÄ‡, Å¼e Coq zna typy wszystkich termÃ³w,
    ktÃ³re zostaÅ‚y skonstruowane â€” gdyby tak nie byÅ‚o, nie mÃ³gÅ‚by
    sam uzupeÅ‚niaÄ‡ argumentÃ³w domyÅ›lnych, a komenda [Check] nie mogÅ‚aby
    dziaÅ‚aÄ‡. *)

Notation "[]" := nil.
Infix "::" := (cons) (at level 60, right associativity ).

Check [].
(* ===> [[]] : list ?254 *)

Check 0 :: 1 :: 2 :: nil.
(* ===> [[0; 1; 2]] : list nat *)

(** Nazwy [nil] i [cons] sÄ… zdecydowanie za dÅ‚ugie w porÃ³wnaniu do swej
    czÄ™stoÅ›ci wystÄ™powania. DziÄ™ki powyÅ¼szym eleganckim notacjom
    zaoszczÄ™dzimy sobie trochÄ™ pisania. JeÅ¼eli jednak notacje utrudniajÄ…
    nam np. odczytanie celu, ktÃ³ry mamy udowodniÄ‡, moÅ¼emy je wyÅ‚Ä…czyÄ‡
    odznaczajÄ…c w CoqIDE View > Display Notations.

    Wynik [[] : list ?254] (lub podobny) wyÅ›wietlony przez Coqa dla [[]]
    mÃ³wi nam, Å¼e [[]] jest listÄ… pewnego ustalonego typu, ale Coq jeszcze
    nie wie, jakiego (bo ma za maÅ‚o informacji, bo wywnioskowaÄ‡ argument
    domyÅ›lny konstruktora [nil]). *)

Notation "[ x ]" := (cons x nil).
Notation "[ x ; y ; .. ; z ]" :=
    (cons x (cons y .. (cons z nil) .. )).

Check [5].
(* ===> [[5]] : list nat *)

Check [0; 1; 2; 3].
(* ===> [[0; 1; 2; 3]] : list nat *)

(** ZauwaÅ¼, Å¼e system notacji Coqa jest bardzo silny â€” ostatnia notacja
    (ta zawierajÄ…ca [..]) jest rekurencyjna. W innych jÄ™zykach tego typu
    notacje sÄ… zazwyczaj wbudowane w jÄ™zyk i ograniczajÄ… siÄ™ do podstawowych
    typÃ³w, takich jak listy wÅ‚aÅ›nie. *)

Fixpoint app {A : Type} (l1 l2 : list A) : list A :=
match l1 with
    | [] => l2
    | h :: t => h :: app t l2
end.

Notation "l1 ++ l2" := (app l1 l2).

(** Funkcje na listach moÅ¼emy definiowaÄ‡ analogicznie do funkcji na
    liczbach naturalnych. Zaczniemy od sÅ‚owa kluczowego [Fixpoint],
    gdyÅ¼ bÄ™dziemy potrzebowaÄ‡ rekurencji. Pierwszym argumentem naszej
    funkcji bÄ™dzie typ [A] â€” musimy go wymieniÄ‡, bo inaczej nie bÄ™dziemy
    mogli mieÄ‡ argumentÃ³w typu [list A] (pamiÄ™taj, Å¼e samo [list]
    jest rodzinÄ… typÃ³w, a nie typem). Zapis [{A : Type}] oznacza,
    Å¼e Coq ma traktowaÄ‡ [A] jako argument domyÅ›lny â€” jest to szybszy
    sposÃ³b, niÅ¼ uÅ¼ycie komendy [Arguments].

    Nasz funkcja ma za zadanie dokleiÄ‡ na koÅ„cu (ang. append) pierwszej
    listy drugÄ… listÄ™. Definicja jest doÅ›Ä‡ intuicyjna: doklejenie jakiejÅ›
    listy na koniec listy pustej daje pierwszÄ… listÄ™, a doklejenie listy
    na koniec listy majÄ…cej gÅ‚owÄ™ i ogon jest doklejeniem jej na koniec
    ogona. *)

Eval compute in [1; 2; 3] ++ [4; 5; 6].
(* ===> [[1; 2; 3; 4; 5; 6]] : list nat *)

(** Wynik dziaÅ‚ania naszej funkcji wyglÄ…da poprawnie, ale niech ciÄ™
    nie zwiodÄ… Å‚adne oczka â€” jedynym sposobem ustalenia poprawnoÅ›ci
    naszego kodu jest udowodnienie, Å¼e posiada on poÅ¼Ä…dane przez
    nas wÅ‚aÅ›ciwoÅ›ci. *)

Theorem app_nil_l :
  forall (A : Type) (l : list A), [] ++ l = l.
Proof.
  intros. cbn. reflexivity.
Qed.

Theorem app_nil_r :
  forall (A : Type) (l : list A), l ++ [] = l.
Proof.
  induction l as [| h t].
    cbn. reflexivity.
    cbn. rewrite IHt. reflexivity.
Qed.

(** Sposoby dowodzenia sÄ… analogiczne jak w przypadku liczb naturalnych.
    Pierwsze twierdzenie zachodzi na mocy samej definicji funkcji [app]
    i dowÃ³d sprowadza siÄ™ do wykonania programu za pomocÄ… taktyki [cbn].
    Drugie jest analogiczne do twierdzenia [plus_n_0], z tÄ… rÃ³Å¼nicÄ…, Å¼e
    w drugim celu zamiast [f_equal] posÅ‚uÅ¼yliÅ›my siÄ™ taktykÄ… [rewrite].

    ZauwaÅ¼ teÅ¼, Å¼e zmianie ulegÅ‚a postaÄ‡ wzorca przekazanego taktyce
    [induction] â€” teraz ma on postaÄ‡ [[| h t]], gdyÅ¼ [list] ma 2
    konstruktory, z ktÃ³rych pierwszy, [nil], nie bierze argumentÃ³w
    (argumenty domyÅ›lne nie sÄ… wymieniane we wzorcach), zaÅ› drugi, [cons],
    ma dwa argumenty â€” gÅ‚owÄ™, tutaj nazwanÄ… [h] (jako skrÃ³t od ang. head)
    oraz ogon, tutaj nazwany [t] (jako skrÃ³t od ang. tail). *)

(** **** Ä†wiczenie (wÅ‚aÅ›ciwoÅ›ci funkcji [app])*)

(** Udowodnij poniÅ¼sze wÅ‚aÅ›ciwoÅ›ci funkcji [app]. WskazÃ³wka: moÅ¼e ci siÄ™
    przydaÄ‡ taktyka [specialize]. *)

Theorem app_assoc :
  forall (A : Type) (l1 l2 l3 : list A),
    l1 ++ (l2 ++ l3) = (l1 ++ l2) ++ l3.
(* begin hide *)
Proof.
  induction l1 as [| h1 t1]; cbn; intros.
    reflexivity.
    rewrite <- IHt1. reflexivity.
Qed.
(* end hide *)

Theorem app_not_comm :
  ~ forall (A : Type) (l1 l2 : list A), l1 ++ l2 = l2 ++ l1.
(* begin hide *)
Proof.
  intro. specialize (H nat [0] [1]). cbn in H. inversion H.
Qed.
(* end hide *)

(** **** Ä†wiczenie ([length]) *)

(** Zdefiniuj funkcjÄ™ [length], ktÃ³ra oblicza dÅ‚ugoÅ›Ä‡ listy, a nastÄ™pnie
    udowodnij poprawnoÅ›Ä‡ swojej implementacji. *)

(* begin hide *)
Fixpoint length {A : Type} (l : list A) : nat :=
match l with
    | [] => 0
    | h :: t => S (length t)
end.
(* end hide *)

Theorem length_nil :
  forall A : Type, length (@nil A) = 0.
(* begin hide *)
Proof.
  intro. cbn. reflexivity.
Qed.
(* end hide *)

Theorem length_cons :
  forall (A : Type) (h : A) (t : list A), length (h :: t) <> 0.
(* begin hide *)
Proof.
  cbn. intros. inversion 1.
Qed.
(* end hide *)

Theorem length_app :
  forall (A : Type) (l1 l2 : list A),
    length (l1 ++ l2) = length l1 + length l2.
(* begin hide *)
Proof.
  induction l1 as [| h1 t1]; cbn; intros.
    reflexivity.
    f_equal. rewrite IHt1. reflexivity.
Qed.
(* end hide *)

End MyList.

(** ** Przydatne komendy *)

(** Czas, aby opisaÄ‡ kilka przydatnych komend. *)

Check unit.
(* ===> unit : Set *)

Print unit.
(* ===> Inductive unit : Set := tt : unit *)

(** Przypomnijmy, Å¼e komenda [Check] wyÅ›wietla typ danego jej termu,
    a [Print] wypisuje jego definicjÄ™. *)

Search nat.

(** [Search] wyÅ›wietla wszystkie obiekty, ktÃ³re zawierajÄ… podanÄ… nazwÄ™.
    W naszym przypadku pokazaÅ‚y siÄ™ wszystkie funkcje, w ktÃ³rych
    sygnaturze wystÄ™puje typ [nat]. *)

SearchAbout nat.

(** [SearchAbout] wyÅ›wietla wszystkie obiekty, ktÃ³re majÄ… jakiÅ› zwiÄ…zek
    z danÄ… nazwÄ…. Zazwyczaj wskaÅ¼e on nam duÅ¼o wiÄ™cej obiektÃ³w, niÅ¼ zwykÅ‚e
    [Search], np. poza funkcjami, w ktÃ³rych sygnaturze wystÄ™puje [nat],
    pokazuje teÅ¼ twierdzenia dotyczÄ…ce ich wÅ‚aÅ›ciwoÅ›ci. *)

SearchPattern (_ + _ = _).

(** [SearchPattern] jako argument bierze wzorzec i wyÅ›wietla wszystkie
    obiekty, ktÃ³re zawierajÄ… podterm pasujÄ…cy do danego wzorca. W naszym
    przypadku pokazaÅ‚y siÄ™ twierdzenia, w ktÃ³rych wystÄ™puje podterm
    majÄ…cy po lewej dodawanie, a po prawej cokolwiek.

    DokÅ‚adny opis wszystkich komend znajdziesz tutaj:
    https://coq.inria.fr/refman/coq-cmdindex.html *)

(** ** WaÅ¼ne typy induktywne *)

Module ImportantTypes.

(** *** Typ pusty *)

Inductive Empty_set : Set := .

(** [Empty_set] jest, jak sama nazwa wskazuje, typem pustym. Å»aden term
    nie jest tego typu. Innymi sÅ‚owy: jeÅ¼eli jakiÅ› term jest typu [Empty_set],
    to mamy sprzecznoÅ›Ä‡. *)

Definition create {A : Type} (x : Empty_set) : A :=
  match x with end.

(** JeÅ¼eli mamy term typu [Empty_set], to moÅ¼emy w sposÃ³b niemal magiczny
    wyczarowaÄ‡ term dowolnego typu [A], uÅ¼ywajÄ…c dopasowania do wzorca z
    pustym wzorcem. *)

(** **** Ä†wiczenie ([create_unique]) *)

(** Udowodnij, Å¼e powyÅ¼sza funkcja jest unikalna. *)

Theorem create_unique :
  forall (A : Type) (f : Empty_set -> A),
    (forall x : Empty_set, create x = f x).
(* begin hide *)
Proof.
  intros. destruct x.
Qed.
(* end hide *)

(** **** Ä†wiczenie ([no_fun_from_nonempty_to_empty]) *)

(** PokaÅ¼, Å¼e nie istniejÄ… funkcje z typu niepustego w pusty. *)

Theorem no_fun_from_nonempty_to_empty :
  forall (A : Type) (a : A) (f : A -> Empty_set), False.
(* begin hide *)
Proof.
  intros. specialize (f a). destruct f.
Qed.
(* end hide *)

(** *** Singleton *)

Inductive unit : Set :=
    | tt : unit.

(** [unit] jest typem, ktÃ³ry ma tylko jeden term, zwany [tt] (nazwa ta
    jest wziÄ™ta z sufitu). *)

Definition delete {A : Type} (a : A) : unit := tt.

(** Funkcja [delete] jest w pewien sposÃ³b "dualna" do napotkanej przez
    nas wczeÅ›niej funkcji [create]. MajÄ…c term typu [Empty_set] mogliÅ›my
    stworzyÄ‡ term dowolnego innego typu, zaÅ› majÄ…c term dowolnego typu
    [A], moÅ¼emy "zapomnieÄ‡ o nim" albo "skasowaÄ‡ go", wysyÅ‚ajÄ…c go
    funkcjÄ… [delete] w jedyny term typu [unit], czyli [tt].

    Uwaga: okreÅ›lenie "skasowaÄ‡" nie ma nic wspÃ³lnego z fizycznym
    niszczeniem albo dealokacjÄ… pamiÄ™ci. Jest to tylko metafora. *)

(** **** Ä†wiczenie ([delete_unique]) *)
(** PokaÅ¼, Å¼e funkcja [delete] jest unikalna. *)

Theorem delete_unique :
  forall (A : Type) (f : A -> unit),
    (forall x : A, delete x = f x).
(* begin hide *)
Proof.
  intros. destruct (delete x). destruct (f x). trivial.
Qed.
(* end hide *)

(** *** Produkt *)

Inductive prod (A B : Type) : Type :=
    | pair : A -> B -> prod A B.

Arguments pair [A] [B] _ _.

(** Produkt typÃ³w [A] i [B] to typ, ktÃ³rego termami sÄ… pary. Pierwszy
    element pary to term typu [A], a drugi to term typu [B]. Tym, co
    charakteryzuje produkt, sÄ… projekcje:
    - [fst : forall A B : Type, prod A B -> A] wyciÄ…ga z pary jej
      pierwszy element
    - [snd : forall A B : Type, prod A B -> B] wyciÄ…ga z pary jej
      drugi element *)

(** **** Ä†wiczenie (projekcje) *)

(** Zdefiniuj projekcje i udowodnij poprawnoÅ›Ä‡ swoich definicji. *)

(* begin hide *)
Definition fst {A B : Type} (p : prod A B) : A :=
match p with
    | pair a b => a
end.

Definition snd {A B : Type} (p : prod A B) : B :=
match p with
    | pair a b => b
end.
(* end hide *)

Theorem proj_spec :
  forall (A B : Type) (p : prod A B),
    p = pair (fst p) (snd p).
(* begin hide *)
Proof.
  destruct p. cbn. trivial.
Qed.
(* end hide *)

(** *** Suma *)

Inductive sum (A B : Type) : Type :=
    | inl : A -> sum A B
    | inr : B -> sum A B.

Arguments inl [A] [B] _.
Arguments inr [A] [B] _.

(** Suma [A] i [B] to typ, ktÃ³rego termy sÄ… albo termami typu [A],
    zawiniÄ™tymi w konstruktor [inl], albo termami typu [B], zawiniÄ™tymi
    w konstruktor [inr]. Suma, w przeciwieÅ„stwie do produktu, zdecydowanie
    nie ma projekcji. *)

(** **** Ä†wiczenie (sumy bez projekcji) *)

(** PokaÅ¼, Å¼e suma nie ma projekcji. *)

Theorem sum_no_fst :
  forall (proj : forall A B : Type, sum A B -> A), False.
(* begin hide *)
Proof.
  intros. apply proj with nat. apply inr. exact 0.
Qed.
(* end hide *)

Theorem sum_no_snd :
  forall (proj : forall A B : Type, sum A B -> B), False.
(* begin hide *)
Proof.
  intros. apply proj with nat. apply inl. exact 0.
Qed.
(* end hide *)

End ImportantTypes.

(** ** Kiedy typ induktywny jest pusty? *)

(** Typy puste to typy, ktÃ³re nie majÄ… Å¼adnych elementÃ³w. Z jednym z nich
    juÅ¼ siÄ™ spotkaliÅ›my â€” byÅ‚ to [Empty_set], ktÃ³ry jest pusty, gdyÅ¼ nie
    ma Å¼adnych konstruktorÃ³w. Czy wszystkie typy puste to typy, ktÃ³re
    nie majÄ… konstruktorÃ³w? *)

Inductive Empty : Type :=
    | c : Empty_set -> Empty.

Theorem Empty_is_empty :
  forall empty : Empty, False.
Proof.
  intro. destruct empty. destruct e.
Qed.

(** Okazuje siÄ™, Å¼e nie. PustoÅ›Ä‡ i niepustoÅ›Ä‡ jest kwestiÄ… czegoÅ› wiÄ™cej,
    niÅ¼ tylko iloÅ›ci konstruktorÃ³w. PowyÅ¼szy przykÅ‚ad pokazuje dobitnie,
    Å¼e waÅ¼ne sÄ… teÅ¼ typy argumentÃ³w konstruktorÃ³w. JeÅ¼eli typ ktÃ³regoÅ› z
    argumentÃ³w konstruktora jest pusty, to nie moÅ¼na uÅ¼yÄ‡ go do zrobienia
    Å¼adnego termu. JeÅ¼eli kaÅ¼dy konstruktor typu [T] ma argument, ktÃ³rego
    typ jest pusty, to sam typ [T] rÃ³wnieÅ¼ jest pusty.

    Wobec powyÅ¼szych rozwaÅ¼aÅ„ moÅ¼emy sformuÅ‚owaÄ‡ nastÄ™pujÄ…ce kryterium:
    typ [T] jest niepusty, jeÅ¼eli ma co najmniej jeden konstruktor, ktÃ³ry
    nie bierze argumentÃ³w, ktÃ³rych typy sÄ… puste. Jakkolwiek jest to bardzo
    dobre kryterium, to jednak nie rozwiewa ono niestety wszystkich moÅ¼liwych
    wÄ…tpliwoÅ›ci. *)

Inductive InfiniteList (A : Type) : Type :=
    | InfiniteCons : A -> InfiniteList A -> InfiniteList A.

(** Czy typ [InfiniteList A] jest niepusty? Skorzystajmy z naszego kryterium:
    ma on jeden konstruktor biorÄ…cy dwa argumenty, jeden typu [A] oraz drugi
    typu [InfiniteList A]. W zaleÅ¼noÅ›ci od tego, czym jest [A], moÅ¼e on byÄ‡
    pusty lub nie â€” przyjmijmy, Å¼e [A] jest niepusty. W przypadku drugiego
    argumentu napotykamy jednak na problem: to, czy [InfiniteList A] jest
    niepusty zaleÅ¼y od tego, czy typ argumentu jego konstruktora, rÃ³wnieÅ¼
    [InfiniteList A], jest niepusty. Sytuacja jest wiÄ™c beznadziejna â€” mamy
    bÅ‚Ä™dne koÅ‚o.

    PowyÅ¼szy przykÅ‚ad pokazuje, Å¼e nasze kryterium moÅ¼e nie poradziÄ‡ sobie
    z rekurencjÄ…. Jak zatem rozstrzygnÄ…Ä‡, czy typ ten jest niepusty? Musimy
    odwoÅ‚aÄ‡ siÄ™ bezpoÅ›rednio do definicji i zastanowiÄ‡ siÄ™, czy moÅ¼liwe jest
    skonstruowanie jakichÅ› jego termÃ³w. W tym celu przypomnijmy, czym sÄ… typy
    induktywne:
    - Typ induktywny to rodzaj planu, ktÃ³ry pokazuje, w jaki sposÃ³b moÅ¼na
      konstruowaÄ‡ jego termy, ktÃ³re sÄ… drzewami.
    - Konstruktory to wÄ™zÅ‚y drzewa. Ich nazwy oraz iloÅ›Ä‡ i typy argumentÃ³w
      nadajÄ… drzewu ksztaÅ‚t i znaczenie.
    - Konstruktory nierekurencyjne to liÅ›cie drzewa.
    - Konstruktory rekurencyjne to wÄ™zÅ‚y wewnÄ™trzne drzewa. *)

(** Kluczowym faktem jest rozmiar termÃ³w: o ile rozgaÅ‚Ä™zienia mogÄ… byÄ‡
    potencjalnie nieskoÅ„czone, o tyle wszystkie gaÅ‚Ä™zie muszÄ… mieÄ‡
    skoÅ„czonÄ… dÅ‚ugoÅ›Ä‡. PociÄ…ga to za sobÄ… bardzo istotny fakt: typy
    majÄ…ce jedynie konstruktory rekurencyjne sÄ… puste, gdyÅ¼ bez uÅ¼ycia
    konstruktorÃ³w nierekurencyjnych moÅ¼emy konstruowaÄ‡ jedynie drzewa
    nieskoÅ„czone (i to tylko przy nierealnym zaÅ‚oÅ¼eniu, Å¼e moÅ¼liwe jest
    zakoÅ„czenie konstrukcji liczÄ…cej sobie nieskoÅ„czonoÅ›Ä‡ krokÃ³w). *)

Theorem InfiniteList_is_empty :
  forall A : Type, InfiniteList A -> False.
Proof.
  intros A l. induction l as [h t]. exact IHt.
Qed.

(** Pokazanie, Å¼e [InfiniteList A] jest pusty, jest bardzo proste â€”
    wystarczy posÅ‚uÅ¼yÄ‡ siÄ™ indukcjÄ…. Indukcja po [l : InfiniteList A]
    daje nam hipotezÄ™ indukcyjnÄ… [IHt : False], ktÃ³rej moÅ¼emy uÅ¼yÄ‡,
    aby natychmiast zakoÅ„czyÄ‡ dowÃ³d.

    Zaraz, co wÅ‚aÅ›ciwie siÄ™ staÅ‚o? Dlaczego dostaliÅ›my zupeÅ‚nie za darmo
    hipotezÄ™ [IHt], ktÃ³ra jest szukanym przez nas dowodem? W ten wÅ‚aÅ›nie
    sposÃ³b przeprowadza siÄ™ dowody indukcyjne: zakÅ‚adamy, Å¼e hipoteza [P]
    zachodzi dla termu [t : InfiniteList A], a nastÄ™pnie musimy pokazaÄ‡,
    Å¼e [P] zachodzi takÅ¼e dla termu [InfiniteCons h t]. Zazwyczaj [P] jest
    predykatem i wykonanie kroku indukcyjnego jest nietrywialne, w naszym
    przypadku jest jednak inaczej â€” postaÄ‡ [P] jest taka sama dla [t] oraz
    dla [InfiniteCons h t] i jest niÄ… [False].

    Czy ten konfundujÄ…cy fakt nie oznacza jednak, Å¼e [list A], czyli typ
    zwykÅ‚ych list, rÃ³wnieÅ¼ jest pusty? SprÃ³bujmy pokazaÄ‡, Å¼e tak jest. *)

Theorem list_empty :
  forall (A : Type), list A -> False.
Proof.
  intros A l. induction l as [| h t].
    Focus 2. exact IHt.
Abort.

(** Pokazanie, Å¼e typ [list A] jest pusty, jest rzecz jasna niemoÅ¼liwe,
    gdyÅ¼ typ ten zdecydowanie pusty nie jest â€” w jego definicji stoi
    jak byk napisane, Å¼e dla dowolnego typu [A] istnieje lista termÃ³w
    typu [A]. Jest niÄ… oczywiÅ›cie [@nil A].

    Przyjrzyjmy siÄ™ naszej prÃ³bie dowodu. PrÃ³bujemy posÅ‚uÅ¼yÄ‡ siÄ™ indukcjÄ…
    w ten sam sposÃ³b co poprzednio. Taktyka [induction] generuje nam dwa
    podcele, gdyÅ¼ [list] ma dwa konstruktory â€” pierwszy podcel dla [nil],
    a drugi dla [cons]. Komenda [Focus] pozwala nam przeÅ‚Ä…czyÄ‡ siÄ™ do
    wybranego celu, w tym przypadku celu nr 2, czyli gdy [l] jest postaci
    [cons h t].

    Sprawa wyglÄ…da identycznie jak poprzednio â€” za darmo dostajemy hipotezÄ™
    [IHt : False], ktÃ³rej uÅ¼ywamy do natychmiastowego rozwiÄ…zania naszego
    celu. Tym, co stanowi przeszkodÄ™ nie do pokonania, jest cel nr 1, czyli
    gdy [l] zrobiono za pomocÄ… konstruktora [nil]. Ten konstruktor nie jest
    rekurencyjny, wiÄ™c nie dostajemy Å¼adnej hipotezy indukcyjnej. Lista [l]
    zostaje w kaÅ¼dym miejscu, w ktÃ³rym wystÄ™puje, zastÄ…piona przez [[]], a
    poniewaÅ¼ nie wystÄ™puje nigdzie â€” znika. Musimy teraz udowodniÄ‡ faÅ‚sz
    wiedzÄ…c jedynie, Å¼e [A] jest typem, co jest niemoÅ¼liwe. *)

(** * Induktywne zdania i predykaty *)

(** Wiemy, Å¼e sÅ‚owo kluczowe [Inductive] pozwala nam definiowaÄ‡ nowe typy
    (a nawet rodziny typÃ³w, jak w przypadku [option]). Wiemy teÅ¼, Å¼e zdania
    sÄ… typami. Wobec tego nie powinno nas dziwiÄ‡, Å¼e induktywnie moÅ¼emy
    definiowaÄ‡ takÅ¼e zdania, spÃ³jniki logiczne, predykaty oraz relacje. *)

(** ** Induktywne zdania *)

Inductive false_prop : Prop := .

Inductive true_prop : Prop :=
    | obvious_proof : true_prop
    | tricky_proof : true_prop
    | weird_proof : true_prop
    | magical_proof : true_prop.

(** Induktywne definicje zdaÅ„ nie sÄ… zbyt ciekawe, gdyÅ¼ pozwalajÄ… definiowaÄ‡
    jedynie zdania faÅ‚szywe (zero konstruktorÃ³w) lub prawdziwe (jeden lub
    wiÄ™cej konstruktorÃ³w). Pierwsze z naszych zdaÅ„ jest faÅ‚szywe (a wiÄ™c
    rÃ³nowaÅ¼ne z [False]), drugie zaÅ› jest prawdziwe (czyli rÃ³wnowaÅ¼ne z [True])
    i to na cztery sposoby! *)

(** **** Ä†wiczenie (induktywne zdania) *)

Theorem false_prop_iff_False : false_prop <-> False.
(* begin hide *)
Proof.
  split; inversion 1.
Qed.
(* end hide *)

Theorem true_prop_iff_True : true_prop <-> True.
(* begin hide *)
Proof.
  split; inversion 1; constructor.
Qed.
(* end hide *)

(** ** Induktywne predykaty *)

(** Przypomnijmy, Å¼e predykaty to funkcje, ktÃ³rych przeciwdziedzinÄ… jest
    sort [Prop], czyli funkcje zwracajÄ…ce zdania logiczne. Predykat
    [P : A -> Prop] moÅ¼na rozumieÄ‡ jako wÅ‚aÅ›ciwoÅ›Ä‡, ktÃ³rÄ… mogÄ… posiadaÄ‡
    termy typu [A], zaÅ› dla konkretnego [x : A] zapis [P x] interpretowaÄ‡
    moÅ¼na "term [x] posiada wÅ‚aÅ›ciwÃ³Å›Ä‡ [P]".

    O ile istniejÄ… tylko dwa rodzaje induktwynych zdaÅ„ (prawdziwe i faÅ‚szywe),
    o tyle induktywnie zdefiniowane predykaty sÄ… duÅ¼o bardziej ciekawe i
    uÅ¼yteczne, gdyÅ¼ dla jednych termÃ³w mogÄ… byÄ‡ prawdziwe, a dla innych nie. *)

Inductive even : nat -> Prop :=
    | even0 : even 0
    | evenSS : forall n : nat, even n -> even (S (S n)).

(** Predykat [even] ma oznaczaÄ‡ wÅ‚aÅ›ciwoÅ›Ä‡ "bycia liczbÄ… parzystÄ…". Jego
    definicjÄ™ moÅ¼na zinterpretowaÄ‡ tak:
    - "[0] jest liczbÄ… przystÄ…"
    - "jeÅ¼eli [n] jest liczbÄ… parzystÄ…, to [n + 2] rÃ³wnieÅ¼ jest
       liczbÄ… parzystÄ…" *)

(** Jak widaÄ‡, induktywna definicja parzystoÅ›ci rÃ³Å¼ni siÄ™ od powszechnie
    uÅ¼ywanej definicji, ktÃ³ra gÅ‚osi, Å¼e "liczba jest parzysta, gdy
    dzieli siÄ™ bez reszty przez 2". RÃ³Å¼nica jest natury filozoficznej:
    definicja induktywna mÃ³wi, jak konstruowaÄ‡ liczby parzyste, podczas
    gdy druga, "klasyczna" definicja mÃ³wi, jak sprawdziÄ‡, czy liczba
    jest parzysta.

    Przez wzglÄ…d na swÄ… konstruktywnoÅ›Ä‡, w Coqu induktywne definicje
    predykatÃ³w czy relacji sÄ… czÄ™sto duÅ¼o bardziej uÅ¼yteczne od tych
    nieinduktywnych, choÄ‡ nie wszystko moÅ¼na zdefiniowaÄ‡ induktywnie. *)

Theorem zero_is_even : even 0.
Proof.
  apply even0.
Qed.

(** Jak moÅ¼emy udowodniÄ‡, Å¼e [0] jest liczbÄ… parzystÄ…? PosÅ‚uÅ¼y nam
    do tego konstruktor [even0], ktÃ³ry wprost gÅ‚osi, Å¼e [even 0].
    Nie daj siÄ™ zwieÅ›Ä‡: [even0], pisane bez spacji, jest nazwÄ…
    konstruktora, podczas gdy [even 0], ze spacjÄ…, jest zdaniem
    (czyli termem typu [Prop]), ktÃ³re moÅ¼na interpretowaÄ‡ jako
    "[0] jest liczbÄ… parzystÄ…". *)

Theorem two_is_even : even 2.
Proof.
  apply evenSS. apply even0.
Qed.

(** Jak moÅ¼emy udowodniÄ‡, Å¼e [2] jest parzyste? Konstruktor [even0]
    nam nie pomoÅ¼e, gdyÅ¼ jego postaÄ‡ ([even 0]) nie pasuje do postaci
    naszego twierdzenia ([even 2]). Pozostaje nam jednak konstruktor
    [evenSS].

    JeÅ¼eli przypomnimy sobie, Å¼e [2] to tak naprawdÄ™ [S (S 0)],
    natychmiast dostrzeÅ¼emy, Å¼e jego konkluzja pasuje do postaci naszego
    twierdzenia. MoÅ¼emy go wiÄ™c zaaplikowaÄ‡ (pamiÄ™taj, Å¼e konstruktory sÄ…
    jak zwykÅ‚e funkcje, tylko Å¼e niczego nie obliczajÄ… â€” nadajÄ… one typom
    ich ksztaÅ‚ty). Teraz wystarczy pokazaÄ‡, Å¼e [even 0] zachodzi, co juÅ¼
    potrafimy. *)

Theorem four_is_even : even 4.
Proof.
  constructor. constructor. constructor.
Qed.

(** Jak pokazaÄ‡, Å¼e [4] jest parzyste? TÄ… samÄ… metodÄ…, ktÃ³ra pokazaliÅ›my,
    Å¼e [2] jest parzyste. [4] to [S (S (S (S 0)))], wiÄ™c moÅ¼emy uÅ¼yÄ‡
    konstruktora [evenSS]. Zamiast jednak pisaÄ‡ [apply evenSS], moÅ¼emy
    uÅ¼yÄ‡ taktyki [constructor]. Taktyka ta dziaÅ‚a na celach, w ktÃ³rych
    chcemy skonstruowaÄ‡ wartoÅ›Ä‡ jakiegoÅ› typu induktywnego (a wiÄ™c takÅ¼e
    gdy dowodzimy twierdzeÅ„ o induktywnych predykatach). Szuka ona
    konstruktora, ktÃ³ry moÅ¼e zaaplikowaÄ‡ na celu, i jeÅ¼eli znajdzie, to
    aplikuje go, a gdy nie â€” zawodzi.

    W naszym przypadku pierwsze dwa uÅ¼ycia [constructor] aplikujÄ…
    konstruktor [evenSS], a trzecie â€” konstruktor [even0]. *)

Theorem the_answer_is_even : even 42.
Proof.
  repeat constructor.
Qed.

(** A co, gdy chcemy pokazaÄ‡, Å¼e [42] jest parzyste? Czy musimy 22 razy
    napisaÄ‡ [constructor]? Na szczÄ™Å›cie nie â€” wystarczy posÅ‚uÅ¼yÄ‡ siÄ™
    kombinatorem [repeat] (jeÅ¼eli nie pamiÄ™tasz, jak dziaÅ‚a, zajrzyj do
    rozdziaÅ‚u 1). *)

Theorem one_not_even_failed : ~ even 1.
Proof.
  unfold not. intro. destruct H.
Abort.

Theorem one_not_even : ~ even 1.
Proof.
  unfold not. intro. inversion H.
Qed.

(** A jak pokazaÄ‡, Å¼e [1] nie jest parzyste? MajÄ…c w kontekÅ›cie dowÃ³d
    na to, Å¼e [1] jest parzyste ([H : even 1]), moÅ¼emy zastantowiÄ‡ siÄ™,
    w jaki sposÃ³b dowÃ³d ten zostaÅ‚ zrobiony. Nie mÃ³gÅ‚ zostaÄ‡ zrobiony
    konstruktorem [even0], gdyÅ¼ ten dowodzi, Å¼e [0] jest parzyste, a
    przecieÅ¼ przekonaliÅ›my siÄ™ juÅ¼, Å¼e [0] to nie [1]. Nie mÃ³gÅ‚ teÅ¼
    zostaÄ‡ zrobiony konstruktorem [evenSS], gdyÅ¼ ten ma w konkluzji
    [even (S (S n))], podczas gdy [1] jest postaci [S 0] â€” nie pasuje
    on do konkluzji [evenSS], gdyÅ¼ "ma za maÅ‚o [S]Ã³w".

    Nasze rozumowanie prowadzi do wniosku, Å¼e za pomocÄ… [even0] i [evenSS],
    ktÃ³re sÄ… jedynymi konstruktorami [even], nie moÅ¼na skonstruowaÄ‡ [even 1],
    wiÄ™c [1] nie moÅ¼e byÄ‡ parzyste. Na podstawie wczeÅ›niejszych doÅ›wiadczeÅ„
    mogÅ‚oby siÄ™ nam wydawaÄ‡, Å¼e [destruct] zaÅ‚atwi sprawÄ™, jednak tak nie
    jest â€” taktyka ta jest w tym przypadku upoÅ›ledzona i nie potrafi nam
    pomÃ³c. Zamiast tego moÅ¼emy siÄ™ posÅ‚uÅ¼yÄ‡ taktykÄ… [inversion]. DziaÅ‚a ona
    dokÅ‚adnie w sposÃ³b opisany w poprzednim akapicie. *)

Theorem three_not_even : ~ even 3.
Proof.
  intro. inversion H. inversion H1.
Qed.

(** Jak pokazaÄ‡, Å¼e [3] nie jest parzyste? PomoÅ¼e nam w tym, jak poprzednio,
    inwersja. Tym razem jednak nie zaÅ‚atwia ona sprawy od razu. JeÅ¼eli
    zastanowimy siÄ™, jak moÅ¼na pokazaÄ‡ [even 3], to dojdziemy do wniosku,
    Å¼e moÅ¼na to zrobiÄ‡ konstruktorem [evenSS], gdyÅ¼ [3] to tak naprawdÄ™
    [S (S 1)]. To wÅ‚aÅ›nie robi pierwsza inwersja: mÃ³wi nam, Å¼e [H : even 3]
    moÅ¼na uzyskaÄ‡ z zaaplikowania [evenSS] do [1], jeÅ¼eli tylko mamy dowÃ³d
    [H1 : even 1] na to, Å¼e [1] jest parzyste. Jak pokazaÄ‡, Å¼e [1] nie
    jest parzyste, juÅ¼ wiemy. *)

(** **** Ä†wiczenie (odd) *)

(** Zdefiniuj induktywny predykat [odd], ktÃ³ry ma oznaczaÄ‡ "bycie liczbÄ…
    nieparzystÄ…" i udowodnij, Å¼e zachowuje siÄ™ on jak naleÅ¼y. *)

(* begin hide *)
Inductive odd : nat -> Prop :=
    | odd1 : odd 1
    | oddSS : forall n : nat, odd n -> odd (S (S n)).
(* end hide *)

Theorem one_odd : odd 1.
(* begin hide *)
Proof.
  constructor.
Qed.
(* end hide *)

Theorem seven_odd : odd 7.
(* begin hide *)
Proof.
  repeat constructor.
Qed.
(* end hide *)

Theorem zero_not_odd : ~ odd 0.
(* begin hide *)
Proof.
  inversion 1.
Qed.
(* end hide *)

Theorem two_not_odd : ~ odd 2.
(* begin hide *)
Proof.
  inversion 1. inversion H1.
Qed.
(* end hide *)

(** ** Indukcja po dowodzie *)

Require Import Arith.

(** Biblioteka [Arith] zawiera rÃ³Å¼ne definicje i twierdzenia dotyczÄ…ce
    arytmetyki. BÄ™dzie nam ona potrzebna w tym podrozdziale.

    Jak udowodniÄ‡, Å¼e suma liczb parzystych jest parzysta? ByÄ‡ moÅ¼e
    wÅ‚aÅ›nie pomyÅ›laÅ‚eÅ› o indukcji. SprÃ³bujmy zatem: *)

Theorem even_sum_failed1 :
  forall n m : nat, even n -> even m -> even (n + m).
Proof.
  induction n as [| n']; cbn; intros.
    trivial.
    induction m as [| m']; rewrite plus_comm; cbn; intros.
      assumption.
      constructor. rewrite plus_comm. apply IHn'.
Abort.

(** PrÃ³bujÄ…c jednak indukcji po [n], a potem po [m], docieramy do martwego
    punktu. Musimy udowodniÄ‡ [even n'], podczas gdy zachodzi [even (S n')]
    (czyli [even n'] jest faÅ‚szywe). Wynika to z faktu, Å¼e przy indukcji
    [n] zwiÄ™ksza siÄ™ o 1 ([P n -> P (S n)]), podczas gdy w definicji
    [even] mamy konstruktor gÅ‚oszÄ…cy, Å¼e ([even n -> even (S (S n))]).

    ByÄ‡ moÅ¼e w drugiej kolejnoÅ›ci pomyÅ›laÅ‚eÅ› o taktyce [destruct]: jeÅ¼eli
    sprawdzimy, w jaki sposÃ³b udowodniono [even n], to przy okazji dowiemy
    siÄ™ teÅ¼, Å¼e [n] moÅ¼e byÄ‡ jedynie postaci [0] lub [S (S n')]. DziÄ™ki
    temu powinniÅ›my uniknÄ…Ä‡ problemu z poprzedniej prÃ³by. *)

Theorem even_sum_failed2 :
  forall n m : nat, even n -> even m -> even (n + m).
Proof.
  intros n m Hn Hm. destruct Hn, Hm; cbn.
    constructor.
    constructor. assumption.
    rewrite plus_comm. cbn. constructor. assumption.
    rewrite plus_comm. cbn. do 2 constructor.
Abort.

(** Niestety, taktyka [destruct] okazaÅ‚a siÄ™ za sÅ‚aba. Predykat [even] jest
    induktywny, a zatem bez indukcji siÄ™ nie obÄ™dzie. RozwiÄ…zaniem naszych
    problemÃ³w nie bÄ™dzie jednak indukcja po [n] lub [m], lecz po dowodzie na
    to, Å¼e [n] jest parzyste. *)

Theorem even_sum :
  forall n m : nat, even n -> even m -> even (n + m).
Proof.
  intros n m Hn Hm. induction Hn as [| n' Hn'].
    cbn. assumption.
    cbn. constructor. assumption.
Qed.

(** Indukcja po dowodzie dziaÅ‚a dokÅ‚adnie tak samo, jak indukcja, z ktÃ³rÄ…
    zetknÄ™liÅ›my siÄ™ dotychczas. RÃ³Å¼ni siÄ™ od niej jedynie tym, Å¼e aÅ¼ do
    teraz robiliÅ›my indukcjÄ™ jedynie po termach, ktÃ³rych typy byÅ‚y sortu
    [Set] lub [Type]. Indukcja po dowodzie to indukcja po termie, ktÃ³rego
    typ jest sortu [Prop].

    W naszym przypadku uÅ¼ycie [induction Hn] ma nastÄ™pujÄ…cy skutek:
    - W pierwszym przypadku [Hn] to po prostu konstruktor [even0], a 
      zatem [n] jest zerem.
    - W drugim przypadku [Hn] to [evenSS n' Hn'], gdzie [n] jest postaci
      [S (S n')], zaÅ› [Hn'] jest dowodem na to, Å¼e [n'] jest parzyste. *)

(** *** Taktyki [replace] i [assert]. *)

(** Przy nastÄ™pnych Ä‡wiczeniach mogÄ… przydaÄ‡ ci siÄ™ taktyki [replace]
    oraz [assert]. *)

Theorem stupid_example_replace :
  forall n : nat, n + 0 = n.
Proof.
  intro. replace (n + 0) with (0 + n).
    trivial.
    apply plus_comm.
Qed.

(** Taktyka [replace t with t'] pozwala nam zastÄ…piÄ‡ w celu kaÅ¼de
    wystÄ…pienie termu [t] termem [t']. JeÅ¼eli [t] nie ma w celu, to
    taktyka zawodzi, a w przeciwnym wypadku dodaje nam jeden podcel,
    w ktÃ³rym musimy udowodniÄ‡, Å¼e [t = t']. MoÅ¼na teÅ¼ zastosowaÄ‡ jÄ…
    w hipotezie, piszÄ…c [replace t with t' in H]. *)

Theorem stupid_example_assert :
  forall n : nat, n + 0 + 0 = n.
Proof.
  intro. assert (H : n + 0 = n).
    apply plus_0_r.
    do 2 rewrite H. trivial.
Qed.

(** Taktyka [assert (x : A)] dodaje do kontekstu term [x] typu [A] oraz
    generuje jeden dodatkowy podcel, w ktÃ³rym musimy skonstruowaÄ‡ [x].
    Zawodzi ona, jeÅ¼eli nazwa [x] jest juÅ¼ zajÄ™ta. *)

(** **** Ä†wiczenie (wÅ‚aÅ›ciwoÅ›ci [even]) *)

(** Udowodnij poniÅ¼sze twierdzenia. Zanim zaczniesz, zastanÃ³w siÄ™, po czym
    naleÅ¼y przeprowadziÄ‡ indukcjÄ™: po wartoÅ›ci, czy po dowodzie? *)

Theorem double_is_even :
  forall n : nat, even (2 * n).
(* begin hide *)
Proof.
  induction n as [| n']; cbn in *.
    constructor.
    rewrite <- plus_n_O in *. rewrite plus_comm. cbn.
      constructor. assumption.
Qed.
(* end hide *)

Theorem even_is_double :
  forall n : nat, even n -> exists k : nat, n = 2 * k.
(* begin hide *)
Proof.
  induction 1.
    exists 0. trivial.
    destruct IHeven. exists (S x). cbn in *. rewrite <- plus_n_O in *.
      rewrite plus_comm. cbn. do 2 f_equal. assumption.
Qed.
(* end hide *)

(** ** Definicje staÅ‚ych i spÃ³jnikÃ³w logicznych *)

(** W rozdziale pierwszym dowiedzieliÅ›my siÄ™, Å¼e produkt zaleÅ¼ny (typ,
    ktÃ³rego termami sÄ… funkcje zaleÅ¼ne), a wiÄ™c i implikacja, jest
    typem podstawowym/wbudowanym oraz Å¼e negacja jest zdefiniowana jako
    implikowanie faÅ‚szu. Teraz, gdy wiemy juÅ¼ co nieco o typach induktywnych,
    nadszedÅ‚ czas by zapoznaÄ‡ siÄ™ z definicjami spÃ³jnikÃ³w logicznych (i nie
    tylko). *)

Module MyConnectives.

(** *** Prawda i faÅ‚sz *)

Inductive False : Prop := .

(** FaÅ‚sz nie ma Å¼adnych konstruktorÃ³w, a zatem nie moÅ¼e zostaÄ‡ w Å¼aden
    sposÃ³b skonstruowany, czyli udowodniony. Jego definicja jest bliÅºniaczo
    podobna do czegoÅ›, co juÅ¼ kiedyÅ› widzieliÅ›my â€” tym czymÅ› byÅ‚ [Empty_set],
    czyli typ pusty. Nie jest to wcale przypadek. NatknÄ™liÅ›my siÄ™ (znowu) na
    przykÅ‚ad korespondencji Curry'ego-Howarda.

    Przypomnijmy, Å¼e gÅ‚osi ona (w sporym uproszczeniu), iÅ¼ sorty [Prop]
    i [Set]/[Type] sÄ… do siebie bardzo podobne. Jednym z tych podobieÅ„stw
    byÅ‚o to, Å¼e dowody implikacji sÄ… funkcjami. Kolejnym jest fakt, Å¼e
    [False] jest odpowiednikiem [Empty_set], od ktÃ³rego rÃ³Å¼ni siÄ™ tym, Å¼e
    Å¼yje w [Prop], a nie w [Set].

    Ta definicja rzuca teÅ¼ trochÄ™ Å›wiatÅ‚a na sposÃ³b wnioskowania "ex falso
    quodlibet" (z faÅ‚szu wynika wszystko), ktÃ³ry poznaliÅ›my w rozdziale
    pierwszym.

    UÅ¼ycie taktyki [destruct] lub [inversion] na termie dowolnego typu
    induktywnego to sprawdzenie, ktÃ³rym konstruktorem term ten zostaÅ‚
    zrobiony â€” generujÄ… one dokÅ‚adnie tyle podcelÃ³w, ile jest moÅ¼liwych
    konstruktorÃ³w. UÅ¼ycie ich na termie typu [False] generuje zero
    podcelÃ³w, co ma efekt natychmiastowego zakoÅ„czenia dowodu. DziÄ™ki
    temu majÄ…c dowÃ³d [False] moÅ¼emy udowodniÄ‡ cokolwiek. *)

Inductive True : Prop :=
    | I : True.

(** [True] jest odpowiednikiem [unit], od ktÃ³rego rÃ³Å¼ni siÄ™ tym, Å¼e Å¼yje
    w [Prop], a nie w [Set]. Ma dokÅ‚adnie jeden dowÃ³d, ktÃ³ry w Coqu
    nazwano, z zupeÅ‚nie nieznanych powodÃ³w (zapewne dla hecy), [I]. *)

(** *** Koniunkcja i dysjunkcja *)

Inductive and (P Q : Prop) : Prop :=
    | conj : P -> Q -> and P Q.

(** DowÃ³d koniunkcji zdaÅ„ [P] i [Q] to para dowodÃ³w: pierwszy element
    pary jest dowodem [P], zaÅ› drugi dowodem [Q]. Koniunkcja jest
    odpowiednkiem produktu, od ktÃ³rego rÃ³Å¼ni siÄ™ tym, Å¼e Å¼yje w [Prop],
    a nie w [Type]. *)

Inductive or (P Q : Prop) : Prop :=
    | or_introl : P -> or P Q
    | or_intror : Q -> or P Q.

(** DowÃ³d dysjunkcji zdaÅ„ [P] i [Q] to dowÃ³d [P] albo dowÃ³d [Q] wraz ze
    wskazaniem, ktÃ³rego zdania jest to dowÃ³d. Dysjunkcja jest odpowiednikiem
    sumy, od ktÃ³rej rÃ³Å¼ni siÄ™ tym, Å¼e Å¼yje w [Prop], a nie w [Type]. *)

End MyConnectives.

(** ** RÃ³wnoÅ›Ä‡ *)

Module MyEq.

(** Czym jest rÃ³wnoÅ›Ä‡? To pytanie stawiaÅ‚o sobie wielu filozofÃ³w,
    szczegÃ³lnie politycznych, zaÅ› wyjÄ…tkowo rzadko nad tÄ… sprawÄ…
    zastanawiali siÄ™ sami bojownicy o rÃ³wnoÅ›Ä‡, tak jakby wszystko
    dokÅ‚adnie wiedzieli. OdpowiedÅº na nie jest jednym z najwiÄ™kszych
    osiÄ…gniÄ™Ä‡ matematyki w dziejach: rÃ³wnoÅ›Ä‡ to jeden z typÃ³w induktywnych,
    ktÃ³re moÅ¼emy zdefiniowaÄ‡ w Coqu. *)

Inductive eq {A : Type} (x : A) : A -> Prop :=
    | eq_refl : eq x x.

(** SprÃ³bujmy przeczytaÄ‡ tÄ™ definicjÄ™: dla danego typu [A] oraz termu
    [x] tego typu, [eq x] jest predykatem, ktÃ³ry ma jeden konstruktor
    gÅ‚oszÄ…cy, Å¼e [eq x x] zachodzi. ChoÄ‡ definicja taka brzmi obco i
    dziwacznie, ma ona swoje uzasadnienie (ktÃ³re niestety poznamy
    dopiero w przyszÅ‚oÅ›ci). *)

Theorem eq_refl_trivial : eq 42 42.
Proof.
  apply eq_refl.
Qed.

(** Poznane przez nas dotychczas taktyki potrafiÄ…ce udowadniaÄ‡ proste
    rÃ³wnoÅ›ci, jak [trivial] czy [reflexivity] dziaÅ‚ajÄ… w ten sposÃ³b,
    Å¼e po prostu aplikujÄ… na celu [eq_refl]. Nazwa [eq_refl] to skrÃ³t
    od ang. "reflexivity of equality", czyli "zwrotnoÅ›Ä‡ rÃ³wnoÅ›ci" â€”
    jest to najwaÅ¼niejsza cecha rÃ³wnoÅ›ci, ktÃ³ra oznacza, Å¼e kaÅ¼dy term
    jest rÃ³wny samemu sobie. *)

Theorem eq_refl_nontrivial : eq (1 + 41) 42.
Proof.
  constructor.
Qed.

(** MogÅ‚oby wydawaÄ‡ siÄ™, Å¼e zwrotnoÅ›Ä‡ nie wystarcza do udowadniania
    "nietrywialnych" rÃ³wnoÅ›ci pokroju [1 + 41 = 42], jednak tak nie jest.
    Dlaczego [eq_refl] odnosi na tym celu sukces skoro [1 + 41] oraz [42]
    zdecydowanie rÃ³Å¼niÄ… siÄ™ postaciÄ…? OdpowiedÅº jest prosta: typ [eq] w
    rzeczywistoÅ›ci owija jedynie rÃ³wnoÅ›Ä‡ pierwotnÄ…, wbudowanÄ… w samo jÄ…dro
    Coqa, ktÃ³rÄ… jest konwertowalnoÅ›Ä‡. *)

Theorem eq_refl_alpha :
  forall A : Type, eq (fun x : A => x) (fun y : A => y).
Proof.
  intro. change (fun x : A => x) with (fun y : A => y).
  apply eq_refl.
Qed.

Theorem eq_refl_beta :
  forall m : nat, eq ((fun n : nat => n + n) m) (m + m).
Proof.
  intro. cbn. apply eq_refl.
Qed.

Definition ultimate_answer : nat := 42.

Theorem eq_refl_delta : eq ultimate_answer 42.
Proof.
  unfold ultimate_answer. apply eq_refl.
Qed.

Theorem eq_refl_iota :
  eq 42 (match 0 with | 0 => 42 | _ => 13 end).
Proof.
  cbn. apply eq_refl.
Qed.

Theorem eq_refl_zeta :
  let n := 42 in eq n 42.
Proof.
  reflexivity.
Qed.

(** Przypomnijmy, co juÅ¼ wiemy o redukcjach:
    - konwersja alfa pozwala nam zmieniÄ‡ nazwÄ™ zmiennej zwiÄ…zanej w
      funkcji anonimowej nowÄ…, jeÅ¼eli ta nie jest jeszcze uÅ¼ywana.
      W naszym przykÅ‚adzie zamieniamy [x] w [fun x : A => x] na [y],
      otrzymujÄ…c [fun y : A => y] â€” konwersja jest legalna. Jednak
      w funkcji [fun x y : nat => x + x] nie moÅ¼emy uÅ¼yÄ‡ konwersji
      alfa, Å¼eby zmieniÄ‡ nazwÄ™ [x] na [y], bo [y] jest juÅ¼ uÅ¼ywana
      (tak nazywa siÄ™ drugi argument).
    - Redukcja beta zastÄ™puje argumentem kaÅ¼de wystÄ…pienie zmiennej
      zwiÄ…zanej w funkcji anonimowej. W naszym przypadku redukcja
      ta zamienia [(fun n : nat => n + n) m] na [m + m] â€” w miejsce
      [n] wstawiamy [m].
    - Redukcja delta odwija definicje. W naszym przypadku zdefiniowaliÅ›my,
      Å¼e [ultimate_answer] oznacza [42], wiÄ™c redukcja delta w miejsce
      [ultimate_answer] wstawia [42].
    - Redukcja jota wykonuje dopasowanie do wzorca. W naszym przypadku [0]
      jest termem, ktÃ³ry postaÄ‡ jest znana (zostaÅ‚ on skonstruowany
      konstruktorem [0]) i ktÃ³ry pasuje do wzorca [| 0 => 42], a zatem
      redukcja jota zamienia caÅ‚e wyraÅ¼enie od [match] aÅ¼ do [end]
      na [42].
    - Redukcja zeta odwija lokalnÄ… definicjÄ™ poczynionÄ… za pomocÄ… [let]a *)

(** Termy [x] i [y] sÄ… konwertowalne, gdy za pomocÄ… serii konwersji alfa
    oraz redukcji beta, delta, jota i zeta oba redukujÄ… siÄ™ do tego samego
    termu (ktÃ³ry dziÄ™ki silnej normalizacji istnieje i jest w postaci
    kanonicznej).

    UwaÅ¼ny czytelnik zada sobie w tym momencie pytanie: skoro rÃ³wnoÅ›Ä‡ to
    konwertowalnoÅ›Ä‡, to jakim cudem rÃ³wne sÄ… termy [0 + n] oraz [n + 0],
    gdzie [n] jest zmiennÄ…, ktÃ³re przecieÅ¼ nie sÄ… konwertowalne?

    Trzeba tutaj dokonaÄ‡ pewnego doprecyzowania. Termy [0 + n] i [n + 0] sÄ…
    konwertowalne dla kaÅ¼dego konkretnego [n], np. [0 + 42] i [42 + 0] sÄ…
    konwertowalne. Konwertowalne nie sÄ… natomiast, gdy [n] jest zmiennÄ… -
    jest tak dlatego, Å¼e nie moÅ¼emy wykonaÄ‡ redukcji iota, bo nie wiemy, czy
    [n] jest zerem czy nastÄ™pnikiem.

    OdpowiedziÄ… na pytanie sÄ… reguÅ‚y eliminacji, gÅ‚Ã³wnie dla typÃ³w
    induktywnych. ReguÅ‚y te majÄ… konkluzje postaci [forall x : I, P x],
    wiÄ™c w szczegÃ³lnoÅ›ci moÅ¼emy uÅ¼yÄ‡ ich dla [P x := x = y] dla jakiegoÅ›
    [y : A]. DziÄ™ki nim przeprowadzaliÅ›my juÅ¼ wielokrotnie mniej wiÄ™cej
    takie rozumowania: [n] jest wprawdzie nie wiadomo czym, ale przez
    indukcjÄ™ moÅ¼e to byÄ‡ albo [0], albo [S n'], gdzie dla [n'] zachodzi
    odpowiednia hipoteza indukcyjna. *)

End MyEq.

(** ** Indukcja wzajemna *)

(** Jest jeszcze jeden rodzaj indukcji, o ktÃ³rym dotychczas nie mÃ³wiliÅ›my:
    indukcja wzajemna (ang. mutual induction). Bez zbÄ™dnego teoretyzowania
    zbadajmy sprawÄ™ na przykÅ‚adzie klasykÃ³w polskiej literatury: *)

(** _Smok to wysuszony zmok_ *)

(** _Zmok to zmoczony smok_ *)

(** StanisÅ‚aw Lem *)

(** Idea stojÄ…ca za indukcjÄ… wzajemnÄ… jest prosta: chcemy przez indukcjÄ™
    zdefiniowaÄ‡ jednoczeÅ›nie dwa obiekty, ktÃ³re mogÄ… siÄ™ nawzajem do siebie
    odwoÅ‚ywaÄ‡.

    W owym definiowaniu nie mamy rzecz jasna peÅ‚nej swobody â€” obowiÄ…zujÄ… te
    same kryteria co w przypadku zwykÅ‚ych, "pojedynczych" definicji typÃ³w
    induktywnych. Wobec tego zauwaÅ¼yÄ‡ naleÅ¼y, Å¼e definicja sÅ‚owa "smok"
    podana przez Lema jest wedÅ‚ug Coqowych standardÃ³w nieakceptowalna, gdyÅ¼
    jeÅ¼eli w definicji _smoka_ rozwiniemy definicjÄ™ _zmoka_, to otrzymamy

    _Smok ty wysuszony zmoczony smok_ *)

(** WidaÄ‡ goÅ‚ym okiem, iÅ¼ prÃ³ba zredukowania (czyli obliczenia) obiektu
    _smok_ nigdy siÄ™ nie skoÅ„czy. Jak juÅ¼ wiemy, niekoÅ„czÄ…ce siÄ™ obliczenia
    w logice odpowiadajÄ… sprzecznoÅ›ci, a zatem ani _smoki_, ani _zmoki_ w
    Coqowym Å›wiecie nie istniejÄ….

    Nie znaczy to bynajmniej, Å¼e wszystkie definicje przez indukcjÄ™
    wzajemnÄ… sÄ… w Coqu niepoprawne, choÄ‡ naleÅ¼y przyznaÄ‡, Å¼e sÄ… doÅ›Ä‡
    rzadko uÅ¼ywane. Czas jednak abyÅ›my ujrzeli pierwszy prawdziwy przkÅ‚ad
    indukcji wzajemnej. *)

Module MutInd.

Inductive even : nat -> Prop :=
    | even0 : even 0
    | evenS : forall n : nat, odd n -> even (S n)

with odd : nat -> Prop :=
    | oddS : forall n : nat, even n -> odd (S n).

(** Aby zrozumieÄ‡ tÄ™ definicjÄ™, zestawmy jÄ… z naszÄ… definicjÄ… parzystoÅ›ci
    z sekcji _Induktywne predykaty_.

    ZdefiniowaliÅ›my tam predykat bycia liczbÄ… parzystÄ… tak:
    - [0] jest parzyste
    - jeÅ¼eli [n] jest parzyste, to [n + 2] teÅ¼ jest parzyste *)

(** Tym razem jednak nie definiujemy jedynie predykatu "jest liczbÄ… parzystÄ…".
    Definiujemy jednoczeÅ›nie dwa predykaty: "jest liczbÄ… parzystÄ…" oraz
    "jest liczbÄ… nieparzystÄ…", ktÃ³re odwoÅ‚ujÄ… siÄ™ do siebi nawzajm. Definicja
    brzmi tak:
    - [0] jest parzyste
    - jeÅ¼eli [n] jest nieparzyste, to [n + 1] jest parzyste
    - jeÅ¼eli [n] jest parzyste, to [n + 1] jest nieparzyste *)

(** Czy definicja taka rzeczywiÅ›cie ma sens? SprawdÅºmy to:
    - [0] jest parzyste na mocy definicji
    - jeÅ¼eli [0] jest parzyste (a jest), to [1] jest nieparzyste
    - jeÅ¼eli [1] jest nieparzyste (a jest), to [2] jest parzyste
    - i tak dalej, ad infinitum
*)

(** Jak widaÄ‡, za pomocÄ… naszej wzajemnie induktywnej definicji [even] moÅ¼na
    wygenerowaÄ‡ wszystkie liczby parzyste (i tylko je), tak wiÄ™c nowe [even]
    jest rÃ³wnowaÅ¼ne staremu [even] z sekcji _Induktywne predykaty_. Podobnie
    [odd] moÅ¼e wygenerowaÄ‡ wszystkie liczby nieparzyste i tylko je. *)

(** **** Ä†wiczenie (upewniajÄ…ce) *)

(** Upewnij siÄ™, Å¼e powyÅ¼szy akapit nie kÅ‚amie. *)

Lemma even_0 : even 0.
(* begin hide *)
Proof. constructor. Qed.
(* end hide *)

Lemma odd_1 : odd 1.
(* begin hide *)
Proof. repeat constructor. Qed.
(* end hide *)

Lemma even_2 : even 2.
(* begin hide *)
Proof. repeat constructor. Qed.
(* end hide *)

Lemma even_42 : even 42.
(* begin hide *)
Proof. repeat constructor. Qed.
(* end hide *)

Lemma not_odd_0 : ~ odd 0.
(* begin hide *)
Proof. inversion 1. Qed.
(* end hide *)

Lemma not_even_1 : ~ even 1.
(* begin hide *)
Proof. inversion 1. inversion H1. Qed.
(* end hide *)

(** **** Ä†wiczenie (wÅ‚aÅ›ciwoÅ›ci [even] i [odd]) *)

(** Udowodnij podstawowe wÅ‚aÅ›ciwoÅ›ci [even] i [odd]. *)

Lemma even_SS :
  forall n : nat, even n -> even (S (S n)).
(* begin hide *)
Proof.
  induction n; intro; do 2 constructor.
    constructor.
    assumption.
Qed.
(* end hide *)

Lemma odd_SS : 
  forall n : nat, odd n -> odd (S (S n)).
(* begin hide *)
Proof.
  induction n; intro; do 2 constructor; auto.
Qed.
(* end hide *)

Lemma even_plus :
  forall n m : nat, even n -> even m -> even (n + m).
(* begin hide *)
Abort.
(* end hide *)

(** JeÅ›li polegÅ‚eÅ› przy ostatnim zadaniu â€” nie przejmuj siÄ™. Specjalnie
    dobraÅ‚em zÅ‚oÅ›liwy przykÅ‚ad.

    W tym momencie naleÅ¼y sobie zadaÄ‡ pytanie: jak dowodziÄ‡ wÅ‚aÅ›ciwoÅ›ci
    typÃ³w wzajemnie induktywnych? Aby udzieliÄ‡ odpowiedzi, sprÃ³bujmy
    udowodniÄ‡ [even_plus] za pomocÄ… indukcji po [n], a potem przeÅ›ledÅºmy,
    co poszÅ‚o nie tak. *)

Lemma even_plus_failed_1 : 
  forall n m : nat, even n -> even m -> even (n + m).
Proof.
  induction n; intros.
    assumption.
    cbn. constructor. inversion H; subst.
Abort.

(** Nasza indukcja po [n] zawiodÅ‚a, gdyÅ¼ nasza hipoteza indukcyjna ma w
    konkluzji [even (n + m)], podczas gdy nasz cel jest postaci
    [odd (n + m)]. ZauwaÅ¼my, Å¼e teoretycznie cel powinno daÄ‡ siÄ™ udowodniÄ‡,
    jako Å¼e mamy hipotezy [even m] oraz [odd n], a suma liczby parzystej i
    nieparzystej jest nieparzysta.

    Nie zraÅ¼ajmy siÄ™ jednak i sprÃ³bujmy indukcji po dowodzie [even n]. *)

Lemma even_plus_failed_2 : 
  forall n m : nat, even n -> even m -> even (n + m).
Proof.
  induction 1; cbn; intro.
    assumption.
    constructor.
Abort.

(** Nasza indukcja po dowodzie hipotezy [even n] zawiodÅ‚a, i to z kretesem,
    gdyÅ¼ w kontekÅ›cie nie mamy nawet Å¼adnej hipotezy indukcyjnej! Co wÅ‚aÅ›ciwie
    siÄ™ staÅ‚o? *)

Check even_ind.
(* ===> even_ind :
     forall P : nat -> Prop,
     P 0 -> (forall n : nat, odd n -> P (S n)) ->
       forall n : nat, even n -> P n *)

(** Jak widaÄ‡, w naszej hipotezie "indukcyjnej" wygenerowanej przez Coqa w
    ogÃ³le nie ma Å¼adnej indukcji. Jest tam jedynie odwoÅ‚anie do predykatu
    [odd]...

    ZauwaÅ¼my jednak, Å¼e naszym celem znÃ³w byÅ‚o [odd (n + m)], a hipotezy
    [odd n] oraz [even m] sprawiajÄ…, Å¼e w teorii powinno daÄ‡ siÄ™ ten cel
    udowodniÄ‡, gdyÅ¼ suma liczby parzystej i nieparzystej jest nieparzysta.

    MogÅ‚oby siÄ™ zdawaÄ‡, Å¼e cierpimy na niedopasowanie (prÃ³ba 1) lub brak
    (prÃ³ba 2) hipotez indukcyjnych. Wydaje siÄ™ teÅ¼, Å¼e skoro w obydwu
    prÃ³bach zatrzymaliÅ›my siÄ™ na celu [odd (n + m)], to pomocne mogÅ‚oby
    okazaÄ‡ siÄ™ poniÅ¼sze twierdzenie. *)

Lemma odd_even_plus_failed :
  forall n m : nat, odd n -> even m -> odd (n + m).
Proof.
  induction n; intros.
    inversion H.
    cbn. constructor. inversion H; subst.
Abort.

(** Niestety â€” nie dla psa kieÅ‚basa, gdyÅ¼ natykamy siÄ™ na problemy bliÅºniaczo
    podobne do tych, ktÃ³re napotkaliÅ›my w poprzednim twierdzeniu: nasza
    hipoteza indukcyjna ma w konkluzji [odd (n + m)], podczas gdy nasz cel
    jest postaci [even (n + m)].

    PrÃ³ba przepchniÄ™cia lematu za pomocÄ… indukcji po dowodzie hipotezy
    [odd n] takÅ¼e nie zadziaÅ‚a, z tych samych powodÃ³w dla ktÃ³rych indukcja
    po [even n] nie pozwoliÅ‚a nam udowodniÄ‡ [even_plus]. ZauwaÅ¼my jednak, Å¼e
    cel jest udowadnialny, gdyÅ¼ jako hipotezy mamy [even n] oraz [even m],
    a suma dwÃ³ch liczb parzystych jest parzysta.

    Wydaje siÄ™, Å¼e wpadliÅ›my w bÅ‚Ä™dne koÅ‚o i jesteÅ›my w matni, bez wyjÅ›cia,
    bez nadziei, bez krzty szans na powodzenie: w dowodzie [even_plus]
    potrzebujemy lematu [odd_even_plus], ale nie moÅ¼emy go udowodniÄ‡, gdyÅ¼
    w dowodzie [odd_even_plus] wymagane jest uÅ¼ycie lematu [even_plus].
    Ehhh, gdybyÅ›my tak mogli udowodniÄ‡ oba te twierdzenia na raz...

    Eureka!

    ZauwaÅ¼, Å¼e w naszych dotychczasowych dowodach przez indukcjÄ™ posÅ‚ugiwaliÅ›my
    siÄ™ zwykÅ‚Ä…, "pojedynczÄ…" indukcjÄ…. ByÅ‚a ona wystarczajÄ…ca, gdyÅ¼ mieliÅ›my do
    czynienia jedynie ze zwykÅ‚ymi typami induktywnymi. Tym razem jednak jest
    inaczej: w ostatnich trzech dowodach chcieliÅ›my uÅ¼yÄ‡ "pojedynczej" indukcji
    do udowodnienia czegoÅ› na temat predykatÃ³w wzajemnie induktywnych.

    Jest to ogromny zgrzyt. Do dowodzenia wÅ‚aÅ›ciwoÅ›ci typÃ³w wzajemnie
    induktywnych powinniÅ›my uÅ¼yÄ‡... o zgrozo, jak mogliÅ›my to przeoczyÄ‡,
    przecieÅ¼ to takie oczywiste... indukcji wzajemnej!

    Najprostszy sposÃ³b przeprowadzenia tego dowodu wyglÄ…da tak: *)

Theorem even_plus :
  forall n m : nat, even n -> even m -> even (n + m)
with odd_even_plus :
  forall n m : nat, odd n -> even m -> odd (n + m).
Proof.
  assumption.
  assumption.
Fail Qed.
Restart.
  destruct n as [| n']; cbn; intros.
    assumption.
    constructor. apply odd_even_plus.
      inversion H. assumption.
      assumption.
  destruct n as [| n']; cbn; intros.
    inversion H.
    constructor. apply even_plus.
      inversion H. assumption.
      assumption.
Qed.

(** Co tu siÄ™ wÅ‚aÅ›ciwie staÅ‚o? Pierwsze dwie linijki sÄ… takie same jak
    poprzednio: stwierdzamy, Å¼e bÄ™dziemy dowodziÄ‡ twierdzenia o podanej
    nazwie i postaci. NastÄ™pnie mamy sÅ‚owo kluczowe [with], ktÃ³re peÅ‚ni
    tu rolÄ™ podobnÄ… jak w definicjach przez indukcjÄ™ wzajemnÄ…: podajÄ…c po
    nim nazwÄ™ i postaÄ‡ twierdzenia mÃ³wimy Coqowi, Å¼e chcemy dowodziÄ‡ tego
    twierdzenia ([odd_even_plus]) jednoczeÅ›nie z poprzednim ([even_plus]).

    Dotychczas po rozpoczÄ™ciu dowodu ukazywaÅ‚ nam siÄ™ jeden cel. Tym razem,
    jako Å¼e dowodzimy dwÃ³ch twierdzeÅ„ jednoczeÅ›nie, mamy przed sobÄ… dwa cele.
    W kontekÅ›cie mamy teÅ¼ od razu dwie hipotezy indukcyjne. Musimy na nie
    bardzo uwaÅ¼aÄ‡: dotychczas hipotezy indukcyjne pojawiaÅ‚y siÄ™ dopiero w
    kroku indukcyjnym i sposÃ³b ich uÅ¼ycia byÅ‚ oczywisty. Tym razem jest
    inaczej â€” jako, Å¼e mamy je od samego poczÄ…tku, moÅ¼emy natychmiast uÅ¼yÄ‡
    ich do "udowodnienia" naszych twierdzeÅ„.

    Niestety, takie "udowodnienie" odpowiada wywoÅ‚aniu rekurencyjnemu na
    argumencie, ktÃ³ry nie jest strukturalnie mniejszy (coÅ› jak [f x := f x]).
    Fakt ten obrazuje wiadomoÅ›Ä‡ o bÅ‚Ä™dzie, jakÄ… Coq daje nam po tej prÃ³bie: *)

(* ===> Error: Cannot guess decreasing argument of fix. *)

(** Zaczynamy dowÃ³d od nowa, tym razem juÅ¼ bez oszukiwania. Musimy udowodniÄ‡
    kaÅ¼dy z naszych celÃ³w osobno, ale moÅ¼emy korzystaÄ‡ z obydwu hipotez
    indukcyjnych. W obydwu celach zaczynamy od analizy przypadkÃ³w, czyli
    rozbicia [n], i rozwiÄ…zania przypadku bazowego. Rozbicie [n] daÅ‚o nam
    [n'], ktÃ³re jest strukturalnie mniejsze od [n], a zatem moÅ¼emy bez obaw
    uÅ¼yÄ‡ naszej hipotezy indukcyjnej. Reszta jest trywialna. *)

Theorem even_double :
  forall n : nat, even (2 * n).
Proof.
  induction n as [| n']; cbn in *; constructor.
  rewrite <- plus_n_O in *. rewrite plus_comm. cbn. constructor.
    assumption.
Qed.

End MutInd.

(** * RÃ³Å¼ne *)

(** ** Rodziny typÃ³w induktywnych *)

(** SÅ‚owo kluczowe [Inductive] pozwala nam definiowaÄ‡ nie tylko typy
    induktywne, ale takÅ¼e rodziny typÃ³w induktywnych â€” i to nawet na
    dwa sposoby. W tym podrozdziale przyjrzymy siÄ™ obu z nich oraz
    rÃ³Å¼nicom miÄ™dzy nimi, a takÅ¼e ich wadom i zaletom. Przyjrzyjmy siÄ™
    raz jeszcze typowi [option]: *)

Print option.
(* ===> Inductive option (A : Type) : Type :=
            | Some : A -> option A
            | None : option A *)

Check Some.
(* ===> Some : forall A : Type, A -> option A *)

Check @None.
(* ===> @None : forall A : Type, option A *)

(** DefiniujÄ…c rodzinÄ™ typÃ³w [option], umieÅ›ciliÅ›my argument bÄ™dÄ…cy typem
    w nawiasach okrÄ…gÅ‚ych tuÅ¼ po nazwie definiowanego typu, a przed [: Type].
    DefiniujÄ…c konstruktory, nie napisaliÅ›my nigdzie [forall A : Type, ...],
    a mimo tego komenda [Check] jasno pokazuje, Å¼e typy obydwu konstruktorÃ³w
    zaczynajÄ… siÄ™ od takiej wÅ‚aÅ›nie kwantyfikacji.

    (Przypomnijmy, Å¼e w przypadku [None] argument [A] jest domyÅ›lny, wiÄ™c
    wyÅ›wietlenie peÅ‚nego typu tego konstruktora wymagaÅ‚o uÅ¼ycia symbolu [@],
    ktÃ³ry oznacza "wyÅ›wietl wszystkie argumenty domyÅ›lne").

    W ogÃ³lnoÅ›ci, definiowanie rodziny typÃ³w [T] jako [T (x1 : A1) ... (xN : AN)]
    ma nastÄ™pujÄ…cy efekt:
    - kwantyfikacja [forall (x1 : A1) ... (xN : AN)] jest dodawana na
      poczÄ…tek kaÅ¼dego konstruktora
    - w konkluzji konstruktora [T] musi wystÄ…piÄ‡ zaaplikowany do tych
      argumentÃ³w, czyli jako [T x1 ... xN] â€” wstawienie innych argumentÃ³w
      jest bÅ‚Ä™dem *)

Fail Inductive option' (A : Type) : Type :=
    | Some' : A -> option' A
    | None' : forall B : Type, option' B.

(** PrÃ³ba zdefiniowania typu [option'] koÅ„czy siÄ™ nastÄ™pujÄ…cym komunikatem
    o bÅ‚Ä™dzie: *)

(* Error: Last occurrence of "option'" must have "A" as 1st argument in 
   "forall B : Type, option' B". *)

(** Drugi sposÃ³b zdefiniowania rodziny typÃ³w [option] przedstawiono
    poniÅ¼ej. Tym razem zamiast umieszczaÄ‡ argument [A : Type] po
    nazwie definiowanego typu, deklarujemy, Å¼e typem [option'] jest
    [Type -> Type]. *)

Inductive option' : Type -> Type :=
    | Some' : forall A : Type, A -> option' A
    | None' : forall B : Type, option' B.

(** Taki zabieg daje nam wiÄ™kszÄ… swobodÄ™: w kaÅ¼dym konstruktorze
    z osobna musimy explicitÃ© umieÅ›ciÄ‡ kwantyfikacjÄ™ po argumencie
    sortu [Type], dziÄ™ki czemu rÃ³Å¼ne konstruktory mogÄ… w konkluzji
    mieÄ‡ [option'] zaaplikowany do rÃ³Å¼nych argumentÃ³w. *)

Check Some'.
(* ===> Some' : forall A : Type, A -> option' A *)

Check None'.
(* ===> None' : forall B : Type, option' B *)

(** ZauwaÅ¼my jednak, Å¼e definicje [option] i [option'] sÄ… rÃ³wnowaÅ¼ne
    â€” typ konstruktora [None'] rÃ³Å¼ni siÄ™ od typu [None] jedynie nazwÄ…
    argumentu ([A] dla [None], [B] dla [None']).

    Jak zatem rozstrzygnÄ…Ä‡, ktÃ³ry sposÃ³b definiowania jest "lepszy"?
    W naszym przypadku lepszy jest sposÃ³b pierwszy, odpowiadajÄ…cy
    typowi [option], gdyÅ¼ jest bardziej zwiÄ™zÅ‚y. Nie jest to jednak
    jedyne kryterium. *)

Check option_ind.
(* ===> option_ind :
            forall (A : Type) (P : option A -> Prop),
            (forall a : A, P (Some a)) -> P None ->
            forall o : option A, P o *)

Check option'_ind.
(* ===> option'_ind :
            forall P : forall T : Type, option' T -> Prop,
            (forall (A : Type) (a : A), P A (Some' A a)) ->
            (forall B : Type, P B (None' B)) ->
            forall (T : Type) (o : option' T), P T o *)

(** Dwa powyÅ¼sze termy to reguÅ‚y indukcyjne, wygenerowane automatycznie
    przez Coqa dla typÃ³w [option] oraz [option']. ReguÅ‚a dla [option]
    jest wizualnie krÃ³tsza, co, jak dowiemy siÄ™ w przyszÅ‚oÅ›ci, oznacza
    zapewne, Å¼e jest prostsza, zaÅ› prostsza reguÅ‚a indukcyjna oznacza
    Å‚atwiejsze dowodzenie przez indukcjÄ™. Jest to w zasadzie najmocniejszy
    argument przemawiajÄ…cy za pierwszym sposobem zdefiniowania [option].

    PowyÅ¼sze rozwaÅ¼ania nie oznaczajÄ… jednak, Å¼e sposÃ³b pierwszy jest
    zawsze lepszy â€” sposÃ³b drugi jest bardziej ogÃ³lny i istniejÄ… rodziny
    typÃ³w, ktÃ³rych zdefiniowanie sposobem pierwszym jest niemoÅ¼liwe.
    Klasycznym przykÅ‚adem jest rodzina typÃ³w [vec]. *)

Inductive vec (A : Type) : nat -> Type :=
    | vnil : vec A 0
    | vcons : forall n : nat, A -> vec A n -> vec A (S n).

(** Konstruktor [vnil] reprezentuje listÄ™ pustÄ…, ktÃ³rej dÅ‚ugoÅ›Ä‡ wynosi
    rzecz jasna [0], zaÅ› [vcons] reprezentuje listÄ™ skÅ‚adajÄ…ca siÄ™ z
    gÅ‚owy i ogona o dÅ‚ugoÅ›ci [n], ktÃ³rej dÅ‚ugoÅ›Ä‡ wynosi oczywiÅ›cie [S n].

    [vec] reprezetuje listy o dÅ‚ugoÅ›ci znanej statycznie (tzn. Coq zna
    dÅ‚ugoÅ›Ä‡ takiej listy juÅ¼ w trakcie sprawdzania typÃ³w), dziÄ™ki czemu
    moÅ¼emy obliczaÄ‡ ich dÅ‚ugoÅ›Ä‡ w czasie staÅ‚ym (po prostu odczytujÄ…c jÄ…
    z typu danej listy).

    ZauwaÅ¼, Å¼e w obu konstruktorach argumenty typu [nat] sÄ… rÃ³Å¼ne, a zatem
    zdefiniowanie tego typu jako [vec (A : Type) (n : nat) ...] byÅ‚oby
    niemoÅ¼liwe.

    PrzykÅ‚ad ten pokazuje nam rÃ³wnieÅ¼, Å¼e przy definiowaniu rodzin typÃ³w
    moÅ¼emy dowolnie mieszaÄ‡ sposoby pierwszy i drugi â€” w naszym przypadku
    argument [A : Type] jest wspÃ³lny dla wszystkich konstruktorÃ³w, wiÄ™c
    umieszczamy go przed ostatnim [:], zaÅ› argument typu [nat] rÃ³Å¼ni siÄ™
    w zaleÅ¼noÅ›ci od konstruktora, a zatem umieszczamy go po ostatnim [:]. *)

(** **** Ä†wiczenie *)

(** Zdefiniuj nastÄ™pujÄ…ce typy (zadbaj o to, Å¼eby wygenerowana reguÅ‚a
    indukcyjna byÅ‚a jak najkrÃ³tsza):
    - typ drzew binarnych przechowujÄ…cych elementy typu [A]
    - typ drzew binarnych przechowujÄ…cych elementy typu [A],
      ktÃ³rych wysokoÅ›Ä‡ jest znana statycznie
    - typ heterogenicznych drzew binarnych (mogÄ… one
      przechowywaÄ‡ elementy rÃ³Å¼nych typÃ³w)
    - typ heterogenicznych drzew binarnych, ktÃ³rych wysokoÅ›Ä‡
      jest znana statycznie *)

(* begin hide *)
Inductive BTree (A : Type) : Type :=
    | BT_Empty : BTree A
    | BT_Node : A -> BTree A -> BTree A -> BTree A.

Inductive EBTree (A : Type) : nat -> Type :=
    | EBT_Empty : EBTree A 0
    | EBT_Node : forall n m : nat,
        A -> EBTree A n -> EBTree A m -> EBTree A (max n m).

Inductive HBTree : Type :=
    | HBT_Empty : HBTree
    | HBT_Node : forall A : Type, A -> HBTree -> HBTree -> HBTree.

Check HBT_Node _ 42 (HBT_Node _ true HBT_Empty HBT_Empty) HBT_Empty.

Inductive EHBTree : nat -> Type :=
    | EHBT_Empty : EHBTree 0
    | EHBT_Node : forall (A : Type) (n m : nat),
        A -> EHBTree n -> EHBTree m -> EHBTree (max n m).
(* end hide *)

(** ** Indukcja wzajemna a indeksowane rodziny typÃ³w *)

Module MutualIndution_vs_InductiveFamilies.

(** Indukcja wzajemna nie jest zbyt uÅ¼yteczna. Pierwszym, praktycznym,
    powodem jest to, Å¼e, jak pewnie zdÄ…Å¼yÅ‚eÅ› siÄ™ juÅ¼ na wÅ‚asnej skÃ³rze
    przekonaÄ‡, jej uÅ¼ywanie jest doÅ›Ä‡ upierdliwe. Drugi, teoretyczny,
    powÃ³d jest taki, Å¼e definicje przez indukcjÄ™ wzajemnÄ… moÅ¼emy Å‚atwo
    zasymulowaÄ‡ za pomocÄ… indeksowanych rodzin typÃ³w. *)

Inductive even : nat -> Prop :=
    | even0 : even 0
    | evenS : forall n : nat, odd n -> even (S n)

with odd : nat -> Prop :=
    | oddS : forall n : nat, even n -> odd (S n).

(** RzuÄ‡my jeszcze raz okiem na znanÄ… nam juÅ¼ definicjÄ™ predykatÃ³w [even]
    i [odd] przez indukcjÄ™ wzajemnÄ…. Nie dzieje siÄ™ tu nic niezwykÅ‚ego, a
    najwaÅ¼niejszym spostrzeÅ¼eniem, jakie moÅ¼emy poczyniÄ‡, jest to, Å¼e
    [even] i [odd] to dwa byty - nie trzy, nie piÄ™Ä‡, ale dwa. *)

Inductive even_odd : bool -> nat -> Prop :=
    | even0' : even_odd true 0
    | evenS' :
        forall n : nat, even_odd false n -> even_odd true (S n)
    | oddS' :
        forall n : nat, even_odd true n -> even_odd false (S n).

Definition even' := even_odd true.
Definition odd' := even_odd false.

(** Co z tego wynika? Ano, zamiast definiowaÄ‡ przez indukcjÄ™ wzajemnÄ… dwÃ³ch
    predykatÃ³w [even] i [odd] moÅ¼emy za jednym zamachem zdefiniowaÄ‡ relacjÄ™
    [even_odd], ktÃ³ra jednoczeÅ›nie odpowiada obu tym predykatom. Kluczem
    w tej sztuczce jest dodatkowy indeks, ktÃ³rym jest dwuelementowy typ
    [bool]. DziÄ™ki niemu moÅ¼emy zakodowaÄ‡ definicjÄ™ [even] za pomocÄ…
    [even_odd true], zaÅ› [odd] jako [even_odd false]. *)

Lemma even_even' :
  forall n : nat, even n -> even' n

with odd_odd' :
  forall n : nat, odd n -> odd' n.
(* begin hide *)
Proof.
  destruct n as [| n']; intro.
    constructor.
    constructor. apply odd_odd'. inversion H; assumption.
  destruct n as [| n']; intro.
    inversion H.
    constructor. apply even_even'. inversion H; assumption.
Qed.
(* end hide *)

Lemma even'_even :
  forall n : nat, even' n -> even n

with odd'_odd :
  forall n : nat, odd' n -> odd n.
(* begin hide *)
Proof.
  destruct n as [| n']; intro.
    constructor.
    constructor. apply odd'_odd. inversion H; assumption.
  destruct n as [| n']; intro.
    inversion H.
    constructor. apply even'_even. inversion H; assumption.
Qed.
(* end hide *)

(** Obie definicje sÄ…, jak widaÄ‡ (Ä‡wiczenie!), rÃ³wnowaÅ¼ne, choÄ‡ pod wzglÄ™dem
    estetycznym oczywiÅ›cie duÅ¼o lepiej wypada indukcja wzajemna. *)

End MutualIndution_vs_InductiveFamilies.

(** Na koniec wypada jeszcze powiedzieÄ‡, Å¼e indeksowane typy induktywne sÄ…
    potÄ™Å¼niejsze od typÃ³w wzajemnie induktywnych. Wynika to z tego prostego
    faktu, Å¼e przez wzajemnÄ… indukcjÄ™ moÅ¼emy zdefiniowaÄ‡ na raz jedynie
    skoÅ„czenie wiele typÃ³w, zaÅ› indeksowane typy induktywne indeksowane
    mogÄ… byÄ‡ typami nieskoÅ„czonymi. *)

(** ** Sumy zaleÅ¼ne i podtypy *)

(** W Coqu, w przeciwieÅ„stwie do wielu jÄ™zykÃ³w imperatywnych, nie ma
    mechanizmu podtypowania, a kaÅ¼de dwa typy sÄ… ze sobÄ… rozÅ‚Ä…czne.
    Nie jest to problemem, gdyÅ¼ podtypowanie moÅ¼emy zasymulowaÄ‡ za
    pomocÄ… sum zaleÅ¼nych, a te zdefiniowaÄ‡ moÅ¼emy induktywnie. *)

Module sigma.

Inductive sigT (A : Type) (P : A -> Type) : Type :=
    | existT : forall x : A, P x -> sigT A P.

(** Typ [sigT] reprezentuje sumÄ™ zaleÅ¼nÄ…, ktÃ³rej elementami sÄ… pary zaleÅ¼ne.
    Pierwszym elementem pary jest [x], ktÃ³ry jest typu [A], zaÅ› drugim
    elementem pary jest term typu [P x]. Suma zaleÅ¼na jest wobec tego pewnym
    uogÃ³lnieniem produktu.

    Niech ciÄ™ nie zmyli nazewnictwo:
    - Suma jest reprezentowana przez typ [sum A B]. Jej elementami sÄ…
      elementy [A] zawiniÄ™te w konstruktor [inl] oraz elementy [B]
      zawiniÄ™te w konstruktor [inr]. Reprezentuje ideÄ™ "lub/albo".
      Typ [B] nie moÅ¼e zaleÅ¼eÄ‡ od typu [A].
    - Produkt jest reprezentowany przez typ [prod A B]. Jego elementami
      sÄ… pary elementÃ³w [A] i [B]. Reprezentuje on ideÄ™ "i/oraz". Typ
      [B] nie moÅ¼e zaleÅ¼eÄ‡ od typu [A].
    - UogÃ³lnieniem produktu jest suma zaleÅ¼na. Jest ona reprezentowana
      przez typ [sigT A P]. Jej elementami sÄ… pary zaleÅ¼ne elementÃ³w
      [A] i [P x], gdzie [x : A] jest pierwszym elementem pary.
      Reprezentuje ona ideÄ™ "i/oraz", gdzie typ [P x] moÅ¼e zaleÅ¼eÄ‡ od
      elementu [x] typu [A].
    - Typ funkcji jest reprezentowany przez [A -> B]. Jego elementami
      sÄ… termy postaci [fun x : A => ...]. ReprezentujÄ… ideÄ™ "daj mi
      coÅ› typu [A], a ja oddam ci coÅ› typu [B]". Typ [B] nie moÅ¼e
      zaleÅ¼eÄ‡ od typu [A].
    - UogÃ³lnieniem typu funkcji jest produkt zaleÅ¼ny [forall x : A, B x].
      Jego elementami sÄ… termu postaci [fun x : A => ...]. Reprezentuje
      on ideÄ™ "daj mi [x] typu [A], a ja oddam ci coÅ› typu [B x]". Typ
      [B x] moÅ¼e zaleÅ¼eÄ‡ od typu elementu [x] typu [A]. *)

(** [sigT] jest najogÃ³lniejszÄ… postaciÄ… pary zaleÅ¼nej â€” [A] jest typem,
    a [P] rodzinÄ… typÃ³w. Mimo swej ogÃ³lnoÅ›ci jest uÅ¼ywany doÅ›Ä‡ rzadko,
    gdyÅ¼ najbardziej przydatnÄ… postaciÄ… sumy zaleÅ¼nej jest typ [sig]: *)

Inductive sig (A : Type) (P : A -> Prop) : Type :=
    | exist : forall x : A, P x -> sig A P.

Arguments exist [A] [P] _ _.

(** Typ [sig A P] moÅ¼na interpretowaÄ‡ jako typ skÅ‚adajÄ…cy siÄ™ z tych
    elementÃ³w [A], ktÃ³re speÅ‚niajÄ… predykat [P]. Formalnie jest to
    para zaleÅ¼na, ktÃ³rej pierwszym elementem jest term typu [A], zaÅ›
    drugim dowÃ³d na to, Å¼e speÅ‚nia on predykat [P]. *)

Definition even_nat : Type := sig nat even.

Definition even_four : even_nat := exist 4 four_is_even.

(** Typ [even_nat] reprezentuje parzyste liczby naturalne, zaÅ› term
    [even_four] to liczba [4] wraz z zaÅ‚Ä…czonym dowodem faktu, Å¼e [4]
    jest parzyste.

    Interpretacja typu [sig] sprawia, Å¼e jest on wykorzystywany bardzo
    czÄ™sto do podawania specyfikacji programÃ³w â€” pozwala on dodaÄ‡ do
    wyniku zwracanego przez funkcjÄ™ informacjÄ™ o jego wÅ‚aÅ›ciwoÅ›ciach.
    W przypadku argumentÃ³w raczej nie jest uÅ¼ywany, gdyÅ¼ proÅ›ciej jest
    po prostu wymagaÄ‡ dowodÃ³w Å¼Ä…danych wÅ‚aÅ›ciwoÅ›ci w osobnych argumentach
    niÅ¼ pakowaÄ‡ je w [sig] po to, Å¼eby i tak zostaÅ‚y pÃ³Åºniej odpakowane. *)

Definition even_42 : sig nat even.
Proof.
  apply (exist 42). repeat constructor.
Defined.

(** Definiowanie wartoÅ›ci typu [sig] jest problematyczne, gdyÅ¼ zawierajÄ…
    one dowody. Napisanie definicji "rÄ™cznie", explicitÃ© podajÄ…c proofterm,
    nie wchodzi w grÄ™. Innym potencjalnym rozwiÄ…zaniem jest napisanie dowodu
    na boku, a nastÄ™pnie uÅ¼ycie go we wÅ‚aÅ›ciwej definicji, ale jest ono
    dÅ‚uÅ¼sze niÅ¼ to konieczne.

    Przypomnijmy sobie, czym sÄ… taktyki. Dowody to termy, ktÃ³rych typy sÄ…
    sortu [Prop], a taktyki sÅ‚uÅ¼Ä… do konstruowania tych dowodÃ³w. PoniewaÅ¼
    dowody nie rÃ³Å¼niÄ… siÄ™ (prawie) niczym od programÃ³w, taktyk moÅ¼na uÅ¼yÄ‡
    takÅ¼e do pisania programÃ³w. Taktyki to metaprogramy (napisane w jÄ™zku
    Ltac), ktÃ³re piszÄ… programy (w jÄ™zku termÃ³w Coqa, zwanym Gallina).

    Wobec tego trybu dowodzenia oraz taktyk moÅ¼emy uÅ¼ywaÄ‡ nie tylko do
    dowodzenia, ale takÅ¼e do definiowania i to wÅ‚aÅ›nie uczyniliÅ›my w
    powyÅ¼szym przykÅ‚adzie. Skonstruowanie termu typu [sig nat even],
    czyli parzystej liczby naturalnej, odbyÅ‚o siÄ™ w nastÄ™pujÄ…cy sposÃ³b.

    Naszym celem jest poczÄ…tkowo [sig nat even], czyli typ, ktÃ³rego
    element chcemy skonstrowaÄ‡. UÅ¼ywamy konstruktora [exist], ktÃ³ry
    w naszym przypadku jest typu [forall x : nat, even n -> sig nat even].
    Wobec tego [exist 42] jest typu [even 42 -> sig nat even], a jego
    zaaplikowanie skutkowaÄ‡ bÄ™dzie zamianÄ… naszego celu na [even 42].
    NastÄ™pnie dowodzimy tego faktu, co koÅ„czy proces definiowania. *)

(** **** Ä†wiczenie *)

(** Zdefiniuj predykat [sorted], ktÃ³ry jest speÅ‚niony, gdy jego argument
    jest listÄ… posortowanÄ…. NastÄ™pnie zdefiniuj typ list liczb naturalnych
    posortowanych wedÅ‚ug relacji [<=] i skonstruuj term tego typu
    odpowiadajÄ…cy liÅ›cie [[42; 666; 1337]]. *)

End sigma.

(** ** Kwantyfikacja egzystencjalna *)

(** Znamy juÅ¼ pary zaleÅ¼ne i wiemy, Å¼e mogÄ… sÅ‚uÅ¼yÄ‡ do reprezentowania
    podtypÃ³w, ktÃ³rych w Coqu brak. Czas zatem uÅ›wiadomiÄ‡ sobie kolejny
    fragment korespondencji Curry'ego-Howarda, a mianowicie definicjÄ™
    kwantyfikacji egzystencjalnej: *)

Module ex.

Inductive ex (A : Type) (P : A -> Prop) : Prop :=
    | ex_intro : forall x : A, P x -> ex A P.

(** [ex] to kolejne wcielenie sumy zaleÅ¼nej. PorÃ³wnaj dokÅ‚adnie tÄ™
    definicjÄ™ z definicjÄ… [sigT] oraz [sig]. [ex] jest niemal identyczne
    jak [sig]: jest to para zaleÅ¼na, ktÃ³rej pierwszym elementem jest
    term [x : A], a drugim dowÃ³d na to, Å¼e [P x] zachodzi. [ex] jednak,
    w przeciwieÅ„stwie do [sig], Å¼yje w [Prop], czyli jest zdaniem â€” nie
    liczÄ… siÄ™ konkretne postaci jego termÃ³w ani ich iloÅ›Ä‡, a jedynie
    fakt ich istnienia. To sprawia, Å¼e [ex] jest doskonaÅ‚ym kandydatem
    do reprezentowania kwantyfikacji egzystencjalnej. *)

(** **** Ä†wiczenie *)

(** Udowodnij, Å¼e dla kaÅ¼dej liczby naturalnej n istnieje liczba od niej
    wiÄ™ksza. NastÄ™pnie zastanÃ³w siÄ™, jak dziaÅ‚a taktyka [exists]. *)

Theorem exists_greater :
  forall n : nat, ex nat (fun k : nat => n < k).
(* begin hide *)
Proof.
  intro. apply (ex_intro _ _ (S n)). unfold lt. apply le_n.
Qed.
(* end hide *)

End ex.

(** * WyÅ¼sze czary *)

(** NajwyÅ¼szy czas nauczyÄ‡ siÄ™ czegoÅ› tak zaawansowanego, Å¼e nawet w Coqu
    (peÅ‚nym przecieÅ¼ dziwnych rzeczy) tego nie ma i nie zapowiada siÄ™ na
    to, Å¼e bÄ™dzie. Mam tu na myÅ›li mechanizmy takie jak indukcja-indukcja,
    indukcja-rekursja oraz indukcja-indukcja-rekursja (jak widaÄ‡, w Å›wiecie
    powaÅ¼nych uczonych, podobnie jak Å›wiecie Goebbelsa, im wiÄ™cej razy
    powtÃ³rzy siÄ™ dane sÅ‚owo, tym wiÄ™cej pÅ‚ynie z niego mocy). *)

(** ** Przypomnienie *)

(** Zanim jednak wyjaÅ›nimy, co to za stwory, przypomnijmy sobie rÃ³Å¼ne, coraz
    bardziej innowacyjne sposoby definiowania przez indukcjÄ™ oraz dowiedzmy
    siÄ™, jak sformuÅ‚owaÄ‡ i udowodniÄ‡ wynikajÄ…ce z nich reguÅ‚y rekursji oraz
    indukcji. *)

Unset Elimination Schemes.

(** PowyÅ¼sza komenda mÃ³wi Coqowi, Å¼eby nie generowaÅ‚ automatycznie reguÅ‚
    indukcji. Przyda nam siÄ™ ona, by uniknÄ…Ä‡ konfliktÃ³w nazw z reguÅ‚ami,
    ktÃ³re bÄ™dziemy pisaÄ‡ rÄ™cznie. *)

(** *** Enumeracje *)

Module enum.

Inductive I : Type :=
    | c0 : I
    | c1 : I
    | c2 : I.

(** Najprymitywniejszymi z typÃ³w induktywnych sÄ… enumeracje. DefiniujÄ…c je,
    wymieniamy po prostu wszystkie ich elementy. *)

Definition I_case_nondep_type : Type :=
  forall P : Type, P -> P -> P -> I -> P.

(** ReguÅ‚a definiowania przez przypadki jest banalnie prosta: jeÅ¼eli w
    jakimÅ› inny typie [P] uda nam siÄ™ znaleÅºÄ‡ po jednym elemencie dla kaÅ¼dego
    z elementÃ³w naszego typu [I], to moÅ¼emy zrobiÄ‡ funkcjÄ™ [I -> P]. *)

Definition I_case_nondep : I_case_nondep_type :=
  fun (P : Type) (c0' c1' c2' : P) (i : I) =>
  match i with
      | c0 => c0'
      | c1 => c1'
      | c2 => c2'
  end.

(** ReguÅ‚Ä™ zdefiniowaÄ‡ moÅ¼emy za pomocÄ… dopasowania do wzorca. *)

Definition I_case_dep_type : Type :=
  forall (P : I -> Type) (c0' : P c0) (c1' : P c1) (c2' : P c2),
    forall i : I, P i.

(** ZaleÅ¼nÄ… reguÅ‚Ä™ definiowania przez przypadki moÅ¼emy uzyskaÄ‡ z poprzedniej
    uzaleÅ¼niajÄ…c przeciwdziedzinÄ™ [P] od dziedziny. *)

Definition I_case_dep : I_case_dep_type :=
  fun (P : I -> Type) (c0' : P c0) (c1' : P c1) (c2' : P c2) (i : I) =>
  match i with
      | c0 => c0'
      | c1 => c1'
      | c2 => c2'
  end.

(** Definicja, jak widaÄ‡, jest taka sama jak poprzednio, wiÄ™c obliczeniowo
    obie reguÅ‚y zachowujÄ… siÄ™ tak samo. RÃ³Å¼nica leÅ¼y jedynie w typach -
    druga reguÅ‚a jest ogÃ³lniejsza. *)

End enum.

(** *** Konstruktory rekurencjne *)

Module rec.

Inductive I : Type :=
    | x : I -> I
    | D : I -> I.

(** Typy induktywne stajÄ… siÄ™ naprawdÄ™ induktywne, gdy konstruktory mogÄ…
    braÄ‡ argumenty typu, ktÃ³ry wÅ‚aÅ›nie definiujemy. DziÄ™ki temu moÅ¼emy
    tworzyÄ‡ type, ktÃ³re majÄ… nieskoÅ„czenie wiele elementÃ³w, z ktÃ³rych
    kaÅ¼dy ma ksztaÅ‚t takiego czy innego drzewa. *)

Definition I_rec_type : Type :=
  forall P : Type,  (P -> P) -> (P -> P) -> I -> P.

(** Typ reguÅ‚y rekursji (czyli rekursora) tworzymy tak jak dla enumeracji:
    jeÅ¼eli w typie [P] znajdziemy rzeczy o takim samym ksztaÅ‚cie jak
    konstruktory typu [I], to moÅ¼emy zrobiÄ‡ funkcjÄ™ [I -> P]. W naszym
    przypadku oba konstruktory majÄ… ksztaÅ‚t [I -> I], wiÄ™c do zdefiniowania
    naszej funkcji musimy znaleÅºÄ‡ odpowiadajÄ…ce im rzeczy typu [P -> P]. *)

Fixpoint I_rec (P : Type) (x' : P -> P) (D' : P -> P) (i : I) : P :=
match i with
    | x i' => x' (I_rec P x' D' i')
    | D i' => D' (I_rec P x' D' i')
end.

(** Definicja rekursora jest prosta. JeÅ¼eli wyobrazimy sobie [i : I] jako
    drzewo, to wÄ™zÅ‚y z etykietkÄ… [x] zastÄ™pujemy wywoÅ‚aniem funkcji [x'],
    a wÄ™zÅ‚y z etykietkÄ… [D] zastÄ™pujemy wywoÅ‚aniami funkcji [D]. *)

Definition I_ind_type : Type :=
  forall (P : I -> Type)
    (x' : forall i : I, P i -> P (x i))
    (D' : forall i : I, P i -> P (D i)),
      forall i : I, P i.

(** ReguÅ‚a indukcji (czyli induktor - cÃ³Å¼ za piÄ™kna nazwa!) powstaje z
    reguÅ‚y rekursji przez uzaleÅ¼nienie przeciwdziedziny [P] od dziedziny
    [I]. *)

Fixpoint I_ind (P : I -> Type)
  (x' : forall i : I, P i -> P (x i)) (D' : forall i : I, P i -> P (D i))
  (i : I) : P i :=
match i with
    | x i' => x' i' (I_ind P x' D' i')
    | D i' => D' i' (I_ind P x' D' i')
end.

(** Podobnie jak poprzednio, implementacja reguÅ‚y indukcji jest identyczna
    jak rekursora, jedynie typy sÄ… bardziej ogÃ³lnej.

    Uwaga: nazywam reguÅ‚y nieco inaczej niÅ¼ te autogenerowane przez Coqa.
    Dla Coqa reguÅ‚a indukcji dla [I] to nasze [I_ind] z [P : I -> Type]
    zastÄ…pionym przez [P : I -> Prop], zaÅ› Coqowe [I_rec] odpowiadaÅ‚oby
    naszemu [I_ind] dla [P : I -> Set].

    JeÅ¼eli smuci ciÄ™ burdel nazewniczy, to nie przejmuj siÄ™ - kiedyÅ› bÄ™dzie
    lepiej. Klasyfikacja reguÅ‚ jest prosta:
    - reguÅ‚y mogÄ… byÄ‡ zaleÅ¼ne lub nie, w zaleÅ¼noÅ›ci od tego czy [P] zaleÅ¼y
      od [I]
    - reguÅ‚y mogÄ… byÄ‡ rekurencyjne lub nie
    - reguÅ‚y mogÄ… byÄ‡ dla sortu [Type], [Prop] albo nawet [Set] *)

End rec.

(** *** Parametry *)

Module param.

Inductive I (A B : Type) : Type :=
    | c0 : A -> I A B
    | c1 : B -> I A B
    | c2 : A -> B -> I A B.

Arguments c0 {A B} _.
Arguments c1 {A B} _.
Arguments c2 {A B} _ _.

(** KolejnÄ… innowacjÄ… sÄ… parametry, ktÃ³rych gÅ‚Ã³wnym zastosowaniem jest
    polimorfizm. DziÄ™ki parametrom moÅ¼emy za jednym zamachem (tylko bez
    skojarzeÅ„ z Islamem!) zdefiniowaÄ‡ nieskoÅ„czenie wiele typÃ³w, po jednym
    dla kaÅ¼dego parametru. *)

Definition I_case_nondep_type : Type :=
  forall (A B P : Type) (c0' : A -> P) (c1' : B -> P) (c2' : A -> B -> P),
    I A B -> P.

(** Typ rekursora jest oczywisty: jeÅ¼eli znajdziemy rzeczy o ksztaÅ‚tach
    takich jak konstruktory [I] z [I] zastÄ…pionym przez [P], to moÅ¼emy
    zrobiÄ‡ funkcjÄ™ [I -> P]. Jako, Å¼e parametry sÄ… zawsze takie samo,
    moÅ¼emy skwantyfikowaÄ‡ je na samym poczÄ…tku. *)

Definition I_case_nondep
  (A B P : Type) (c0' : A -> P) (c1' : B -> P) (c2' : A -> B -> P)
  (i : I A B) : P :=
match i with
    | c0 a => c0' a
    | c1 b => c1' b
    | c2 a b => c2' a b
end.

(** Implementacja jest banalna. *)

Definition I_case_dep_type : Type :=
  forall (A B : Type) (P : I A B -> Type)
    (c0' : forall a : A, P (c0 a))
    (c1' : forall b : B, P (c1 b))
    (c2' : forall (a : A) (b : B), P (c2 a b)),
      forall i : I A B, P i.

(** A reguÅ‚Ä™ indukcjÄ™ uzyskujemy przez uzaleÅ¼nienie [P] od [I]. *)

Definition I_case_dep
  (A B : Type) (P : I A B -> Type)
  (c0' : forall a : A, P (c0 a))
  (c1' : forall b : B, P (c1 b))
  (c2' : forall (a : A) (b : B), P (c2 a b))
  (i : I A B) : P i :=
match i with
    | c0 a => c0' a
    | c1 b => c1' b
    | c2 a b => c2' a b
end.

End param.

(** *** Indukcja wzajemna *)

Module mutual.

Inductive Smok : Type :=
    | Wysuszony : Zmok -> Smok

with Zmok : Type :=
    | Zmoczony : Smok -> Zmok.

(** Indukcja wzajemna pozwala definiowaÄ‡ na raz wiele typÃ³w, ktÃ³re mogÄ…
    odwoÅ‚ywaÄ‡ siÄ™ do siebie nawzajem. CytujÄ…c klasykÃ³w: smok to wysuszony
    zmok, zmok to zmoczony smok. *)

Definition Smok_case_nondep_type : Type :=
  forall S : Type, (Zmok -> S) -> Smok -> S.

Definition Zmok_case_nondep_type : Type :=
  forall Z : Type, (Smok -> Z) -> Zmok -> Z.

(** ReguÅ‚a niezaleÅ¼nej analizy przypadkÃ³w dla [Smok]a wyglÄ…da banalnie:
    jeÅ¼eli ze [Zmok]a potrafimy wyprodukowaÄ‡ [S], to ze [Smok]a teÅ¼.
    Dla [Zmok]a jest analogicznie. *)

Definition Smok_case_nondep
  (S : Type) (Wy : Zmok -> S) (smok : Smok) : S :=
match smok with
    | Wysuszony zmok => Wy zmok
end.

Definition Zmok_case_nondep
  (Z : Type) (Zm : Smok -> Z) (zmok : Zmok) : Z :=
match zmok with
    | Zmoczony smok => Zm smok
end.

(** Implementacja jest banalna. *)

Definition Smok_rec_type : Type :=
  forall S Z : Type, (Z -> S) -> (S -> Z) -> Smok -> S.

Definition Zmok_rec_type : Type :=
  forall S Z : Type, (Z -> S) -> (S -> Z) -> Zmok -> Z.

(** Typ rekursora jest jednak nieco bardziej zaawansowany. Å»eby zdefiniowaÄ‡
    funkcjÄ™ typu [Smok -> S], musimy mieÄ‡ nie tylko rzeczy w ksztaÅ‚cie
    konstruktorÃ³w [Smok]a, ale takÅ¼e w ksztaÅ‚cie konstruktorÃ³w [Zmok]a,
    gdyÅ¼ rekurencyjna struktura obu typÃ³w jest ze sobÄ… nierozerwalnie
    zwiÄ…zana. *)

Fixpoint Smok_rec
  (S Z : Type) (Wy : Z -> S) (Zm : S -> Z) (smok : Smok) : S :=
match smok with
    | Wysuszony zmok => Wy (Zmok_rec S Z Wy Zm zmok)
end

with Zmok_rec
  (S Z : Type) (Wy : Z -> S) (Zm : S -> Z) (zmok : Zmok) : Z :=
match zmok with
    | Zmoczony smok => Zm (Smok_rec S Z Wy Zm smok)
end.

(** Implementacja wymaga rekursji wzajemnej, ale poza nie jest jakoÅ›
    wybitnie groÅºna. *)

Definition Smok_ind_type : Type :=
  forall (S : Smok -> Type) (Z : Zmok -> Type)
    (Wy : forall zmok : Zmok, Z zmok -> S (Wysuszony zmok))
    (Zm : forall smok : Smok, S smok -> Z (Zmoczony smok)),
      forall smok : Smok, S smok.

Definition Zmok_ind_type : Type :=
  forall (S : Smok -> Type) (Z : Zmok -> Type)
    (Wy : forall zmok : Zmok, Z zmok -> S (Wysuszony zmok))
    (Zm : forall smok : Smok, S smok -> Z (Zmoczony smok)),
      forall zmok : Zmok, Z zmok.

Fixpoint Smok_ind
  (S : Smok -> Type) (Z : Zmok -> Type)
  (Wy : forall zmok : Zmok, Z zmok -> S (Wysuszony zmok))
  (Zm : forall smok : Smok, S smok -> Z (Zmoczony smok))
  (smok : Smok) : S smok :=
match smok with
    | Wysuszony zmok => Wy zmok (Zmok_ind S Z Wy Zm zmok)
end

with Zmok_ind
  (S : Smok -> Type) (Z : Zmok -> Type)
  (Wy : forall zmok : Zmok, Z zmok -> S (Wysuszony zmok))
  (Zm : forall smok : Smok, S smok -> Z (Zmoczony smok))
  (zmok : Zmok) : Z zmok :=
match zmok with
    | Zmoczony smok => Zm smok (Smok_ind S Z Wy Zm smok)
end.

(** MajÄ…c rekursor, napisanie typu reguÅ‚y indukcji jest banalne, podobnie
    jak jego implementacja. *)

End mutual.

(** *** Indeksy *)

Module index.

Inductive I : nat -> Type :=
    | c0 : bool -> I 0
    | c42 : nat -> I 42.

(** OstatniÄ… poznanÄ… przez nas innowacjÄ… sÄ… typy indeksowane. Tutaj rÃ³wnieÅ¼
    definiujemy za jednym zamachem (ekhem...) duÅ¼o typÃ³w, ale nie sÄ… one
    niezaleÅ¼ne jak w przypadku parametrÃ³w, lecz mogÄ… od siebie wzajemnie
    zaleÅ¼eÄ‡. SÅ‚owem, tak naprawdÄ™ definiujemy przez indukcjÄ™ funkcjÄ™
    typu [A_1 -> ... -> A_n -> Type/Prop], gdzie [A_i] to indeksy. *)

Definition I_case_very_nondep_type : Type :=
  forall (P : Type) (c0' : bool -> P) (c42' : nat -> P),
    forall n : nat, I n -> P.

Definition I_case_very_nondep
  (P : Type) (c0' : bool -> P) (c42' : nat -> P)
  {n : nat} (i : I n) : P :=
match i with
    | c0 b => c0' b
    | c42 n => c42' n
end.

(** MoÅ¼liwych reguÅ‚ analizy przypadkÃ³w mamy tutaj trochÄ™ wiÄ™cej niÅ¼ w
    przypadku parametrÃ³w. W powyÅ¼szej regule [P] nie zaleÅ¼y od indeksu
    [n : nat]... *)

Definition I_case_nondep_type : Type :=
  forall (P : nat -> Type) (c0' : bool -> P 0) (c42' : nat -> P 42),
    forall n : nat, I n -> P n.

Definition I_case_nondep
  (P : nat -> Type) (c0' : bool -> P 0) (c42' : nat -> P 42)
  {n : nat} (i : I n) : P n :=
match i with
    | c0 b => c0' b
    | c42 n => c42' n
end.

(** ... a w powyÅ¼szej tak. Jako, Å¼e indeksy zmieniajÄ… siÄ™ pomiÄ™dzy
    konstruktorami, kaÅ¼dy z nich musi kwantyfikowaÄ‡ je osobno (co akurat
    nie jest potrzebne w naszym przykÅ‚adzie, gdyÅ¼ jest zbyt prosty). *)

Definition I_case_dep_type : Type :=
  forall (P : forall n : nat, I n -> Type)
    (c0' : forall b : bool, P 0 (c0 b))
    (c42' : forall n : nat, P 42 (c42 n)),
      forall (n : nat) (i : I n), P n i.

Definition I_case_dep
  (P : forall n : nat, I n -> Type)
  (c0' : forall b : bool, P 0 (c0 b))
  (c42' : forall n : nat, P 42 (c42 n))
  (n : nat) (i : I n) : P n i :=
match i with
    | c0 b => c0' b
    | c42 n => c42' n
end.

(** OgÃ³lnie reguÅ‚a jest taka: reguÅ‚a niezaleÅ¼na (pierwsza) nie zaleÅ¼y od
    niczego, a zaleÅ¼na (trzecia) zaleÅ¼y od wszystkiego. ReguÅ‚a druga jest
    poÅ›rednia - ot, take ciepÅ‚e kluchy. *)

End index.

(** Nie zapomnijmy ponownie nakazaÄ‡ Coqowi generowania reguÅ‚ indukcji. *)
Set Elimination Schemes.

(** ** Indukcja-indukcja *)

Module ind_ind.

(** Po powtÃ³rce nadszedÅ‚ czas nowoÅ›ci. Zacznijmy od nazwy, ktÃ³ra jest iÅ›cie
    kretyÅ„ska: indukcja-indukcja. KaÅ¼dy rozsÄ…dny czÅ‚owiek zgodzi siÄ™,
    Å¼e duÅ¼o lepszÄ… nazwÄ… byÅ‚oby coÅ› w stylu "indukcja wzajemna indeksowana".

    Ta alternatywna nazwa rzuca sporo Å›wiatÅ‚a: indukcja-indukcja to poÅ‚Ä…czenie
    i uogÃ³lnienie mechanizmÃ³w definiowania typÃ³w wzajemnie induktywnych oraz
    indeksowanych typÃ³w induktywnych.

    Typy wzajemnie induktywne mogÄ… odnosiÄ‡ siÄ™ do siebie nawzajem, ale co
    to dokÅ‚adnie znaczy? Ano to, Å¼e konstruktory kaÅ¼dego typu mogÄ… braÄ‡
    argumenty wszystkch innych typÃ³w definiowanych jednoczeÅ›nie z nim. To
    jest clou caÅ‚ej sprawy: konstruktory.

    A co to ma do typÃ³w indeksowanych? Ano, zastanÃ³wmy siÄ™, co by siÄ™ staÅ‚o,
    gdybyÅ›my chcieli zdefiniowaÄ‡ przez wzajemnÄ… indukcjÄ™ typ [A] oraz rodzinÄ™
    typÃ³w [B : A -> Type]. OtÃ³Å¼ nie da siÄ™: konstruktory [A] mogÄ… odnosiÄ‡
    siÄ™ do [B] i vice-versa, ale [A] nie moÅ¼e byÄ‡ indeksem [B].

    Indukcja-indukcja to coÅ›, co... tam taram tam tam... pozwala wÅ‚aÅ›nie na
    to: moÅ¼emy jednoczeÅ›nie zdefiniowaÄ‡ typ i indeksowanÄ… nim rodzinÄ™ typÃ³w.
    I wszystko to ukryte pod takÄ… smutnÄ… nazwÄ…... lobby teoriotypowe nie
    chciaÅ‚o, Å¼ebyÅ› siÄ™ o tym dowiedziaÅ‚.

    Czas na przykÅ‚ad! *)

Fail

Inductive slist {A : Type} (R : A -> A -> Prop) : Type :=
    | snil : slist R
    | scons : forall (h : A) (t : slist A), ok h t -> slist A

with ok {A : Type} {R : A -> A -> Prop} : A -> slist R -> Prop :=
    | ok_snil : forall x : A, ok x snil
    | ok_scons :
        forall (h : A) (t : slist A) (p : ok h t) (x : A),
          R x h -> ok x (scons h t p).

(* ===> The reference slist was not found in the current environment. *)

(** Jako siÄ™ juÅ¼ wczeÅ›niej rzekÅ‚o, indukcja-indukcja nie jest wspierana
    przez Coqa - powyÅ¼sza definicja koÅ„czy siÄ™ informacjÄ… o bÅ‚Ä™dzie: Coq
    nie widzi [slist] kiedy czyta indeksy [ok] wÅ‚aÅ›nie dlatego, Å¼e nie
    dopuszcza on moÅ¼liwoÅ›ci jednoczesnego definiowania rodziny (w tym
    wypadku relacji) [ok] wraz z jednym z jej indeksÃ³w, [slist].

    BÄ™dziemy zatem musieli poradziÄ‡ sobie z przykÅ‚adem jakoÅ› inaczej -
    po prostu damy go sobie za pomocÄ… aksjomatÃ³w. Zanim jednak to zrobimy,
    omÃ³wimy go dokÅ‚adniej, gdyÅ¼ deklarowanie aksjomatÃ³w jest niebezpieczne
    i nie chcemy siÄ™ pomyliÄ‡.

    ZamysÅ‚em powyÅ¼szego przykÅ‚adu byÅ‚o zdefiniowanie typu list posortowanych
    [slist R], gdzie [R] peÅ‚ni rolÄ™ relacji porzÄ…dku, jednoczeÅ›nie z relacjÄ…
    [ok : A -> slist R -> Prop], gdzie [ok x l] wyraÅ¼a, Å¼e dostawienie [x]
    na poczÄ…tek listy posortowanej [l] daje listÄ™ posortowanÄ….

    PrzykÅ‚ad jest oczywiÅ›cie doÅ›Ä‡ bezsensowny, bo dokÅ‚adnie to samo moÅ¼na
    osiÄ…gnÄ…Ä‡ bez uÅ¼ywania indukcji-indukcji - wystarczy najpierw zdefiniowaÄ‡
    listy, a potem relacjÄ™ bycia listÄ… posortowanÄ…, a na koniec zapakowaÄ‡
    wszystko razem. Nie bÄ™dziemy siÄ™ tym jednak przejmowaÄ‡.

    Definicja [slist R] jest nastÄ™pujÄ…ca:
    - [snil] to lista pusta
    - [scons] robi posortowanÄ… listÄ™ z gÅ‚owy [h] i ogona [t] pod warunkiem, Å¼e
      dostanie teÅ¼ dowÃ³d zdania [ok h t] mÃ³wiÄ…cego, Å¼e moÅ¼na dostawiÄ‡ [h] na
      poczÄ…tek listy [t] *)

(** Definicja [ok] teÅ¼ jest banalna:
    - kaÅ¼dy [x : A] moÅ¼e byÄ‡ dostawiony do pustej listy
    - jeÅ¼eli mamy listÄ™ [scons h t p] oraz element [x], o ktÃ³rym wiemy,
      Å¼e jest mniejszy od [h], tzn. [R x h], to [x] moÅ¼e zostaÄ‡ dostawiony
      do listy [scons h t p] *)

(** Jak powinny wyglÄ…daÄ‡ reguÅ‚y rekursji oraz indukcji? Na szczÄ™Å›cie wciÄ…Å¼
    dziaÅ‚ajÄ… schematy, ktÃ³re wypracowaliÅ›my dotychczas.

    ReguÅ‚a rekursji mÃ³wi, Å¼e jeÅ¼eli znajdziemy w typie [P] coÅ› o ksztaÅ‚cie
    [slist R], a w relacji [Q] coÅ› o ksztaÅ‚cie [ok], to moÅ¼emy zdefiniowaÄ‡
    funkcjÄ™ [slist R -> P] oraz [forall (x : A) (l : slist R), ok x l -> Q].

    ReguÅ‚e indukcji moÅ¼na uzyskaÄ‡ dodajÄ…c tyle zaleÅ¼noÅ›ci, ile tylko zdoÅ‚amy
    unieÅ›Ä‡.

    Zobaczmy wiÄ™c, jak zrealizowaÄ‡ to wszystko za pomocÄ… aksjomatÃ³w. *)

Axioms
  (slist : forall {A : Type}, (A -> A -> Prop) -> Type)
  (ok : forall {A : Type} {R : A -> A -> Prop}, A -> slist R -> Prop).

(** Najpierw musimy zadeklarowaÄ‡ [slist], gdyÅ¼ wymaga tego typ [ok]. Obie
    definicje wyglÄ…dajÄ… dokÅ‚adnie tak, jak nagÅ‚Ã³wki w powyÅ¼szej definicji
    odrzuconej przez Coqa.

    WidaÄ‡ teÅ¼, Å¼e gdybyÅ›my chcieli zdefiniowaÄ‡ rodziny [A] i [B], ktÃ³re sÄ…
    nawzajem swoimi indeksami, to nie moglibyÅ›my tego zrobiÄ‡ nawet za pomocÄ…
    aksjomatÃ³w. Rodzi to pytanie o to, ktÃ³re dokÅ‚adnie definicje przez
    indukcjÄ™-indukcjÄ™ sÄ… legalne. OdpowiedÅº brzmi: nie wiem, ale moÅ¼e kiedyÅ›
    siÄ™ dowiem. *)

Axioms
  (snil : forall {A : Type} {R : A -> A -> Prop}, slist R)
  (scons :
    forall {A : Type} {R : A -> A -> Prop} (h : A) (t : slist R),
      ok h t -> slist R)
  (ok_snil :
    forall {A : Type} {R : A -> A -> Prop} (x : A), ok x (@snil _ R))
  (ok_scons :
    forall
      {A : Type} {R : A -> A -> Prop}
      (h : A) (t : slist R) (p : ok h t)
      (x : A), R x h -> ok x (scons h t p)).

(** NastÄ™pnie definiujemy konstruktory: najpierw konstruktory [slist], a
    potem [ok]. Musimy to zrobiÄ‡ w tej kolejnoÅ›ci, bo konstruktor [ok_snil]
    odnosi siÄ™ do [snil], a [ok_scons] do [scons].

    Znowu widzimy, Å¼e gdyby konstruktory obu typÃ³w odnosiÅ‚y siÄ™ do siebie
    nawzajem, to nie moglibyÅ›my zdefiniowaÄ‡ takiego typu aksjomatycznie. *)

Axiom
  (ind : forall
    (A : Type) (R : A -> A -> Prop)
    (P : slist R -> Type)
    (Q : forall (h : A) (t : slist R), ok h t -> Type)
    (Psnil : P snil)
    (Pscons :
      forall (h : A) (t : slist R) (p : ok h t),
        P t -> Q h t p -> P (scons h t p))
    (Qok_snil : forall x : A, Q x snil (ok_snil x))
    (Qok_scons :
      forall
        (h : A) (t : slist R) (p : ok h t)
        (x : A) (H : R x h),
          P t -> Q h t p -> Q x (scons h t p) (ok_scons h t p x H)),
    {f : (forall l : slist R, P l) &
    {g : (forall (h : A) (t : slist R) (p : ok h t), Q h t p) |
      f snil = Psnil /\
      (forall (h : A) (t : slist R) (p : ok h t),
        f (scons h t p) = Pscons h t p (f t) (g h t p)) /\
      (forall x : A,
        g x snil (ok_snil x) = Qok_snil x) /\
      (forall
        (h : A) (t : slist R) (p : ok h t)
        (x : A) (H : R x h),
          g x (scons h t p) (ok_scons h t p x H) =
          Qok_scons h t p x H (f t) (g h t p))
    }}).

(** Ugh, co za potfur. SprÃ³bujmy rozÅ‚oÅ¼yÄ‡ go na czynniki pierwsze.

    Przede wszystkim, Å¼eby za duÅ¼o nie pisaÄ‡, zobaczymy tylko reguÅ‚Ä™ indukcji.
    Teoretycznie powinny to byÄ‡ dwie reguÅ‚y (tak jak w przypadku [Smok]a i
    [Zmok]a) - jedna dla [slist] i jedna dla [ok], ale Å¼eby za duÅ¼o nie
    pisaÄ‡, moÅ¼emy zapisaÄ‡ je razem.

    Typ [A] i relacja [R] sÄ… parametrami obu definicji, wiÄ™c skwantyfikowane
    sÄ… na samym poczÄ…tku. Nasza reguÅ‚a pozwala nam zdefiniowaÄ‡ przez wzajemnÄ…
    rekursjÄ™ dwie funkcje, [f : forall l : slist R, P l] oraz
    [g : forall (h : A) (t : slist R) (p : ok h t), Q h t p]. Tak wiÄ™c [P]
    to kodziedzina [f], a [Q] - [g].

    Teraz potrzebujemy rozwaÅ¼yÄ‡ wszystkie moÅ¼liwe przypadki - tak jak przy
    dopasowaniu do wzorca. Przypadek [snil] jest doÅ›Ä‡ banalny. Przypadek
    [scons] jest trochÄ™ ciÄ™Å¼szy. Przede wszystkim chcemy, Å¼eby konkluzja
    byÅ‚a postaci [P (scons h t p)], ale jak powinny wyglÄ…daÄ‡ hipotezy
    indukcyjne?

    Jedyna sÅ‚uszna odpowiedÅº brzmi: odpowiadajÄ… one typom wszystkich moÅ¼liwych
    wywoÅ‚aÅ„ rekurencyjnych [f] i [g] na strukturalnych podtermach
    [scons h t p]. Jedynymi typami speÅ‚niajÄ…cymi te warunki sÄ… [P t] oraz
    [Q h t p], wiÄ™c dajemy je sobie jako hipotezy indukcyjne.

    Przypadki dla [Q] wyglÄ…dajÄ… podobnie: [ok_snil] jest banalne, a dla
    [ok_scons] konkluzja musi byÄ‡ jedynej sÅ‚usznej postaci, a hipotezami
    indukcyjnymi jest wszystko, co pasuje.

    W efekcie otrzymujemy dwie funkcje, [f] i [g]. Tym razem nastÄ™puje jednak
    maÅ‚y twist: poniewaÅ¼ nasza definicja jest aksjomatyczna, zagwarantowaÄ‡
    musimy sobie takÅ¼e reguÅ‚y obliczania, ktÃ³re dotychczas byÅ‚y zamilaczne,
    bo wynikaÅ‚y z definicji przez dopasowanie do wzorca. Teraz wszystkie te
    "dopasowania" musimy napisaÄ‡ rÄ™cznie w postaci odpowiednio
    skwantyfikowanych rÃ³wnaÅ„. Widzimy wiÄ™c, Å¼e [Psnil], [Pscons], [Qok_snil]
    i [Qok_scons] odpowiadajÄ… klauzulom w dopasowaniu do wzorca.

    Ufff... udaÅ‚o siÄ™. Tak spreparowanÄ… definicjÄ… aksjomatycznÄ… moÅ¼emy siÄ™
    jako-tako posÅ‚ugiwaÄ‡: *)

Definition rec'
  {A : Type} {R : A -> A -> Prop}
  (P : Type) (snil' : P) (scons' : A -> P -> P) :
  {f : slist R -> P |
    f snil = snil' /\
    forall (h : A) (t : slist R) (p : ok h t),
      f (scons h t p) = scons' h (f t)
  }.
Proof.
  destruct
  (
    ind
    A R
    (fun _ => P) (fun _ _ _ => True)
    snil' (fun h _ _ t _ => scons' h t)
    (fun _ => I) (fun _ _ _ _ _ _ _ => I)
  )
  as (f & g & H1 & H2 & H3 & H4).
  exists f. split.
    exact H1.
    exact H2.
Defined.

(** MoÅ¼emy na przykÅ‚ad doÅ›Ä‡ Å‚atwo zdefiniowaÄ‡ niezaleÅ¼ny rekursor tylko dla
    [slist], nie odnoszÄ…cy siÄ™ w Å¼aden sposÃ³b do [ok]. Widzimy jednak, Å¼e
    "programowanie" w taki aksjomatyczny sposÃ³b jest doÅ›Ä‡ ciÄ™Å¼kie - zamiast
    eleganckich dopasowaÅ„ do wzorca musimy rÄ™cznie wpisywaÄ‡ argumenty do
    reguÅ‚y indukcyjnej. *)

Require Import List.
Import ListNotations.

Definition toList'
  {A : Type} {R : A -> A -> Prop} :
  {f : slist R -> list A |
    f snil = [] /\
    forall (h : A) (t : slist R) (p : ok h t),
      f (scons h t p) = h :: f t
  }.
Proof.
  exact (rec' (list A) [] cons).
Defined.

Definition toList
  {A : Type} {R : A -> A -> Prop} : slist R -> list A :=
    proj1_sig toList'.

(** UÅ¼ywanie takiego rekursora jest juÅ¼ duÅ¼o prostsze, co ilustruje powyÅ¼szy
    przykÅ‚ad funkcji, ktÃ³ra zapomina o tym, Å¼e lista jest posortowana i daje
    nam zwykÅ‚Ä… listÄ™.

    PrzykÅ‚adowe posortowane listy wyglÄ…dajÄ… tak: *)

Definition slist_01 : slist le :=
  scons 0
    (scons 1
      snil
      (ok_snil 1))
    (ok_scons 1 snil (ok_snil 1) 0 (le_S 0 0 (le_n 0))).

(** Niezbyt piÄ™kna, prawda? *)

Compute toList slist_01.

(** Utrapieniem jest teÅ¼ to, Å¼e nasza funkcja siÄ™ nie oblicza. Jest tak, bo
    zostaÅ‚a zdefiniowana za pomocÄ… reguÅ‚y indukcji, ktÃ³ra jest aksjomatem.
    Aksjomaty zaÅ›, jak wiadomo, nie obliczajÄ… siÄ™.

    Wyniku powyÅ¼szego wywoÅ‚ania nie bÄ™dÄ™ nawet wklejaÅ‚, gdyÅ¼ jest naprawdÄ™
    ohydny. *)

Lemma toList_slist_01_result :
  toList slist_01 = [0; 1].
Proof.
  unfold toList, slist_01.
  destruct toList' as (f & H1 & H2); cbn.
  rewrite 2!H2, H1. reflexivity.
Qed.

(** Najlepsze, co moÅ¼emy osiÄ…gnÄ…Ä‡, majÄ…c takÄ… definicjÄ™, to udowodnienie, Å¼e
    jej wynik faktycznie jest taki, jak siÄ™ spodziewamy. *)

(** **** Ä†wiczenie *)

(** Zdefiniuj dla list posortowanych funkcjÄ™ [slen], ktÃ³ra liczy ich dÅ‚ugoÅ›Ä‡.
    Udowodnij oczywiste twierdzenie wiÄ…Å¼Ä…ce ze sobÄ… [slen], [toList] oraz
    [length]. *)

(* begin hide *)

Definition slen'
  {A : Type} {R : A -> A -> Prop} :
  {f : slist R -> nat |
    f snil = 0 /\
    forall (h : A) (t : slist R) (p : ok h t),
      f (scons h t p) = S (f t)}.
Proof.
  exact (rec' nat 0 (fun _ n => S n)).
Defined.

Definition slen {A : Type} {R : A -> A -> Prop} : slist R -> nat :=
  proj1_sig slen'.

Lemma slen_toList_length :
  forall {A : Type} {R : A -> A -> Prop} (l : slist R),
    slen l = length (toList l).
Proof.
  intros A R.
  eapply (projT1 (
    ind A R (fun l => slen l = length (toList l))
      (fun _ _ _ => True)
      _ _
      (fun _ => I)
      (fun _ _ _ _ _ _ _ => I))).
  Unshelve.
  all: cbn; unfold slen, toList;
  destruct slen' as (f & Hf1 & Hf2), toList' as (g & Hg1 & Hg2); cbn.
    rewrite Hf1, Hg1. cbn. reflexivity.
    intros. rewrite Hf2, Hg2, H. cbn. reflexivity.
Qed.

(* end hide *)

(** **** Ä†wiczenie *)

(** Udowodnij, Å¼e przykÅ‚ad faktycznie jest bez sensu: zdefiniuje relacjÄ™
    [sorted : (A -> A -> Prop) -> list A -> Prop], gdzie [sorted R l]
    oznacza, Å¼e lista [l] jest posortowana wedÅ‚ug porzÄ…dku [R]. UÅ¼ywajÄ…c
    [sorted] zdefiniuj typ list posortowanych [slist' R], a nastÄ™pnie znajdÅº
    dwie funkcje [f : slist R -> slist' R] i [f : slist' R -> slist R],
    ktÃ³re sÄ… swoimi odwrotnoÅ›ciami. *)

(* begin hide *)

Inductive sorted {A : Type} (R : A -> A -> Prop) : list A -> Prop :=
    | sorted_nil : sorted R []
    | sorted_singl : forall x : A, sorted R [x]
    | sorted_cons :
        forall (x y : A) (t : list A),
          R x y -> sorted R (y :: t) -> sorted R (x :: y :: t).

Arguments sorted_nil {A R}.
Arguments sorted_singl {A R} _.
Arguments sorted_cons {A R x y t} _ _.

Definition slist' {A : Type} (R : A -> A -> Prop) : Type :=
  {l : list A | sorted R l}.

Lemma toList_sorted :
  forall {A : Type} {R : A -> A -> Prop} (l : slist R),
    sorted R (toList l).
Proof.
  intros A R.
  refine (projT1 (ind
    A R
    (fun l => sorted R (toList l))
    (fun x t _ =>
      t = snil \/
      exists (h : A) (t' : slist R) (p : ok h t'),
        t = scons h t' p /\ R x h)
    _ _ _ _)).
  1-2: unfold toList; destruct toList' as (f & H1 & H2); cbn.
    rewrite H1. constructor.
    intros. rewrite H2. decompose [or ex and] H0; clear H0; subst.
      rewrite H1. constructor.
      rewrite H2 in *. constructor; assumption.
    intros. left. reflexivity.
    intros. decompose [or ex and] H1; clear H1; subst.
      right. exists h, snil, p. split; trivial.
      right. exists h, (scons x0 x1 x2), p. split; trivial.
Qed.

(** TODO *)

(* end hide *)

(** **** Ä†wiczenie *)

(** Å»eby przekonaÄ‡ siÄ™, Å¼e przykÅ‚ad byÅ‚ naprawdÄ™ bezsensowny, zdefiniuj
    rodzinÄ™ typÃ³w [blist : (A -> A -> Prop) -> A -> Type], gdzie elementami
    [blist R x] sÄ… listy posortowane, ktÃ³rych elementy sÄ… [R]-wiÄ™ksze od [x].
    UÅ¼yj [blist] do zdefiniowania typu [slist'' R], a nastÄ™pnie udowodnij,
    Å¼e [slist R] i [slist'' R] sÄ… sobie rÃ³wnowaÅ¼ne. *)

End ind_ind.

(** **** *)

(** Na koniec wypadaÅ‚oby jeszcze wspomnieÄ‡, do czego tak naprawdÄ™ moÅ¼na w
    praktyce uÅ¼yÄ‡ indukcji-indukcji (definiowanie list posortowanych nie
    jest jednÄ… z tych rzeczy, o czym przekonaÅ‚eÅ› siÄ™ w Ä‡wiczeniach). OtÃ³Å¼
    najciekawszym przykÅ‚adem wydaje siÄ™ byÄ‡ formalizacja teorii typÃ³w, czyli,
    parafrazujÄ…c, implementacja Coqa w Coqu.

    Å»eby siÄ™ za to zabraÄ‡, musimy zdefiniowaÄ‡ konteksty, typy i termy, a
    takÅ¼e relacje konwertowalnoÅ›ci dla typÃ³w i termÃ³w. SÄ… tutaj moÅ¼liwe dwa
    podejÅ›cia:
    - Curry'ego (ang. Curry style lub mÄ…drzej extrinsic style) - staramy
      siÄ™ definiowaÄ‡ wszystko osobno, a potem zdefiniowaÄ‡ relacje "term x
      jest typu A w kontekÅ›cie Î“", "typ A jest poprawnie sformowany w
      kontekÅ›cie Î“" etc. NajwaÅ¼niejszÄ… cechÄ… tego sposobu jest to, Å¼e
      moÅ¼emy tworzyÄ‡ termy, ktÃ³rym nie da siÄ™ przypisaÄ‡ Å¼adnego typu oraz
      typy, ktÃ³re nie sÄ… poprawnie sformowane w Å¼adnym kontekÅ›cie.
    - Churcha (ang. Church style lub mÄ…drzej intrinsic style) - definiujemy
      wszystko na raz w jednej wielkiej wzajemnej indukcji. ZamiastÃ³w
      typÃ³w definiujemy od razu predykat "typ A jest poprawnie sformowany
      w kontekÅ›cie Î“", a zamiast termÃ³w definiujemy od razu relacjÄ™
      "term x ma typ A w kontekÅ›cie Î“". ParafrazujÄ…c - wszystkie termy,
      ktÃ³re jesteÅ›my w stanie skonstruowaÄ‡, sÄ… poprawnie typowane (a
      wszystkie typy poprawnie sformowane w swoich kontekstach). *)

(** Zamiast tyle gadaÄ‡ zobaczmy, jak mogÅ‚oby to wyglÄ…daÄ‡ w Coqu. OczywiÅ›cie
    bÄ™dÄ… to same nagÅ‚Ã³wki, bo podanie tutaj peÅ‚nej definicji byÅ‚oby mocno
    zaciemniajÄ…cym przegiÄ™ciem. *)

(*
Inductive Ctx : Type := ...

with Ty : Ctx -> Type := ...

with Term : forall Î“ : Ctx, Ty Î“ -> Type := ...

with TyConv : forall Î“ : Ctx, Ty Î“ -> Ty Î“ -> Prop := ...

with
  TermConv :
    forall (Î“ : Ctx) (A : Ty Î“),
      Term Î“ A -> Term Î“ A -> Prop := ...
*)

(** NagÅ‚Ã³wki w tej definicji powinniÅ›my interpretowaÄ‡ tak:
    - [Ctx] to typ reprezentujÄ…cy konteksty.
    - [Ty] ma reprezentowaÄ‡ typy, ale nie jest to typ, lecz rodzina typÃ³w
      indeksowana kontekstami - kaÅ¼dy typ jest typem w jakimÅ› kontekÅ›cie,
      np. [list A] jest typem w kontekÅ›cie zawierajÄ…cym [A : Type], ale
      nie jest typem w pustym kontekÅ›cie.
    - [Term] ma reprezentowaÄ‡ termy, ale nie jest to typ, lecz rodzina typÃ³w
      indeksowana kontekstami i typami - kaÅ¼dy term ma jakiÅ› typ, a typy,
      jak juÅ¼ siÄ™ rzekÅ‚o, zawsze sÄ… typami w jakimÅ› kontekÅ›cie. PrzykÅ‚ad:
      jeÅ¼eli [x] jest zmiennÄ…, to [cons x nil] jest typu [list A] w
      kontekÅ›cie, w ktÃ³rym [x] jest typu [A], ale nie ma Å¼adnego typu (i nie
      jest nawet poprawnym termem) w kontekÅ›cie pustym ani w Å¼adnym, w ktÃ³rym
      nie wystÄ™puje [x].
    - [TyConv Î“ A B] zachodzi, gdy typy [A] i [B] sÄ… konwertowalne, czyli
      obliczajÄ… siÄ™ do tego samego (relacja taka jest potrzebna, gdyÅ¼ w Coqu
      i ogÃ³lnie w teorii typÃ³w wystÄ™powaÄ‡ mogÄ… takie typy jak [if true then
      nat else bool], ktÃ³ry jest konwertowalny z [nat]). Jako siÄ™ rzekÅ‚o,
      typy zawsze wystÄ™pujÄ… w kontekÅ›cie, wiÄ™c konwertowalne mogÄ… byÄ‡ teÅ¼
      tylko w kontekÅ›cie.
    - [TermConv Î“ A x y] znaczy, Å¼e termy [x] i [y] sÄ… konwertowalne,
      np. [if true then 42 else 0] jest konwertowalne z [42]. PoniewaÅ¼ kaÅ¼dy
      term ciÄ…gnie za sobÄ… swÃ³j typ, [TermConv] ma jako indeks typ [A], a
      poniewaÅ¼ typ ciÄ…gnie za sobÄ… kontekst, indeksem [TermConv] jest takÅ¼e
      [Î“]. *)

(** Jak widaÄ‡, indukcji-indukcji jest w powyÅ¼szym przykÅ‚adzie na pÄ™czki -
    jest ona wrÄ™cz teleskopowa, gdyÅ¼ [Ctx] jest indeksem [Ty], [Ctx] i [Ty]
    sÄ… indeksami [Term], a [Ctx], [Ty] i [Term] sÄ… indeksami [TermConv].

    CÃ³Å¼, to by byÅ‚o na tyle w tym temacie. Åawa oburzonych wyraÅ¼a w tym
    momencie swoje najwyÅ¼sze oburzenie na brak indukcji-indukcji w Coqu:
    https://www.sadistic.pl/lawa-oburzonych-vt22270.htm

    Jednak uszy do gÃ³ry - istniejÄ… juÅ¼ jÄ™zyki, ktÃ³re jakoÅ› sobie radzÄ… z
    indukcjÄ…-indukcjÄ…. Jednym z nich jest wspomniana we wstÄ™pie Agda,
    ktÃ³rÄ… moÅ¼na znaleÅºÄ‡ tu:
    https://agda.readthedocs.io/en/latest/ *)

(** **** Ä†wiczenie *)

(** Typ stert binarnych [BHeap R], gdzie [R : A -> A -> Prop] jest relacjÄ…
    porzÄ…dku, skÅ‚ada siÄ™ z drzew, ktÃ³re mogÄ… byÄ‡ albo puste, albo byÄ‡ wÄ™zÅ‚em
    przechowujÄ…cym wartoÅ›Ä‡ [v : A] wraz z dwoma poddrzewami [l r : BHeap R],
    przy czym [v] musi byÄ‡ [R]-wiÄ™ksze od wszystkich elementÃ³w [l] oraz [r].

    UÅ¼yj indukcji-indukcji, Å¼eby zdefiniowaÄ‡ jednoczeÅ›nie typ [BHeap R] oraz
    relacjÄ™ [ok], gdzie [ok v h] zachodzi, gdy [v] jest [R]-wiÄ™ksze od
    wszystkich elementÃ³w [h].

    Najpierw napisz pseudodefinicjÄ™, a potem przetÅ‚umacz jÄ… na odpowiedni
    zestaw aksjomatÃ³w.

    NastÄ™pnie uÅ¼yj swojej aksjomatycznej definicji, aby zdefiniowaÄ‡ funkcjÄ™
    [mirror], ktÃ³ra tworzy lustrzane odbicie sterty [h : BHeap R]. WskazÃ³wka:
    prawdopodobnie nie uda ci siÄ™ zdefiniowaÄ‡ [mirror]. ZastanÃ³w siÄ™,
    dlaczego jest tak trudno. *)

(* begin hide *)

Module BHeap.

Fail

Inductive BHeap {A : Type} (R : A -> A -> Prop) : Type :=
    | E : BHeap R
    | N : forall (v : A) (l r : BHeap R), ok v l -> ok v r -> BHeap R

with ok {A : Type} {R : A -> A -> Prop} : A -> BHeap R -> Prop :=
    | ok_E : forall v : A, ok v E
    | ok_N :
        forall (v x : A) (l r : BHeap A) (pl : ok x l) (pr : ok x r),
          R x v -> ok v (N x l r pl pr).

Axioms
  (BHeap : forall {A : Type} (R : A -> A -> Prop), Type)
  (ok : forall {A : Type} {R : A -> A -> Prop}, A -> BHeap R -> Prop)
  (E : forall {A : Type} {R : A -> A -> Prop}, BHeap R)
  (N :
    forall
      {A : Type} {R : A -> A -> Prop}
      (v : A) (l r : BHeap R),
        ok v l -> ok v r -> BHeap R)
  (ok_E : forall {A : Type} {R : A -> A -> Prop} (v : A), ok v (@E _ R))
  (ok_N :
    forall
      {A : Type} {R : A -> A -> Prop}
      (v x : A) (l r : BHeap R) (pl : ok x l) (pr : ok x r),
        R x v -> ok v (N x l r pl pr))
  (ind : forall
    (A : Type) (R : A -> A -> Prop)
    (P : BHeap R -> Type)
    (Q : forall (v : A) (h : BHeap R), ok v h -> Type)
    (PE : P E)
    (PN : forall (v : A) (l r : BHeap R) (pl : ok v l) (pr : ok v r),
            P l -> P r -> Q v l pl -> Q v r pr -> P (N v l r pl pr))
    (Qok_E :
      forall v : A, Q v E (ok_E v))
    (Qok_N :
      forall
        (v x : A) (l r : BHeap R) (pl : ok x l) (pr : ok x r) (H : R x v),
          P l -> P r -> Q x l pl -> Q x r pr ->
            Q v (N x l r pl pr) (ok_N v x l r pl pr H)),
    {f : forall h : BHeap R, P h &
    {g : forall (v : A) (h : BHeap R) (p : ok v h), Q v h p |
      f E = PE /\
      (forall (v : A) (l r : BHeap R) (pl : ok v l) (pr : ok v r),
        f (N v l r pl pr) =
        PN v l r pl pr (f l) (f r) (g v l pl) (g v r pr)) /\
      (forall v : A, g v E (ok_E v) = Qok_E v) /\
      (forall
        (v x : A) (l r : BHeap R) (pl : ok x l) (pr : ok x r) (H : R x v),
          g v (N x l r pl pr) (ok_N v x l r pl pr H) =
          Qok_N v x l r pl pr H (f l) (f r) (g x l pl) (g x r pr))
    }}).

(*
Definition mirror
  {A : Type} {R : A -> A -> Prop}
  :
  {f : BHeap R -> BHeap R &
  {g : forall {v : A} {h : BHeap R}, ok v h -> ok v (f h) |
    f E = E /\
    (forall (v : A) (l r : BHeap R) (pl : ok v l) (pr : ok v r),
      f (N v l r pl pr) = N v (f r) (f l) (g v r pr) (g v l pl)) /\
    (forall v : A, g v E (ok_E v) = ok_E v)
  }}.
Proof.
  Check (
    ind
      A R
      (fun _ => BHeap R)). 
      (fun _ _ _ => True)
      E (fun v _ _ _ _ l' r' pl' pr' => N v r' l' pr' pl')
      (fun _ => I) (fun _ _ _ _ _ _ _ _ _ _ _ => I)
  ).
*)

End BHeap.

(* end hide *)

(** **** Ä†wiczenie *)

(** Typ drzew wyszukiwaÅ„ binarnych [BST R], gdzie [R : A -> A -> Prop] jest
    relacjÄ… porzÄ…dku, skÅ‚ada siÄ™ z drzew, ktÃ³re mogÄ… byÄ‡ albo puste, albo byÄ‡
    wÄ™zÅ‚em przechowujÄ…cym wartoÅ›Ä‡ [v : A] wraz z dwoma poddrzewami
    [l r : BST R], przy czym [v] musi byÄ‡ [R]-wiÄ™ksze od wszystkich elemtnÃ³w
    [l] oraz [R]-mniejsze od wszystkich elementÃ³w [r].

    UÅ¼yj indukcji-indukcji, Å¼eby zdefiniowaÄ‡ jednoczeÅ›nie typ [BST R] wraz
    z odpowiednimi relacjami zapewniajÄ…cymi poprawnoÅ›Ä‡ konstrukcji wÄ™zÅ‚a.
    WyprÃ³buj trzy podejÅ›cia:
    - jest jedna relacja, [oklr], gdzie [oklr v l r] oznacza, Å¼e z [v], [l] i
      [r] moÅ¼na zrobiÄ‡ wÄ™zeÅ‚
    - sÄ… dwie relacje, [okl] i [okr], gdzie [okl v l] oznacza, Å¼e [v] jest
      [R]-wiÄ™ksze od wszystkich elementÃ³w [l], zaÅ› [okr v r], Å¼e [v] jest
      [R]-mniejsze od wszystkich elementÃ³w [r]
    - jest jedna relacja, [ok], gdzie [ok v t] oznacza, Å¼e [v] jest
      [R]-mniejsze od wszystkich elementÃ³w [t] *)

(** Najpierw napisz pseudodefinicjÄ™, a potem przetÅ‚umacz jÄ… na odpowiedni
    zestaw aksjomatÃ³w.

    NastÄ™pnie uÅ¼yj swojej aksjomatycznej definicji, aby zdefiniowaÄ‡ funkcjÄ™
    [mirror], ktÃ³ra tworzy lustrzane odbicie drzewa [t : BST R]. WskazÃ³wka:
    doÅ›Ä‡ moÅ¼liwe, Å¼e ci siÄ™ nie uda. *)

(* begin hide *)

(** TODO: przetÅ‚umacz na odpowiedni zestaw aksjomatÃ³w, zdefiniuj mirror *)

Module BST.

Fail

Inductive BST {A : Type} (R : A -> A -> Prop) : Type :=
    | E : BST R
    | N : forall (v : A) (l r : BST R), ok v l r -> BST R

with ok {A : Type} {R : A -> A -> Prop} : A -> BST R -> BST R -> Prop :=
    | ok_EE : forall v : A, ok v E E
    | ok_EN :
        forall (v vl : A) (ll lr : BST R),
          R vl v -> ok v (N vl ll lr) E
    | ok_NE :
        forall (v vr : A) (rl rr : BST R),
          R v vr -> ok v E (N vr rl rr)
    | ok_NN :
        forall (v vl vr : A) (ll lr rl rr : BST R),
          R vl v -> R v vr -> ok v (N vl ll lr) (N vr rl rr).

Fail

Inductive BST {A : Type} (R : A -> A -> Prop) : Type :=
    | E : BST R
    | N : forall (v : A) (l r : BST R), okl v l -> okr v r -> BST R

with okl {A : Type} {R : A -> A -> Prop} : A -> BST R -> Prop :=
    | okl_E : forall v : A, okl v E
    | okl_N :
        forall (v vl : A) (ll lr : BST R),
          R vl v -> okl v (N vl ll lr)

with okr {A : Type} {R : A -> A -> Prop} : A -> BST R -> Prop :=
    | okr_E : forall v : V, okr v E
    | okr_N :
        forall (v vr : A) (rl rr : BST R),
          R v vr -> okr v (N vr rl rr).

Definition inv {A : Type} (R : A -> A -> Prop) : A -> A -> Prop :=
  fun x y => R y x.

Fail

Inductive BST {A : Type} (R : A -> A -> Prop) : Type :=
    | E : BST R
    | N : forall (v : A) (l r : BST R),
            @ok A R v l -> @ok A (inv R) v r -> BST R

with ok {A : Type} {R : A -> A -> Prop} : A -> BST R -> Prop :=
    | ok_E : forall v : A, ok v E
    | ok_N :
        forall (v x : A) (l r : BST R),
          R x v -> ok v (N x l r).

End BST.

(* end hide *)

(** ** Indukcja-rekursja *)

Module ind_rec.

(** A oto kolejny potfur do naszej kolekcji: indukcja-rekursja. Nazwa, choÄ‡
    brzmi tak gÅ‚upio, jak "indukcja-indukcja", niesie ze sobÄ… jednak duÅ¼o
    wiÄ™cej wyobraÅºni: indukcja-rekursja pozwala nam jednoczeÅ›nie definiowaÄ‡
    typy induktywne oraz operujÄ…ce na nich funkcje rekurencyjne.

    Co to dokÅ‚adnie znaczy? Dotychczas nasz modus operandi wyglÄ…daÅ‚ tak, Å¼e
    najpierw definiowaliÅ›my jakiÅ› typ induktywny, a potem przez rekursjÄ™
    definiowaliÅ›my operujÄ…ce na nim funkcje, np:
    - najpierw zdefiniowaliÅ›my typ [nat], a potem dodawanie, mnoÅ¼enie etc.
    - najpierw zdefiniowaliÅ›my typ [list A], a potem [app], [rev] etc. *)

(** Dlaczego mielibyÅ›my chcieÄ‡ definiowaÄ‡ typ i funkcjÄ™ jednoczeÅ›nie? Dla
    tego samego, co zawsze, czyli zaleÅ¼noÅ›ci - indukcja-rekursja pozwala,
    Å¼eby definicja typu odnosiÅ‚a siÄ™ do funkcji, ktÃ³ra to z kolei jest
    zdefiniowana przez rekursjÄ™ strukturalnÄ… po argumencie o tym typie.

    Zobaczmy dobrze nam juÅ¼ znany bezsensowny przykÅ‚ad, czyli listy
    posortowane, tym razem zaimplementowane za pomocÄ… indukcji-rekursji. *)

(*
Inductive slist {A : Type} (R : A -> A -> bool) : Type :=
    | snil : slist R
    | scons :
        forall (h : A) (t : slist R), ok h t = true -> slist R

with

Definition ok
  {A : Type} {R : A -> A -> bool} (x : A) (t : slist R) : bool :=
match t with
    | snil => true
    | scons h _ _ => R x h
end.
*)

(** Coq niestety nie wspiera indukcji-rekursji, a prÃ³ba napisania powyÅ¼szej
    definicji koÅ„czy siÄ™ bÅ‚Ä™dem skÅ‚adni, przy ktÃ³rym nie pomaga nawet komenda
    [Fail]. Podobnie jak poprzednio, pomoÅ¼emy sobie za pomocÄ… aksjomatÃ³w,
    jednak najpierw przeÅ›ledÅºmy definicjÄ™.

    Typ slist dziaÅ‚a nastÄ™pujÄ…co:
    - [R] to jakiÅ› porzÄ…dek. ZauwaÅ¼, Å¼e tym razem [R : A -> A -> bool], a
      wiÄ™c porzÄ…dek jest reprezentowany przez funkcjÄ™, ktÃ³ra go rozstrzyga
    - [snil] to lista pusta
    - [scons h t p] to lista z gÅ‚owÄ… [h] i ogonem [t], zaÅ› [p : ok h t = true]
      to dowÃ³d na to, Å¼e dostawienie [h] przed [t] daje listÄ™ posortowanÄ…. *)

(** Tym razem jednak [ok] nie jest relacjÄ…, lecz funkcjÄ… zwracajÄ…cÄ… [bool],
    ktÃ³ra dziaÅ‚a nastÄ™pujÄ…co:
    - dla [snil] zwrÃ³Ä‡ [true] - kaÅ¼de [x : A] moÅ¼na dostawiÄ‡ do listy pustej
    - dla [scons h _ _] zwrÃ³Ä‡ wynik porÃ³wnania [x] z [h] *)

(** IstotÄ… mechanizmu indukcji-rekursji w tym przykÅ‚adzie jest to, Å¼e [scons]
    wymaga dowodu na to, Å¼e funkcja [ok] zwraca [true], podczas gdy funkcja
    ta jest zdefiniowana przez rekursjÄ™ strukturalnÄ… po argumencie typu
    [slist R].

    UÅ¼ycie indukkcji-rekursji do zaimplementowania [slist] ma swoje zalety:
    dla konkretnych list (zÅ‚oÅ¼onych ze staÅ‚ych, a nie ze zmiennych) dowody
    [ok h t = true] bÄ™dÄ… postaci [eq_refl], bo [ok] po prostu obliczy siÄ™
    do [true]. W przypadku indukcji-indukcji dowody na [ok h t] byÅ‚y caÅ‚kiem
    sporych rozmiarÃ³w drzewami. Innymi sÅ‚owy, udaÅ‚o nam siÄ™ zastÄ…piÄ‡ czÄ™Å›Ä‡
    termu obliczeniami. Ten intrygujÄ…cy motyw jeszcze siÄ™ w przyszÅ‚oÅ›ci
    pojawi, gdy omawiaÄ‡ bÄ™dziemy dowÃ³d przez reflekcjÄ™.

    DosyÄ‡ gadania! Zobaczmy, jak zakodowaÄ‡ powyÅ¼szÄ… definicjÄ™ za pomocÄ…
    aksjomatÃ³w. *)

Axioms
  (slist : forall {A : Type}, (A -> A -> bool) -> Type)
  (ok :
    forall {A : Type} {R : A -> A -> bool}, A -> slist R -> bool)
  (snil :
    forall {A : Type} {R : A -> A -> bool}, slist R)
  (scons :
    forall
      {A : Type} {R : A -> A -> bool}
      (h : A) (t : slist R),
        ok h t = true -> slist R)
  (ok_snil :
    forall {A : Type} {R : A -> A -> bool} (x : A),
      ok x (@snil _ R) = true)
  (ok_scons :
    forall
      {A : Type} {R : A -> A -> bool}
      (x h : A) (t : slist R) (p : ok h t = true),
        ok x (scons h t p) = R x h).

(** Najpierw musimy zadeklarowaÄ‡ [slist], a nastÄ™pnie [ok], gdyÅ¼ typ [ok]
    zaleÅ¼y od [slist]. NastÄ™pnym krokiem jest zadeklarowanie konstruktorÃ³w
    [slist], a pÃ³Åºniej rÃ³wnaÅ„ definiujÄ…cych funkcjÄ™ [ok] - koniecznie w tej
    kolejnoÅ›ci, gdyÅ¼ rÃ³wnania zaleÅ¼Ä… od konstruktorÃ³w.

    Jak widaÄ‡, aksjomaty sÄ… bardzo proste i sprowadzajÄ… siÄ™ do przepisania
    powyÅ¼szej definicji odrzuconej przez Coqa. *)

Axiom
  ind : forall
    (A : Type) (R : A -> A -> bool)
    (P : slist R -> Type)
    (Psnil : P snil)
    (Pscons :
      forall (h : A) (t : slist R) (p : ok h t = true),
        P t -> P (scons h t p)),
    {f : forall l : slist R, P l |
      f snil = Psnil /\
      (forall (h : A) (t : slist R) (p : ok h t = true),
        f (scons h t p) = Pscons h t p (f t))}.

(** Innym zyskiem z uÅ¼ycia indukcji-rekursji jest postaÄ‡ reguÅ‚y indukcyjnej.
    Jest ona duÅ¼o prostsza, niÅ¼ w przypadku indukcji-indukcji, gdyÅ¼ teraz
    definiujemy tylko jeden typ, zaÅ› towarzyszÄ…ca mu funkcja nie wymaga w
    regule niczego specjalnego - po prostu pojawia siÄ™ w niej tam, gdzie
    spodziewamy siÄ™ jej po definicji [slist], ale nie robi niczego
    ponad to. MoÅ¼e to sugerowaÄ‡, Å¼e zamiast indukcji-indukcji, o ile to
    moÅ¼liwe, lepiej jest uÅ¼ywaÄ‡ indukcji-rekursji, a predykaty i relacje
    definiowaÄ‡ przez rekursjÄ™.

    PowyÅ¼szÄ… reguÅ‚Ä™ moÅ¼emy odczytaÄ‡ nastÄ™pujÄ…co:
    - [A : Type] i [R : A -> A -> bool] to parametry [slist], wiÄ™c muszÄ… siÄ™
      pojawiÄ‡
    - [P : slist R -> Type] to przeciwdziedzina funkcji definiowanej za
      pomocÄ… reguÅ‚y
    - [Psnil] to wynik funkcji dla [snil]
    - [Pscons] produkuje wynik funkcji dla [scons h t p] z hipotezy
      indukcyjnej/wywoÅ‚ania rekurencyjnego dla [t]
    - [f : forall l : slist R, P l] to funkcja zdefiniowana przez reguÅ‚Ä™,
      zaÅ› rÃ³wnania formalizujÄ… to, co zostaÅ‚o napisane powyÅ¼ej o [Psnil]
      i [Pscons] *)

(** Termy induktywno-rekurencyjnego [slist R] wyglÄ…dajÄ… nastÄ™pujÄ…co
    (najpierw definiujemy sobie funkcjÄ™ rozstrzygajÄ…cÄ… standardowy
    porzÄ…dek na liczbach naturalnych): *)

Fixpoint leb (n m : nat) : bool :=
match n, m with
    | 0, _ => true
    | _, 0 => false
    | S n', S m' => leb n' m'
end.

Definition slist_01 : slist leb :=
  scons 0
    (scons 1
      snil
      (ok_snil 1))
    (ok_scons 0 1 snil (ok_snil 1)).

(** Nie wyglÄ…da wiele lepiej od poprzedniej, induktywno-induktywnej wersji,
    prawda? Ta raÅ¼Ä…ca kiepskoÅ›Ä‡ nie jest jednak zasÅ‚ugÄ… indukcji-rekursji,
    lecz kodowania za pomocÄ… aksjomatÃ³w - funkcja [ok] siÄ™ nie oblicza,
    wiÄ™c zamiast [eq_refl] musimy uÅ¼ywaÄ‡ aksjomatÃ³w [ok_snil] i [ok_scons].

    W tym momencie znÃ³w wkracza Å‚awa oburzonych i wyraÅ¼a swoje oburzenie na
    fakt, Å¼e Coq nie wspiera indukcji-rekursji (ale Agda juÅ¼ tak). Gdyby
    [Coq] wspieraÅ‚ indukcjÄ™-rekursjÄ™, to ten term wyglÄ…daÅ‚by tak: *)

Fail Definition slist_01 : slist leb :=
  scons 0 (scons 1 snil eq_refl) eq_refl.

(** DuÅ¼o lepiej, prawda? Na koniec zobaczmy, jak zdefiniowaÄ‡ funkcjÄ™
    zapominajÄ…cÄ… o fakcie, Å¼e lista jest posortowana. *)

Require Import List.
Import ListNotations.

Definition toList'
  {A : Type} {R : A -> A -> bool} :
  {f : slist R -> list A |
    f snil = [] /\
    forall (h : A) (t : slist R) (p : ok h t = true),
      f (scons h t p) = h :: f t
  }.
Proof.
  exact (ind A R (fun _ => list A) [] (fun h _ _ r => h :: r)).
Defined.

Definition toList
  {A : Type} {R : A -> A -> bool} : slist R -> list A :=
    proj1_sig toList'.

(** Ponownie jest to duÅ¼o prostsze, niÅ¼ w przypadku indukcji-indukcji -
    wprawdzie wciÄ…Å¼ musimy rÄ™cznie wpisywaÄ‡ termy do reguÅ‚y indukcji,
    ale dziÄ™ki prostocie reguÅ‚y jest to znacznie Å‚atwiejsze. Alternatywnie:
    udaÅ‚o nam siÄ™ zaoszczÄ™dziÄ‡ trochÄ™ czasu na definiowaniu reguÅ‚y rekursji,
    co w przypadku indukcji-indukcji byÅ‚o niemal konieczne, Å¼eby nie
    zwariowaÄ‡. *)

Lemma toList_slist_01_result :
  toList slist_01 = [0; 1].
Proof.
  unfold toList, slist_01.
  destruct toList' as (f & H1 & H2).
  cbn. rewrite 2!H2, H1. reflexivity.
Qed.

(** Udowodnienie, Å¼e nasza funkcja daje taki wynik jak chcieliÅ›my, jest
    rÃ³wnie proste jak poprzednio. *)

(** **** Ä†wiczenie *)

(** Zdefiniuj dla list posortowanych funkcjÄ™ [slen], ktÃ³ra liczy ich dÅ‚ugoÅ›Ä‡.
    Udowodnij oczywiste twierdzenie wiÄ…Å¼Ä…ce ze sobÄ… [slen], [toList] oraz
    [length]. Czy byÅ‚o Å‚atwiej, niÅ¼ w przypadku indukcji-indukcji? *)

(* begin hide *)

Definition slen'
  {A : Type} {R : A -> A -> bool} :
  {f : slist R -> nat |
    f snil = 0 /\
    forall (h : A) (t : slist R) (p : ok h t = true),
      f (scons h t p) = S (f t)}.
Proof.
  exact (ind A R (fun _ => nat) 0 (fun _ _ _ n => S n)).
Defined.

Definition slen {A : Type} {R : A -> A -> bool} : slist R -> nat :=
  proj1_sig slen'.

Lemma slen_toList_length :
  forall {A : Type} {R : A -> A -> bool} (l : slist R),
    slen l = length (toList l).
Proof.
  intros A R.
  eapply (proj1_sig
    (ind A R (fun l : slist R => slen l = length (toList l)) _ _)).
  Unshelve.
  all: cbn; unfold slen, toList;
  destruct slen' as (f & Hf1 & Hf2), toList' as (g & Hg1 & Hg2); cbn.
    rewrite Hf1, Hg1. cbn. reflexivity.
    intros. rewrite Hf2, Hg2, H. cbn. reflexivity.
Qed.

(** Czy byÅ‚o Å‚atwiej, niÅ¼ zrobiÄ‡ to samo indukcjÄ…-indukcjÄ…? Tak, ale tylko
    troszkÄ™. *)

(* end hide *)

End ind_rec.

(** **** Ä†wiczenie *)

(** No cÃ³Å¼, jeszcze raz to samo. Zdefiniuj za pomocÄ… indukcji-rekursji
    jednoczeÅ›nie typ stert binarnych [BHeap R], gdzie [R : A -> A -> bool]
    rozstrzyga porzÄ…dek, i funkcjÄ™ [ok : A -> BHeap R -> bool], gdzie
    [ok x h = true], gdy stertÄ™ [h] moÅ¼na podczepiÄ‡ pod element [x].

    Najpierw napisz pseudodefinicjÄ™, a potem przetÅ‚umacz jÄ… na odpowiedni
    zestaw aksjomatÃ³w.

    NastÄ™pnie uÅ¼yj swojej aksjomatycznej definicji, aby zdefiniowaÄ‡ funkcjÄ™
    [mirror], ktÃ³ra tworzy lustrzane odbicie sterty [h : BHeap R]. WskazÃ³wka:
    tym razem powinno ci siÄ™ udaÄ‡.

    PorÃ³wnaj induktywno-rekurencyjnÄ… implementacjÄ™ [BHeap R] i [ok] z
    implementacjÄ… przez indukcjÄ™-indukcjÄ™. KtÃ³ra jest bardziej ogÃ³lna?
    KtÃ³ra jest "lÅ¼ejsza"? KtÃ³ra jest lepsza? *)

(* begin hide *)
Module BHeap'.

(*
Inductive BHeap {A : Type} (R : A -> A -> bool) : Type :=
    | E : BHeap R
    | N :
      forall (v : A) (l r : BHeap R),
        ok R v l = true -> ok R v r = true -> BHeap R

with ok {A : Type} (R : A -> A -> bool) (x : A) (h : BHeap R) : bool :=
match h with
    | E => true
    | N v l r _ _ => R x v
end.
*)

Axioms
  (BHeap : forall {A : Type} (R : A -> A -> bool), Type)
  (ok : forall {A : Type} (R : A -> A -> bool) (x : A) (h : BHeap R), bool)
  (E : forall {A : Type} {R : A -> A -> bool}, BHeap R)
  (N :
    forall {A : Type} {R : A -> A -> bool} (v : A) (l r : BHeap R),
      ok R v l = true -> ok R v r = true -> BHeap R)
  (ok_E :
    forall {A : Type} {R : A -> A -> bool} (x : A),
      ok R x E = true)
  (ok_N :
    forall
      {A : Type} {R : A -> A -> bool}
      (x v : A) (l r : BHeap R)
      (eql : ok R v l = true) (eqr : ok R v r = true),
        ok R x (N v l r eql eqr) = R x v)
  (ind :
    forall
      {A : Type} {R : A -> A -> bool}
      (P : BHeap R -> Type)
      (P_E : P E)
      (P_N :
        forall
          (v : A) (l r : BHeap R)
          (eql : ok R v l = true) (eqr : ok R v r = true),
            P l -> P r -> P (N v l r eql eqr)),
      {f : forall h : BHeap R, P h |
        f E = P_E /\
        (forall
          (v : A) (l r : BHeap R)
          (eql : ok R v l = true) (eqr : ok R v r = true),
            f (N v l r eql eqr) = P_N v l r eql eqr (f l) (f r))}).

Definition mirror
  {A : Type} {R : A -> A -> bool} (h : BHeap R) :
  {h' : BHeap R | forall x : A, ok R x h' = ok R x h}.
Proof.
  revert h.
  apply (@ind A R (fun h : BHeap R =>
    {h' : BHeap R | forall x : A, ok R x h' = ok R x h}
  )).
    exists E. reflexivity.
    intros * [l' IHl'] [r' IHr']. eexists (N v r' l' _ _). Unshelve.
      Focus 2. rewrite IHr'. assumption.
      Focus 2. rewrite IHl'. assumption.
      intros. rewrite !ok_N. reflexivity.
Defined.

End BHeap'.
(* end hide *)

(** **** Ä†wiczenie *)

(** No cÃ³Å¼, jeszcze raz to samo. Zdefiniuj za pomocÄ… indukcji-rekursji
    jednoczeÅ›nie typ drzew wyszukiwaÅ„ binarnych [BST R], gdzie
    [R : A -> A -> bool] rozstrzyga porzÄ…dek, oraz funkcje
    [oklr]/[okl] i [okr]/[ok], ktÃ³re dbajÄ… o odpowiednie warunki (wybierz
    tylko jeden wariant z trzech, ktÃ³re testowaÅ‚eÅ› w tamtym zadaniu).

    Najpierw napisz pseudodefinicjÄ™, a potem przetÅ‚umacz jÄ… na odpowiedni
    zestaw aksjomatÃ³w.

    NastÄ™pnie uÅ¼yj swojej aksjomatycznej definicji, aby zdefiniowaÄ‡ funkcjÄ™
    [mirror], ktÃ³ra tworzy lustrzane odbicie drzewa [t : BST R]. WskazÃ³wka:
    tym razem powinno ci siÄ™ udaÄ‡.

    PorÃ³wnaj induktywno-rekurencyjnÄ… implementacjÄ™ [BST R] z implementacjÄ…
    przez indukcjÄ™-indukcjÄ™. KtÃ³ra jest bardziej ogÃ³lna? KtÃ³ra jest
    "lÅ¼ejsza"? KtÃ³ra jest lepsza? *)

(* begin hide *)
Module BST'.

(** TODO: definicja chyba jest Åºle... *)

(*
Inductive BST {A : Type} (R : A -> A -> bool) : Type :=
  | E : BST R
  | N : forall (v : A) (l r : BST R),
          okl R v l = true -> okr R v l = true -> BST R

with okl {A : Type} (R : A -> A -> bool) (x : A) (t : BST R) : bool :=
match t with
  | E => true
  | N v _ _ _ _ => R x v
end

with okr {A : Type} (R : A -> A -> bool) (x : A) (t : BST R) : bool :=
match t with
  | E => true
  | N v _ _ _ _ => negb (R x v)
*)

Axioms
  (BST : forall {A : Type} (R : A -> A -> bool), Type)
  (okl : forall {A : Type} {R : A -> A -> bool} (x : A) (t : BST R), bool)
  (okr : forall {A : Type} {R : A -> A -> bool} (x : A) (t : BST R), bool)
  (E : forall {A : Type} {R : A -> A -> bool}, BST R)
  (N :
    forall
      {A : Type} {R : A -> A -> bool}
      (v : A)
      (l r : BST R)
      (eql : okl v l = true) (eqr : okr v r = true), BST R)
  (okl_E :
    forall {A : Type} {R : A -> A -> bool} (x : A),
      okl x (@E A R) = true)
  (okl_N :
    forall
      {A : Type} {R : A -> A -> bool}
      (x v : A) (l r : BST R)
      (eql : okl v l = true) (eqr : okr v r = true),
        okl x (N v l r eql eqr) = R x v)
  (okr_E :
    forall {A : Type} {R : A -> A -> bool} (x : A),
      okr x (@E A R) = true)
  (okr_N :
    forall
      {A : Type} {R : A -> A -> bool}
      (x v : A) (l r : BST R)
      (eql : okl v l = true) (eqr : okr v r = true),
        okr x (N v l r eql eqr) = negb (R x v))
  (ind :
    forall
      {A : Type} {R : A -> A -> bool}
      (P : BST R -> Type)
      (P_E : P E)
      (P_N :
        forall
          (v : A) (l r : BST R)
          (eql : okl v l = true) (eqr : okr v r = true),
            P l -> P r -> P (N v l r eql eqr)),
      {f : forall t : BST R, P t |
        f E = P_E /\
        forall
          (v : A) (l r : BST R)
          (eql : okl v l = true) (eqr : okr v r = true),
            f (N v l r eql eqr) = P_N v l r eql eqr (f l) (f r)
      }
  ).

Definition mirror
  {A : Type} {R : A -> A -> bool} (t : BST R) :
    {t' : BST (fun x y : A => negb (R x y)) |
      (forall x : A, okl x t' = okr x t) /\
      (forall x : A, okr x t' = okl x t)}.
Proof.
  revert t.
  apply (@ind A R (fun t : BST R =>
    {t' : BST (fun x y : A => negb (R x y)) |
      (forall x : A, okl x t' = okr x t) /\
      (forall x : A, okr x t' = okl x t)
    }
  )).
    exists E. split; intro; rewrite !okl_E, !okr_E; reflexivity.
    intros v l r eql eqr (l' & IHl1 & IHl2) (r' & IHr1 & IHr2).
      eexists (N v r' l' _ _). Unshelve.
        Focus 2. rewrite IHr1, eqr. reflexivity.
        Focus 2. rewrite IHl2, eql. reflexivity.
        split; intro; rewrite okl_N, okr_N.
          reflexivity.
          destruct (R x v); reflexivity.
Defined.

End BST'.
(* end hide *)

(** Podobnie jak poprzednio, pojawia siÄ™ pytanie: do czego w praktyce
    moÅ¼na uÅ¼yÄ‡ indukcji-rekursji (poza rzecz jasna gÅ‚upimi strukturami
    danych, jak listy posortowane)? W Å›wierszczykach dla bystrzakÃ³w
    (czyli tzw. "literaturze naukowej") przewija siÄ™ gÅ‚Ã³wnie jeden (ale
    jakÅ¼e uÅ¼yteczny) pomysÅ‚: uniwersa.

    Czym sÄ… uniwersa i co majÄ… wspÃ³lnego z indukcjÄ…-rekursjÄ…? Najlepiej
    bÄ™dzie przekonaÄ‡ siÄ™ na przykÅ‚adzie programowania generycznego: *)

(** **** Ä†wiczenie (zdecydowanie za trudne) *)

(** Zaimplementuj generycznÄ… funkcjÄ™ [flatten], ktÃ³ra spÅ‚aszcza dowolnie
    zagnieÅ¼dÅ¼one listy list do jednej, pÅ‚askiej listy.

    [flatten 5 = [5]]

    [flatten [1; 2; 3] = [1; 2; 3]]

    [flatten [[1]; [2]; [3]] = [1; 2; 3]]

    [flatten [[[1; 2]]; [[3]]; [[4; 5]; [6]]] = [1; 2; 3; 4; 5; 6]] *)

(** Trudne, prawda? Ale robialne, a robi siÄ™ to tak.

    W typach argumentÃ³w [flatten] na powyÅ¼szym przykÅ‚adzie widaÄ‡ pewien
    wzorzec: sÄ… to kolejno [nat], [list nat], [list (list nat)],
    [list (list (list nat))] i tak dalej. MoÅ¼emy ten "wzorzec" bez problemu
    opisaÄ‡ za pomocÄ… nastÄ™pujÄ…cego typu: *)

Inductive FlattenType : Type :=
    | Nat : FlattenType
    | List : FlattenType -> FlattenType.

(** Å»eby byÅ‚o Å›mieszniej, [FlattenType] to dokÅ‚adnie to samo co [nat], ale
    przemilczmy to. Co dalej? MoÅ¼emy myÅ›leÄ‡ o elementach [FlattenType] jak
    o kodach prawdziwych typÃ³w, a skoro sÄ… to kody, to moÅ¼na teÅ¼ napisaÄ‡
    funkcjÄ™ dekodujÄ…cÄ…: *)

Fixpoint decode (t : FlattenType) : Type :=
match t with
    | Nat => nat
    | List t' => list (decode t')
end.

(** [decode] kaÅ¼demu kodowi przyporzÄ…dkowuje odpowiadajÄ…cy mu typ. O
    kodach moÅ¼emy myÅ›leÄ‡ jak o nazwach - [Nat] to nazwa [nat], zaÅ›
    [List t'] to nazwa typu [list (decode t')], np. [List (List Nat)]
    to nazwa typu [list (list nat)].

    Para [(FlattenType, decode)] jest przykÅ‚adem uniwersum.

    Uniwersum to, najproÅ›ciej piszÄ…c, worek, ktÃ³ry zawiera jakieÅ› typy.
    Formalnie uniwersum skÅ‚ada siÄ™ z typu kodÃ³w (czyli "nazw" typÃ³w) oraz
    funkcji dekodujÄ…cej, ktÃ³ra przyporzÄ…dkowuje kodom prawdziwe typy.

    Programowanie generyczne to programowanie funkcji, ktÃ³re operujÄ… na
    kolekcjach typÃ³w o dowolnych ksztaÅ‚tach, czyli na uniwersach wÅ‚aÅ›nie.
    GenerycznoÅ›Ä‡ od polimorfizmu rÃ³Å¼ni siÄ™ tym, Å¼e funkcja polimorficzna
    dziaÅ‚a dla dowolnego typu, zaÅ› generyczna - tylko dla typu o pasujÄ…cym
    ksztaÅ‚cie.

    Jak dokoÅ„czyÄ‡ implementacjÄ™ funkcji [flatten]? Kluczowe jest zauwaÅ¼enie,
    Å¼e moÅ¼emy zdefiniowaÄ‡ [flatten] przez rekursjÄ™ strutkuralnÄ… po argumencie
    domyÅ›lnym typu [FlattenType]. Ostatni problem to jak zrobiÄ‡, Å¼eby Coq sam
    zgadywaÅ‚ kod danego typu - dowiemy siÄ™ tego w rozdziale o klasach.

    Co to wszystko ma wspÃ³lnego z uniwersami? Ano, jeÅ¼eli chcemy definiowaÄ‡
    bardzo zaawansowane funkcje generyczne, musimy mieÄ‡ do dyspozycji bardzo
    potÄ™Å¼ne uniwersa i to wÅ‚aÅ›nie je zapewnia nam indukcja-rekursja. PoniewaÅ¼
    w powyÅ¼szym przykÅ‚adzie generycznoÅ›Ä‡ nie byÅ‚a zbyt wyrafinowana, nie byÅ‚o
    potrzeby uÅ¼ywania indukcji-rekursji, jednak uszy do gÃ³ry: przykÅ‚ad nieco
    bardziej skomplikowanego uniwersum pojawi siÄ™ jeszcze w tym rozdziale. *)

(** **** Ä†wiczenia *)

(** Nieco podchwytliwe zadanie: zdefiniuj uniwersum funkcji [nat -> nat],
    [nat -> (nat -> nat)], [(nat -> nat) -> nat],
    [(nat -> nat) -> (nat -> nat)] i tak dalej, dowolnie zagnieÅ¼dÅ¼onych.

    Zagadka: czy potrzebna jest nam indukcja-rekursja? *)

(** ** Indeksowana indukcja-rekursja *)

(** Za siedmioma gÃ³rami, za siedmioma lasami, za siedmioma rzekami, za
    siedmioma budkami telefonicznymi, nawet za indukcjÄ…-rekursjÄ… (choÄ‡
    tylko o kroczek) leÅ¼y indeksowana indukcja-rekursja, czyli poÅ‚Ä…czenie
    indukcji-rekursji oraz indeksowanych rodzin typÃ³w.

    Jako, Å¼e w porÃ³wnaniu do zwykÅ‚ej indukcji-rekursji nie ma tu za wiele
    innowacyjnoÅ›ci, przejdÅºmy od razu do przykÅ‚adu przydatnej techniki,
    ktÃ³rÄ… nasza tytuÅ‚owa bohaterka umoÅ¼liwia, a zwie siÄ™ on metodÄ…
    induktywnej dziedziny.

    Pod tÄ… nazwÄ… kryje siÄ™ sposÃ³b definiowania funkcji, pozwalajÄ…cy oddzieliÄ‡
    samÄ… definicjÄ™ od dowodu jej terminacji. JeÅ¼eli ten opis nic ci nie mÃ³wi,
    nie martw siÄ™: dotychczas definiowaliÅ›my tylko tak prymitywne funkcje, Å¼e
    tego typu fikoÅ‚ki nie byÅ‚y nam potrzebne.

    Metoda induktywnej dziedziny polega na tym, Å¼eby zamiast funkcji
    [f : A -> B], ktÃ³ra nie jest strukturalnie rekurencyjna (na co Coq
    nie pozwala) napisaÄ‡ funkcjÄ™ [f : forall x : A, D x -> B], gdzie
    [D : A -> Type] jest "predykatem dziedziny", ktÃ³ry sprawia, Å¼e dziwna
    rekursja z oryginalnej definicji [f] staje siÄ™ rekursjÄ… strukturalnÄ…
    po dowodzie [D x]. Å»eby zdefiniowaÄ‡ oryginalne [f : A -> B] wystarczy
    udowodniÄ‡, Å¼e kaÅ¼de [x : A] speÅ‚nia predykat dziedziny.

    Co to wszystko ma wspÃ³lnego z indeksowanÄ… indukcjÄ…-rekursjÄ…? JuÅ¼ piszÄ™.
    OtÃ³Å¼ metoda ta nie wymaga w ogÃ³lnoÅ›ci indukcji-rekursji - ta staje siÄ™
    potrzebna dopiero, gdy walczymy z bardzo zÅ‚oÅ›liwymi funkcjami, czyli
    takimi, w ktÃ³rych rekursja jest zagnieÅ¼dÅ¼ona, tzn. robimy wywoÅ‚anie
    rekurencyjne na wyniku poprzedniego wywoÅ‚ania rekurencyjnego.

    Predykat dziedziny dla takiej funkcji musi zawieraÄ‡ konstruktor w stylu
    "jeÅ¼eli wynik wywoÅ‚ania rekurencyjnego na x naleÅ¼y do dziedziny, to x teÅ¼
    naleÅ¼y do dziedziny".To wÅ‚aÅ›nie tu ujawnia siÄ™ indukcja-rekursja: Å¼eby
    zdefiniowaÄ‡ predykat dziedziny, musimy odwoÅ‚aÄ‡ siÄ™ do funkcji (Å¼eby mÃ³c
    powiedzieÄ‡ coÅ› o wyniku wywoÅ‚ania rekurencyjnego), a Å¼eby zdefiniowaÄ‡
    funkcjÄ™, musimy mieÄ‡ predykat dziedziny.

    Brzmi skomplikowanie? JeÅ¼eli czegoÅ› nie rozumiesz, to jesteÅ› debi...
    a nie, czekaj. JeÅ¼eli czegoÅ› nie rozumiesz, to nie martw siÄ™: powyÅ¼szy
    przykÅ‚ad miaÅ‚ na celu jedynie zilustrowaÄ‡ jakieÅ› praktyczne zastosowanie
    indeksowanej indukcji-rekursji. Do metody induktywnej dziedziny powrÃ³cimy
    w kolejnym rozdziale. PokaÅ¼emy, jak wyeliminowaÄ‡ z niej indukcjÄ™-rekursjÄ™,
    tak Å¼eby uzyskane za jej pomocÄ… definicje moÅ¼na byÅ‚o odpalaÄ‡ w Coqu.
    Zobaczymy teÅ¼, jakimi sposobami dowodziÄ‡, Å¼e kaÅ¼dy element dziedziny
    speÅ‚nia predykat dziedziny, co pozwoli nam odzyskaÄ‡ oryginalnÄ… definicjÄ™
    funkcji, a takÅ¼e dowiemy siÄ™, jak z "predykatu" o typie [D : A -> Type]
    zrobiÄ‡ prawdziwy predykat [D : A -> Prop]. *)

(** ** Indukcja-indukcja-rekursja *)

(** Ufff... przebrnÄ™liÅ›my przez indukcjÄ™-indukcjÄ™ i (indeksowanÄ…)
    indukcjÄ™-rekursjÄ™. Czy mogÄ… istnieÄ‡ jeszcze potÄ™Å¼niejsze i bardziej
    innowacyjne sposoby definiowania typÃ³w przez indukcjÄ™?

    AleÅ¼ oczywiÅ›cie. Jest nim... uwaga uwaga, niespodzianka...
    indukcja-indukcja-rekursja, ktÃ³ra jest nie tylko strasznym
    potfurem, ale teÅ¼ powinna dostaÄ‡ Oskara za najlepszÄ… nazwÄ™.

    Chodzi tu oczywiÅ›cie o poÅ‚Ä…czenie indukcji-indukcji i indukcji-rekursji:
    moÅ¼emy jednoczeÅ›nie zdefiniowaÄ‡ jakiÅ› typ [A], rodzinÄ™ typÃ³w [B]
    indeksowanÄ… przez [A] oraz operujÄ…ce na nich funkcje, do ktÃ³rych
    konstruktory [A] i [B] mogÄ… siÄ™ odwoÅ‚ywaÄ‡.

    Nie ma tu jakiejÅ› wielkiej filozofii: wszystkiego, co powinieneÅ› wiedzieÄ‡
    o indukcji-indukcji-rekursji, dowiedziaÅ‚eÅ› siÄ™ juÅ¼ z dwÃ³ch poprzednich
    podrozdziaÅ‚Ã³w. Nie muszÄ™ chyba dodawaÄ‡, Å¼e Å‚awa oburzonych jest oburzona
    faktem, Å¼e Coq nie wspiera indukcji-indukcji-rekursji.

    Rodzi siÄ™ jednak to samo super powaÅ¼ne pytanie co zawsze, czyli do czego
    moÅ¼na tego taÅ‚atajstwa uÅ¼yÄ‡? Przez caÅ‚kiem dÅ‚ugi czas nie miaÅ‚em pomysÅ‚u,
    ale okazuje siÄ™, Å¼e jest jedno takie zastosowanie i w sumie narzuca siÄ™
    ono samo.

    Przypomnij sobie metodÄ™ induktywno-rekurencyjnej dziedziny, czyli jedno
    ze sztandarowych zastosowaÅ„ indeksowanej indukcji-rekursji. Zaczynamy od
    typu [I : Type], na ktÃ³rym chcemy zdefiniowaÄ‡ funkcjÄ™ o niestandardowym
    ksztaÅ‚cie rekursji. W tym celu definiujemy dziedzinÄ™ [D : I -> Type]
    wraz z funkcjÄ… [f : forall i : I, D i -> R].

    ZauwaÅ¼my, jaki jest zwiÄ…zek typu [I] z funkcjÄ… [f]: najpierw jest typ,
    potem funkcja. Co jednak, gdy musimy [I] oraz [f] zdefiniowaÄ‡ razem za
    pomocÄ… indukcji-rekursji? Wtedy [f] moÅ¼e byÄ‡ zdefiniowane jedynie za
    pomocÄ… rekursji strukturalnej po [I], co wyklucza rekursjÄ™ o fikuÅ›nym
    ksztaÅ‚cie...

    I tu wchodzi indukcja-indukcja-rekursja, caÅ‚a na biaÅ‚o. MoÅ¼emy uÅ¼yÄ‡
    jej w taki sposÃ³b, Å¼e definiujemy jednoczeÅ›nie:
    - typ [I], ktÃ³ry odnosi siÄ™ do funkcji [f]
    - predykat dziedziny [D : I -> Type], ktÃ³ry jest indeksowany przez [I]
    - funkcjÄ™ [f], ktÃ³ra zdefiniowana jest przez rekursjÄ™ strukturalnÄ… po
      dowodzie naleÅ¼enia do dziedziny

    Jak widaÄ‡, typ zaleÅ¼y od funkcji, funkcja od predykatu, a predykat od
    typu i koÅ‚o siÄ™ zamyka.

    NastÄ™puje jednak skÄ…dinÄ…d uzasadnione pytanie: czy faktycznie istnieje
    jakaÅ› sytuacja, w ktÃ³rej powyÅ¼szy schemat dziaÅ‚ania jest tym sÅ‚usznym?
    OdpowiedÅº pÃ³ki co moÅ¼e byÄ‡ tylko jedna: nie wiem, ale siÄ™ domyÅ›lam. *)

(** ** Najstraszniejszy potfur *)

(** Na koniec dodam jeszcze na zachÄ™tÄ™ (albo zniechÄ™tÄ™, zaleÅ¼y jakie kto
    ma podejÅ›cie), Å¼e istnieje jeszcze jeden potfur, straszniejszy nawet
    niÅ¼ indukcja-indukcja-rekursja, ale jest on zbyt straszny jak na ten
    rozdziaÅ‚ i byÄ‡ moÅ¼e w ogÃ³le zbyt straszny jak na tÄ™ ksiÄ…Å¼kÄ™ - panie
    boÅ¼e, daj odwagÄ™ na omÃ³wienie go! *)

(** * Dobre, zÅ‚e i podejrzane typy induktywne *)

(** Poznana przez nas dotychczas definicja typÃ³w induktywnych (oraz wszelkich
    ich ulepszeÅ„, jak indukcja-indukcja, indukcja-rekursja etc.) jest
    niepeÅ‚na. Tak jak Å›wiat peÅ‚en jest zÅ‚oczyÅ„cÃ³w oszukujÄ…cych starszych
    ludzi metodÄ… "na wnuczka", tak nie kaÅ¼dy typ podajÄ…cy siÄ™ za induktywny
    faktycznie jest praworzÄ…dnym obywatelem krainy typÃ³w induktywnych. *)

(** Na szczÄ™Å›cie typy induktywne to istoty bardzo prostolinijne, zaÅ› te zÅ‚e
    moÅ¼na odrÃ³Å¼niÄ‡ od tych dobrych goÅ‚ym okiem, za pomocÄ… bardzo prostego
    kryterium: zÅ‚e typy induktywne to te, ktÃ³re nie sÄ… Å›ciÅ›le pozytywne.
    Zanim jednak dowiemy siÄ™, jak rozpoznawaÄ‡ zÅ‚e typy, poznajmy najpierw
    dwa powody, przez ktÃ³re zÅ‚e typy induktywne sÄ… zÅ‚e. *)

(** ** Nieterminacja jako ÅºrÃ³dÅ‚o zÅ‚a na Å›wiecie *)

(** Przyjrzyjmy siÄ™ poniÅ¼szemu typowemu przypadkowi negatywnego typu
    induktywnego (czyli takiego, ktÃ³ry wyglÄ…da na induktywny, ale ma
    konstruktory z negatywnymi wystÄ…pieniami argumentu indukcyjnego): *)

Fail Inductive wut (A : Type) : Type :=
    | C : (wut A -> A) -> wut A.

(* ===> The command has indeed failed with message:
        Non strictly positive occurrence of "wut"
        in "(wut A -> A) -> wut A". *)

(** Uwaga: poprzedzenie komendÄ… [Fail] innej komendy oznajmia Coqowi, Å¼e
    spodziewamy siÄ™, iÅ¼ komenda zawiedzie. Coq akceptuje komendÄ™ [Fail c],
    jeÅ¼eli komenda [c] zawodzi, i wypisuje komunikat o bÅ‚Ä™dzie. JeÅ¼eli
    komenda [c] zakoÅ„czy siÄ™ sukcesem, komenda [Fail c] zwrÃ³ci bÅ‚Ä…d.
    Komenda [Fail] jest przydatna w sytuacjach takich jak obecna, gdy
    chcemy zilustrowaÄ‡ fakt, Å¼e jakaÅ› komenda zawodzi.

    Pierwszym powodem nielegalnoÅ›ci nie-Å›ciÅ›le-pozytywnych typÃ³w induktywnych
    jest to, Å¼e uniewaÅ¼niajÄ… one filozoficznÄ… interpretacjÄ™ teorii typÃ³w i
    ogÃ³lnie wszystkiego, co robimy w Coqu. W praktyce problemy filozoficzne
    mogÄ… teÅ¼ prowadziÄ‡ do sprzecznoÅ›ci.

    ZaÅ‚Ã³Å¼my, Å¼e Coq akceptuje powyÅ¼szÄ… definicjÄ™ [wut]. MoÅ¼emy wtedy napisaÄ‡
    nastÄ™pujÄ…cy program: *)

Fail Definition loop (A : Type) : A :=
  let f (w : wut A) : A :=
    match w with
        | C g => g w
    end
  in f (C f).

(** JuÅ¼ sam typ tego programu wyglÄ…da podejrzanie: dla kaÅ¼dego typu [A]
    zwraca on element typu [A]. Nie dziwota wiÄ™c, Å¼e moÅ¼emy uzyskaÄ‡ z niego
    dowÃ³d faÅ‚szu. *)

Fail Definition santa_is_a_pedophile : False := loop False.

(** Paradoksalnie jednak to nie ta raÅ¼Ä…ca sprzecznoÅ›Ä‡ jest naszym najwiÄ™kszym
    problemem - nie z kaÅ¼dego zÅ‚ego typu induktywnego da siÄ™ tak Å‚atwo dostaÄ‡
    sprzecznoÅ›Ä‡ (a przynajmniej ja nie umiem; systematyczny sposÃ³b dostawania
    sprzecznoÅ›ci z istnienia takich typÃ³w zobaczymy pÃ³Åºniej). W rzeczywistoÅ›ci
    jest nim nieterminacja.

    Nieterminacja (ang. nontermination, divergence) lub kolokwialniej
    "zapÄ™tlenie" to sytuacja, w ktÃ³rej program nigdy nie skoÅ„czy siÄ™
    wykonywaÄ‡. Ta wÅ‚aÅ›nie bolÄ…czka jest przypadÅ‚oÅ›ciÄ… [loop], czego nie
    trudno domyÅ›liÄ‡ siÄ™ z nazwy.

    Dlaczego tak jest? Przyjrzyjmy siÄ™ definicji [loop]. Za pomocÄ… [let]a
    definiujemy funkcjÄ™ [f : wut A -> A], ktÃ³ra odpakowuje swÃ³j argument
    [w], wyciÄ…ga z niego funkcjÄ™ [g : wut A -> A] i aplikuje [g] do [w].
    Wynikiem programu jest [f] zaaplikowane do siebie samego zawiniÄ™tego
    w konstruktor [C].

    Przyjrzyjmy siÄ™, jak przebiegajÄ… prÃ³by wykonania tego nieszczÄ™snego
    programu:

    [loop A =]

    [let f := ... in f (C f) =]

    [let f := ... in match C f with | C g => g (C f) end =]

    [let f := ... in f (C f)]

    i tak dalej.

    Nie powinno nas to dziwiÄ‡ - praktycznie rzecz biorÄ…c aplikujemy [f] samo
    do siebie, zaÅ› konstruktor [C] jest tylko poÅ›rednikiem sprawiajÄ…cym, Å¼e
    typy siÄ™ zgadzajÄ…. OgÃ³lniej sytuacja, w ktÃ³rej coÅ› odnosi siÄ™ samo do
    siebie, nazywa siÄ™ autoreferencjÄ… i czÄ™sto prowadzi do rÃ³Å¼nych wesoÅ‚ych
    paradoksÃ³w. *)

(** **** Ä†wiczenie *)

(** PoniÅ¼szÄ… zagadkÄ™ pozwolÄ™ sobie wesoÅ‚o nazwaÄ‡ "paradoks hetero". Zagadka
    brzmi tak:

    NiektÃ³re sÅ‚owa opisujÄ… same siebie, np. sÅ‚owo "krÃ³tki" jest krÃ³tkie,
    a niektÃ³re inne nie, np. sÅ‚owo "dÅ‚ugi" nie jest dÅ‚ugie. Podobnie sÅ‚owo
    "polski" jest sÅ‚owem polskim, ale sÅ‚owo "niemiecki" nie jest sÅ‚owem
    niemieckim. SÅ‚owa, ktÃ³re nie opisujÄ… samych siebie bÄ™dziemy nazywaÄ‡
    sÅ‚owami heterologicznymi. Pytanie: czy sÅ‚owo "heterologiczny" jest
    heterologiczne? *)

(** Czujesz sprzecznoÅ›Ä‡? Innym przyk... dobra, wystarczy tych gÅ‚upot.

    Przyjrzyjmy siÄ™ teraz problemom filozoficznym powodowanym przez
    nieterminacjÄ™. W skrÃ³cie: zmienia ona fundamentalne wÅ‚aÅ›ciwoÅ›ci
    obliczeÅ„, co prowadzi do zmiany interpretacji pojÄ™cia typu, zaÅ›
    to pociÄ…ga za sobÄ… kolejne przykre skutki, takie jak np. to, Å¼e
    reguÅ‚y eliminacji tracÄ… swoje uzasadnienie.

    Brzmi mega groÅºnie, prawda? Kiedy wspomniaÅ‚em o tym Sokratesowi, to
    sturlaÅ‚ siÄ™ z podÅ‚ogi na sufit bez poÅ›rednictwa Å›ciany.

    Na szczÄ™Å›cie tak naprawdÄ™, to sprawa jest prosta. W Coqu wymagamy, aby
    kaÅ¼de obliczenie siÄ™ koÅ„czyÅ‚o. WartoÅ›ci, czyli koÅ„cowe wyniki obliczeÅ„
    (ktÃ³re sÄ… teÅ¼ nazywane postaciami kanonicznymi albo normalnymi), moÅ¼emy
    utoÅ¼samiaÄ‡ z elementami danego typu. Dla przykÅ‚adu wynikami obliczania
    termÃ³w typu [bool] sÄ… [true] i [false], wiÄ™c moÅ¼emy myÅ›leÄ‡, Å¼e sÄ… to
    elementy typu [bool] i [bool] skÅ‚ada siÄ™ tylko z nich. To z kolei daje
    nam uzasadnienie reguÅ‚y eliminacji (czyli indukcji) dla typu [bool]:
    Å¼eby udowodniÄ‡ [P : bool -> Prop] dla kaÅ¼dego [b : bool], wystarczy
    udowodniÄ‡ [P true] i [P false], gdyÅ¼ [true] i [false] sÄ… jedynymi
    elementami typu [bool].

    Nieterminacja obraca tÄ™ jakÅ¼e piÄ™knÄ… filozoficznÄ… wizjÄ™ w perzynÄ™:
    nie kaÅ¼de obliczenie siÄ™ koÅ„czy, a przez to powstajÄ… nowe, "dziwne"
    elementy rÃ³Å¼nych typÃ³w. [loop bool] nigdy nie obliczy siÄ™ do [true] ani
    do [false], wiÄ™c moÅ¼emy traktowaÄ‡ je jako nowy element typu [bool]. To
    sprawia, Å¼e [bool], typ z zaÅ‚oÅ¼enia dwuelementowy, ma teraz co najmniej
    trzy elementy - [true], [false] i [loop bool]. Z tego teÅ¼ powodu reguÅ‚a
    eliminacji przestaje obowiÄ…zywaÄ‡, bo wymaga ona dowodÃ³w jedynie dla
    [true] i [false], ale milczy na temat [loop bool]. MoglibyÅ›my prÃ³bowaÄ‡
    naiwnie jÄ… zaÅ‚ataÄ‡, uwzglÄ™dniajÄ…c ten dodatkowy przypadek, ale tak po
    prawdzie, to nie wiadomo nawet za bardzo jakie jeszcze paskudztwa
    rozpanoszyÅ‚y siÄ™ w typie [bool] z powodu nieterminacji.

    MoraÅ‚ jest prosty: nieterminacja to wynalazek szatana, a negatywne
    typy induktywne to teÅ¼ wynalazek szatana. *)

(** ** Twierdzenie Cantora *)

(** Zanim zaczniemy ten rozdziaÅ‚ na powaÅ¼nie, mam dla ciebie wesoÅ‚y Å‚amaniec
    jÄ™zykowy:

    Cantor - kanciarz, ktÃ³ry skradÅ‚ zza kurtyny kantoru z Kantonu kontury
    kartonu Koranicznemu kanarowi, ktÃ³ry czasem karaÅ‚ karczystych kafarÃ³w
    czarami za karÄ™ za kantowanie i za zakatowanie zza kontuaru konarem
    kontrkulturowych kuluarowych karÅ‚Ã³w.

    Dobra, wystarczy. Reszta tego podrozdziaÅ‚u bÄ™dzie Å›miertelnie powaÅ¼na,
    a przyjrzymy siÄ™ w niej jednemu z mega klasycznych twierdzeÅ„ z koÅ„ca
    XIX w. gÅ‚oszÄ…cemu mniej wiÄ™cej, Å¼e "zbiÃ³r potÄ™gowy zbioru liczb
    naturalnych ma wiÄ™kszÄ… moc od zbioru liczb naturalnych".

    Co za beÅ‚kot, pomyÅ›lisz zapewne. Ten podrozdziaÅ‚ poÅ›wiÄ™cimy wÅ‚aÅ›nie temu,
    Å¼eby Ã³w beÅ‚kot nieco wyklarowaÄ‡. JeÅ¼eli zaÅ› zastanawiasz siÄ™, po co nam
    to, to odpowiedÅº jest prosta - na (uogÃ³lnionym) twierdzeniu Cantora
    opieraÄ‡ siÄ™ bÄ™dzie nasza systematyczna metoda dowodzenia nielegalnoÅ›ci
    negatywnych typÃ³w induktywnych.

    OczywiÅ›cie oryginalne sformuÅ‚owanie twierdzenia powstaÅ‚o na dÅ‚ugo przed
    powstaniem teorii typÃ³w czy Coqa, co objawia siÄ™ np. w tym, Å¼e mÃ³wi ono
    o _zbiorze_ liczb naturalnych, podczas gdy my dysponujemy _typem_ liczb
    naturalnych. Musimy wiÄ™c oryginalne sformuÅ‚owanie lekko przeformuÅ‚owaÄ‡,
    a takÅ¼e wprowadziÄ‡ wszystkie niezbÄ™dne nam pojÄ™cia. *)

Definition surjective {A B : Type} (f : A -> B) : Prop :=
  forall b : B, exists a : A, f a = b.

(** Pierwszym niezbÄ™dnym pojÄ™ciem jest pojÄ™cie surjekcji. PowyÅ¼sza definicja
    gÅ‚osi, Å¼e funkcja jest surjektywna, gdy kaÅ¼dy element przeciwdziedziny
    jest wynikiem funkcji dla pewnego elementu dziedziny. Surjekcja to
    funkcja, ktÃ³ra jest surjektywna.

    O co chodzi w tej definicji? Samo sÅ‚owo "surjekcja" pochodzi od fr.
    "sur" - "na" oraz Å‚ac. "iacere" - "rzucaÄ‡". Ubogim tÅ‚umaczeniem na
    polski moÅ¼e byÄ‡ sÅ‚owo "narzut".

    Idea jest taka, Å¼e surjekcja [f : A -> B] "narzuca" swojÄ… dziedzinÄ™
    [A] na przeciwdziedzinÄ™ [B] tak, Å¼e [A] "pokrywa" caÅ‚e [B]. Innymi
    sÅ‚owy, kaÅ¼dy element [b : B] jest pokryty przez jakiÅ› element [a : A],
    co wyraÅ¼a rÃ³wnoÅ›Ä‡ [f a = b]. OczywiÅ›cie to [a] nie musi byÄ‡ unikalne -
    [b] moÅ¼e byÄ‡ pokrywane przez duÅ¼o rÃ³Å¼nych [a]. Jak widaÄ‡, dokÅ‚adnie to
    jest napisane w powyÅ¼szej definicji.

    MÃ³wiÄ…c jeszcze proÅ›ciej: jeÅ¼eli [f : A -> B] jest surjekcjÄ…, to typ
    [A] jest wiÄ™kszy (a precyzyjniej mÃ³wiÄ…cy, nie mniejszy) niÅ¼ typ [B].
    OczywiÅ›cie nie znaczy to, Å¼e jeÅ¼eli [f] nie jest surjekcjÄ…, to typ
    [A] jest mniejszy niÅ¼ [B] - mogÄ… przecieÅ¼ istnieÄ‡ inne surjekcje.
    Powiemy, Å¼e [A] jest mniejszy od [B], jeÅ¼eli nie istnieje Å¼adna
    surjekcja miÄ™dzy nimi.

    SprÃ³bujmy rozrysowaÄ‡ to niczym Jacek Gmoch... albo nie, bo nie umiem
    jeszcze rysowaÄ‡, wiÄ™c zamiast tego bÄ™dzie przykÅ‚ad i Ä‡wiczenie. *)

Definition isZero (n : nat) : bool :=
match n with
    | 0 => true
    | _ => false
end.

Lemma surjective_isZero : surjective isZero.
Proof.
  unfold surjective. destruct b.
    exists 0. cbn. reflexivity.
    exists 42. cbn. reflexivity.
Qed.

(** Funkcja [isZero], ktÃ³ra sprawdza, czy jej argument jest zerem, jest
    surjekcjÄ…, bo kaÅ¼dy element typu [bool] moÅ¼e byÄ‡ jej wynikiem -
    [true] jest wynikiem dla [0], zaÅ› [false] jest jej wynikiem dla
    wszystkich innych argumentÃ³w. Wobec tego moÅ¼emy skonkludowaÄ‡, Å¼e
    typ [nat] jest wiÄ™kszy niÅ¼ typ [bool] i w rzeczywistoÅ›ci faktycznie
    tak jest: [bool] ma dwa elementy, a [nat] nieskoÅ„czenie wiele.

    Do kwestii tego, ktÃ³ry typ ma ile elementÃ³w wrÃ³cimy jeszcze w rozdziale
    o typach i funkcjach. Tam teÅ¼ zapoznamy siÄ™ lepiej z surjekcjami i
    innymi rodzajami funkcji. Tymczasem Ä‡wiczenie: *)

(** **** Ä†wiczenie *)

(** Czy funkcja [plus 5] jest surjekcjÄ…? A funkcja [fun n : nat => minus n 5]?
    SprawdÅº swoje odpowiedzi w Coqu. Na koniec filozoficznie zinterpretuj
    otrzymany wynik.

    WskazÃ³wka: [minus] to funkcja z biblioteki standardowej, ktÃ³ra
    implementuje odejmowanie liczb naturalnych z obciÄ™ciem, tzn. np.
    [2 - 5 = 0]. UÅ¼yj [Print]a, Å¼eby dokÅ‚adnie zbadaÄ‡ jej definicjÄ™. *)

(* begin hide *)
(* Jest oczywiste, Å¼e [+ 5] nie jest surjekcjÄ…, zaÅ› [- 5] tak. *)
(* end hide *)

(** Pozostaje jeszcze kwestia tego, czym jest "zbiÃ³r potÄ™gowy zbioru liczb
    naturalnych". Mimo groÅºnej nazwy sprawa jest prosta: jest to archaiczne
    okreÅ›lenie na typ funkcji [nat -> bool]. KaÅ¼dÄ… funkcjÄ™ [f : nat -> bool]
    moÅ¼emy interpretowaÄ‡ jako pewnÄ… kolekcjÄ™ (czyli wÅ‚aÅ›nie zbiÃ³r) elementÃ³w
    typu [nat], zaÅ› [f n], czyli wynik [f] na konkretnym [n], mÃ³wi nam, czy
    [n] znajduje siÄ™ w tej kolekcji, czy nie.

    To w zasadzie wyczerpuje zestaw pojÄ™Ä‡ potrzebnych nam do sformuÅ‚owania
    twierdzenia. PojawiajÄ…ca siÄ™ w oryginalnej wersji "wiÄ™ksza moc" to po
    prostu synonim okreÅ›lenia "wiÄ™kszy", ktÃ³re potrafimy juÅ¼ wyraÅ¼aÄ‡ za
    pomocÄ… pojÄ™cia surjekcji. Tak wiÄ™c nowszÄ… (czyli bardziej postÄ™powÄ…)
    wersjÄ™ twierdzenia Cantora moÅ¼emy sformuÅ‚owaÄ‡ nastÄ™pujÄ…co: nie istnieje
    surjekcja z [nat] w [nat -> bool]. Lub jeszcze bardziej obrazowo: [nat]
    jest mniejsze niÅ¼ [nat -> bool]. *)

Theorem Cantor :
  forall f : nat -> (nat -> bool), ~ surjective f.
Proof.
  unfold surjective. intros f Hf.
  pose (diagonal := fun n : nat => negb (f n n)).
  destruct (Hf diagonal) as [n Hn].
  apply (f_equal (fun h : nat -> bool => h n)) in Hn.
  unfold diagonal in Hn. destruct (f n n); inversion Hn.
Qed.

(** DowÃ³d twierdzenia jest rÃ³wnie legendarny jak samo twierdzenie, a na
    dodatek bajecznie prosty i niesamowicie uÅ¼yteczny - jeÅ¼eli bÄ™dziesz
    zajmowaÅ‚ siÄ™ w Å¼yciu matematykÄ… i informatykÄ…, spotkasz go w jeszcze
    wielu odsÅ‚onach. Metoda stojÄ…ca za dowodem nazywana bywa argumentem
    przekÄ…tniowym - choÄ‡ nazwa ta moÅ¼e siÄ™ wydawaÄ‡ dziwna, to za chwilÄ™
    stanie siÄ™ zupeÅ‚nia jasna.

    O co wiÄ™c chodzi w powyÅ¼szym dowodzie? Po pierwsze zauwaÅ¼my, Å¼e
    mamy do czynienia z funkcjÄ… [f : nat -> (nat -> bool)], czyli
    funkcjÄ…, ktÃ³ra bierze liczbÄ™ naturalnÄ… i zwraca funkcjÄ™ z liczb
    naturalnych w [bool]. PamiÄ™tajmy jednak, Å¼e [->] Å‚Ä…czy w prawo
    i wobec tego typ [f] moÅ¼emy zapisaÄ‡ teÅ¼ jako [nat -> nat -> bool].
    Tak wiÄ™c [f] jest funkcjÄ…, ktÃ³ra bierze dwie liczby naturalne i
    zwraca element typu [bool].

    DziÄ™ki temu zabiegowi moÅ¼emy wyobraÅ¼aÄ‡ sobie [f] jako dwuwymiarowÄ…
    tabelkÄ™, ktÃ³rej wiersze i kolumny sÄ… indeksowane liczbami naturalnymi,
    a komÃ³rki w tabelce wypeÅ‚nione sÄ… wartoÅ›ciami typu [bool]. Przyjmijmy,
    Å¼e pierwszy argument [f] to indeks wiersza, zaÅ› drugi to indeks kolumny.
    W takim ukÅ‚adzie [f n m] to wartoÅ›Ä‡ [n]-tej funkcji na argumencie [m].
    Wobec tego twierdzenie moÅ¼emy sparafrazowaÄ‡ mÃ³wiÄ…c, Å¼e kaÅ¼da funkcja
    [nat -> bool] znajduje siÄ™ w ktÃ³rymÅ› wierszu tabelki.

    To tyle wyobraÅºni - moÅ¼emy juÅ¼ udowodniÄ‡ twierdzenie. Na poczÄ…tku
    oczywiÅ›cie bierzemy dowolne [f] oraz zakÅ‚adamy, Å¼e jest surjekcjÄ…,
    uprzednio odwijajÄ…c definicjÄ™ bycia surjekcjÄ….

    Teraz musimy jakoÅ› wyciÄ…gnÄ…Ä‡ sprzecznoÅ›Ä‡ z hipotezy [Hf], czyli,
    uÅ¼ywajÄ…c naszej tabelkowej parafrazy, znaleÅºÄ‡ funkcjÄ™ z [nat] w
    [bool], ktÃ³ra nie znajduje siÄ™ w tabelce. A nie znajdowaÄ‡ siÄ™ w
    tabelce, panie Ferdku, to znaczy: rÃ³Å¼niÄ‡ siÄ™ od kaÅ¼dej funkcji z
    tabelki na jakimÅ› argumencie.

    Zamiast jednak szukaÄ‡ takiej funkcji po omacku, skonstruujmy jÄ…
    z tego, co mamy pod rÄ™kÄ… - czyli z naszej tabelki. Jak przerobiÄ‡
    funkcje z tabelki na nowÄ…, ktÃ³rej w nie ma w tabelce? Tu wÅ‚aÅ›nie
    ujawnia siÄ™ geniuszalnoÅ›Ä‡ Cantora: uÅ¼yjemy metody przekÄ…tniowej,
    czyli spojrzymy na przekÄ…tnÄ… naszej tabelki.

    Definiujemy wiÄ™c nowÄ… funkcjÄ™ [diagonal : nat -> bool] nastÄ™pujÄ…co:
    dla argumentu [n : nat] bierzemy funkcjÄ™ z [n]-tego wiersza w tabelce,
    patrzymy na [n]-tÄ… kolumnÄ™, czyli na wartoÅ›Ä‡ funkcji na argumencie
    [n], i zwracamy negacjÄ™ tego, co tam znajdziemy. Czujesz sprzecznoÅ›Ä‡?

    Nasze zaÅ‚oÅ¼enie mÃ³wi, Å¼e [diagonal] znajduje siÄ™ w ktÃ³rymÅ› wierszu
    tabelki - niech ma on numer [n]. Wiemy jednak, Å¼e [g] rÃ³Å¼ni siÄ™ od
    [n]-tej funkcji z tabelki na argumencie [n], gdyÅ¼ zdefiniowaliÅ›my jÄ…
    jako negacjÄ™ tej wÅ‚aÅ›nie komÃ³rki w tabelce. Dostajemy stÄ…d rÃ³wnoÅ›Ä‡
    [f n n = diagonal n = negb (f n n)], co po analizie przypadkÃ³w daje
    ostatecznie [true = false] lub [false = true].

    VoilÃ ! SprzecznoÅ›Ä‡ osiÄ…gniÄ™ta, a zatem poczÄ…tkowe zaÅ‚oÅ¼enie byÅ‚o
    bÅ‚Ä™dne i nie istnieje Å¼adna surjekcja z [nat] w [nat -> bool]. *)

(** **** Ä†wiczenie *)

(** Udowodnij, Å¼e nie ma surjekcji z [nat] w [nat -> nat]. Czy jest surjekcja
    z [nat -> bool] w [(nat -> bool) -> bool]? A w [nat -> bool -> bool]? *)

(** Poznawszy twierdzenie Cantora, moÅ¼emy powrÃ³ciÄ‡ do Å›cisÅ‚ej pozytywnoÅ›ci,
    czyÅ¼ nie? OtÃ³Å¼ nie, bo twierdzenie Cantora jest biedne. Å»eby czerpaÄ‡
    garÅ›ciami niebotyczne profity, musimy najpierw uogÃ³lniÄ‡ je na dowolne
    dwa typy [A] i [B] znajdujÄ…c kryterium mÃ³wiÄ…ce, kiedy nie istnieje
    surjekcja z [A] w [A -> B]. *)

Theorem Cantor' :
  forall {A B : Type} (f : A -> (A -> B)) (modify : B -> B),
    (forall b : B, modify b <> b) -> ~ surjective f.
Proof.
  unfold surjective. intros A B f modify H Hf.
  pose (g := fun x : A => modify (f x x)).
  destruct (Hf g) as [x Hx].
  apply (f_equal (fun h => h x)) in Hx.
  unfold g in Hx. apply (H (f x x)).
  symmetry. assumption.
Qed.

(** UogÃ³lnienie jest doÅ›Ä‡ banalne. Najpierw zastÄ™pujemy [nat] i [bool] przez
    dowolne typy [A] i [B]. W oryginalnym twierdzeniu nie uÅ¼yliÅ›my Å¼adnej
    wÅ‚aÅ›ciwoÅ›ci liczb naturalnych, wiÄ™c nie musimy szukaÄ‡ Å¼adnych kryteriÃ³w
    dla typu [A]. Nasza tabelka moÅ¼e rÃ³wnie dobrze byÄ‡ indeksowana elementami
    dowolnego typu - dalej jest to tabelka i dalej ma przekÄ…tnÄ….

    Twierdzenie byÅ‚o jednak zaleÅ¼ne od pewnej wÅ‚aÅ›ciwoÅ›ci [bool], mianowicie
    funkcja [diagonal] byÅ‚a zdefiniowana jako negacja przekÄ…tnej. ByÅ‚o nam
    jednak potrzeba po prostu funkcji, ktÃ³ra dla kaÅ¼dego elementu z przekÄ…tnej
    zwraca element [bool] od niego rÃ³Å¼ny. PoniewaÅ¼ [bool] ma dokÅ‚adnie dwa
    elementy, to negacja jest jedynÄ… takÄ… funkcjÄ….

    Jednak w ogÃ³lnym przypadku dobra bÄ™dzie dowolna [B]-endofunkcja bez
    punktÃ³w staÅ‚ych. Ha! NagÅ‚y atak Å¼argonu bezzÄ™bnych ryb, co? ZrÃ³bmy
    krÃ³tkÄ… przerwÄ™, Å¼eby zbadaÄ‡ sposÃ³b komunikacji tych czarodziejskich
    zwierzÄ…t pÅ‚ywajÄ…cych po uczelnianych korytarzach.

    Endofunkcja to funkcja, ktÃ³ra ma takÄ… samÄ… dziedzinÄ™ i przeciwdziedzinÄ™.
    MoÅ¼na siÄ™ zatem domyÅ›laÄ‡, Å¼e [B]-endofunkcja to funkcja o typie [B -> B].
    Punkt staÅ‚y zaÅ› to takie [x], Å¼e [f x = x]. Jest to wiÄ™c dokÅ‚adnie ta
    wÅ‚asnoÅ›Ä‡, ktÃ³rej chcemy, Å¼eby poÅ¼Ä…dana przez nas funkcja nie miaÅ‚a dla
    Å¼adnego [x]. Jak widaÄ‡, Å¼argon bezzÄ™bnych ryb jest rÃ³wnie zwiÄ™zÅ‚y jak
    niepenetrowalny dla zwykÅ‚ych Å›miertelnikÃ³w.

    PodsumowujÄ…c: w uogÃ³lnionym twierdzeniu Cantora nie wymagamy niczego od
    [A], zaÅ› od [B] wymagamy tylko, Å¼eby istniaÅ‚a funkcja [modify : B -> B],
    ktÃ³ra speÅ‚nia [forall b : B, modify b <> b]. DowÃ³d twierdzenia jest taki
    jak poprzednio, przy czym zastÄ™pujemy uÅ¼ycie [negb] przez [modify]. *)

(** **** Ä†wiczenie *)

(** ZnajdÅº jedyny sÅ‚uszny typ [B], dla ktÃ³rego nie istnieje Å¼adna
    [B]-endofunkcja bez punktÃ³w staÅ‚ych.

    PodpowiedÅº: to zadanie jest naprawdÄ™ proste i naprawdÄ™ istnieje jedyny
    sÅ‚uszny typ o tej wÅ‚aÅ›ciwoÅ›ci.

    Pytanie (bardzo trudne): czy da siÄ™ udowodniÄ‡ w Coqu, Å¼e istnieje
    dokÅ‚adnie jeden taki typ? JeÅ¼eli tak, to w jakim sensie typ ten
    jest unikalny i jakich aksjomatÃ³w potrzeba do przepchniÄ™cia dowodu? *)

(* begin hide *)

Lemma unit_unique_type_without_endofunction_without_fixpoints :
  (forall {A : Type} (x y : A), {x = y} + {x <> y}) ->
  forall A : Type,
    (forall f : A -> A, exists x : A, f x = x) ->
      exists (f : A -> unit) (g : unit -> A),
        (forall x : A, g (f x) = x).
Proof.
  intros K A H.
  destruct (H (fun x : A => x)) as [x eq].
  exists (fun _ => tt), (fun _ => x).
  intro y.
  pose (f := fun a : A =>
    match K A a x with
        | left _ => y
        | _ => x
    end).
  specialize (H f).
  destruct H as [z H].
  unfold f in H.
  destruct (K A z x); congruence.
Qed.

(* end hide *)

(** ** Twierdzenie Cantora jako mÅ‚ot na negatywnoÅ›Ä‡ *)

(** Z Cantorem po naszej stronie moÅ¼emy wreszcie kupiÄ‡ ruble... ekhem,
    moÅ¼emy wreszcie zaprezentowaÄ‡ ogÃ³lnÄ… metodÄ™ dowodzenia, Å¼e negatywne
    typy induktywne prowadzÄ… do sprzecznoÅ›ci. Mimo szumnej nazwy ogÃ³lna
    metoda nie jest aÅ¼ taka ogÃ³lna i czÄ™sto bÄ™dziemy musieli siÄ™ bonusowo
    napracowaÄ‡. *)

Module Example1.

(** Otworzymy sobie nowy moduÅ‚, Å¼eby nie zaÅ›miecaÄ‡ globalnej przestrzeni
    nazw - wszystkie nasze zÅ‚e typy bÄ™dÄ… siÄ™ nazywaÅ‚y [wut]. Przy okazji,
    zdecydowanie powinieneÅ› nabraÄ‡ podejrzeÅ„ do tej nazwy - jeÅ¼eli coÅ› w
    tej ksiÄ…Å¼ce nazywa siÄ™ [wut], to musi to byÄ‡ zÅ‚owrogie, podejrzane
    paskudztwo. *)

Axioms
  (wut : Type -> Type)
  (C : forall {A : Type}, (wut A -> A) -> wut A)
  (dcase : forall
    (A : Type)
    (P : wut A -> Type)
    (PC : forall f : wut A -> A, P (C f)),
      {f : forall w : wut A, P w |
        forall g : wut A -> A, f (C g) = PC g}).

(** [wut] to aksjomatyczne kodowanie tego samego typu, o ktÃ³rym poprzednio
    tylko udawaliÅ›my, Å¼e istnieje. ZauwaÅ¼my, Å¼e nie jest nam potrzebna
    reguÅ‚a indukcji - wystarczy jeden z prostszych eliminatorÃ³w, mianowicie
    [dcase], czyli zaleÅ¼na reguÅ‚a analizy przypadkÃ³w. *)

Definition bad (A : Type) : wut A -> (wut A -> A).
Proof.
  apply (dcase A (fun _ => wut A -> A)).
  intro f. exact f.
Defined.

(** Dlaczego typ [wut A] jest nielegalny, a jego definicja za pomocÄ… komendy
    [Inductive] jest odrzucana przez Coqa? Poza wspomnianymi w poprzednim
    podrozdziale problemami filozoficznymi wynikajÄ…cymi z nieterminacji,
    jest teÅ¼ drugi, bardziej namacalny powÃ³d: istnienie typu [wut A] jest
    sprzeczne z (uogÃ³lnionym) twierdzeniem Cantora.

    PowÃ³d tej sprzecznoÅ›ci jest doÅ›Ä‡ prozaiczny: za pomocÄ… konstruktora [C]
    moÅ¼emy z dowolnej funkcji [wut A -> A] zrobiÄ‡ element [wut A], a skoro
    tak, to dowolne [w : wut A] moÅ¼emy odpakowaÄ‡ i wyjÄ…Ä‡ z niego funkcjÄ™
    [f : wut A -> A]. *)

Lemma bad_sur :
  forall A : Type, surjective (bad A).
Proof.
  unfold surjective. intros A f.
  unfold bad. destruct (dcase _) as [g H].
  exists (C f). apply H.
Qed.

(** Skoro moÅ¼emy wÅ‚oÅ¼yÄ‡ dowolnÄ… funkcjÄ™, to moÅ¼emy takÅ¼e wyjÄ…Ä‡ dowolnÄ…
    funkcjÄ™, a zatem mamy do czynienia z surjekcjÄ…. *)

Lemma worst : False.
Proof.
  apply (Cantor' (bad bool) negb).
    destruct b; inversion 1.
    apply bad_sur.
Qed.

(** W poÅ‚Ä…czeniu zaÅ› z twierdzeniem Cantora surjekcja [wut A -> (wut A -> A)]
    prowadzi do sprzecznoÅ›ci - wystarczy za [A] wstawiÄ‡ [bool]. *)

End Example1.

(** PrzykÅ‚ad moÅ¼e ci siÄ™ jednak wydaÄ‡ niezadowalajÄ…cy - typ [wut] jest
    przecieÅ¼ doÅ›Ä‡ nietypowy, bo ma tylko jeden konstruktor. A co, gdy
    konstruktorÃ³w jest wiÄ™cej?

    PoczÄ…tkowo miaÅ‚em opisaÄ‡ kilka przypadkÃ³w z wiÄ™kszÄ… liczbÄ… konstruktorÃ³w,
    ale stwierdziÅ‚em, Å¼e jednak mi siÄ™ nie chce. W Ä‡wiczeniach zobaczymy, czy
    bÄ™dziesz w stanie sam wykombinowaÄ‡, jak siÄ™ z nimi uporaÄ‡. *)

(** **** Ä†wiczenie *)

(** PoniÅ¼sze paskudztwo Å‚amie prawo Å›cisÅ‚ej pozytywnoÅ›ci nie jednym, lecz
    aÅ¼ dwoma swoimi konstruktorami.

    Zakoduj ten typ aksjomatycznie i udowodnij, Å¼e jego istnienie prowadzi
    do sprzecznoÅ›ci. Metoda jest podobna jak w naszym przykÅ‚adzie, ale
    trzeba jÄ… troszkÄ™ dostosowaÄ‡ do zastanej sytuacji. *)

Module Exercise1.

Fail Inductive wut : Type :=
    | C0 : (wut -> bool) -> wut
    | C1 : (wut -> nat) -> wut.

(* begin hide *)
Axioms
  (wut : Type)
  (C0 : (wut -> bool) -> wut)
  (C1 : (wut -> nat) -> wut)
  (ind : forall
    (P : wut -> Type)
    (PC0 : forall f : wut -> bool, P (C0 f))
    (PC1 : forall f : wut -> nat, P (C1 f)),
      {f : forall w : wut, P w |
        (forall g : wut -> bool, f (C0 g) = PC0 g) /\
        (forall g : wut -> nat, f (C1 g) = PC1 g)
      }
  ).

Definition bad :
  wut -> (wut -> bool).
Proof.
  apply (ind (fun _ => wut -> bool)).
    intro f. exact f.
    intros _ _. exact true.
Defined.

Lemma bad_sur :
  forall (f : wut -> bool), exists w : wut, bad w = f.
Proof.
  intro. unfold bad. destruct (ind _) as (g & H1 & H2).
  exists (C0 f). apply H1.
Defined.
(* end hide *)

Lemma wut_illegal : False.
(* begin hide *)
Proof.
  destruct (bad_sur (fun w : wut => negb (bad w w))).
  unfold bad in H. destruct (ind _).
  apply (f_equal (fun f => f x)) in H.
  destruct (x0 x x); inversion H.
Qed.
(* end hide *)

End Exercise1.

(** **** Ä†wiczenie *)

(** PoniÅ¼sze paskudztwo ma jeden konstruktor negatywny, a drugi pozytywny,
    niczym typowa panienka z borderlinem...

    Polecenie jak w poprzednim Ä‡wiczeniu. *)

Module Exercise2.

Fail Inductive wut : Type :=
    | C0 : (wut -> wut) -> wut
    | C1 : nat -> wut.

(* begin hide *)
Axioms
  (wut : Type)
  (C0 : (wut -> wut) -> wut)
  (C1 : nat -> wut)
  (ind : forall
    (P : wut -> Type)
    (PC0 : forall f : wut -> wut, P (C0 f))
    (PC1 : forall n : nat, P (C1 n)),
      {f : forall w : wut, P w |
        (forall g : wut -> wut, f (C0 g) = PC0 g) /\
        (forall n : nat, f (C1 n) = PC1 n)
      }
  ).

Definition bad :
  wut -> (wut -> wut).
Proof.
  apply (ind (fun _ => wut -> wut)).
    intro f. exact f.
    intros _ w. exact w.
Defined.

Lemma bad_sur :
  forall (f : wut -> wut), exists w : wut, bad w = f.
Proof.
  intro. unfold bad. destruct (ind _) as (g & H1 & H2).
  exists (C0 f). apply H1.
Defined.

Definition discern : wut -> bool.
Proof.
  apply ind; intros _.
    exact true.
    exact false.
Defined.

Lemma discern' :
  forall (f : wut -> wut) (n : nat),
    C0 f = C1 n -> False.
Proof.
  intros. apply (f_equal discern) in H.
  unfold discern in H. destruct (ind _) as (g & p & q).
  rewrite p, q in H. inversion H.
Qed.

Definition change : wut -> wut.
Proof.
  apply (ind (fun _ => wut)).
    intro. exact (C1 0).
    intro. apply (C0 (fun w => w)).
Defined.

Lemma change_neq :
  forall w : wut, change w <> w.
Proof.
  apply ind.
    intros f H. unfold change in H. destruct (ind _) as (g & H1 & H2).
      rewrite H1 in H. eapply discern'. symmetry. exact H.
    intros n H. unfold change in H. destruct (ind _) as (g & H1 & H2).
      rewrite H2 in H. apply discern' in H. assumption.
Qed.
(* end hide *)

Lemma wut_illegal : False.
(* begin hide *)
Proof.
  destruct (bad_sur (fun w : wut => change (bad w w))).
  unfold bad in H. destruct (ind _).
  apply (f_equal (fun f => f x)) in H.
  apply (change_neq (x0 x x)).
  symmetry. assumption.
Qed.
(* end hide *)

End Exercise2.

(** **** Ä†wiczenie *)

(** PoniÅ¼szy typ reprezentuje termy beztypowego rachunku lambda, gdzie [V]
    to typ reprezentujÄ…cy zmienne. Co to za zwierzÄ…tko ten rachunek lambda
    to my siÄ™ jeszcze przekonamy... chyba, oby.

    Taki sposÃ³b reprezentowania rachunku lambda (i ogÃ³lnie skÅ‚adni jÄ™zykÃ³w
    programowania) nazywa siÄ™ HOAS, co jest skrÃ³tem od ang. Higher Order
    Abstract Syntax. W wielu jÄ™zykach funkcyjnych jest to popularna technika,
    ale w Coqu, jak zaraz udowodnisz, jest ona nielegalna. Åawa oburzonych
    jest rzecz jasna oburzona! *)

Module Exercise3.

Fail Inductive Term (V : Type) : Type :=
    | Var : V -> Term V
    | Lam : (Term V -> Term V) -> Term V
    | App : Term V -> Term V -> Term V.

(* begin hide *)
Axiom
  (Term : Type -> Type)
  (Var : forall {V : Type}, V -> Term V)
  (Lam : forall {V : Type}, (Term V -> Term V) -> Term V)
  (App : forall {V : Type}, Term V -> Term V -> Term V)
  (case : forall
    (V : Type)
    (P : Term V -> Type)
    (PVar : forall v : V, P (Var v))
    (PLam : forall g : Term V -> Term V, P (Lam g))
    (PApp : forall t1 t2 : Term V, P (App t1 t2)),
      {f : forall t : Term V, P t |
        (forall v : V, f (Var v) = PVar v) /\
        (forall g : Term V -> Term V, f (Lam g) = PLam g) /\
        (forall t1 t2 : Term V, f (App t1 t2) = PApp t1 t2)}).

Definition bad {V : Type} : Term V -> (Term V -> Term V).
Proof.
  apply (case _ (fun _ => Term V -> Term V)).
    intros _. exact (fun x => x).
    intro f. exact f.
    intros _ _. exact (fun x => x).
Defined.

Definition discern {V : Type}  : Term V -> nat.
Proof.
  apply (case V (fun _ => nat)); repeat intros _.
    exact 0.
    exact 1.
    exact 2.
Defined.
(* end hide *)

Lemma Term_illegal : False.
(* begin hide *)
Proof.
  eapply (Cantor' (@bad unit)). Unshelve. all: cycle 1.
    red. intro f. exists (Lam f). unfold bad. destruct (case _).
      decompose [and] a. apply H1.
    apply case; intros.
      exact (App (Var tt) (Var tt)).
      exact (Var tt).
      exact (Var tt).
    apply case; cbn; intros;
    destruct (case _) as [f H];
    decompose [and] H; clear H;
    rewrite ?H0, ?H2, ?H3;
    intro; apply (f_equal discern) in H;
    unfold discern in H; destruct case; decompose [and] a;
    congruence.
Qed.
(* end hide *)

End Exercise3.

(** **** Ä†wiczenie *)

(** PoniÅ¼sze bydle jest najgorsze z moÅ¼liwych - pÃ³ki co nie wiem, jak to
    udowodniÄ‡. Powodzenia! *)

Module Exercise4.

Fail Inductive wut : Type :=
    | C : (wut -> wut) -> wut.

(* begin hide *)
Axioms
  (wut : Type)
  (C : (wut -> wut) -> wut)
  (ind : forall
    (P : wut -> Type)
    (PC : forall f : wut -> wut, (forall w : wut, P (f w)) -> P (C f)),
      {f : forall w : wut, P w |
        forall g : wut -> wut,
          f (C g) = PC g (fun w => f (g w))
      }
    ).

Definition bad :
  wut -> (wut -> wut).
Proof.
  apply (ind (fun _ => wut -> wut)).
    intros f _. exact f.
Defined.

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective. intro f. exists (C f).
  unfold bad. destruct (ind _). apply e.
Defined.

Definition change : wut -> wut.
Proof.
  apply ind.
  intros f g. exact (g (C f)).
(*  intro w. exact (C (fun _ => w)).*)

(*  apply (ind (fun _ => wut)).*)
  
(*  intro f. apply f. exact (C (fun _ => C f)).*)
Defined.

Lemma change_neq :
  forall w : wut, change w <> w.
Proof.
  apply ind. intros f H' H.
  pose (H'' := H' (C f)).
  unfold change in *. destruct (ind _).
Admitted.
(* end hide *)

Lemma wut_illegal : False.
(* begin hide *)
Proof.
  apply (Cantor' bad change).
    apply change_neq.
    unfold surjective. apply bad_sur.
Qed.
(* end hide *)

End Exercise4.

(** ** Poradnik rozpoznawania negatywnych typÃ³w induktywnych *)

(** Skoro juÅ¼ wiemy, Å¼e negatywne typy induktywne sÄ… wynalazkiem szatana,
    to czas podaÄ‡ proste kryterium na ich rozpoznawanie. JeÅ¼eli jesteÅ›
    sprytny, to pewnie sam zdÄ…Å¼yÅ‚eÅ› juÅ¼ zauwaÅ¼yÄ‡ ogÃ³lnÄ… reguÅ‚Ä™. Jednak aby
    nie dyskryminowaÄ‡ osÃ³b maÅ‚o sprytnych, trzeba napisaÄ‡ jÄ… wprost.

    Kryterium jest banalne. MajÄ…c dany typ [T] musimy rzuciÄ‡ okiem na jego
    konstruktory, a konkretniej na ich argumenty. Argumenty nieindukcyjne
    (czyli o typach, w ktÃ³rych nie wystÄ™puje [T]) sÄ… zupeÅ‚nie niegroÅºnie.
    InteresowaÄ‡ nas powinny jedynie argumenty indukcyjne, czyli takie, w
    ktÃ³rych wystÄ™puje typ [T].

    NiektÃ³re typy argumentÃ³w indukcyjnych sÄ… niegroÅºne, np. [T * T], [T + T],
    [list T] albo [{t : T | P t}], ale powodujÄ… one, Å¼e Coq nie potrafi
    wygenerowaÄ‡ odpowiedniej reguÅ‚y indukcji i zadowala siÄ™ jedynie reguÅ‚Ä…
    analizy przypadkÃ³w. Nie prowadzÄ… one do sprzecznoÅ›ci, ale powinniÅ›my ich
    unikaÄ‡.

    Argument typu [T * T] moÅ¼na zastÄ…piÄ‡ dwoma argumentami typu [T]
    i podobnie dla [{t : T | P t}]. Konstruktor z argumentem typu [T + T]
    moÅ¼emy rozbiÄ‡ na dwa konstruktory (i powinniÅ›my, bo jest to bardziej
    czytelne). Konstruktor z wystÄ…pieniem [list T] moÅ¼emy przerobiÄ‡ na
    definicjÄ™ przez indukcjÄ™ wzajemnÄ… (Ä‡wiczenie: sprawdÅº jak), ale lepiej
    chyba po prostu zaimplementowaÄ‡ reguÅ‚Ä™ indukcji rÄ™cznie. *)

Inductive T0 : Type :=
    | c0 : T0
    | c1 : nat -> T0
    | c2 : T0 -> T0
    | c3 : nat * T0 -> T0
    | c4 : T0 * T0 -> T0
    | c5 : T0 + T0 -> T0
    | c6 : list T0 -> T0.

(** Rodzaje nieszkodliwych typÃ³w argumentÃ³w widaÄ‡ na powyÅ¼szym przykÅ‚adzie.
    Konstruktory [c0] i [c1] sÄ… nieindukcyjne, wiÄ™c sÄ… ok. Konstruktor [c2]
    jest indukcyjny - jest jeden argument typu [T0]. ZauwaÅ¼, Å¼e typem
    konstruktora [c2] jest [T0 -> T0], ale nie oznacza to, Å¼e [T0]
    wystÄ™puje po lewej stronie strzaÅ‚ki!

    Jest tak, gdyÅ¼ ostatnie wystÄ…pienie [T0] jest konkluzjÄ… konstruktora
    [c2]. WaÅ¼ne sÄ… tylko wystÄ…pienia po lewej stronie strzaÅ‚ki w argumentach
    (gdyby konstruktor [c2] nie byÅ‚ legalny, to jedynymi legalnymi typami
    induktywnymi byÅ‚yby enumeracje).

    Konstruktory [c3], [c4], [c5] i [c6] sÄ… induktywne i rÃ³wnieÅ¼ w peÅ‚ni
    legalne, ale sÄ… one powodem tego, Å¼e Coq nie generuje dla [T0] reguÅ‚y
    indukcji, a jedynie reguÅ‚Ä™ analizy przypadkÃ³w (choÄ‡ nazwa siÄ™ ona
    [T0_ind]). *)

(** **** Ä†wiczenie *)

(** Zrefaktoryzuj powyÅ¼szy upoÅ›ledzony typ. *)

(* begin hide *)
(** TODO *)
(* end hide *)

(** Problem pojawia siÄ™ dopiero, gdy typ [T] wystÄ™puje po lewej stronie
    strzaÅ‚ki, np. [T -> bool], [T -> T], [(T -> T) -> T], lub gdy jest
    skwantyfikowany uniwersalnie, np. [forall t : T, P t],
    [forall f : (forall t : T, P t), Q f].

    W trzech poprzednich podrozdziaÅ‚ach mierzyliÅ›my siÄ™ z sytuacjami, gdy
    typ [T] wystÄ™powaÅ‚ bezpoÅ›rednio na lewo od strzaÅ‚ki, ale oczywiÅ›cie
    moÅ¼e on byÄ‡ dowolnie zagnieÅ¼dÅ¼ony. Dla kaÅ¼dego wystÄ…pienia [T] w
    argumentach moÅ¼emy policzyÄ‡, na lewo od ilu strzaÅ‚ek (albo jako
    jak mocno zagnieÅ¼dÅ¼ona dziedzina kwantyfikacji) siÄ™ ono znajduje.
    LiczbÄ™ tÄ™ nazywaÄ‡ bÄ™dziemy niedobroÅ›ciÄ…. W zaleÅ¼noÅ›ci od niedobroÅ›ci,
    wystÄ…pienie nazywamy:
    - 0 - wystÄ…pienie Å›ciÅ›le pozytywne
    - liczba nieparzysta - wystÄ…pienie negatywne
    - liczba parzysta (poza 0) - wystÄ…pienie pozytywne *)

(** JeÅ¼eli w definicji mamy wystÄ…pienie negatywne, to typ moÅ¼emy nazywaÄ‡
    negatywnym typem induktywnym (choÄ‡ oczywiÅ›cie nie jest to typ
    induktywny). JeÅ¼eli nie ma wystÄ…pieÅ„ negatywnych, ale sÄ… wystÄ…pienia
    pozytywne, to typ nazywamy pozytywnym typem induktywnym (lub nie Å›ciÅ›le
    pozytywnym typem induktywnym), choÄ‡ oczywiÅ›cie rÃ³wnieÅ¼ nie jest to typ
    induktywny. JeÅ¼eli wszystkiego wystÄ…pienia sÄ… Å›ciÅ›le pozytywne, to mamy
    do czynienia po prostu z typem induktywnym.

    Podobne nazewnictwo moÅ¼emy sobie wprowadziÄ‡ dla konstruktorÃ³w
    (konstruktory negatywne, pozytywne i Å›ciÅ›le pozytywne), ale nie
    ma sensu, bo za tydzieÅ„ i zapomnisz o tych maÅ‚o istotnych detalach.
    WaÅ¼ne, Å¼ebyÅ› zapamiÄ™taÅ‚ najwaÅ¼niejsze, czyli ideÄ™. *)

Fail Inductive T1 : Type :=
    | T1_0 : T1 -> T1
    | T1_1 : (T1 -> T1) -> T1
    | T1_2 : ((T1 -> T1) -> T1) -> T1
    | T1_3 : forall (t : T1) (P : T1 -> Prop), P t -> T1.

(** W powyÅ¼szym przykÅ‚adzie wystÄ…pienie [T1] w pierwszym argumencie
    [T1_0] jest Å›ciÅ›le pozytywne (na lewo od 0 strzaÅ‚ek). Pierwsze
    wystÄ…pienie [T1] w [T1_1] jest negatywne (na lewo od 1 strzaÅ‚ki),
    a drugie Å›ciÅ›le pozytywne (na lewo od 0 strzaÅ‚ek). Pierwsze
    wystÄ…pienie [T1] w [T1_2] jest pozytywne (na lewo od 2 strzaÅ‚ek),
    drugie negatywne (na lewo od 1 strzaÅ‚ki), trzecie zaÅ› Å›ciÅ›le
    pozytywne (na lewo od 0 strzaÅ‚ek). Pierwsze wystÄ…pienie [T1] w
    [T1_3] jest negatywne (dziedzina kwantyfikacji), drugie zaÅ›
    pozytywne (na lewo od jednej strzaÅ‚ki, ale ta strzaÅ‚ka jest w
    typie, po ktÃ³rym kwantyfikujemy).

    Konstruktor [T1_0] jest Å›ciÅ›le pozytywne, zaÅ› konstruktory [T1_1],
    [T1_2] oraz [T1_3] sÄ… negatywne. Wobec tego typ [T1] jest negatywnym
    typem induktywnym (czyli wynalazkiem szatana, ktÃ³rego zaakceptowanie
    prowadzi do sprzecznoÅ›ci). *)

(** **** Ä†wiczenie *)

Fail Inductive T2 : Type :=
    | T2_0 : forall f : (forall g : (forall t : T2, nat), Prop), T2
    | T2_1 : (((((T2 -> T2) -> T2) -> T2) -> T2) -> T2) -> T2
    | T2_2 :
      ((forall (n : nat) (P : T2 -> Prop),
        (forall t : T2, nat)) -> T2) -> T2 -> T2 -> T2.

(** Policz niedobroÅ›Ä‡ kaÅ¼dego wstÄ…pienia [T2] w powyÅ¼szej definicji.
    Sklasyfikuj konstruktory jako negatywne, pozytywne lub Å›ciÅ›le
    pozytywne. NastÄ™pnie sklasyfikuj sam typ jako negatywny, pozytywny
    lub Å›ciÅ›le pozytywny. *)

(** **** Ä†wiczenie *)

(* Inductive T : Type := *)

(** Rozstrzygnij, czy nastÄ™pujÄ…ce konstruktory speÅ‚niajÄ… kryterium Å›cisÅ‚ej
    pozytywnoÅ›ci. JeÅ¼eli tak, narysuj wesoÅ‚ego jeÅ¼a. JeÅ¼eli nie, napisz
    zapÄ™tlajÄ…cÄ… siÄ™ funkcjÄ™ podobnÄ… do [loop] (zakÅ‚adamy, Å¼e typ [T] ma
    tylko ten jeden konstruktor). NastÄ™pnie sprawdÅº w Coqu, czy udzieliÅ‚eÅ›
    poprawnej odpowiedzi.
    - [| C1 : T]
    - [| C2 : bool -> T]
    - [| C3 : T -> T]
    - [| C4 : T -> nat -> T]
    - [| C5 : forall A : Type, T -> A -> T]
    - [| C6 : forall A : Type, A -> T -> T]
    - [| C7 : forall A : Type, (A -> T) -> T]
    - [| C8 : forall A : Type, (T -> A) -> T]
    - [| C9 : (forall x : T, T) -> T]
    - [| C10 : (forall (A : Type) (x : T), A) -> T]
    - [| C11 : forall A B C : Type,
                  A -> (forall x : T, B) -> (C -> T) -> T] *)

(* begin hide *)
(* C1-C7 sÄ… legalne, C8-C11 nie. *)
(* end hide *)

(** ** Kilka bonusowych puÅ‚apek *)

(** Wiemy juÅ¼, Å¼e niektÃ³re typy argumentÃ³w indukcyjnych sÄ… ok ([T * T],
    [list T]), a niektÃ³re inne nie ([T -> T], [forall t : T, ...]).
    UwaÅ¼ny i Å¼Ä…dny wiedzy czytelnik (daj boÅ¼e, Å¼eby tacy istnieli) zeche
    zapewne postawiÄ‡ sobie pytanie: ktÃ³re dokÅ‚adnie typy argumentÃ³w
    indukcyjnych sÄ… ok, a ktÃ³re sÄ… wynalazkiem szatana?

    NajproÅ›ciej bÄ™dzie sprawÄ™ zbadaÄ‡ empirycznie, czyli na przykÅ‚adzie.
    Å»eby zaÅ› przykÅ‚ad byÅ‚ reprezentatywny, niech parametrem definicji
    bÄ™dzie dowolna funkcja [F : Type -> Type]. *)

Fail Inductive wut (F : Type -> Type) : Type :=
    | wut_0 : F (wut F) -> wut F.
(* ===> The command has indeed failed with message:
        Non strictly positive occurrence of "wut" in
        "F (wut F) -> wut F". *)

(** Jak widaÄ‡, jeÅ¼eli zaaplikujemy [F] do argumentu indukcyjnego, to Coq
    krzyczy, Å¼e to wystÄ…pienie nie jest Å›ciÅ›le pozytywne. Dlaczego tak
    jest, skoro [F] nie jest ani strzaÅ‚kÄ…, ani kwantyfikatorem uniwersalnym?
    Dlatego, Å¼e choÄ‡ nie jest nimi, to moÅ¼e nimi zostaÄ‡. JeÅ¼eli zdefiniujemy
    sobie gdzieÅ› na boku [F := fun A : Type => A -> bool], to wtedy
    [wut_0 F : (wut F -> bool) -> wut F], a z takim diabelstwem juÅ¼ siÄ™
    mierzyliÅ›my i wiemy, Å¼e nie wrÃ³Å¼y ono niczego dobrego.

    MoraÅ‚ z tej historii jest doÅ›Ä‡ banalny: gdy definiujemy typ induktywny
    [T], jedynymi prawilnymi typami dla argumentu indukcyjnego sÄ… [T] oraz
    typy funkcji, ktÃ³re majÄ… [T] jako konkluzjÄ™ ([A -> T], [A -> B -> T]
    etc.). Wszystkie inne albo rodzÄ… problemy z automatycznÄ… generacjÄ…
    reguÅ‚ indukcji ([T * T], [list T]), albo prowadzÄ… do sprzecznoÅ›ci
    ([T -> T], [forall t : T, ...]), albo mogÄ… prowadziÄ‡ do sprzecznoÅ›ci,
    jeÅ¼eli wstawi siÄ™ za nie coÅ› niedobrego ([F T]). *)

(** **** Ä†wiczenie *)

Module wutF.

Definition F (A : Type) : Type := A -> bool.

(** Zakoduj aksjomatycznie rodzinÄ™ typÃ³w [wut] z powyÅ¼szego przykÅ‚adu.
    NastÄ™pnie wstaw za parametr zdefiniowane powyÅ¼ej [F] i udowodnij,
    Å¼e typ [wut F] prowadzi do sprzecznoÅ›ci. *)

(* begin hide *)
Axioms
  (wut : (Type -> Type) -> Type)
  (wut_0 : forall {F : Type -> Type}, F (wut F) -> wut F)
  (wut_ind :
    forall
      (F : Type -> Type)
      (P : wut F -> Type)
      (Pwut_0 : forall x : F (wut F), P (wut_0 x)),
        {f : forall x : wut F, P x |
          forall x : F (wut F), f (wut_0 x) = Pwut_0 x}).

Definition bad : wut F -> wut F -> bool.
Proof.
  apply (wut_ind F (fun _ => wut F -> bool)).
  unfold F. intro f. exact f.
Defined.

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective.
  intro f. exists (wut_0 f).
  unfold bad. destruct (wut_ind _).
  rewrite e. reflexivity.
Qed.

Lemma bad_not_sur :
  ~ surjective bad.
Proof.
  unfold surjective. intro H.
  destruct (H (fun w : wut F => negb (bad w w))) as [w eq].
  apply (f_equal (fun f => f w)) in eq.
  unfold bad in eq. destruct (wut_ind _).
  destruct (x w w); inversion eq.
Qed.
(* end hide *)

Lemma wut_illegal : False.
(* begin hide *)
Proof.
  apply bad_not_sur. apply bad_sur.
Qed.
(* end hide *)

End wutF.

(** To jeszcze nie koniec wraÅ¼eÅ„ na dziÅ› - pÃ³ki co omÃ³wiliÅ›my wszakÅ¼e
    kryterium Å›cisÅ‚ej pozytywnoÅ›ci jedynie dla bardzo prostych typÃ³w
    induktywnych. SÅ‚owem nie zajÄ…knÄ™liÅ›my siÄ™ nawet na temat typÃ³w
    wzajemnie induktywnych czy indeksowanych typÃ³w induktywnych. Nie
    trudno bÄ™dzie nam jednak uzupeÅ‚niÄ‡ naszÄ… wiedzÄ™, gdyÅ¼ w przypadku
    oby tych mechanizmÃ³w kryterium Å›cisÅ‚ej pozytywnoÅ›ci wyglÄ…da podobnie
    jak w znanych nam juÅ¼ przypadkach. *)

Fail Inductive X : Type :=
    | X0 : X
    | X1 : (Y -> X) -> X

with Y : Type :=
    | Y0 : Y
    | Y1 : X -> Y.

(* ===> The command has indeed failed with message:
        Non strictly positive occurrence of "Y"
        in "(Y -> X) -> X". *)

(** Jak widaÄ‡, definicja [X] i [Y] przez wzajemnÄ… indukcjÄ™ jest nielegalna,
    gdyÅ¼ jedyny argument konstruktora [X1] ma typ [Y -> X]. MogÅ‚oby wydawaÄ‡
    siÄ™, Å¼e wszystko jest w porzÄ…dku, wszakÅ¼e [X] wystÄ™puje tutaj na pozycji
    Å›ciÅ›le pozytywnej. Jednak poniewaÅ¼ jest to definicja przez indukcjÄ™
    wzajemnÄ…, kryterium Å›cisÅ‚ej pozytywnoÅ›ci stosuje siÄ™ nie tylko do
    wystÄ…pieÅ„ [X], ale takÅ¼e do wystÄ…pieÅ„ [Y] - wszystkie wystÄ…pienia [X]
    oraz [Y] muszÄ… byÄ‡ Å›ciÅ›le pozytywne zarÃ³wno w konstruktorach typu [X],
    jak i w konstruktorach typu [Y]. *)

(** **** Ä†wiczenie *)

(** Zakoduj aksjomatycznie definicjÄ™ typÃ³w [X] i [Y]. SprÃ³buj napisaÄ‡
    zapÄ™tlajÄ…cÄ… siÄ™ funkcjÄ™ [loop] (czy raczej dwie wzajemnie rekurencyjne
    zapÄ™tlajÄ…ce siÄ™ funkcje [loopx] i [loopy]), a nastÄ™pnie udowodnij za
    pomocÄ… twierdzenia Cantora, Å¼e typy [X] i [Y] sÄ… nielegalne. *)

Module XY.

(* begin hide *)

Axioms
  (X Y : Type)
  (X0 : X)
  (X1 : (Y -> X) -> X)
  (Y0 : Y)
  (Y1 : X -> Y)
  (dcase :
    forall
      (P : X -> Type) (Q : Y -> Type)
      (PX0 : P X0)
      (PX1 : forall f : Y -> X, P (X1 f))
      (QY0 : Q Y0)
      (QY1 : forall x : X, Q (Y1 x)),
        {f : forall x : X, P x &
        {g : forall y : Y, Q y |
          f X0 = PX0 /\
          (forall h : Y -> X, f (X1 h) = PX1 h) /\
          g Y0 = QY0 /\
          (forall x : X, g (Y1 x) = QY1 x)}})
  (dcaseX :
    forall
      (P : X -> Type)
      (PX0 : P X0)
      (PX1 : forall f : Y -> X, P (X1 f)),
        {f : forall x : X, P x &
          f X0 = PX0 /\
          forall h : Y -> X, f (X1 h) = PX1 h})
  (dcaseY :
    forall
      (P : Y -> Type)
      (PY0 : P Y0)
      (PY1 : forall x : X, P (Y1 x)),
        {f : forall y : Y, P y &
          f Y0 = PY0 /\
          forall x : X, f (Y1 x) = PY1 x}).

Definition bad : X -> (X -> X).
Proof.
  apply (dcaseX (fun _ => X -> X)).
    intro x. exact x.
    intros f x. exact (f (Y1 x)).
Defined.

Require Import FunctionalExtensionality.

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective. intro f.
  unfold bad. destruct (dcaseX _) as (bad & eq1 & eq2).
  exists (X1 (projT1 (dcaseY (fun _ => X) X0 f))).
  rewrite eq2.
  extensionality x. cbn.
  destruct (dcaseY _) as (aux & eq1' & eq2').
  cbn. rewrite eq2'. reflexivity.
Qed.

Definition discern : X -> bool.
Proof.
  apply dcaseX.
    exact true.
    intros _. exact false.
Defined.

Definition change : X -> X.
Proof.
  apply dcaseX.
    exact (X1 (fun _ => X0)).
    intros _. exact X0.
Defined.

Lemma change_neq :
  forall x : X, change x <> x.
Proof.
  intros x H. apply (f_equal discern) in H.
  unfold change in H; destruct (dcaseX _) as (change & eq1 & eq2).
  unfold discern in H; destruct (dcaseX _) as (discern & eq1' & eq2').
  revert x H.
  apply (dcaseX (fun x => discern (change x) = discern x -> False)); intros.
    rewrite eq1, eq1', eq2' in H. inversion H.
    rewrite eq2, eq1', eq2' in H. inversion H.
Qed.
(* end hide *)

Lemma XY_illegal : False.
(* begin hide *)
Proof.
  apply (Cantor' bad change change_neq). apply bad_sur.
Qed.

Definition loop (x : X) : X := bad x x.

Lemma loop_nontermination :
  X0 = loop (X1 (projT1 (dcaseY (fun _ => X) X0 loop))).
Proof.
  unfold loop, bad.
  destruct (dcaseX _) as (bad & eq1 & eq2).
  destruct (dcaseY _) as (aux & eq1' & eq2'); cbn.
  rewrite eq2. rewrite eq2'.
Abort.
(* end hide *)

End XY.

(** ** Jeszcze wiÄ™cej puÅ‚apek *)

(** To juÅ¼ prawie koniec naszej wÄ™drÃ³wki przez Å›wiat nielegalnych typÃ³w
    "induktywnych". DowiedzieliÅ›my siÄ™, Å¼e negatywne typy induktywne
    prowadzÄ… do nieterminacji i nauczyliÅ›my siÄ™ wykorzystywaÄ‡ twierdzenie
    Cantora do dowodzenia nielegalnoÅ›ci takich typÃ³w.

    PoznaliÅ›my teÅ¼ jednak klasyfikacjÄ™ typÃ³w wyglÄ…dajÄ…cych na induktywne
    (Å›ciÅ›le pozytywne, pozytywne, negatywne), a w szczegÃ³lnoÅ›ci pojÄ™cie
    "niedobroÅ›ci" indukcyjnego wystÄ…pienia definiowanego typu w konstruktorze
    (upraszczajÄ…c, na lewo od ilu strzaÅ‚ek znajduje siÄ™ to wystÄ…pienie).

    PiszÄ™ "jednak", gdyÅ¼ z jej powodu moÅ¼emy czuÄ‡ pewien niedosyt - wszystkie
    dotychczasowe przykÅ‚ady byÅ‚y typami negatywnymi o niedobroÅ›ci rÃ³wnej 1.
    Podczas naszej intelektualnej wÄ™drÃ³wki zwiedziliÅ›my mniej miejscÃ³wek,
    niÅ¼ moglibyÅ›my chcieÄ‡. W tym podrozdziale sprÃ³bujemy ten przykry niedosyt
    zaÅ‚ataÄ‡, rozwaÅ¼ajÄ…c (nie Å›ciÅ›le) pozytywne typy induktywne. Zobaczymy
    formalny dowÃ³d na to, Å¼e nie sÄ… one legalne (lub, precyzyjniej piszÄ…c,
    dowÃ³d na to, Å¼e conajmniej jeden z nich nie jest legalny). Zanim jednak
    to siÄ™ stanie, zobaczmy, czy wypracowane przez nas techniki dziaÅ‚ajÄ… na
    negatywne typy induktywne o niedobroÅ›ci innej niÅ¼ 1. *)

Module T3.

Fail Inductive T3 : Type :=
    | T3_0 : (((T3 -> bool) -> bool) -> bool) -> T3.

(** Przyjrzyjmy siÄ™ powyÅ¼szej definicji. WystÄ™pienie indukcyjne typu [T3]
    ma wspÃ³Å‚czynnik niedobroÅ›ci rÃ³wny 3, gdyÅ¼ znajduje siÄ™ na lewo od 3
    strzaÅ‚ek. Prawe strony wszystkich z nich to [bool]. *)

Axioms
  (T3 : Type)
  (T3_0 : (((T3 -> bool) -> bool) -> bool) -> T3)
  (T3_case :
    forall
      (P : T3 -> Type)
      (PT3_0 : forall f : ((T3 -> bool) -> bool) -> bool, P (T3_0 f)),
        {f : forall x : T3, P x |
          forall g : ((T3 -> bool) -> bool) -> bool,
            f (T3_0 g) = PT3_0 g}).

(** Po ciÄ™Å¼kich bojach, przez ktÃ³re przeszedÅ‚eÅ›, aksjomatyczne kodowanie
    tego typu nie powinno ciÄ™ dziwiÄ‡. Warto zauwaÅ¼yÄ‡ jedynie, Å¼e do naszej
    dyspozycji mamy jedynie reguÅ‚Ä™ zaleÅ¼nej analizy przypadkÃ³w, gdyÅ¼ nie
    wiadomo, jak miaÅ‚yby wyglÄ…daÄ‡ wywoÅ‚ania indukcyjne.

    Zanim zobaczymy, jak pokazaÄ‡ nielegalnoÅ›Ä‡ tego typu metodÄ… Cantora,
    przypomnijmy sobie pewien kluczowy fakt dotyczÄ…cy negacji i jego
    banalne uogÃ³lnienie. *)

(** **** Ä†wiczenie *)

Lemma triple_negation :
  forall P : Prop, ~ ~ ~ P -> ~ P.
(* begin hide *)
Proof.
  intros P f x. apply f. intro g. apply g. exact x.
Qed.
(* end hide *)

Lemma triple_arrow :
  forall A B : Type, (((A -> B) -> B) -> B) -> (A -> B).
(* begin hide *)
Proof.
  intros A B f x. apply f. intro g. apply g. exact x.
Qed.
(* end hide *)

(** Ä†wiczenie to przypomina nam, Å¼e jeÅ¼eli chodzi o spamowanie negacjÄ…, to
    sÄ… w zasadzie tylko trzy sytuacje:
    - brak negacji
    - pojedyncza negacja
    - podwÃ³jna negacja *)

(** JeÅ¼eli mamy do czynienia z wiÄ™kszÄ… liczbÄ… negacji, to moÅ¼emy zdejmowaÄ‡
    po dwie aÅ¼ dojdziemy do ktÃ³regoÅ› z powyÅ¼szych przypadkÃ³w. PoniewaÅ¼
    negacja to tylko implikacja, ktÃ³rej kodziedzinÄ… jest [False], a nie
    korzystamy w dowodzie z Å¼adnych specjalnych wÅ‚aÅ›ciwoÅ›ci [False],
    analogiczna wÅ‚aÅ›ciwoÅ›Ä‡ zachodzi takÅ¼e dla dowolnego innego [B : Type]. *)

Definition bad : T3 -> (T3 -> bool).
Proof.
  apply (T3_case (fun _ => T3 -> bool)).
  intros f x. apply f. intro g. apply g. exact x.
Defined.

(** Wobec powyÅ¼szych rozwaÅ¼aÅ„ definicja funkcji bad zupeÅ‚nie nie powinna
    ciÄ™ zaskakiwaÄ‡. Szczerze piszÄ…c, reszta dowodu rÃ³wnieÅ¼ nie jest jakoÅ›
    specjalnie wymagajÄ…ca czy oÅ›wiecajÄ…ca. *)

(** **** Ä†wiczenie *)

(** DokoÅ„cz dowÃ³d. *)

Lemma bad_sur :
  surjective bad.
(* begin hide *)
Proof.
  unfold surjective.
  intro f. exists (T3_0 (fun g => g f)).
  unfold bad. destruct (T3_case _).
  rewrite e. reflexivity.
Qed.
(* end hide *)

Theorem T3_illegal : False.
(* begin hide *)
Proof.
  apply (Cantor' bad negb).
    destruct b; inversion 1.
    exact bad_sur.
Qed.
(* end hide *)

(** **** Ä†wiczenie *)

(** Napisanie zapÄ™tlajÄ…cej siÄ™ funkcji [loop : T3 -> bool] teÅ¼ nie jest
    jakoÅ› wybitnie trudne. Napisz jÄ… i udowodnij (nieformlanie), Å¼e
    istnieje takie [x : T3], Å¼e [loop x] siÄ™ zapÄ™tla. *)

(* begin hide *)
Fail Fixpoint loop (x : T3) : bool :=
match x with
    | T3_0 f => f (fun g : T3 -> bool => g (T3_0 f))
end.

(**
  Niech f := fun g => g loop

  loop (T3_0 f) =
  f (fun g => g (T3_0 f))
  loop (T3_0 f) =
  ...
*)

(* end hide *)

End T3.

(** MoraÅ‚ z powyÅ¼szych rozwaÅ¼aÅ„ jest prosty: nasze techniki dziaÅ‚ajÄ… takÅ¼e
    na negatywne typy induktywne o niedobroÅ›ci rÃ³wnej 3. MyÅ›lÄ™, Å¼e jesteÅ›
    caÅ‚kiem skÅ‚onny uwierzyÄ‡ teÅ¼, Å¼e zadziaÅ‚ajÄ… na te o niedobroÅ›ci rÃ³wnej
    5, 7 i tak dalej.

    To wszystko jest prawdÄ… jednak tylko wtedy, gdy wszystkie typy po prawych
    stronach strzaÅ‚ek bÄ™dÄ… takie same. A co, gdy bÄ™dÄ… rÃ³Å¼ne? *)

Module T4.

Fail Inductive T4 : Type :=
    | c0 : (((T4 -> bool) -> nat) -> Color) -> T4.

Axioms
  (T4 : Type)
  (c0 : (((T4 -> bool) -> nat) -> Color) -> T4)
  (dcase :
    forall
      (P : T4 -> Type)
      (Pc0 : forall f : ((T4 -> bool) -> nat) -> Color, P (c0 f)),
        {f : forall x : T4, P x |
          forall g : ((T4 -> bool) -> nat) -> Color,
            f (c0 g) = Pc0 g}).

(** PowyÅ¼szy przykÅ‚ad jest podobny do poprzedniego, ale tym razem zamiast
    trzech wystÄ…pieÅ„ [bool] mamy [bool], [nat] oraz [Color] (to typ, ktÃ³ry
    zdefiniowaliÅ›my na samym poczÄ…tku tego rozdziaÅ‚u, gdy uczyliÅ›my siÄ™
    o enumeracjach). *)

Definition bad : T4 -> (T4 -> bool).
Proof.
  apply (dcase (fun _ => T4 -> _)).
  intros f x.
  apply (
    fun c : Color =>
    match c with
        | R => true
        | _ => false
    end).
  apply f. intro g.
  apply (fun b : bool => if b then 0 else 1).
  exact (g x).
Defined.

(** Nasz modus operandi bÄ™dzie taki jak poprzednio: sprÃ³bujemy wyjÄ…Ä‡ z
    elementu [T4] funkcjÄ™ typu [T4 -> bool]. W tym celu uÅ¼ywamy zaleÅ¼nej
    reguÅ‚y analizy przypadkÃ³w i wprowadzamy rzeczy do kontekstu.

    Tym razem nie moÅ¼emy jednak bezpoÅ›rednio zaaplikowaÄ‡ [f], gdyÅ¼ jej
    kodziedzinÄ… jest [Color], a my musimy skonstruowaÄ‡ coÅ› typu [bool].
    MoÅ¼emy temu jednak zaradziÄ‡ aplikujÄ…c do celu skonstruowanÄ… naprÄ™dce
    funkcjÄ™ typu [Color -> bool]. Ta funkcja powinna byÄ‡ surjekcjÄ… (jeÅ›li
    nie wierzysz, sprawdÅº, co siÄ™ stanie, jeÅ¼eli zamienimy jÄ… na funckjÄ™
    staÅ‚Ä…).

    MoÅ¼emy juÅ¼ zaaplikowaÄ‡ [f] i wprowadziÄ‡ [g] do kontekstu. ChcielibyÅ›my
    teraz zaaplikowaÄ‡ [g], ale nie moÅ¼emy, bo typy siÄ™ nie zgadzajÄ… - [g]
    zwraca [bool], a my musimy skonstruowaÄ‡ liczbÄ™ naturalnÄ…. Robimy tutaj
    to samo co poprzednio - aplikujemy do celu jakÄ…Å› funkcjÄ™ [bool -> nat].
    Tym razem nie musi ona byÄ‡ surjekcjÄ… (nie jest to nawet moÅ¼liwe, gdyÅ¼
    nie ma surjekcji z [bool] w [nat]). DziÄ™ki temu moÅ¼emy zaaplikowaÄ‡ [g]
    i zakoÅ„czyÄ‡, uÅ¼ywajÄ…c [g x]. *)

Require Import FunctionalExtensionality.

(** Å»eby pokazaÄ‡, Å¼e [bad] jest surjekcjÄ…, bÄ™dziemy potrzebowaÄ‡ aksjomatu
    ekstensjonalnoÅ›ci dla funkcji (ang. functional extensionality axiom,
    w skrÃ³cie funext). GÅ‚osi on, Å¼e dwie funkcje [f, g : A -> B] sÄ… rÃ³wne,
    jeÅ¼eli uda nam siÄ™ pokazaÄ‡, Å¼e dajÄ… rÃ³wne wyniki dla kaÅ¼dego argumentu
    (czyli [forall x : A, f x = g x]).

    ImportujÄ…c powyÅ¼szy moduÅ‚ zakÅ‚adamy prawdziwoÅ›Ä‡ tego aksjomatu oraz
    uzyskujemy dostÄ™p do taktyki [extensionality], ktÃ³ra uÅ‚atwia dowody
    wymagajÄ…ce uÅ¼ycia ekstensjonalnoÅ›ci. *)

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective. intro f.
  unfold bad. destruct (dcase _) as [bad eq].
  exists (c0 (
    fun g : (T4 -> bool) -> nat =>
    match g f with
       | 0 => R
       | _ => G
    end)).
  rewrite eq.
  extensionality t.
  destruct (f t); reflexivity.
Qed.

(** DowÃ³d jest prawie taki jak zawsze: odwijamy definicjÄ™ surjektywnoÅ›ci i
    wprowadzamy hipotezy do kontekstu, a nastÄ™pnie odwijamy definicjÄ™ [bad]
    i rozbijamy jÄ… dla czytelnoÅ›ci na wÅ‚aÅ›ciwÄ… funkcjÄ™ [bad] oraz rÃ³wnanie
    [eq].

    NastÄ™pnie musimy znaleÅºÄ‡ [a : T4], ktÃ³re [bad] mapuje na [f]. Zaczynamy
    od [c0], bo jest to jedyny konstruktor [T4]. Bierze on jako argument
    funkcjÄ™ typu [((T4 -> bool) -> nat) -> Color]. Å»eby jÄ… wyprodukowaÄ‡,
    bierzemy na wejÅ›ciu funkcjÄ™ [g : (T4 -> bool) -> nat] i musimy zrobiÄ‡
    coÅ› typu [Color].

    Nie moÅ¼e to byÄ‡ jednak byle co - musimy uÅ¼yÄ‡ [f], a jedynym sensownym
    sposobem, Å¼eby to zrobiÄ‡, jest zaaplikowaÄ‡ [g] do [f]. Musimy zadbaÄ‡
    teÅ¼ o to, Å¼eby odwrÃ³ciÄ‡ funkcje konwertujÄ…ce [Color -> bool] oraz
    [bool -> nat], ktÃ³rych uÅ¼yliÅ›my w definicji [bad]. Pierwsza z nich
    konwertowaÅ‚a [R] (czyli kolor czerwony) na [true], a inne kolory na
    [false], zaÅ› druga konwertowaÅ‚a [true] na [0], a [false] na [1].
    Wobec tego dopasowujÄ…c [g f : nat] musimy przekonwertowaÄ‡ [0] na [R],
    zaÅ› [1] na coÅ› innego niÅ¼ [R], np. na [G] (czyli kolor zielony).

    ZnalazÅ‚szy odpowiedni argument, moÅ¼emy przepisaÄ‡ rÃ³wnanie definiujÄ…ce
    [bad]. To juÅ¼ prawie koniec, ale prÃ³ba uÅ¼ycia taktyki [reflexivity] w
    tym momencie skoÅ„czyÅ‚aby siÄ™ poraÅ¼kÄ…. Na ratunek przychodzi nam
    aksjomat ekstensjonalnoÅ›ci, ktÃ³rego uÅ¼ywamy piszÄ…c [extensionality t].
    DziÄ™ki temu pozostaje nam pokazaÄ‡ jedynie, Å¼e [f t] jest rÃ³wne tej
    drugie funkcji dla argumentu [t]. W tym celu rozbijamy [f t], a oba
    wyraÅ¼enia okazujÄ… siÄ™ byÄ‡ konwertowalne. *)

Theorem T4_illegal : False.
Proof.
  apply (Cantor' bad negb).
    destruct b; inversion 1.
    apply bad_sur.
Qed.

(** Skoro mamy surjekcjÄ™ z [T4] w [T4 -> bool], katastrofy nie da siÄ™
    uniknÄ…Ä‡.

    MoglibyÅ›my siÄ™ teÅ¼ zastanowiÄ‡ nad napisaniem zapÄ™tlajÄ…cej siÄ™ funkcji
    [loop], ale coÅ› czujÄ™, Å¼e ty coÅ› czujesz, Å¼e byÅ‚oby to babranie siÄ™
    w niepotrzebnym problemie. Wobec tego (oczywiÅ›cie o ile dotychczas
    siÄ™ nie skapnÄ…Å‚eÅ›) poczuj siÄ™ oÅ›wiecony! *)

Definition loop (x : T4) : bool := bad x x.

(** Ha! Tak tak, [loop] nie jest niczym innym niÅ¼ lekko rozmnoÅ¼onÄ… wersjÄ…
    [bad]. *)

Lemma loop_nontermination :
  true = loop (c0 (
    fun g : (T4 -> bool) -> nat =>
    match g loop with
       | 0 => R
       | _ => G
    end)).
Proof.
  unfold loop, bad. destruct (dcase _) as [bad eq].
  rewrite 5!eq.
Abort.

(** A skoro [loop] to tylko inne [bad], to nie powinno ciÄ™ teÅ¼ wcale a
    wcale zdziwiÄ‡, Å¼e najbardziej oczywisty argument, dla ktÃ³rego [loop]
    siÄ™ zapÄ™tla, jest Å¼ywcem wziÄ™ty z dowodu [bad_sur] (choÄ‡ oczywiÅ›cie
    musimy zastÄ…piÄ‡ [f] przez [loop]).

    OczywiÅ›cie niemoÅ¼liwe jest, Å¼eby formalnie udowodniÄ‡ w Coqu, Å¼e coÅ›
    siÄ™ zapÄ™tla. PowyÅ¼szy lemat ma byÄ‡ jedynie demonstracjÄ… - rÄ™czne
    rozpisanie tego przykÅ‚adu byÅ‚oby zbyt karkoÅ‚omne. Jak widaÄ‡ z dowodu,
    przepisywanie rÃ³wnania definiujÄ…cego [bad] tworzy wesoÅ‚Ä… piramidkÄ™
    zrobionÄ… z [match]y i [if]Ã³w. JeÅ¼eli chcesz poczuÄ‡ peÅ‚niÄ™ zapÄ™tlenia,
    wypbrÃ³uj taktykÄ™ [rewrite !eq] - zapÄ™tli siÄ™ ona, gdyÅ¼ rÃ³wnanie [eq]
    moÅ¼na przepisywaÄ‡ w nieskoÅ„czonoÅ›Ä‡. *)

End T4.

(** MogÅ‚oby siÄ™ wydawaÄ‡, Å¼e teraz to juÅ¼ na pewno nasze metody dziaÅ‚ajÄ… na
    wszystkie moÅ¼liwe negatywne typy induktywne. CytujÄ…c Tadeusza Sznuka:
    "Nic bardziej mylnego!". *)

Module T5.

Axioms
  (T5 : Type)
  (c0 : (((T5 -> nat) -> bool) -> Color) -> T5)
  (dcase :
    forall
      (P : T5 -> Type)
      (Pc0 : forall f : ((T5 -> nat) -> bool) -> Color, P (c0 f)),
        {f : forall x : T5, P x |
          forall g : ((T5 -> nat) -> bool) -> Color,
            f (c0 g) = Pc0 g}).

(** RzuÄ‡my okiem na powyÅ¼szy typ. WyglÄ…da podobnie do poprzedniego, ale jest
    nieco inny - typy [nat] i [bool] zamieniÅ‚y siÄ™ miejscami. Jakie rodzi to
    konsekwencje? SprawdÅºmy. *)

Definition bad : T5 -> (T5 -> nat).
Proof.
  apply (dcase (fun _ => T5 -> _)).
  intros f x.
  apply (
    fun c : Color =>
    match c with
        | R => 0
        | G => 1
        | B => 2
    end).
  apply f. intro g.
  apply isZero. exact (g x).
Defined.

(** Definicja [bad] jest podobna jak poprzednio, ale tym razem konwertujemy
    [Color] na [nat] za pomocÄ… funkcji, ktÃ³ra nie jest surjekcjÄ…. *)

Require Import FunctionalExtensionality.

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective. intro f.
  unfold bad. destruct (dcase _) as [bad eq].
  exists (c0 (
    fun g : (T5 -> nat) -> bool =>
    match g f with
        | true => R
        | false => B
    end)).
  rewrite eq. extensionality t.
  destruct (f t); cbn.
    reflexivity.
Abort.

(** DowÃ³d rÃ³wnieÅ¼ przebiega podobnie jak poprzednio. ZaÅ‚amuje siÄ™ on dopiero,
    gdy na samym koÅ„cu rozbijamy wyraÅ¼enie [f t] i upraszczamy uÅ¼ywajÄ…c [cbn].
    W pierwszym podcelu [0 = 0] jeszcze jakoÅ› udaje siÄ™ nam udowodniÄ‡, ale w
    drugim naszym oczom ukazuje siÄ™ cel [2 = S n].

    Problem polega na tym, Å¼e [f t] moÅ¼e byÄ‡ dowolnÄ… liczbÄ… naturalnÄ…, ale
    zastosowana przez nas funkcja konwertujÄ…ca [Color -> nat] moÅ¼e zwracaÄ‡
    jedynie [0], [1] lub [2]. Teraz widzimy jak na dÅ‚oni, skÄ…d wziÄ…Å‚ siÄ™
    wymÃ³g, by funkcja konwertujÄ…ca byÅ‚a surjekcjÄ…. *)

Definition loop (x : T5) : nat := bad x x.

Lemma loop_nontermination :
  42 = loop (c0 (
    fun g : (T5 -> nat) -> bool =>
    match g loop with
        | true => R
        | false => G
    end)).
Proof.
  unfold loop, bad. destruct (dcase _) as [bad eq].
  rewrite 15!eq.
Abort.

(** Co ciekawe, mimo Å¼e nie jesteÅ›my w stanie pokazaÄ‡ surjektywnoÅ›ci [bad],
    to wciÄ…Å¼ moÅ¼emy uÅ¼yÄ‡ tej funkcji do zdefiniowania zapÄ™tlajÄ…cej siÄ™
    funkcji [loop], zupeÅ‚nie jak w poprzednim przykÅ‚adzie.

    Niesmak jednak pozostaje, gdyÅ¼ szczytem naszych ambicji nie powinno byÄ‡
    ograniczanie siÄ™ do zdefiniowania [loop], lecz do formalnego udowodnienia
    nielegalnoÅ›ci [T5]. Czy wszystko stracone? Czy umrzemy? Tu dramatyczna
    pauza.

    Nie.

    Okazuje siÄ™, Å¼e jest pewien trikowy sposÃ³b na rozwiÄ…zanie tego problemu,
    a mianowicie: zamiast prÃ³bowaÄ‡ wyjÄ…Ä‡ z [T5] funkcjÄ™ [T5 -> nat], wyjmiemy
    stamtÄ…d po prostu funckjÄ™ [T5 -> bool] i to mimo tego, Å¼e jej tam nie ma!
*)

Definition bad' : T5 -> (T5 -> bool).
Proof.
  apply (dcase (fun _ => T5 -> _)).
  intros f x.
  apply (
    fun c : Color =>
    match c with
        | R => true
        | _ => false
    end).
  apply f. intro g.
  apply isZero. exact (g x).
Defined.

(** W kluczowych momentach najpierw konwertujemy [Color] na [bool] tak jak
    w jednym z poprzednich przykÅ‚adÃ³w, a potem konwertujemy [nat] na [bool]
    za pomocÄ… funkcji [isZero]. *)

Require Import FunctionalExtensionality.

Lemma bad'_sur :
  surjective bad'.
Proof.
  unfold surjective. intro f.
  unfold bad'. destruct (dcase _) as [bad' eq].
  exists (c0 (
    fun g : (T5 -> nat) -> bool =>
      if g (fun t : T5 => if f t then 0 else 1) then R else G)).
  rewrite eq.
  extensionality t.
  destruct (f t); cbn; reflexivity.
Qed.

(** PoniewaÅ¼ obydwie nasze funkcjÄ™ konwertujÄ…ce byÅ‚y surjekcjami, moÅ¼emy je
    teraz odwrÃ³ciÄ‡ i wykazaÄ‡ ponad wszelkÄ… wÄ…tpliwoÅ›Ä‡, Å¼e [bad'] faktycznie
    jest surjekcjÄ…. *)

Theorem T5_illegal : False.
Proof.
  apply (Cantor' bad' negb).
    destruct b; inversion 1.
    apply bad'_sur.
Qed.

(** SprÃ³bujmy podsumowaÄ‡, co tak naprawdÄ™ staÅ‚o siÄ™ w tym przykÅ‚adzie.

    Tym razem, mimo Å¼e do [T5] moÅ¼emy wÅ‚oÅ¼yÄ‡ dowolnÄ… funkcjÄ™ [T5 -> nat],
    to nie moÅ¼emy jej potem wyjÄ…Ä‡, uzyskujÄ…c surjekcjÄ™, gdyÅ¼ zawadzajÄ…
    nam w tym typy po prawych stronach strzaÅ‚ek ([bool] i [Color]), ktÃ³re
    majÄ… za maÅ‚o elementÃ³w, Å¼eby mÃ³c surjektywnie przekonwertowaÄ‡ je na
    typ [nat].

    Jednak jeÅ¼eli mamy wszystkie moÅ¼liwe funkcje typu [T5 -> nat], to
    moÅ¼emy przerobiÄ‡ je (w locie, podczas "wyciÄ…gania") na wszystkie
    moÅ¼liwe funkcje typu [T5 -> bool], skÅ‚adajÄ…c je z odpowiedniÄ…
    surjekcjÄ… (np. [isZero]). PoniewaÅ¼ typ [bool] i [Color] jesteÅ›my
    w stanie surjektywnie przekonwertowaÄ‡ na [bool], reszta procesu
    dziaÅ‚a podobnie jak w poprzednich przykÅ‚adach. *)

Definition loop' (x : T5) : bool := bad' x x.

Lemma loop_nontermination :
  true = loop' (c0 (
    fun g : (T5 -> nat) -> bool =>
    match g (fun t : T5 => if loop' t then 0 else 1) with
        | true => R
        | false => G
    end)).
Proof.
  unfold loop', bad'. destruct (dcase _) as [bad' eq].
  rewrite 15!eq.
Abort.

(** Takie trikowe [bad'] wciÄ…Å¼ pozwala nam bez wiÄ™kszych przeszkÃ³d
    zdefiniowaÄ‡ zapÄ™tlajÄ…cÄ… siÄ™ funkcjÄ™ [loop']. OsiÄ…gnÄ™liÅ›my wiÄ™c
    peÅ‚en sukces.

    W ogÃ³lnoÅ›ci nasz trik moÅ¼naby sformuÅ‚owaÄ‡ tak: jeÅ¼eli mamy konstruktor
    negatywny typu [T], to moÅ¼emy wyjÄ…Ä‡ z niego funkcjÄ™ [T -> A], gdzie [A]
    jest najmniejszym z typÃ³w wystÄ™pujÄ…cych po prawych stronach strzaÅ‚ek.

    No, teraz to juÅ¼ na pewno mamy obcykane wszystkie przypadki, prawda?
    Tadeuszu Sznuku przybywaj: "OtÃ³Å¼ nie tym razem!". *)

End T5.

Module T6.

Axioms
  (T6 : Type)
  (c0 : (((T6 -> unit) -> bool) -> Color) -> T6)
  (dcase :
    forall
      (P : T6 -> Type)
      (Pc0 : forall f : ((T6 -> unit) -> bool) -> Color, P (c0 f)),
        {f : forall x : T6, P x |
          forall g : ((T6 -> unit) -> bool) -> Color,
            f (c0 g) = Pc0 g}).

(** Kolejnym upierdliwym przypadkiem, burzÄ…cym nawet nasz ostateczny
    trik, jest sytuacja, w ktÃ³rej po prawej stronie strzaÅ‚ki wystÄ…pi
    typ [unit]. OczywiÅ›cie zgodnie z trikiem moÅ¼emy z [T6] wyciÄ…gnÄ…Ä‡
    surjekcjÄ™ [T6 -> unit], ale jest ona oczywiÅ›cie bezuÅ¼yteczna, bo
    takÄ… samÄ… moÅ¼emy zrobiÄ‡ za darmo, stale zwracajÄ…c po prostu [tt].
    Surjekcja ta nie wystarcza rzecz jasna, Å¼eby odpaliÄ‡ twierdzenie
    Cantora.

    Tym razem jednak nie powinniÅ›my spodziewaÄ‡ siÄ™, Å¼e upierdliwoÅ›Ä‡ tÄ™
    bÄ™dzie daÅ‚o siÄ™ jakoÅ› obejÅ›Ä‡. Typ [T6 -> unit] jest jednoelementowy
    (jedynym elementem jest [fun _ => tt]) podobnie jak [unit]. Bardziej
    poetycko moÅ¼emy powiedzieÄ‡, Å¼e [T6 -> unit] i [unit] sÄ… izomorficzne,
    czyli prawie rÃ³wne - rÃ³Å¼niÄ… siÄ™ tylko nazwami elementÃ³w ("nazwa"
    jedynego elementu [unit]a to [tt]).

    Skoro tak, to typ konstruktora [c0], czyli
    [(((T6 -> unit) -> bool) -> Color) -> T6)], moÅ¼emy rÃ³wnie dobrze
    zapisaÄ‡ jako [((unit -> bool) -> Color) -> T6)]. ZauwaÅ¼my teraz,
    Å¼e [unit -> bool] jest izomorficzne z [bool], gdyÅ¼ ma tylko dwa
    elementy, a mianowicie [fun _ => true] oraz [fun _ => false].
    Tak wiÄ™c typ [c0] moÅ¼emy jeszcze proÅ›ciej zapisaÄ‡ jako
    [(bool -> Color) -> T6], a to oznacza, Å¼e typ [T6] jest jedynie
    owijkÄ… na funkcje typu [bool -> Color]. Twierdzenie Cantora nie
    pozwala tutaj uzyskaÄ‡ sprzecznoÅ›ci.

    Czy zatem takie typy sa legalne? Syntaktycznie nie - Coq odrzuca je
    podobnie jak wszystkie inne negatywne typy induktywne. Semantycznie
    rÃ³wnieÅ¼ nie - o ile nie moÅ¼emy uzyskaÄ‡ jawnej sprzecznoÅ›ci, to nasze
    rozwaÅ¼ania o nieterminacji wciÄ…Å¼ sÄ… w mocy.

    Przypomnij sobie poprzedni przykÅ‚ad i nieudanÄ… prÃ³bÄ™ wyÅ‚uskania z
    [T5] surjekcji [T5 -> nat]. UdaÅ‚o nam siÄ™ zaimplementowaÄ‡ funkcjÄ™
    [bad], ktÃ³rej surjektywnoÅ›ci nie potrafiliÅ›my pokazaÄ‡, ale pomimo
    tego bez problemu udaÅ‚o nam siÄ™ uÅ¼yÄ‡ jej do napisania funkcji [loop].
    W obecnym przykÅ‚adzie jest podobnie i nieterminacja to najlepsze, na
    co moÅ¼emy liczyÄ‡. *)

(** **** Ä†wiczenie *)

(** Zdefiniuj funkcjÄ™ [bad], a nastÄ™pnie uÅ¼yj jej do zdefiniowania funkcji
    [loop]. Zademonstruj w sposÃ³b podobny jak poprzednio, Å¼e [loop] siÄ™
    zapÄ™tla. *)

(* begin hide *)
Definition bad : T6 -> (T6 -> unit).
Proof.
  apply (dcase (fun _ => T6 -> _)).
  intros f x.
  apply (
    fun c : Color =>
    match c with
        | R => tt
        | G => tt
        | B => tt
    end).
  apply f. intro g.
  apply (
    fun u : unit =>
    match u with
        | tt => true
    end).
  exact (g x).
Defined.

Definition loop (x : T6) : unit := bad x x.

Lemma loop_nontermination :
  tt = loop (c0 (
    fun g : (T6 -> unit) -> bool =>
    match g loop with
        | true => R
        | false => G
    end)).
Proof.
  unfold loop, bad. destruct (dcase _) as [bad eq].
  rewrite 4!eq.
Abort.
(* end hide *)

End T6.

(** No, teraz to juÅ¼ na pewno wiemy wszystko... *)

(** **** Ä†wiczenie *)

(** OtÃ³Å¼ nie do koÅ„ca. Ostatnim hamulcowym, groÅºniejszym nawet niÅ¼ [unit],
    jest wystÄ…pienie po prawej stronie strzaÅ‚ki typu (czy raczej zdania)
    [False]. W tym przypadku nie tylko nie pomaga nam Cantor, ale nie
    pomaga teÅ¼ nieterminacja, gdyÅ¼ najzwyczajniej w Å›wiecie nie da siÄ™
    zdefiniowaÄ‡ Å¼adnej funkcji.

    Jako, Å¼e za cholerÄ™ nie wiem, co z tym fantem zrobiÄ‡, zostawiam go tobie
    jako Ä‡wiczenie: wymyÅ›l metodÄ™ pokazywania nielegalnoÅ›ci negatywnych typÃ³w
    induktywnych, w ktÃ³rych po prawej stronie strzaÅ‚ki jest co najmniej
    jedno wystÄ…pienie [False]. *)

Module T8.

Axioms
  (T8 : Type)
  (c0 : (((T8 -> bool) -> False) -> Color) -> T8)
  (dcase :
    forall
      (P : T8 -> Type)
      (Pc0 : forall f : ((T8 -> bool) -> False) -> Color, P (c0 f)),
        {f : forall x : T8, P x |
          forall g : ((T8 -> bool) -> False) -> Color,
            f (c0 g) = Pc0 g}).

(* begin hide *)

Definition bad : T8 -> (T8 -> bool).
Proof.
  apply (dcase (fun _ => T8 -> _)).
  intros f x.
  apply (
    fun c : Color =>
    match c with
        | R => true
        | _ => false
    end).
  apply f. intro g.
Abort.

(* end hide *)

End T8.



(** ** Promocja 2 w 1 czyli paradoksy Russella i Girarda *)

(** _Istnieje teoria, Å¼e jeÅ›li kiedyÅ› ktoÅ› siÄ™ dowie, dlaczego powstaÅ‚o i
    czemu sÅ‚uÅ¼y uniwersum, to zniknie ono i zostanie zastÄ…pione czymÅ›
    znacznie dziwaczniejszym i jeszcze bardziej pozbawionym sensu_. *)

(** _Istnieje takÅ¼e teoria, Å¼e dawno juÅ¼ tak siÄ™ staÅ‚o_. *)

(** Douglas Adams, _Restauracja na koÅ„cu wszechÅ›wiata_ *)

(** W poprzednich podrozdziaÅ‚ach poznaliÅ›my twierdzenie Cantora oraz
    nauczyliÅ›my siÄ™ uÅ¼ywaÄ‡ go jako mÅ‚ota na negatywne typy induktywne.

    W tym podrozdziale zapoznamy siÄ™ z dwoma paradoksami (a precyzyjniej
    piszÄ…c, z dwoma wersjami tego samego paradoksu), ktÃ³re okaÅ¼Ä… siÄ™ byÄ‡
    Å›ciÅ›le powiÄ…zane z twierdzeniem Cantora, a ktÃ³re bÄ™dÄ… sÅ‚uÅ¼yÄ‡ nam gdy
    staniemy w szranki z negatwynymi typami induktywno-rekurencyjnymi
    (czyli tymi, ktÃ³re definiuje siÄ™ przez indukcjÄ™-rekursjÄ™). O tak: w
    tym podrozdziale, niczym Thanos, staniemy do walki przeciw uniwersum!

    Zacznijmy od paradoksu Russella. Jest to bardzo stary paradoks, odkryty
    w roku 1901 przez... zgadnij kogo... gdy Ã³w czÅ‚ek szukaÅ‚ dziury w caÅ‚ym
    w naiwnej teorii zbiorÃ³w (ktÃ³ra to teoria jest juÅ¼ od dawna martwa).

    SformuÅ‚owanie paradoksu brzmi nastÄ™pujÄ…co: niech V bÄ™dzie zbiorem
    wszystkich zbiorÃ³w, ktÃ³re nie naleÅ¼Ä… same do siebie. Pytanie: czy
    V naleÅ¼y do V?

    Gdzie tu paradoks? OtÃ³Å¼ jeÅ¼eli V naleÅ¼y do V, to na mocy definicji V,
    V nie naleÅ¼y do V. JeÅ¼eli zaÅ› V nie naleÅ¼y do V, to na mocy definicji V,
    V naleÅ¼y do V. Nie trzeba chyba dodawaÄ‡, Å¼e jednoczesne naleÅ¼enie i
    nienaleÅ¼enie prowadzi do sprzecznoÅ›ci. *)

(** **** Ä†wiczenie *)

(** To genialne Ä‡wiczenie wymyÅ›liÅ‚em dziÄ™ki zabÅ‚Ä…dzeniu na esperanckiej
    Wikipedii (ha! nikt nie spodziewaÅ‚ siÄ™ esperanckiej Wikipedii w
    Ä‡wiczeniu dotyczÄ…cym paradoksu Russella). Ä†wiczenie brzmi tak:

    W Wikipedii niektÃ³re artykuÅ‚y sÄ… listami (nie, nie w sensie typu
    induktywnego :)), np. lista krajÃ³w wedÅ‚ug PKB per capita. Pytanie:
    czy moÅ¼na stworzyÄ‡ w Wikipedii listÄ™ wszystkich list? Czy na liÅ›cie
    wszystkich list ona sama jest wymieniona? Czy moÅ¼na w Wikipedii
    stworzyÄ‡ listÄ™ wszystkich list, ktÃ³re nie wymieniajÄ… same siebie? *)

(** Na czym tak naprawdÄ™ polega paradoks? JakiÅ› mÄ…dry (czyli przemÄ…drzaÅ‚y)
    filozof mÃ³gÅ‚by rzec, Å¼e na naduÅ¼yciu pojÄ™cia zbioru... albo czymÅ›
    rÃ³wnie absurdalnym. OtÃ³Å¼ nie! Paradoks Russella polega na tym samym,
    co caÅ‚a masa innych paradoksÃ³w, czyli na autoreferencji.

    Z autoreferencjÄ… spotkaliÅ›my siÄ™ juÅ¼ co najmniej raz, w rozdziale
    pierwszym. Przypomnij sobie, Å¼e golibroda goli tych i tylko tych,
    ktÃ³rzy sami siebie nie golÄ…. Czy golibroda goli sam siebie?

    Takie postawienie sprawy daje paradoks. Podobnie z Russellem: V zawiera
    te i tylko te zbiory, ktÃ³re nie zawierajÄ… same siebie. Czy V zawiera
    V? Wot, paradoks. Å»eby lepiej wczuÄ‡ siÄ™ w ten klimat, czas na wiÄ™cej
    Ä‡wiczeÅ„. *)

(** **** Ä†wiczenie *)

(** A jak jest z poniÅ¼szym paradoksem wujka Janusza?

    Wujek Janusz lubi tych (i tylko tych) czÅ‚onkÃ³w rodziny, ktÃ³rzy sami
    siebie nie lubiÄ… oraz nie lubi tych (i tylko tych), ktÃ³rzy sami siebie
    lubiÄ…. Czy wujek Janusz lubi sam siebie? *)

(** **** Ä†wiczenie *)

(** PowyÅ¼sze Ä‡wiczenie miaÅ‚o byÄ‡ ostatnim, ale co tam, dam jeszcze trochÄ™.
    Co czuje serce twoje (ewentualnie: co widzisz przed oczyma duszy swojej)
    na widok poniÅ¼szych wesoÅ‚ych zdaÅ„?

    "To zdanie jest faÅ‚szywe."

    "Zdanie po prawej jest faÅ‚szywe. Zdanie po lewej jest prawdziwe."

    "Zdanie po prawej jest prawdziwe. Zdanie po lewej jest faÅ‚szywe."
*)

(** Dobra, wystarczy juÅ¼ tych paradoksÃ³w... a nie, czekaj. PrzecieÅ¼ zostaÅ‚
    nam do omÃ³wienia jeszcze paradoks Girarda. Jednak poznawszy juÅ¼ tajniki
    autoreferencji, powinno pÃ³jÅ›Ä‡ jak z pÅ‚atka.

    Paradoks Girarda to paradoks, ktÃ³ry moÅ¼e zaistnieÄ‡ w wielu systemach
    formalnych, takich jak teorie typÃ³w, jÄ™zyki programowania, logiki i
    inne takie. Å¹rÃ³dÅ‚em caÅ‚ego zÅ‚a jest zazwyczaj stwierdzenie w stylu
    [Type : Type]. *)

Check Type.
(* ===> Type : Type *)

(** O nie! CzyÅ¼byÅ›my wÅ‚aÅ›nie zostali zaatakowani przez paradoks Girarda?
    W tym miejscu naleÅ¼y przypomnieÄ‡ (albo obwieÅ›ciÄ‡ - niestety nie pamiÄ™tam,
    czy juÅ¼ o tym wspominaÅ‚em), Å¼e [Type] jest w Coqu jedynie synonimem dla
    czegoÅ› w stylu [Type(i)], gdzie [i] jest "poziomem" sortu [Type], zaÅ›
    kaÅ¼de [Type(i)] Å¼yje tak naprawdÄ™ w jakimÅ› [Type(j)], gdzie [j] jest
    wiÄ™ksze od [i] - typy niÅ¼szego poziomu Å¼yjÄ… w typach wyÅ¼szego poziomu.
    BÄ™dziesz mÃ³gÅ‚ Ã³w fakt ujrzeÄ‡ na wÅ‚asne oczy, gdy w CoqIDE zaznaczysz
    opcjÄ™ [View > Display universe levels]. *)

Check Type.
(* ===> Type@{Top.590} : Type@{Top.590+1} *)

(** Jak widaÄ‡, jest mniej wiÄ™cej tak jak napisaÅ‚em wyÅ¼ej. Nie przejmuj siÄ™
    tym tajemniczym [Top] - to tylko nic nieznaczÄ…cy bibelocik. W twoim
    przypadku rÃ³wnieÅ¼ poziom uniwersum moÅ¼e byÄ‡ inny niÅ¼ [590]. Co wiÄ™cej,
    poziom ten bÄ™dzie siÄ™ zwiÄ™kszaÅ‚ wraz z kaÅ¼dym odpaleniem komendy [Check
    Type] (czyÅ¼byÅ› pomyÅ›laÅ‚ wÅ‚aÅ›nie o doliczeniu w ten sposÃ³b do zyliona?).

    Skoro juÅ¼ wiemy, Å¼e NIE zostaliÅ›my zaatakowani przez paradoks Girarda,
    to w czym problem z tym caÅ‚ym [Type : Type]? JakiÅ› przemÄ…drzaÅ‚y (czyli
    mÄ…dry) adept informatyki teoretycznej mÃ³gÅ‚by odpowiedzieÄ‡, Å¼e to zaleÅ¼y
    od konkretnego systemu formalnego albo coÅ› w tym stylu. OtÃ³Å¼ niet! Jak
    zawsze, chodzi oczywiÅ›cie o autoreferencjÄ™.

    Gdyby ktoÅ› byÅ‚ zainteresowany, to najlepsze dotychczas sformuÅ‚owanie
    paradoksu znalazÅ‚em (zupeÅ‚nie przez przypadek, wcale nie szukajÄ…c) w
    pracy "An intuitionistic theory of types" Martina-LÃ¶fa (swojÄ… drogÄ…,
    ten koleÅ› wymyÅ›liÅ‚ podstawy duÅ¼ej czÄ™Å›ci wszystkiego, czym siÄ™ tutaj
    zajmujemy). MoÅ¼na jÄ… przeczytaÄ‡ tu (paradoks Girarda jest pod koniec
    pierwszej sekcji):
    archive-pml.github.io/martin-lof/pdfs
    /An-Intuitionistic-Theory-of-Types-1972.pdf

    Nasze sformuÅ‚owanie paradoksu bÄ™dzie w sumie podobne do tego z powyÅ¼szej
    pracy (co jest w sumie ciekawe, bo wymyÅ›liÅ‚em je samodzielnie i to przez
    przypadek), ale dowÃ³d sprzecznoÅ›ci bÄ™dzie inny - na szczÄ™Å›cie duÅ¼o
    prostszy (albo i nie...).

    Dobra, koniec tego glÄ™dzenia. Czas na konkrety. *)

(*
Fail Inductive U : Type :=
    | Pi : forall (A : U) (B : El A -> U), U
    | UU : U

with El (u : U) : Type :=
match u with
    | Pi A B => forall x : El A, B x
    | UU => U
end.
*)

(** PowyÅ¼sza induktywno-rekurencyjna definicja typu [U] (i interpretujÄ…cej
    go funkcji [El]), ktÃ³rÄ… Coq rzecz jasna odrzuca (uczcijmy Å‚awÄ™ oburzonych
    minutÄ… oburzenia) to definicja pewnego uniwersum.

    W tym miejscu wypadaÅ‚oby wytÅ‚umaczyÄ‡, czym sÄ… uniwersa. OtÃ³Å¼ odpowiedÅº
    jest doÅ›Ä‡ prosta: uniwersum skÅ‚ada siÄ™ z typu [U : Type] oraz funkcji
    [El : U -> Type]. Intuicja w tym wszystkim jest taka, Å¼e elementami
    typu [U] sÄ… nazwy typÃ³w (czyli bytÃ³w sortu [Type]), zaÅ› fukncja [El]
    zwraca typ, ktÃ³rego nazwÄ™ dostanie.

    ChoÄ‡ z definicji widaÄ‡ to na pierwszy rzut oka, to zaskakujÄ…cy moÅ¼e
    wydaÄ‡ ci siÄ™ fakt, Å¼e w zasadzie kaÅ¼dy typ moÅ¼na zinterpretowaÄ‡ jako
    uniwersum i to zazwyczaj na bardzo wiele rÃ³Å¼nych sposobÃ³w (tyle ile
    rÃ³Å¼nych interpretacji [El] jesteÅ›my w stanie wymyÅ›liÄ‡). Najlepiej
    bÄ™dzie, jeÅ¼eli przemyÅ›lisz to wszystko w ramach Ä‡wiczenia. *)

(** **** Ä†wiczenie *)

(** Ä†wiczenie bÄ™dzie konceptualne, a skÅ‚ada siÄ™ na nie kilka Å‚amigÅ‚Ã³wek:
    - zinterpretuj [False] jako uniwersum
    - zinterpretuj [unit] jako uniwersum (ile jest moÅ¼liwych sposobÃ³w?)
    - czy istnieje uniwersum, ktÃ³re zawiera nazwÄ™ samego siebie? Uwaga:
      to nie jest tak proste, jak moÅ¼e siÄ™ wydawaÄ‡ na pierwszy rzut oka.
    - wymyÅ›l ideologicznie sÅ‚usznÄ… interpretacjÄ™ typu [nat] jako uniwersum
      (tak, jest taka). NastÄ™pnie wymyÅ›l jakÄ…Å› gÅ‚upiÄ… interpretacjÄ™ [nat]
      jako uniwersum. Dlaczego ta interpretacja jest gÅ‚upia?
    - zdefiniuj uniwersum, ktÃ³rego elementami sÄ… nazwy typÃ³w funkcji z
      n-krotek liczb naturalnych w liczby naturalne. Uwaga: rozwiÄ…zanie
      jest bardzo eleganckie i moÅ¼esz siÄ™ go nie spodziewaÄ‡.
    - czy istnieje uniwersum, ktÃ³rego interpretacja jest surjekcjÄ…? Czy
      da siÄ™ w Coqu udowodniÄ‡, Å¼e tak jest albo nie jest? Uwaga: tak
      bardzo podchwytliwe, Å¼e aÅ¼ sam siÄ™ zÅ‚apaÅ‚em. *)

(* begin hide *)

(** Odpowiedzi:
    - [False] to uniwersum puste, w ktÃ³rym nic nie ma - a myÅ›laÅ‚eÅ›, Å¼e co?
    - [unit] to uniwersum zawierajÄ…ce nazwÄ™ jednego, wybranego typu - teÅ¼
      niezbyt odkrywcza odpowiedÅº. Interpretacji jest tyle ile typÃ³w.
    - Tak, istnieje uniwersum zawierajÄ…ce nazwÄ™ samego siebie, np. [unit].
    - Ideologicznie sÅ‚uszna interpretacja [nat] to uniwersum typÃ³w
      skoÅ„czonych - [El n] to typ n-elementowy. GÅ‚upia interpretacja:
      kaÅ¼de [n] jest nazwÄ… dla tego samego typu, np. [nat].
    - Tutaj maÅ‚y twist, bo tym uniwersum teÅ¼ jest [nat]
    - Tutaj teÅ¼ trochÄ™ twist, bo takie uniwersum oczywiÅ›cie istnieje i
      nazywa siÄ™... baram bam bam bam... fanfary... [Type]! No cÃ³Å¼, nie
      tego siÄ™ spodziewaÅ‚eÅ›, prawda? A co do tego, czy istnieje takie
      induktywne uniwersum, to myÅ›lÄ™, Å¼e dla kaÅ¼dego kandydata z osobna
      daÅ‚oby siÄ™ pokazaÄ‡, Å¼e nie jest ono wystarczajÄ…co dobre. *)

(* end hide *)

(** Skoro wiemy juÅ¼, czym sÄ… uniwersa, przyjrzyjmy siÄ™ temu, ktÃ³re wÅ‚aÅ›nie
    zdefiniowaliÅ›my. Å»ebyÅ› nie musiaÅ‚ w rozpaczy przewijaÄ‡ do gÃ³ry, tak
    wyglÄ…da aksjomatyczne kodowanie tego uniwersum: *)

Module PoorUniverse.

Axioms
  (U : Type)
  (El : U -> Type)

  (Pi : forall (A : U) (B : El A -> U), U)
  (UU : U)

  (El_Pi :
    forall (A : U) (B : El A -> U),
      El (Pi A B) = forall (x : El A), El (B x))
  (El_UU : El UU = U)

  (ind : forall
    (P : U -> Type)
    (PPi :
      forall (A : U) (B : El A -> U),
        P A -> (forall x : El A, P (B x)) -> P (Pi A B))
    (PUU : P UU),
      {f : forall u : U, P u |
        (forall (A : U) (B : El A -> U),
          f (Pi A B) =
          PPi A B (f A) (fun x : El A => f (B x))) /\
        (f UU = PUU)
      }
  ).

(** [U] to typ, ktÃ³rego elementami sÄ… nazwy typÃ³w, zaÅ› [El] jest jego
    interpretacjÄ…. Nazwy moÅ¼emy tworzyÄ‡ tylko na dwa sposoby: jeÅ¼eli [A : U]
    jest nazwÄ… typu, zaÅ› [B : El A -> U] jest rodzinÄ… nazw typÃ³w indeksowanÄ…
    przez elementy typu [A], to [Pi A B] jest nazwÄ… typu
    [forall x : El A, El (B x)]. Drugim konstruktorem jest [UU], ktÃ³re
    oznacza nazwÄ™ samego uniwersum, tzn. [El UU = U].

    ReguÅ‚a indukcji jest doÅ›Ä‡ prosta: jeÅ¼eli [P : U -> Type] jest rodzinÄ…
    typÃ³w (tych prawdziwych) indeksowanÄ… przez [U] (czyli nazwy typÃ³w), to
    Å¼eby zdefiniowaÄ‡ funkcjÄ™ [f : forall u : U, P u] musimy mieÄ‡ dwie rzeczy:
    po pierwsze, musimy pokazaÄ‡, Å¼e [P (Pi A B)] zachodzi, gdy zachodzi [P A]
    oraz [P (B x)] dla kaÅ¼dego [x : El A]. Po drugie, musi zachodziÄ‡ [P UU].

    Mimo, Å¼e uniwersum wydaje siÄ™ biedne, jest ono Å›miertelnie sprzeczne,
    gdyÅ¼ zawiera nazwÄ™ samego siebie. JeÅ¼eli rozwiÄ…zaÅ‚eÅ› (poprawnie, a nie
    na odwal!) ostatnie Ä‡wiczenie, to powinieneÅ› wiedzieÄ‡, Å¼e niektÃ³re
    uniwersa mogÄ… zawieraÄ‡ nazwy samego siebie i wcale to a wcale nie daje
    to Å¼adnych problemÃ³w.

    Dlaczego wiÄ™c w tym przypadku jest inaczej? Skoro [UU] nie jest zÅ‚e samo
    w sobie, to problem musi leÅ¼eÄ‡ w [Pi], bo niby gdzie indziej? Zobaczmy
    wiÄ™c, gdzie kryje siÄ™ sprzecznoÅ›Ä‡. W tym celu posÅ‚uÅ¼ymy siÄ™ twierdzeniem
    Cantora: najpierw pokaÅ¼emy surjekcjÄ™ [U -> (U -> U)], a potem, za pomocÄ…
    metody przekÄ…tniowej, Å¼e taka surjekcja nie moÅ¼e istnieÄ‡. *)

(*
Definition bad (u : U) : U -> U :=
match u with
    | Pi UU B => B
    | _ => fun u : U => U
end.
*)

(** JeÅ¼eli dostajemy [Pi A B], gdzie [A] to [UU], to wtedy [B : El A -> U]
    tak naprawdÄ™ jest typu [U -> U] (bo [El UU = U]). W innych przypadkach
    wystarczy po prostu zwrÃ³ciÄ‡ funkcjÄ™ identycznoÅ›ciowÄ…. Niestety Coq nie
    wspiera indukcji-rekursji (Å‚awa oburzonych), wiÄ™c funkcjÄ™ [bad] musimy
    zdefiniowaÄ‡ rÄ™cznie: *)

Definition bad : U -> (U -> U).
Proof.
  apply (ind (fun _ => U -> U)).
    Focus 2. exact (fun u : U => u).
    intros A B _ _. revert A B.
      apply (ind (fun A : U => (El A -> U) -> (U -> U))).
        intros; assumption.
        intro B. rewrite El_UU in B. exact B.
Defined.

(** PowyÅ¼sza definicja za pomocÄ… taktyk dziaÅ‚a dokÅ‚adnie tak samo jak
    nieformalna definicja [bad] za pomocÄ… dopasowania do wzorca. Jedyna
    rÃ³Å¼nica jest taka, Å¼e [El UU] nie jest definicyjnie rÃ³wne [U], lecz
    sÄ… one jedynie zdaniowo rÃ³wne na mocy aksjomatu [El_UU : El UU = U].
    Musimy wiÄ™c przepisaÄ‡ go w [B], Å¼eby typy siÄ™ zgadzaÅ‚y.

    Zanim bÄ™dziemy mogli pokazaÄ‡, Å¼e [bad] jest surjekcjÄ…, czeka nas kilka
    niemiÅ‚ych detali technicznych (gdyby [El UU] i [U] byÅ‚y definicyjnie
    rÃ³wne, wszystkie te problemy by zniknÄ™Å‚y). *)

Check eq_rect.
(* ===> forall (A : Type) (x : A) (P : A -> Type),
          P x -> forall y : A, x = y -> P y *)

Check eq_rect_r.
(* ===> forall (A : Type) (x : A) (P : A -> Type),
          P x -> forall y : A, y = x -> P y *)

(** [eq_rect] oraz [eq_rect_r] to groÅºnie wyglÄ…dajÄ…ce lematy, ale sprawa tak
    na prawdÄ™ jest doÅ›Ä‡ prosta: to one wykonujÄ… caÅ‚Ä… pracÄ™ za kaÅ¼dym razem,
    kiedy uÅ¼ywasz taktyki [rewrite]. JeÅ¼eli cel jest postaci [P x] i uÅ¼yjemy
    na nim [rewrite H], gdzie [H : x = y], to [rewrite] zamienia cel na
    [eq_rect _ _ _ cel _ H], ktÃ³re jest juÅ¼ typu [P y]. [eq_rect_r] dziaÅ‚a
    podobnie, ale tym razem rÃ³wnoÅ›Ä‡ jest postaci [y = x] (czyli obrÃ³cona).

    PoniewaÅ¼ w definicji [bad] uÅ¼ywaliÅ›my [rewrite]'a, to przy dowodzeniu,
    Å¼e [bad] jest surjekcjÄ…, bÄ™dziemy musieli zmierzyÄ‡ siÄ™ wÅ‚aÅ›nie z
    [eq_rect] i [eq_rect_r]. StÄ…d poniÅ¼szy lemat, ktÃ³ry mÃ³wi mniej wiÄ™cej,
    Å¼e jeÅ¼eli przepiszemy z prawa na lewo, a potem z lewa na prawo, to tak,
    jakby nic siÄ™ nie staÅ‚o. *)

Lemma right_to_left_to_right :
  forall
    (A : Type) (P : A -> Type) (x y : A) (p : x = y) (u : P y),
      eq_rect x P (@eq_rect_r A y P u x p) y p = u.
Proof.
  destruct p. cbn. reflexivity.
Qed.

(** DowÃ³d jest banalny. PoniewaÅ¼ [eq_rect] i [eq_rect_r] sÄ… zdefiniowane
    przez dopasowanie do wzorca [p : x = y], to wystarczy [p] potraktowaÄ‡
    [destruct]em, a dalej wszystko juÅ¼ Å‚adnie siÄ™ oblicza. *)

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective, bad; intro f.
  destruct (ind _) as [bad [bad_Pi bad_UU]].
  destruct (ind _) as [bad' [bad'_Pi bad'_UU]].
  pose (f' := eq_rect_r (fun T : Type => T -> U) f El_UU).
  exists (Pi UU f'). unfold f'.
  rewrite bad_Pi, bad'_UU, right_to_left_to_right. reflexivity.
Qed.

(** Dlaczego [bad] jest surjekcjÄ…? Intuicyjnie piszÄ…c, kaÅ¼dÄ… funkcjÄ™
    [U -> U] moÅ¼emy wÅ‚oÅ¼yÄ‡ do konstruktora [Pi] jako jego drugi argument,
    jeÅ¼eli tylko zamienimy pierwsze [U] na [El UU]. Skoro kaÅ¼dÄ… moÅ¼emy
    tam wÅ‚oÅ¼yÄ‡, to kaÅ¼dÄ… moÅ¼emy wyjÄ…Ä‡. Ot i caÅ‚y sekret.

    Technicznie dowÃ³d realizujemy tak: odwijamy definicje i wprowadzamy do
    kontekstu funkcjÄ™ [f]. NastÄ™pnie rozbijamy [ind _] pochodzÄ…ce z definicji
    [bad], rozkÅ‚adajÄ…c w ten sposÃ³b definicjÄ™ [bad] na wÅ‚aÅ›ciwe [bad] (sama
    funkcja), [bad'] (wewnÄ™trzna funkcja pomocnicza) oraz rÃ³wnania dla [bad]
    i [bad'] dla poszczegÃ³lnych przypadkÃ³w.

    NastÄ™pnie musimy znaleÅºÄ‡ takie [a : U], Å¼e [bad a = f]. Robimy to, co
    zasugerowaÅ‚em wyÅ¼ej, czyli w [f : U -> U] pierwsze [U] zamieniamy na
    [El UU], uzyskujÄ…c w ten sposÃ³b [f']. Temu wÅ‚aÅ›nie sÅ‚uÅ¼y uÅ¼ycie
    [eq_rect_r] (nie uÅ¼ywamy [rewrite], bo potrzeba nam wiÄ™kszej precyzji).

    Wobec tego szukanym przez nas elementem [U], ktÃ³remu [bad] przyporzÄ…dkuje
    [f], jest [Pi UU f']. MoÅ¼emy w tym miejscu odwinÄ…Ä‡ definicjÄ™ [f']. Gdyby
    Coq wspieraÅ‚ indukcjÄ™-rekursjÄ™, to w tym miejscu wystarczyÅ‚oby uÅ¼yÄ‡ tylko
    [reflexivity] - [bad (Pi UU f')] obliczyÅ‚oby siÄ™ do [f] na mocy definicji
    [bad] oraz dziÄ™ki temu, Å¼e [El UU] obliczyÅ‚oby siÄ™ do [U]. Niestety Coq
    nie wspiera indukcji rekursji (Å‚awa oburzonych), wiÄ™c musimy wszystkie
    te trzy kroki obliczeÅ„ wykonaÄ‡ rÄ™cznie za pomocÄ… taktyki [rewrite].

    Ufff, udaÅ‚o siÄ™! JeÅ¼eli przeraÅ¼a ciÄ™ ten dowÃ³d - nie martw siÄ™. Chodzi
    w nim o to samo, o co chodziÅ‚o w poprzednich dowodach bycia surjekcjÄ….
    Ten jest po prostu trochÄ™ bardziej skomplikowany, bo indukcja-rekursja
    jest nieco bardziej skomplikowana do uÅ¼ycia w Coqu niÅ¼ prymitywniejsze
    formy indukcji. *)

Definition change : U -> U.
Proof.
  apply ind.
    intros. exact UU.
    exact (Pi UU (fun _ => UU)).
Defined.

(** Teraz czas udowodniÄ‡, Å¼e [bad] nie jest surjekcjÄ…. Zrobimy to metodÄ…
    przekÄ…tniowÄ…, a w tym celu potrzebowaÄ‡ bÄ™dziemy funkcji [U -> U], ktÃ³ra
    dla kaÅ¼dego argumentu zwraca coÅ›, co jest od niego rÃ³Å¼ne.

    Na szczÄ™Å›cie sprawa jest prosta: jeÅ¼eli argumentem jest [Pi A B], to
    zwracamy [UU], zaÅ› jeÅ¼eli [UU], to zwracamy [Pi UU (fun _ => UU)]. *)

Definition discern : U -> bool.
Proof.
  apply ind.
    intros. exact true.
    exact false.
Defined.

(** PrzydaÅ‚aby siÄ™ teÅ¼ funkcja, ktÃ³ra pozwoli nam rozrÃ³Å¼niÄ‡ konstruktory
    typu [U]. Normalnie uÅ¼ylibyÅ›my do tego taktyki [inversion], ale
    uÅ¼ywamy kodowania aksjomatycznego, wiÄ™c [inversion] nie zadziaÅ‚a i
    musimy rÄ™cznie zaimplementowaÄ‡ sobie coÅ› w jej stylu.

    Nasza funkcja dla [Pi] zwraca [true], a dla [UU] daje [false]. *)

Lemma change_neq :
  forall u : U, change u <> u.
Proof.
  apply ind.
    intros A B H1 H2 eq.
      apply (f_equal discern) in eq.
      unfold change, discern in eq.
      destruct (ind _) as [d [d_Pi d_UU]],
               (ind _) as [ch [ch_Pi ch_UU]].
      rewrite d_Pi, ch_Pi, d_UU in eq. inversion eq.
    intro eq.
      apply (f_equal discern) in eq.
      unfold change, discern in eq.
      destruct (ind _) as [d [d_Pi d_UU]],
               (ind _) as [ch [ch_Pi ch_UU]].
      rewrite ch_UU, d_Pi, d_UU in eq. inversion eq.
Qed.

(** WypadaÅ‚oby teÅ¼ pokazaÄ‡, Å¼a nasza funkcja dziaÅ‚a tak, jak sobie tego
    Å¼yczymy. DowÃ³d jest bardzo prosty, ale aksjomatyczne kodowanie znacznie
    go zaciemnia.

    Zaczynamy od indukcji po [u : U]. W pierwszym przypadku mamy hipotezÄ™
    [eq : change (Pi A B) = Pi A B], a skoro tak, to po zaaplikowaniu
    [discern] musi byÄ‡ takÅ¼e [discern (change (Pi A B)) = discern (Pi A B)].

    NastÄ™pnie rozkÅ‚adamy definicje [change] i [discern] na atomy ([change]
    nazywa siÄ™ teraz [ch], a [discern] nazywa siÄ™ [d]). Przepisujemy
    odpowiednie rÃ³wnania w hipotezie [eq], dziÄ™ki czemu uzyskujemy
    [false = true], co jest sprzeczne. Drugi przypadek jest analogiczny. *)

Lemma bad_not_sur :
  ~ surjective bad.
Proof.
  unfold surjective. intro.
  destruct (H (fun u : U => change (bad u u))) as [u eq].
  apply (f_equal (fun f => f u)) in eq.
  apply (change_neq (bad u u)). symmetry. assumption.
Qed.

(** Teraz moÅ¼emy juÅ¼ pokazaÄ‡, Å¼e [bad] nie jest surjekcjÄ…. W tym celu
    wyobraÅºmy sobie [bad] jako kwadratowÄ… tabelkÄ™, ktÃ³rej wiersze i
    kolumny sÄ… indeksowane przez [U]. Tworzymy nowÄ… funkcjÄ™ [U -> U]
    biorÄ…c elementy z przekÄ…tnej i modyfikujÄ…c je za pomocÄ… [change].

    Skoro [bad] jest surjekcjÄ…, to ta nowa funkcja musi byÄ‡ postaci
    [bad u] dla jakiegoÅ› [u : U]. AplikujÄ…c obie strony jeszcze raz
    do [u] dostajemy rÃ³wnanie [bad u u = change (bad u u)], ktÃ³re
    jest sprzeczne na mocy lematu [change_neq]. *)

Definition U_illegal : False.
Proof.
  apply bad_not_sur. apply bad_sur.
Qed.

(** PoniewaÅ¼ [bad] jednoczeÅ›nie jest i nie jest surjekcjÄ…, nastepuje nagÅ‚y
    atak sprzecznoÅ›ci. Definicja uniwersum [U] przez indukcjÄ™-rekursjÄ™ jest
    nielegalna. Tak wÅ‚aÅ›nie prezentujÄ… siÄ™ paradoksy Russella i Girarda w
    Coqowym wydaniu. *)

End PoorUniverse.

(** **** Ä†wiczenie *)

(** Tak naprawdÄ™, to w tym podrozdziale byliÅ›my co najwyÅ¼ej bieda-Thanosem,
    gdyÅ¼ uniwersum, z ktÃ³rym siÄ™ Å›cieraliÅ›my, samo byÅ‚o biedne. W niniejszym
    Ä‡wiczeniu zmierzysz siÄ™ z uniwersum, ktÃ³re zawiera teÅ¼ nazwy typu pustego,
    typu [unit] i liczb naturalnych, nazwy produktÃ³w, sum i funkcji, a takÅ¼e
    sum zaleÅ¼nych.

    MÃ³wiÄ…c wprost: zakoduj aksjomatycznie poniÅ¼szÄ… definicjÄ™ uniwersum [U],
    a nastÄ™pnie udowodnij, Å¼e jest ona nielegalna. Nie powinno to byÄ‡
    trudne - metoda jest podobna jak w przypadku biednego uniwersum. *)

Module NonPoorUniverse.

(*
Fail Inductive U : Type :=
    | Empty : U
    | Unit : U
    | Nat : U
    | Prod : U -> U -> U
    | Sum : U -> U -> U
    | Arr : U -> U -> U
    | Pi : forall (A : U) (B : El A -> U), U
    | Sigma: forall (A : U) (B : El A -> U), U
    | UU : U

with El (u : U) : Type :=
match u with
    | Empty => Empty_set
    | Unit => unit
    | Nat => nat
    | Prod A B => El A * El B
    | Sum A B => El A + El B
    | Arr A B => El A -> El B
    | Pi A B => forall x : El A, B x
    | Sigma A B => {x : El A & El (B x)}
    | UU => U
end.
*)

(* begin hide *)
Axioms
  (U : Type)
  (El : U -> Type)

  (Empty : U)
  (Unit : U)
  (Nat : U)
  (Prod : U -> U -> U)
  (Sum : U -> U -> U)
  (Arr : U -> U -> U)
  (Pi : forall (A : U) (B : El A -> U), U)
  (Sigma : forall (A : U) (B : El A -> U), U)
  (UU : U)

  (El_Empty : El Empty = Empty_set)
  (El_Unit : El Unit = unit)
  (El_Nat : El Nat = nat)
  (El_Prod : forall A B : U, El (Prod A B) = (El A * El B)%type)
  (El_Sum : forall A B : U, El (Sum A B) = (El A + El B)%type)
  (El_Arr : forall A B : U, El (Arr A B) = El A -> El B)
  (El_Pi :
    forall (A : U) (B : El A -> U),
      El (Pi A B) = forall (x : El A), El (B x))
  (El_Sigma :
    forall (A : U) (B : El A -> U),
      El (Pi A B) = {x : El A & El (B x)})
  (El_UU : El UU = U)

  (ind : forall
    (P : U -> Type)
    (PEmpty : P Empty)
    (PUnit : P Unit)
    (PNat : P Nat)
    (PProd :
      forall A B : U, P A -> P B -> P (Prod A B))
    (PSum :
      forall A B : U, P A -> P B -> P (Sum A B))
    (PArr :
      forall A B : U, P A -> P B -> P (Arr A B))
    (PPi :
      forall (A : U) (B : El A -> U),
        P A -> (forall x : El A, P (B x)) -> P (Pi A B))
    (PSigma :
      forall (A : U) (B : El A -> U),
        P A -> (forall x : El A, P (B x)) -> P (Sigma A B))
    (PUU : P UU),
      {f : forall A : U, P A |
        (f Empty = PEmpty) /\
        (f Unit = PUnit) /\
        (f Nat = PNat) /\
        (forall A B : U,
          f (Prod A B) = PProd A B (f A) (f B)) /\
        (forall A B : U,
          f (Sum A B) = PSum A B (f A) (f B)) /\
        (forall A B : U,
          f (Arr A B) = PArr A B (f A) (f B)) /\
        (forall (A : U) (B : El A -> U),
          f (Pi A B) =
          PPi A B (f A) (fun x : El A => f (B x))) /\
        (forall (A : U) (B : El A -> U),
          f (Sigma A B) =
          PSigma A B (f A) (fun x : El A => f (B x))) /\
        (f UU = PUU)
      }).

Definition bad : U -> (U -> U).
Proof.
  apply (ind (fun _ => U -> U)).
    1-6,8-9: intros; assumption.
    intros A B _ _. revert A B.
      apply (ind (fun A : U => (El A -> U) -> (U -> U))).
        1-8: intros; assumption.
        intro f. rewrite El_UU in f. exact f.
Defined.

Lemma right_to_left_to_right :
  forall
    (A : Type) (P : A -> Type) (x y : A) (p : x = y) (u : P y),
      eq_rect x P (@eq_rect_r A y P u x p) y p = u.
Proof.
  destruct p. cbn. reflexivity.
Qed.

Lemma bad_sur :
  surjective bad.
Proof.
  unfold surjective, bad; intro f.
  destruct (ind _) as [g H]; decompose [and] H; clear H.
  destruct (ind _) as [h H']; decompose [and] H'; clear H'.
  pose (f' := eq_rect_r (fun T : Type => T -> U) f El_UU).
  exists (Pi UU f').
  rewrite H6. rewrite H17.
  unfold f'. rewrite right_to_left_to_right. reflexivity.
Qed.

Definition change : U -> U.
Proof.
  apply ind.
    exact Nat.
    all: intros; exact Empty.
Defined.

Definition discern : U -> nat.
Proof.
  apply ind; intros.
    exact 0.
    exact 1.
    exact 2.
    exact 3.
    exact 4.
    exact 5.
    exact 6.
    exact 7.
    exact 8.
Defined.

Ltac help H :=
  apply (f_equal discern) in H;
  cbn in H; unfold discern in H;
  destruct (ind _) as [help Hhelp];
  decompose [and] Hhelp; clear Hhelp;
  congruence.

Lemma change_neq :
  forall u : U, change u <> u.
Proof.
  apply ind; unfold change;
  destruct (ind _) as [f H]; decompose [and] H; clear H;
  intros; try help H; help H9.
Qed.

Lemma bad_not_sur :
  ~ surjective bad.
Proof.
  unfold surjective. intro.
  destruct (H (fun u : U => change (bad u u))) as [u eq].
  apply (f_equal (fun f => f u)) in eq.
  apply (change_neq (bad u u)). symmetry. assumption.
Qed.
(* end hide *)

Theorem U_illegal : False.
(* begin hide *)
Proof.
  apply bad_not_sur. apply bad_sur.
Qed.
(* end hide *)

End NonPoorUniverse.

(** ** Pozytywne typy induktywne *)

(** Na koniec rozprawimy siÄ™ z pozytywnymi typami "induktywnymi" (ale tylko
    do pewnego stopnia; tak po prawdzie, to raczej one rozprawiÄ… siÄ™ z
    nami). *)

Fail Inductive Pos : Type :=
    | Pos0 : ((Pos -> bool) -> bool) -> Pos.
(* ===> The command has indeed failed with message:
        Non strictly positive occurrence of "Pos" in
        "((Pos -> bool) -> bool) -> Pos". *)

(** Coq odrzuca powyÅ¼szÄ… definicjÄ™ typu [Pos], gdyÅ¼ pierwsze wystÄ…pienie [Pos]
    w typie konstruktora [Pos0] nie jest Å›ciÅ›le pozytywne. I faktycznie - gdy
    policzymy niedobroÅ›Ä‡ tego wystÄ…pienia zgodnie z naszym wzorem, to wyjdzie,
    Å¼e wynosi ona 2, gdyÅ¼ [Pos] wystÄ™puje na lewo od dwÃ³ch strzaÅ‚ek (pamiÄ™taj,
    Å¼e najbardziej zewnÄ™trzna strzaÅ‚ka, czyli ta, na prawo od ktÃ³rej teÅ¼ jest
    [Pos], nie liczy siÄ™ - wzÃ³r dotyczy tylko argumentÃ³w konstruktora, a nie
    caÅ‚ego konstruktora). *)

Axioms
  (Pos : Type)
  (Pos0 : ((Pos -> bool) -> bool) -> Pos)
  (dcase :
    forall
      (P : Pos -> Type)
      (PPos0 : forall g : (Pos -> bool) -> bool, P (Pos0 g)),
        {f : forall x : Pos, P x |
          forall g : (Pos -> bool) -> bool,
            f (Pos0 g) = PPos0 g}).

(** SprÃ³bujmy zawalczyÄ‡ z typem [Pos] naszÄ… metodÄ… opartÄ… o twierdzenie
    Cantora. Najpierw kodujemy typ [Pos] aksjomatycznie, a nastÄ™pnie
    sprÃ³bujemy zdefiniowaÄ‡ [bad], czyli surjekcjÄ™ z [Pos] w [Pos -> bool]. *)

Definition bad : Pos -> (Pos -> bool).
Proof.
  apply (dcase (fun _ => Pos -> bool)).
  intros f x.
  apply f. intro y.
  apply f. intro z.
  apply f. intro w.
  (* ad infinitum *)
Abort.

(** MogÅ‚oby siÄ™ wydawaÄ‡, Å¼e wyciÄ…gniÄ™cie z [Pos] funkcji [Pos -> bool]
    nie moÅ¼e byÄ‡ trudniejsze, niÅ¼ zabranie dziecku cukierka. Niestety
    jednak nie jest tak, gdyÅ¼ w [Pos] tak naprawdÄ™ nie ma Å¼adnej takiej
    funkcji - jest funkcja [(Pos -> bool) -> bool], a to juÅ¼ zupeÅ‚nie
    coÅ› innego.

    Å»eby lepiej zrozumieÄ‡ tÄ™ materiÄ™, musimy metaforycznie zinterpretowaÄ‡
    znany nam juÅ¼ wspÃ³Å‚czynnik niedobroÅ›ci i wynikajÄ…cy z niego podziaÅ‚
    na wystÄ…pienia Å›ciÅ›le pozytywne, pozytywne i negatywne. DziÄ™ki tej
    interpretacji dowiemy siÄ™ teÅ¼, dlaczego nieparzysta niedobroÅ›Ä‡ jest
    negatywna, a niezerowa parzysta jest pozytywna.

    NajproÅ›ciej jest zinterpretowaÄ‡ wystÄ…pienia Å›ciÅ›le pozytywne, gdyÅ¼
    mieliÅ›my juÅ¼ z nimi sporo do czynienia. WeÅºmy konstruktor
    [cons : A -> list A -> list A]. Jest tutaj jedno Å›ciÅ›le pozytywne
    wystÄ…pienie typu [list A], ktÃ³re moÅ¼emy interpretowaÄ‡ tak: gdy
    uÅ¼ywamy dopasowania do wzorca i dopasuje siÄ™ [cons h t], to "mamy"
    element [t] typu [list A]. Ot i caÅ‚a filozofia.

    ZaÅ‚Ã³Å¼my teraz na chwilÄ™, Å¼e Coq akceptuje negatywne i pozytywne
    typy induktywne. Co by byÅ‚o, gdybyÅ›my dopasowali konstruktor postaci
    [c : (T -> bool) -> T]? Tym razem nie mamy elementu typu [T], lecz
    funkcjÄ™ [f : T -> bool]. ParafrazujÄ…c: musimy "daÄ‡" funkcji [f]
    element typu [T], Å¼eby dostaÄ‡ [bool].

    A co by byÅ‚o, gdybyÅ›my dopasowali konstruktor postaci
    [c : ((T -> bool) -> bool) -> T]? Tym razem rÃ³wnieÅ¼ nie mamy Å¼adnego
    elementu typu [T], lecz funkcjÄ™ [f : ((T -> bool) -> bool)].
    ParafrazujÄ…c: musimy daÄ‡ funkcji [f] jakÄ…Å› funkcjÄ™ typu [T -> bool],
    Å¼eby dostaÄ‡ [bool]. Ale gdy konstruujemy funkcjÄ™ [T -> bool], to na
    wejÅ›ciu dostajemy [T]. Tak wiÄ™c poczÄ…tkowo nie mamy Å¼adnego [T], ale
    gdy o nie poprosimy, to moÅ¼emy je dostaÄ‡. Ba! Jak pokazuje przykÅ‚ad,
    moÅ¼emy dostaÄ‡ bardzo duÅ¼o [T].

    Taka wÅ‚aÅ›nie jest rÃ³Å¼nica miÄ™dzy Å›cisÅ‚Ä… pozytywnoÅ›ciÄ… (mamy coÅ›),
    negatywnoÅ›ciÄ… (musimy coÅ› daÄ‡) i pozytywnoÅ›ciÄ… (moÅ¼emy coÅ› dostaÄ‡,
    i to nawet w duÅ¼ej liczbie sztuk). ZauwaÅ¼my, Å¼e jedynie w przypadku
    negatywnym moÅ¼emy wyjÄ…Ä‡ z [T] funkcjÄ™ [T -> coÅ›] (chyba, Å¼e zawadza
    nam [unit] lub [False]), bo to jedyny przypadek, gdy Å¼Ä…dajÄ… od nas
    [T] (a skoro Å¼Ä…dajÄ… [T], to muszÄ… mieÄ‡ funkcjÄ™, ktÃ³ra coÅ› z tym [T]
    zrobi). W przypadku pozytywnym nie ma Å¼adnej takiej funkcji - to my
    dostajemy [T] i musimy coÅ› z niego wyprodukowaÄ‡, wiÄ™c to my jesteÅ›my
    tÄ… funkcjÄ…!

    Ufff... mam nadziejÄ™, Å¼e powyÅ¼sza bajeczka jest sformuÅ‚owana zrozumiale,
    bo lepszego wytÅ‚umaczenia nie udaÅ‚o mi siÄ™ wymyÅ›liÄ‡.

    MoglibyÅ›my w tym miejscu zastanowiÄ‡ siÄ™, czy nie uda nam siÄ™ pokazaÄ‡
    sprzecznoÅ›ci choÄ‡ na metapoziomie, poprzez napisanie nieterminujÄ…cej
    funkcji [loop]. Szczerze piszÄ…c, to niezbyt w to wierzÄ™. Przypomnij
    sobie, Å¼e okazaÅ‚o siÄ™, Å¼e funkcja [loop] jest bardzo Å›ciÅ›le powiÄ…zana
    z funkcjÄ… [bad], zaÅ› esencja nieterminacji polegaÅ‚a na przekazaniu
    do [loop] jako argument czegoÅ›, co zawieraÅ‚o [loop] jako podterm
    (jeÅ¼eli nie zauwaÅ¼yÅ‚eÅ›, to wszystkie nasze nieterminujÄ…ce funkcje
    udaÅ‚o nam siÄ™ zdefiniowaÄ‡ jedynie za pomocÄ… reguÅ‚y zaleÅ¼nej analizy
    przypadkÃ³w - bez indukcji, bez rekursji!). To daje nam jako takÄ…
    podstawÄ™ by wierzyÄ‡, Å¼e nawet nieterminacja nie jest w tym przypadku
    osiÄ…galna. *)

(* begin hide *)
Definition loop : Pos -> bool.
Proof.
  apply dcase.
  intros f.
  apply f. intro x.
Abort.

(* f : (Pos -> bool) -> bool *)

Fail Fixpoint loop (x : Pos) : bool :=
match x with
    | Pos0 f => f loop
end.

(*

    Niech x := Pos0 (fun y : Pos -> bool => 

    loop (Pos0 (fun _ => true)) =
    (fun _ => true) loop =
    true

*)
(* end hide *)

(** W tym momencie naleÅ¼y sobie zadaÄ‡ zasadnicze pytanie: dlaczego w ogÃ³le
    pozytywne typy induktywne sÄ… nielegalne? PrzecieÅ¼ odrÃ³Å¼nienie wystÄ…pienia
    pozytywnego od negatywnego nie jest czymÅ› trudnym, wiÄ™c Coq nie moÅ¼e ich
    od tak po prostu nie rozrÃ³Å¼niaÄ‡ - musi mieÄ‡ jakiÅ› powÃ³d!

    I faktycznie, powÃ³d jest. Nie ma on jednak wiele wspÃ³lnego z mechanizmem
    (pozytywnych) typÃ³w induktywnych samym w sobie, a z impredykatywnoÅ›ciÄ…
    sortu [Prop]. Trudne sÅ‚owo, co? Nie pamiÄ™tam, czy juÅ¼ to wyjaÅ›niaÅ‚em,
    wiÄ™c wyjaÅ›niÄ™ jeszcze raz.

    ImpredykatywnoÅ›Ä‡ (lub teÅ¼ impredykatywizm) to pewna forma autoreferencji,
    ktÃ³ra czasem jest nieszkodliwa, a czasem bardzo mordercza. Przyjrzyjmy
    siÄ™ nastÄ™pujÄ…cej definicji: "wujek Janusz to najbardziej wÄ…sata osoba w
    tym pokoju". Definicja ta jest impredykatywna, gdyÅ¼ definiuje ona wujka
    Janusza poprzez wyrÃ³Å¼nienie go z pewnej kolekcji osÃ³b, ale definicja tej
    kolekcji osÃ³b musi odwoÅ‚ywaÄ‡ siÄ™ do wujka Janusza ("w pokoju sÄ… wujek
    Janusz, ciocia GraÅ¼ynka, Sebastianek i Karynka"). W Coqu impredykatywny
    jest sort [Prop], co ilustruje przykÅ‚ad: *)

Definition X : Prop := forall P : Prop, P.

(** Definicja zdania [X] jest impredykatywna, gdyÅ¼ kwantyfikujemy w niej po
    wszystkich zdaniach ([forall P : Prop]), a zatem kwantyfikujemy takÅ¼e
    po zdaniu [X], ktÃ³re wÅ‚aÅ›nie definiujemy.

    ImpredykatywnoÅ›Ä‡ sortu [Prop] jest niegroÅºna (no chyba, Å¼e pragniemy
    pozytywnych typÃ³w induktywnych, to wtedy jest), ale impredykatywnoÅ›Ä‡
    dla [Type] byÅ‚aby zabÃ³jcza, co zresztÄ… powinien nam byÅ‚ uÅ›wiadomiÄ‡
    paradoks Russella.

    Dobra, koniec gadania. PoniÅ¼szy przykÅ‚ad poÅ›rednio pochodzi z sekcji
    3.1 pracy "Inductively defined types", ktÃ³rej autorami sÄ… Thierry
    Coquand oraz Christine Pauling-Mohring, zaÅ› bezpoÅ›rednio jest przerÃ³bkÄ…
    kodu wziÄ™tego z
    vilhelms.github.io/posts/why-must-inductive-types-be-strictly-positive *)

Fail Inductive Pos' : Type :=
    | Pos'0 : ((Pos' -> Prop) -> Prop) -> Pos'.

Axioms
  (Pos' : Type)
  (Pos'0 : ((Pos' -> Prop) -> Prop) -> Pos')
  (dcase' :
    forall
      (P : Pos' -> Type)
      (PPos'0 : forall g : (Pos' -> Prop) -> Prop, P (Pos'0 g)),
        {f : forall x : Pos', P x |
          forall g : (Pos' -> Prop) -> Prop,
            f (Pos'0 g) = PPos'0 g}).

(** Jak widaÄ‡, podejrzanym typem jest [Pos'], bliÅºniaczo podobne do [Pos],
    ale zamiast [bool] wystÄ™puje tutaj [Prop]. *)

Definition unwrap : Pos' -> ((Pos' -> Prop) -> Prop).
Proof.
  apply (dcase' (fun _ => (Pos' -> Prop) -> Prop)).
  intros f. exact f.
Defined.

(** Zaczynamy od zdefiniowania funkcji odwijajÄ…cej konstruktor. *)

Lemma Pos'0_inj :
  forall x y : (Pos' -> Prop) -> Prop,
    Pos'0 x = Pos'0 y -> x = y.
Proof.
  intros.
  apply (f_equal unwrap) in H. unfold unwrap in H.
  destruct (dcase' _) as [unwrap eq].
  rewrite 2!eq in H.
  assumption.
Qed.

(** DziÄ™ki [unwrap] moÅ¼emy Å‚atwo pokazaÄ‡, Å¼e konstruktor [Pos'0] jest
    injekcjÄ… (to coÅ›, co w przypadku zwykÅ‚ych typÃ³w induktywnych dostajemy
    za darmo od taktyki [inversion], ale cÃ³Å¼, nie tym razem!). *)

Definition i {A : Type} : A -> (A -> Prop) := 
  fun x y => x = y.

Lemma i_inj :
  forall (A : Type) (x y : A), i x = i y -> x = y.
Proof.
  unfold i. intros.
  apply (f_equal (fun f => f y)) in H.
  rewrite H. reflexivity.
Qed.

(** Kolejnym krokiem jest zdefiniowanie funkcji [i], ktÃ³ra jest injekcjÄ…
    z dowolnego typu [A] w typ [A -> Prop]. ZauwaÅ¼my, Å¼e krok ten w
    kluczowy sposÃ³b korzysta z rÃ³wnoÅ›ci, Å¼yjÄ…cej w sorcie [Prop] - gdyby
    zamiast [Prop] byÅ‚o [bool], nie moglibyÅ›my zdefiniowaÄ‡ tej injekcji. *)

Definition f (P : Pos' -> Prop) : Pos' := Pos'0 (i P).

Lemma f_inj :
  forall P Q : Pos' -> Prop, f P = f Q -> P = Q.
Proof.
  unfold f. intros.
  apply (f_equal unwrap) in H. unfold unwrap in H.
  destruct (dcase' _) as [unwrap eq].
  rewrite 2!eq in H.
  apply i_inj in H. assumption.
Qed.

(** SkÅ‚adajÄ…c ze soba [i] oraz konstruktor [Pos'0] dostajemy injekcjÄ™ z
    [Pos' -> Prop] w [Pos']. *)

Definition wut (x : Pos') : Prop :=
  exists P : Pos' -> Prop, f P = x /\ ~ P x.

Definition x : Pos' := f wut.

(** Tutaj nastÄ™pujÄ… najwiÄ™ksze czary, ktÃ³re uÅ¼ywajÄ… impredykatywnoÅ›ci. Nie
    mam Å¼adnego dobrej bajeczki, ktÃ³ra by je wyjaÅ›niaÅ‚a. *)

Lemma paradox : wut x <-> ~ wut x.
Proof.
  split.
    destruct 1 as (P & H1 & H2). intro H.
      unfold x in H1. apply f_inj in H1. subst. contradiction.
    intro H. unfold wut. exists wut. split.
      unfold x. reflexivity.
      assumption.
Qed.

(** [paradox] to twierdzenie, ktÃ³re chwyta esencjÄ™ caÅ‚ej sprawy. Z lewa na
    prawo rozbijamy dowÃ³d [wut x] i dostajemy predykat [P]. Wiemy, Å¼e
    [f P = x], ale [x = f wut], a poniewaÅ¼ [f] jest injekcjÄ…, to [P = wut].
    To jednak koÅ„czy siÄ™ sprzecznoÅ›ciÄ…, bo [wut x], ale [~ P x].

    Z prawa na lewo jest Å‚atwiej. Mamy [~ wut x] i musimy udowodniÄ‡ [wut x].
    Wystarczy, Å¼e istnieje pewien predykat, na ktÃ³ry wybieramy oczywiÅ›cie
    [wut], ktÃ³ry speÅ‚nia [f wut = x], co jest prawdÄ… na mocy definicji [x],
    oraz [~ wut x], co zachodzi na mocy zaÅ‚oÅ¼enia. *)

Theorem Pos'_illegal : False.
Proof.
  pose paradox. firstorder.
Qed.

(** No i bum. Jak widaÄ‡, pozytywne typy induktywne prowadzÄ… do sprzecznoÅ›ci,
    ale nie ma to z nimi wiele wspÃ³lnego, za to ma wiele wspÃ³lnego z sortem
    [Prop] i jego impredykatywnoÅ›ciÄ…. *)

(** * Podsumowanie *)

(** To juÅ¼ koniec naszej przydÅ‚ugiej podrÃ³Å¼y przez mechanizmy definiowania
    typÃ³w przez indukcjÄ™. W jej trakcie nauczyliÅ›my siÄ™ bardzo wielu rzeczy.

    ZaczÄ™liÅ›my od definiowania prostych enumeracji, operujÄ…cych na nich
    funkcji definiowanych za pomocÄ… dopasowania do wzorca oraz omÃ³wienia
    mechanizmu obliczania wyniku funkcji.

    NastÄ™pnie poznaliÅ›my rÃ³Å¼ne rozszerzenia tego podstawowego pomysÅ‚u
    definiowania typu za pomocÄ… konstruktorÃ³w reprezentujÄ…cych moÅ¼liwe
    wartoÅ›ci:
    - rekurencjÄ™, dziÄ™ki ktÃ³rej moÅ¼emy definiowaÄ‡ typy, ktÃ³rych
      termy majÄ… najprzerÃ³Å¼niejsze drzewiaste ksztaÅ‚ty
    - parametryzowane typy induktywne, ktÃ³rych gÅ‚Ã³wnym zastosowaniem
      jest definiowanie kontenerÃ³w o takich samych ksztaÅ‚tach, ale
      rÃ³Å¼nych przechowywanych typach
    - indukcjÄ™ wzajemnÄ…, w praktyce niezbyt uÅ¼ytecznÄ…, dziÄ™ki ktÃ³rej
      moÅ¼emy na raz zdefiniowaÄ‡ wiele typÃ³w odnoszÄ…cych siÄ™ do siebie
      nawzajem
    - indeksowane rodziny typÃ³w induktywnych, dziÄ™ki ktÃ³rym moÅ¼emy
      przez indukcjÄ™ definiowaÄ‡ predykaty oraz relacje
    - indukcjÄ™-indukcjÄ™, dziÄ™ki ktÃ³rej moÅ¼emy jednoczeÅ›nie zdefiniowaÄ‡
      typ oraz indeksowanÄ… nim rodzinÄ™ typÃ³w
    - indukcjÄ™-rekursjÄ™, dziÄ™ki ktÃ³rej moÅ¼emy jednoczesnie zdefiniowaÄ‡
      typ oraz funkcjÄ™ operujÄ…cÄ… na tym typie *)

(** NauczyliÅ›my siÄ™ definiowaÄ‡ funkcje przez rekursjÄ™ oraz dowodziÄ‡ ich
    wÅ‚aÅ›ciwoÅ›ci przez indukcjÄ™. PoznaliÅ›my definicje poznanych w pierwszym
    rozdziale spÃ³jnikÃ³w logicznych oraz odpowiadajÄ…cych im konstrukcji na
    typach, a takÅ¼e definicjÄ™ bardzo waÅ¼nej rodziny typÃ³w, czyli rÃ³wnoÅ›ci.

    PoznaliÅ›my podstawowe obiekty, ktÃ³rymi musi potrafiÄ‡ posÅ‚ugiwaÄ‡
    siÄ™ kaÅ¼dy programista, informatyk czy matematyk, a mianowicie
    wartoÅ›ci boolowskie, liczby naturalne oraz listy.

    NauczyliÅ›my siÄ™ formuÅ‚owaÄ‡ i implementowaÄ‡ reguÅ‚y indukcyjne (TODO:
    opisaÄ‡ to w gÅ‚Ã³wnym tekÅ›cie, a nie dopiero w przypomnieniu), a takÅ¼e,
    co powiÄ…zane, programowaÄ‡ listy przy pomocy foldÃ³w i unfoldÃ³w.

    Na koÅ„cu poznaliÅ›my kryterium Å›cisÅ‚ej pozytywnoÅ›ci, ktÃ³re obowiÄ…zuje
    wszystkie definicje typÃ³w induktywnych. DowiedzieliÅ›my siÄ™, Å¼e negatywne
    typy induktywne prowadzÄ… do nieterminacji, ktÃ³ra jest zÅ‚em wcielonym.
    PoznaliÅ›my pojÄ™cie surjekcji oraz twierdzenie Cantora, ktÃ³re rÃ³wnieÅ¼
    zabrania negatywnym typom induktywnym istnienia.

    PoznaliÅ›my teÅ¼ paradoks Russela/Girarda i jego zwiÄ…zek z twierdzeniem
    Cantora, autoreferencjÄ… oraz ideÄ… uniwersum zdefiniowanego za pomocÄ…
    indukcji-rekursji.

    Ostatecznie dowiedzieliÅ›my siÄ™, Å¼e pozytywne typy induktywne takÅ¼e sÄ…
    nielegalne, choÄ‡ jesteÅ›my wobec nich raczej bezsilni, no chyba Å¼e chodzi
    o impredykatywny (tego sÅ‚owa teÅ¼ siÄ™ nauczyliÅ›my) sort [Prop].

    CaÅ‚kiem sporo, prawda? Nie? No to w kolejnych rozdziaÅ‚ach bÄ™dzie jeszcze
    wiÄ™cej. *)