(** * I1: Ltac â€” jÄ™zyk taktyk *)

(** Matematycy uwaÅ¼ajÄ…, Å¼e po osiÄ…gniÄ™ciu pewnego poziomu zaawansowania i
    obycia (nazywanego zazwyczaj "mathematical maturity") skrupulatne
    rozpisywanie kaÅ¼dego kroku dowodu przestaje mieÄ‡ sens i pozwalajÄ…
    sobie zarzuciÄ‡ je na rzecz bardziej wysokopoziomowego opisu rozumowania.

    MyÅ›lÄ™, Å¼e ta sytuacja ma miejsce w twoim przypadku â€” znasz juÅ¼ sporÄ…
    czÄ™Å›Ä‡ jÄ™zyka termÃ³w Coqa (zwanego Gallina) i potrafisz dowodziÄ‡ rÃ³Å¼nych
    wÅ‚aÅ›ciwoÅ›ci programÃ³w. DoszedÅ‚eÅ› do punktu, w ktÃ³rym rÄ™czne klepanie
    dowodÃ³w przestaje byÄ‡ produktywne, a staje siÄ™ nudne i mÄ™czÄ…ce.

    Niestety, natura dowodu formalnego nie pozwala nam od tak po prostu
    pominÄ…Ä‡ maÅ‚o ciekawych krokÃ³w. Czy chcemy czy nie, aby Coq przyjÄ…Å‚
    dowÃ³d, kroki te muszÄ… zostaÄ‡ wykonane. Wcale nie znaczy to jednak,
    Å¼e to my musimy je wykonaÄ‡ â€” mogÄ… zrobiÄ‡ to za nas programy.

    Te programy to oczywiÅ›cie taktyki. WiÄ™kszoÅ›Ä‡ prymitywnych taktyk, jak
    [intro], [destruct], czy [assumption] juÅ¼ znamy. ChoÄ‡ nie wiesz o tym,
    uÅ¼ywaliÅ›my teÅ¼ wielokrotnie taktyk caÅ‚kiem zaawansowanych, takich jak
    [induction] czy [inversion], bez ktÃ³rych nasze formalne Å¼ycie byÅ‚oby
    drogÄ… przez mÄ™kÄ™.

    Wszystkie one sÄ… jednak taktykami wbudowanymi, danymi nam z gÃ³ry przez
    Coqowych bogÃ³w i nie mamy wpÅ‚ywu na ich dziaÅ‚anie. JeÅ¼eli nie jesteÅ›my
    w stanie zrobiÄ‡ czegoÅ› za ich pomocÄ…, jesteÅ›my zgubieni. Czas najwyÅ¼szy
    nauczyÄ‡ siÄ™ pisaÄ‡ wÅ‚asne taktyki, ktÃ³re pomogÄ… nam wykonywaÄ‡ maÅ‚o ciekawe
    kroki w dowodach, a w dalszej perspektywie takÅ¼e przeprowadzaÄ‡ bardziej
    zaawansowane rozumowania zupeÅ‚nie automatycznie.

    W tym rozdziale poznamy podstawy jÄ™zyka [Ltac], ktÃ³ry sÅ‚uÅ¼y do tworzenia
    wÅ‚asnych taktyk. Jego skÅ‚adniÄ™ przedstawiono i skrupulatnie opisano tu:
    https://coq.inria.fr/refman/proof-engine/ltac.html#syntax

    ChoÄ‡ przykÅ‚ad znaczy wiÄ™cej niÅ¼ 0x3E8 stron manuala i postaram siÄ™
    dokÅ‚adnie zilustrowaÄ‡ kaÅ¼dy istotny moim zdaniem konstrukt jÄ™zyka
    [Ltac], to i tak polecam zapoznaÄ‡ siÄ™ z powyÅ¼szym linkiem.

    Upewnij siÄ™ teÅ¼, Å¼e rozumiesz dokÅ‚adnie, jak dziaÅ‚ajÄ… podstawowe
    kombinatory taktyk, ktÃ³re zostaÅ‚y omÃ³wione w rozdziale 1, gdyÅ¼ nie
    bÄ™dziemy omawiaÄ‡ ich drugi raz. *)

(** * ZarzÄ…dzanie celami i selektory *)

(** DowodzÄ…c (lub konstruujÄ…c cokolwiek za pomocÄ… taktyk) mamy do rozwiÄ…zania
    jeden lub wiÄ™cej celÃ³w. Cele sÄ… ponumerowane i domyÅ›lnie zawsze pracujemy
    nad tym, ktÃ³ry ma numer 1.

    Jednak wcale nie musi tak byÄ‡ â€” moÅ¼emy zaznaczyÄ‡ inny cel i zaczÄ…Ä‡ nad
    nim pracowaÄ‡. SÅ‚uÅ¼y do tego komenda [Focus]. Cel o numerze n moÅ¼emy
    zaznaczyÄ‡ komendÄ… [Focus n]. JeÅ¼eli to zrobimy, wszystkie pozostaÅ‚e cele
    chwilowo znikajÄ…. Do stanu domyÅ›lnego, w ktÃ³rym pracujemy nad celem nr 1
    i wszystkie cele sÄ… widoczne moÅ¼emy wrÃ³ciÄ‡ za pomocÄ… komendy [Unfocus]. *)

Goal forall P Q R : Prop, P /\ Q /\ R -> R /\ Q /\ P.
Proof.
  repeat split.
  Focus 3.
  Unfocus.
  Focus 2.
Abort.

(** Komenda [Focus] jest uÅ¼yteczna gÅ‚Ã³wnie gdy ktÃ³ryÅ› z dalszych celÃ³w jest
    Å‚atwiejszy niÅ¼ obecny. MoÅ¼emy wtedy przeÅ‚Ä…czyÄ‡ siÄ™ na niego, rozwiÄ…zaÄ‡
    go i wyniesione stÄ…d doÅ›wiadczenie przenieÅ›Ä‡ na trudniejsze cele. Jest
    wskazane, Å¼eby po zakoÅ„czeniu dowodu zrefaktoryzowaÄ‡ go tak, aby komenda
    [Focus] w nim nie wystÄ™powaÅ‚a.

    Nie jest teÅ¼ tak, Å¼e zawsze musimy pracowaÄ‡ nad celem o numerze 1. MoÅ¼emy
    pracowaÄ‡ na dowolnym zbiorze celÃ³w. Do wybierania celÃ³w, na ktÃ³re chcemy
    zadziaÅ‚aÄ‡ taktykami, sÅ‚uÅ¼Ä… selektory. Jest ich kilka i majÄ… takÄ… skÅ‚adniÄ™:
    - [n: t] â€” uÅ¼yj taktyki t na n-tym celu. [1: t] jest rÃ³wnowaÅ¼ne [t].
    - [a-b: t] â€” uÅ¼yj taktyki t na wszystkich celach o numerach od a do b
    - [a_1-b_1, ..., a_n-b_n: t] â€” uÅ¼yj taktyki [t] na wszystkich celach
      o numerach od a_1 do b_1, ..., od a_n do b_n (zamiast a_i-b_i
      moÅ¼emy teÅ¼ uÅ¼yÄ‡ pojedynczej liczby)
    - [all: t]  ­- uÅ¼yj [t] na wszystkich celach
    - zamiast [t], w powyÅ¼szych przypadkach moÅ¼emy teÅ¼ uÅ¼yÄ‡ wyraÅ¼enia
      [> t_1 | ... | t_n], ktÃ³re aplikuje taktykÄ™ [t_i] do i-tego celu
      zaznaczonego danym selektorem *)

Goal forall P Q R : Prop, P /\ Q /\ R -> R /\ Q /\ P.
Proof.
  destruct 1 as [H [H' H'']]. repeat split.
  3: assumption. 2: assumption. 1: assumption.
Restart.
  destruct 1 as [H [H' H'']]. repeat split.
  1-2: assumption. assumption.
Restart.
  destruct 1 as [H [H' H'']]. repeat split.
  1-2, 3: assumption.
Restart.
  destruct 1 as [H [H' H'']]. repeat split.
  1-3: assumption.
Restart.
  destruct 1 as [H [H' H'']]. repeat split.
  all: assumption.
Restart.
  destruct 1 as [H [H' H'']]. repeat split.
  all: [> assumption | assumption | assumption].
Qed.

(** ZauwaÅ¼my, Å¼e powyÅ¼sze selektory dziaÅ‚ajÄ… jedynie, gdy zostanÄ… umieszczone
    przed wszystkimi taktykami, ktÃ³rych dotyczÄ…. PrÃ³ba uÅ¼ycia ich jako
    argumenty dla innych taktyk jest bÅ‚Ä™dem.

    Dla przykÅ‚adu, w czwartym z powyÅ¼szych dowodÃ³w nie moÅ¼emy napisaÄ‡
    [repeat split; 1-3: assumption], gdyÅ¼ koÅ„czy siÄ™ to bÅ‚Ä™dem skÅ‚adni
    (nie wspominajÄ…c o tym, Å¼e jest to bez sensu, gdyÅ¼ dla uzyskania
    poÅ¼Ä…danego efektu wystarczy napisaÄ‡ [repeat split; assumption]. *)

Goal forall P Q R : Prop, P /\ Q /\ R -> R /\ Q /\ P.
Proof.
  destruct 1 as [H [H' H'']].
  repeat split; only 1-3: assumption.
Qed.

(** Nie wszystko jednak stracone! Å»eby mÃ³c uÅ¼ywaÄ‡ wyraÅ¼eÅ„ zawierajÄ…cych
    selektory jako argumenty taktyk, moÅ¼emy posÅ‚uÅ¼yÄ‡ siÄ™ sÅ‚owem [only].
    Mimo tego, i tak nie moÅ¼emy napisaÄ‡ [repeat split; only all: ...],
    gdyÅ¼ koÅ„czy siÄ™ to bÅ‚Ä™dem skadni. *)

Goal forall P Q R S : Prop, P -> P /\ Q /\ R /\ S.
Proof.
  repeat split.
  revgoals. all: revgoals. all: revgoals.
  swap 1 3. all: swap 1 3. all: swap 1 3.
  cycle 42. all: cycle 3. all: cycle -3.
Abort.

(** Jest jeszcze kilka innych taktyk do Å¼onglowania celami. PamiÄ™taj, Å¼e
    wszystkie z nich dziaÅ‚ajÄ… na liÅ›cie celÃ³w wybranych selektorami â€”
    domyÅ›lnie wybrany jest tylko cel numer 1 i wtedy taktyki te nie majÄ…
    Å¼adnego skutku.

    [revgoals] odwraca kolejnoÅ›Ä‡ celÃ³w, na ktÃ³rych dziaÅ‚a. W naszym przypadku
    [revgoals] nie robi nic (odwraca kolejnoÅ›Ä‡ celu [P] na [P]), natomiast
    [all: revgoals] zamienia kolejnoÅ›Ä‡ celÃ³w z [P â€” Q â€” R â€” S] na
    [S â€” R â€” Q â€” P].

    [swap n m] zamienia miejscami cele n-ty i m-ty. W przykÅ‚adzie [swap 1 3]
    nic nie robi, gdyÅ› domyÅ›lnie wybrany jest tylko cel numer 1, a zatem nie
    moÅ¼na zamieniÄ‡ go miejscami z celem nr 3, ktÃ³rego nie ma. [all: swap 1 3]
    zamienia kolejnoÅ›Ä‡ celÃ³w z [P â€” Q â€” R â€” S] na [R â€” Q â€” P â€” S].

    [cycle n] przesuwa cele cyklicznie o [n] do przodu (lub do tyÅ‚u, jeÅ¼eli
    argument jest liczbÄ… ujemnÄ…). W naszym przykÅ‚adzie [cycle 42] nic nie robi
    (przesuwa cyklicznie cel [P] o 42 miejsca, co daje w wyniku [P]), zaÅ›
    [all: cycle 3] zamienia kolejnoÅ›Ä‡ celÃ³w z [P â€” Q â€” R â€” S] na
    [S â€” P â€” Q â€” R].

    Taktyki te nie sÄ… zbyt uÅ¼yteczne, a przynajmniej ja nigdy ich nie uÅ¼yÅ‚em,
    ale dla kompletnoÅ›ci wypadaÅ‚o o nich wspomnieÄ‡. JeÅ¼eli wÄ…tpisz w
    uÅ¼ytecznoÅ›Ä‡ selektorÃ³w... cÃ³Å¼, nie dziwiÄ™ ci siÄ™. Selektory przydajÄ… siÄ™
    gÅ‚Ã³wnie gdy chcemy napisaÄ‡ taktykÄ™ rozwiÄ…zujÄ…cÄ… wszystkie cele i
    sprawdzamy jej dziaÅ‚anie na kaÅ¼dym celu z osobna. W pozostaÅ‚ych przypadkach
    sÄ… tylko zbÄ™dnym balastem. *)

(** * Podstawy jÄ™zyka Ltac *)

(** Ltac jest funkcyjnym jÄ™zykiem programowania, podobnie jak jÄ™zyk termÃ³w
    Coqa (zwany Gallina), lecz te dwa jÄ™zyki sÄ… diametralnie rÃ³Å¼ne:
    - Ltac jest kompletny w sensie Turinga, a Gallina nie. W szczegÃ³lnoÅ›ci,
      taktyki mogÄ… siÄ™ zapÄ™tlaÄ‡ i nie rodzi to Å¼adnych problemÃ³w natury
      logicznej.
    - Ltac jest bardzo sÅ‚abo typowany, podczas gdy Gallina dysponuje potÄ™Å¼nym
      systemem typÃ³w.
    - W Ltacu nie moÅ¼emy definiowaÄ‡ typÃ³w danych, a jedynie taktyki dziaÅ‚ajÄ…ce
      na kontekstach i celu, podczas gdy Gallina pozwala na definiowanie
      bardzo szerokiej klasy typÃ³w i dziaÅ‚ajÄ…cych na nich funkcji.
    - Ltac, jako metajÄ™zyk jezyka Gallina, posiada dostÄ™p do rÃ³Å¼nych rzeczy,
      do ktÃ³rych Gallina nie ma dostÄ™pu, takich jak dopasowanie termÃ³w
      dowolnego typu. Dla przykÅ‚adu, w Ltacu moÅ¼emy odrÃ³Å¼niÄ‡ termy [4] oraz
      [2 + 2] pomimo tego, Å¼e sÄ… konwertowalne. *)

(** W Ltacu moÅ¼emy manipulowaÄ‡ trzema rodzajami bytÃ³w: taktykami, termami
    Coqa oraz liczbami caÅ‚kowitymi â€” te ostatnie nie sÄ… tym samym, co liczby
    caÅ‚kowite Coqa i bÄ™dziemy ich uÅ¼ywaÄ‡ sporadycznie. Zanim zobaczymy
    przykÅ‚ad, przyjrzyjmy siÄ™ taktyce [pose] oraz konstruktowi [let]. *)

Goal True.
Proof.
  pose true.
  pose (nazwa := 123).
Abort.

(** [pose t] dodaje do kontekstu term o domyÅ›lnej nazwie, ktÃ³rego ciaÅ‚em
    jest [t]. MoÅ¼emy teÅ¼ napisaÄ‡ [pose x := t], dziÄ™ki czemu zyskujemy
    kontrolÄ™ nad nazwÄ… termu. *)

Goal True.
Proof.
  Fail let x := 42 in pose x.
  let x := constr:(42) in pose x.
  let x := split in idtac x.
Abort.

(** W Ltacu, podobnie jak w jÄ™zyku Gallina, mamy do dyspozycji konstrukt
    [let]. Za jego pomocÄ… moÅ¼emy nadaÄ‡ nazwÄ™ dowolnemu wyraÅ¼eniu jÄ™zyka
    Ltac. Jego dziaÅ‚anie jest podobne jak w jÄ™zyku Gallina, a wiÄ™c nie
    ma co siÄ™ nad nim rozwodziÄ‡. Jest teÅ¼ konstrukt [let rec], ktÃ³ry
    odpowiada [fix]owi Galliny.

    SprÃ³bujmy dodaÄ‡ do kontekstu liczbÄ™ [42], nazwanÄ… dowolnie. KomendÄ…
    [let x := 42 in pose x] nie udaje nam siÄ™ tego osiÄ…gnÄ…Ä‡. O przyczynie
    niepowodzenia Coq informuje nas wprost: zmienna [x] nie jest termem.
    Czym zatem jest? Jak juÅ¼ siÄ™ rzekÅ‚o, Ltac posiada wbudowany typ liczb
    caÅ‚kowitych, ktÃ³re nie sÄ… tym samym, co induktywnie zdefiniowane liczby
    caÅ‚kowite Coqa. W tym kontekÅ›cie [42] jest wiÄ™c liczbÄ… caÅ‚kowitÄ… Ltaca,
    a zatem nie jest termem.

    Aby wymusiÄ‡ na Ltacu zinterpretowanie [42] jako termu Coqa, musimy
    posÅ‚uÅ¼yÄ‡ siÄ™ zapisem [constr:()]. DziÄ™ki niemu argument znajdujÄ…cy
    siÄ™ w nawiasach zostanie zinterpretowany jako term. Efektem dziaÅ‚ania
    drugiej taktyki jest wiÄ™c dodanie termu [42 : nat] do kontekstu,
    nazwanego domyÅ›lnie [n] (co jest, o dziwo, doÅ›Ä‡ rozsÄ…dnÄ… nazwÄ…).

    WyraÅ¼enie [let x := split in idtac x] pokazuje nam, Å¼e taktyki rÃ³wnieÅ¼
    sÄ… wyraÅ¼eniami Ltaca i mogÄ… byÄ‡ przypisywane do zmiennych (a takÅ¼e
    wyÅ›wietlane za pomocÄ… taktyki [idtac]) tak jak kaÅ¼de inne wyraÅ¼enie. *)

Ltac garbage n :=
  pose n; idtac "Here's some garbage: " n.

Goal True.
Proof.
  garbage 0.
Abort.

Ltac garbage' :=
  fun n => pose n; idtac "Here's some garbage: " n.

Goal True.
Proof.
  garbage' 0.
Abort.

(** DowolnÄ… taktykÄ™, ktÃ³rej moÅ¼emy uÅ¼yÄ‡ w dowodzie, moÅ¼emy teÅ¼ nazwaÄ‡
    za pomocÄ… komendy [Ltac] i odwoÅ‚ywaÄ‡ siÄ™ do niej w dowodach za pomocÄ…
    tej nazwy. Komenda [Ltac] jest wiÄ™c taktykowym odpowiednikiem komendy
    [Fixpoint].

    Podobnie jak [Fixpoint]y i inne definicje, tak i taktyki zdefiniowane
    za pomocÄ… komendy [Ltac] mogÄ… braÄ‡ argumenty, ktÃ³rymi mogÄ… byÄ‡ liczby,
    termy, nazwy hipotez albo inne taktyki.

    Zapis [Ltac name arg_1 ... arg_n := body] jest jedynie skrÃ³tem, ktÃ³ry
    oznacza [Ltac name := fun arg_1 ... arg_n => body]. Jest to uwaga
    mocno techniczna, gdyÅ¼ pierwszy zapis jest prawie zawsze preferowany
    wobec drugiego. *)

(** * Backtracking *)

(** PoznaÅ‚eÅ› juÅ¼ kombinator alternatywy [||]. Nie jest to jednak jedyny
    kombinator sÅ‚uÅ¼Ä…cy do wyraÅ¼ania tej idei â€” sÄ… jeszcze kombinatory [+]
    oraz [tryif t1 then t2 else t3]. RÃ³Å¼niÄ… siÄ™ one dziaÅ‚aniem â€” [||] jest
    left-biased, podczas gdy [+] nie jest biased i moÅ¼e powodowaÄ‡
    backtracking.

    Nie przestrasz siÄ™ tych dziwnych sÅ‚Ã³w. StojÄ…ce za nimi idee sÄ… z grubsza
    bardzo proste. WczeÅ›niej dowiedziaÅ‚eÅ› siÄ™, Å¼e taktyka moÅ¼e zawieÅ›Ä‡ lub
    zakoÅ„czyÄ‡ siÄ™ sukcesem. W rzeczywistoÅ›ci sprawa jest nieco bardziej
    ogÃ³lna: kaÅ¼da taktyka moÅ¼e zakoÅ„czyÄ‡ siÄ™ dowolnÄ… iloÅ›ciÄ… sukcesÃ³w. Zero
    sukcesÃ³w oznacza, Å¼e taktyka zawodzi. WiÄ™kszoÅ›Ä‡ taktyk, ktÃ³re dotychczas
    poznaliÅ›my, mogÅ‚a zakoÅ„czyÄ‡ siÄ™ co najwyÅ¼ej jednym sukcesem. SÄ… jednak i
    takie, ktÃ³re mogÄ… zakoÅ„czyÄ‡ siÄ™ dwoma lub wiÄ™cej sukcesami.

    Proces dowodzenia za pomocÄ… taktyk moÅ¼na zobrazowaÄ‡ za pomocÄ… procesu
    przeszukiwania drzewa, ktÃ³rego wierzchoÅ‚kami sÄ… czÄ™Å›ciowo skonstruowane
    prooftermy, zaÅ› krawÄ™dziami â€” sukcesy pochodzÄ…ce od wywoÅ‚ywania taktyk.
    LiÅ›Ä‡mi sÄ… prooftermy (dowÃ³d siÄ™ udaÅ‚) lub Å›lepe zauÅ‚ki (dowÃ³d siÄ™ nie
    udaÅ‚).

    W takiej wizualizacji taktyka moÅ¼e wyzwalaÄ‡ backtracking, jeÅ¼eli jej
    uÅ¼ycie prowadzi do powstania rozgaÅ‚Ä™zienia w drzewie. Samo drzewo
    przeszukiwane jest w gÅ‚Ä…b, a backtracking polega na tym, Å¼e jeÅ¼eli
    trafimy na Å›lepy zauÅ‚ek (dowÃ³d siÄ™ nie powiÃ³dÅ‚), to cofamy siÄ™ (ang.
    "to backtrack" â€” cofaÄ‡ siÄ™) do ostatniego punktu rozgaÅ‚Ä™zienia i
    prÃ³bujemy pÃ³jÅ›Ä‡ innÄ… gaÅ‚Ä™ziÄ….

    TÄ™ intuicjÄ™ dobrze widaÄ‡ na poniÅ¼szym przykÅ‚adzie. *)

Ltac existsNatFrom n :=
  exists n || existsNatFrom (S n).

Ltac existsNat := existsNatFrom O.

Goal exists n : nat, n = 42.
Proof.
  Fail (existsNat; reflexivity).
Abort.

Ltac existsNatFrom' n :=
  exists n + existsNatFrom' (S n).

Ltac existsNat' := existsNatFrom' O.

Goal exists n : nat, n = 42.
Proof.
  existsNat'; reflexivity.
Qed.

(** PrÃ³ba uÅ¼ycia taktyki [existsNat], ktÃ³ra uÅ¼ywa kombinatora [||], do
    udowodnienia, Å¼e [exists n : nat, n = 42] koÅ„czy siÄ™ niepowodzeniem.
    Jest tak, gdyÅ¼ [||] nie moÅ¼e powodowaÄ‡ backtrackingu â€” jeÅ¼eli taktyka
    [t1] dokona postÄ™pu, to wtedy [t1 || t2] ma taki sam efekt, jak [t1],
    a w przeciwnym wypadku taki sam jak [t2]. Nawet jeÅ¼eli zarÃ³wno [t1]
    jak i [t2] zakoÅ„czÄ… siÄ™ sukcesami, to sukcesy [t1 || t2] bÄ™dÄ… sukcesami
    tylko [t1].

    Na mocy powyÅ¼szych rozwaÅ¼aÅ„ moÅ¼emy skonkludowaÄ‡, Å¼e taktyka [existsNat]
    ma co najwyÅ¼ej jeden sukces i dziaÅ‚a jak [exists n] dla pewnej liczby
    naturalnej [n]. PoniewaÅ¼ uÅ¼ycie [exists 0] na celu [exists n : nat, n = 42]
    dokonuje postÄ™pu, to taktyka [existsNat] ma taki sam efekt, jak [exists 0].
    PrÃ³ba uÅ¼ycia [reflexivity] zawodzi, a poniewaÅ¼ nie ma juÅ¼ wiÄ™cej sukcesÃ³w
    pochodzÄ…cych od [existsNat] do wyprÃ³bowania, nie wyzwala backtrackingu.
    Wobec tego caÅ‚a taktyka [existsNat; reflexivity] koÅ„czy siÄ™ poraÅ¼kÄ….

    Inaczej sytuacja wyglÄ…da w przypadku [existsNat'], ktÃ³ra bazuje na
    kombinatorze [+]. Sukcesy [t1 + t2] to wszystkie sukcesy [t1], po
    ktÃ³rych nastÄ™pujÄ… wszystkie sukcesy [t2]. Wobec tego zbiÃ³r sukcesÃ³w
    [existsNat'] jest nieskoÅ„czony i wyglÄ…da tak: [exists 0], [exists 1],
    [exists 2]... UÅ¼ycie taktyki [reflexivity], ktÃ³re koÅ„czy siÄ™ poraÅ¼kÄ…
    wyzwala backtracking, wiÄ™c caÅ‚e wykonanie taktyki moÅ¼na zobrazowaÄ‡ tak:
    - [exists 0; reflexivity] â€” poraÅ¼ka
    - [exists 1; reflexivity] â€” poraÅ¼ka
    - ...
    - [exists 42; reflexivity] â€” sukces *)

(** Na koniec zaznaczyÄ‡ naleÅ¼y, Å¼e backtracking nie jest za darmo â€” im go
    wiÄ™cej, tym wiÄ™cej rozgaÅ‚Ä™zieÅ„ w naszym drzewie poszukiwaÅ„, a zatem
    tym wiÄ™cej czasu zajmie wykonanie taktyki. W przypadku uÅ¼ycia taktyk
    takich jak [existsNat], ktÃ³re majÄ… nieskoÅ„czony zbiÃ³r sukcesÃ³w, dowÃ³d
    moÅ¼e nie zostaÄ‡ znaleziony nigdy, nawet jeÅ¼eli istnieje.

    Jednym ze sposobÃ³w radzenia sobie z tym problemem moÅ¼e byÄ‡ kombinator
    [once], ktÃ³ry ogranicza liczbÄ™ sukcesÃ³w taktyki do co najwyÅ¼ej jednego,
    zapobiegajÄ…c w ten sposÃ³b potencjalnemu wyzwoleniu backtrackingu. Innymi
    sÅ‚owy, [once t] zawsze ma 0 lub 1 sukcesÃ³w. *)

Goal exists n : nat, n = 42.
Proof.
  Fail once existsNat'; reflexivity.
Abort.

(** PowyÅ¼ej byliÅ›my w stanie udowodniÄ‡ to twierdzenie za pomocÄ… taktyki
    [existsNat'], gdyÅ¼ jej 42  sukces pozwalaÅ‚ taktyce [reflexivity]
    uporaÄ‡ siÄ™ z celem. Jednak jeÅ¼eli uÅ¼yjemy na tej taktyce kombinatora
    [once], to zbiÃ³r jej sukcesÃ³w zostanie obciÄ™ty do co najwyÅ¼ej jednego

    Skoro [existsNat'] byÅ‚o rÃ³wnowaÅ¼ne ktÃ³remuÅ› z [exists 0], [exists 1],
    [exists 2], ..., to [once existsNat'] jest rÃ³wnowaÅ¼ne [exists 0], a
    zatem zawodzi.

    Innym sposobem okieÅ‚znywania backtrackingu jest kombinator [exactly_once],
    ktÃ³ry pozwala upewniÄ‡ siÄ™, Å¼e dana taktyka ma dokÅ‚adnie jeden sukces.
    JeÅ¼eli [t] zawodzi, to [exactly_once t] zawodzi tak jak [t]. JeÅ¼eli [t]
    ma jeden sukces, [exactly_once t] dziaÅ‚a tak jak [t]. JeÅ¼eli [t] ma dwa
    lub wiÄ™cej sukcesÃ³w, [exactly_once t] zawodzi. *)

Goal exists n : nat, n = 42.
Proof.
  exactly_once existsNat.
Restart.
  Fail exactly_once existsNat'.
Abort.

(** Taktyka [existsNat], zrobiona kombinatorem alternatywy [||], ma dokÅ‚adnie
    jeden sukces, a wiÄ™c [exactly_once existsNat] dziaÅ‚a tak jak [existsNat].
    Z drugiej strony taktyka [existsNat'], zrobiona mogÄ…cym dokonywaÄ‡ nawrotÃ³w
    kombinatorem alternatywy [+], ma wiele sukcesÃ³w i wobec tego
    [exactly_once existsNat'] zawodzi. *)

(** **** Ä†wiczenie (existsNat'') *)

(** Przepisz taktykÄ™ [existsNat'] za pomocÄ… konstruktu [let rec] â€”
    caÅ‚oÅ›Ä‡ ma wyglÄ…daÄ‡ tak: [Ltac existsNat'' := let rec ...] *)

(* begin hide *)
Ltac existsNat'' :=
  let rec f n := exists n + f (n + 1) in f 0.
(* end hide *)

Goal exists n : nat, n = 42.
Proof.
  existsNat''; reflexivity.
Qed.

(** **** Ä†wiczenie (exists_length_3_letrec) *)

(** Udowodnij poniÅ¼sze twierdzenie za pomocÄ… pojedynczej taktyki, ktÃ³ra
    generuje wszystkie moÅ¼liwe listy wartoÅ›ci boolowskich. CaÅ‚oÅ›Ä‡ zrÃ³b za
    pomocÄ… konstruktu [let rec] w miejscu, tj. bez uÅ¼ycia komendy [Ltac]. *)

Require Import List.
Import ListNotations.

Theorem exists_length_3_letrec :
  exists l : list bool, length l = 3.
(* begin hide *)
Proof.
  let rec
    f l := exists l + f (true :: l) + f (false :: l)
  in f (@nil bool); reflexivity.
Qed.
(* end hide *)

(** **** Ä†wiczenie (trivial_goal) *)

(** ZnajdÅº taki trywialnie prawdziwy cel i takÄ… taktykÄ™, ktÃ³ra wywoÅ‚uje
    [existsNat'], Å¼e taktyka ta nie skoÅ„czy pracy i nigdy nie znajdzie
    dowodu, mimo Å¼e dla czÅ‚owieka znalezienie dowodu jest banalne. *)

(* begin hide *)
Lemma trivial_goal :
  (exists n : nat, n = S n) \/ True.
Proof.
  Fail Timeout 1 (left; existsNat'; reflexivity) + (right; trivial).
Abort.
(* end hide *)

(** **** Ä†wiczenie (search) *)

(** Napisz taktykÄ™ [search], ktÃ³ra potrafi udowodniÄ‡ cel bÄ™dÄ…cy dowolnie
    zÅ‚oÅ¼onÄ… dysjunkcjÄ… pod warunkiem, Å¼e jeden z jej czÅ‚onÃ³w zachodzi na
    mocy zaÅ‚oÅ¼enia. UÅ¼yj rekursji, ale nie uÅ¼ywaj konstruktu [let rec].

    WskazÃ³wka: jeÅ¼eli masz problem, udowodnij poÅ‚owÄ™ poniÅ¼szych twierdzeÅ„
    rÄ™cznie i sprÃ³buj dostrzec powtarzajÄ…cy si wzorzec. *)

(* begin hide *)
Ltac search := try assumption; (left; search) + (right; search).
(* end hide *)

Section search.

Hypotheses A B C D E F G H I J : Prop.

Theorem search_0 :
  I -> A \/ B \/ C \/ D \/ E \/ F \/ G \/ I \/ J.
Proof. search. Qed.

Theorem search_1 :
  I -> (((((((A \/ B) \/ C) \/ D) \/ E) \/ F) \/ G) \/ I) \/ J.
Proof. search. Qed.

Theorem search_2 :
  F -> (A \/ B) \/ (C \/ ((D \/ E \/ (F \/ G)) \/ H) \/ I) \/ J.
Proof. search. Qed.

Theorem search_3 :
  C -> (J \/ J \/ ((A \/ A \/ (C \/ D \/ (E \/ E))))).
Proof. search. Qed.

Theorem search_4 :
  A -> A \/ B \/ C \/ D \/ E \/ F \/ G \/ I \/ J.
Proof. search. Qed.

Theorem search_5 :
  D -> ~A \/ ((~B \/ (I -> I) \/ (J -> J)) \/ (D \/ (~D -> ~~D) \/ B \/ B)).
Proof. search. Qed.

Theorem search_6 :
  C -> (~~C /\ ~~~C) \/ ((C /\ ~C) \/ (~C /\ C) \/ (C -> C) \/ (C \/ ~C)).
Proof. search. Qed.

End search.

(** **** Ä†wiczenie (inne_kombinatory_dla_alternatywy) *)

(** Zbadaj samodzielnie na podstawie dokumentacji, jak dziaÅ‚ajÄ… nastÄ™pujÄ…ce
    kombinatory:
    - [tryif t1 then t2 else t3]
    - [first [t_1 | ... | t_N]]
    - [solve [t_1 | ... | t_N]] *)

(** Precyzyjniej piszÄ…c: sprawdÅº kiedy odnoszÄ… sukces i zawodzÄ…, czy mogÄ…
    wyzwalaÄ‡ backtracking oraz wymyÅ›l jakieÅ› mÄ…dre przykÅ‚ady, ktÃ³ry dobrze
    ukazujÄ… ichdziaÅ‚anie w kontraÅ›cie do [||] i [+]. *)

(** * Dopasowanie kontekstu i celu *)

(** Chyba najwaÅ¼niejszym konstruktem Ltaca jest [match goal], ktÃ³ry prÃ³buje
    dopasowaÄ‡ kontekst oraz cel do podanych wzorcÃ³w. MajÄ… one postaÄ‡
    [| kontekst |- cel => taktyka].

    WyraÅ¼enie [kontekst] jest listÄ… hipotez, ktÃ³rych szukamy w kontekÅ›cie,
    tzn. jest postaci [x_1 : A_1, x_2 : A_2...], gdzie [x_i] jest nazwÄ…
    hipotezy, zaÅ› [A_1] jest wzorcem dopasowujÄ…cym jej typ. WyraÅ¼enie [cel]
    jest wzorcem dopasowujÄ…cym typ, ktÃ³ry reprezentuje nasz cel. Po strzaÅ‚ce
    [=>] nastÄ™puje taktyka, ktÃ³rej chcemy uÅ¼yÄ‡, jeÅ¼eli dany wzorzec zostanie
    dopasowany.

    Zamiast wzorcÃ³w postaci [| kontekst |- cel => taktyka] moÅ¼emy teÅ¼ uÅ¼ywaÄ‡
    wzorcÃ³w postaci [| |- cel => taktyka], ktÃ³re dopasowujÄ… jedynie cel, zaÅ›
    kontekst ignorujÄ…; wzorcÃ³w postaci [| kontekst |- _ => taktyka], ktÃ³re
    dopasowujÄ… jedynie kontekst, a cel ignorujÄ…; oraz wzorca [_], ktÃ³ry
    oznacza "dopasuj cokolwiek".

    Zobaczmy, jak to wyglÄ…da na przykÅ‚adach. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  match goal with
      | x : Prop |- _ => idtac x
  end.
Abort.

(** W powyÅ¼szym przykÅ‚adzie szukamy w celu zdaÅ„ logicznych, czyli termÃ³w
    typu [Prop] i wypisujemy je. Nazwy szukanych obiektÃ³w sÄ… lokalne dla
    kaÅ¼dej gaÅ‚Ä™zi dopasowania i nie muszÄ… pokrywaÄ‡ siÄ™ z rzeczywistymi
    nazwami termÃ³w w kontekÅ›cie. W naszym przypadku nazywamy szukane przez
    nas zdanie [x], choÄ‡ zdania obecne w naszym kontekÅ›cie tak naprawdÄ™
    nazywajÄ… siÄ™ [P], [Q], [R] oraz [S].

    Przeszukiwanie obiektÃ³w w kontekÅ›cie odbywa siÄ™ w kolejnoÅ›ci od
    najnowszego do najstarszego. Do wzorca [x : Prop] najpierw prÃ³bujemy
    dopasowaÄ‡ [H1 : R], ale [R] to nie [Prop], wiÄ™c dopasowanie zawodzi.
    Podobnie dla [H0 : Q] oraz [H : P]. NastÄ™pnie natrafiamy na [S : Prop],
    ktÃ³re pasuje do wzorca. DziÄ™ki temu na prawo od strzaÅ‚ki [=>] nazwa [x]
    odnosi siÄ™ do dopasowanego zdania [S]. Za pomocÄ… taktyki [idtac x]
    wyÅ›wietlamy [x] i faktycznie odnosi siÄ™ on do [S]. Po skutecznym
    dopasowaniu i wykonaniu taktyki [idtac x] caÅ‚y [match] koÅ„czy siÄ™
    sukcesem. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  Fail match goal with
      | x : Prop |- _ => idtac x; fail
  end.
Abort.

(** W tym przykÅ‚adzie podobnie jak wyÅ¼ej szukamy w kontekÅ›cie zdaÅ„ logicznych,
    ale taktyka po prawej od [=>] zawodzi. [match] dziaÅ‚a tutaj nastÄ™pujÄ…co:
    - prÃ³bujemy do wzorca [x : Prop] dopasowaÄ‡ [H1 : R], ale bez powodzenia
      i podobnie dla [H0 : Q] oraz [H : P].
    - znajdujemy dopasowanie [S : Prop]. Taktyka [idtac x] wypisuje do okna
      Messages wiadomoÅ›Ä‡ "S" i koÅ„czy siÄ™ sukcesem, ale [fail] zawodzi.
    - Wobec powyÅ¼szego prÃ³bujemy kolejnego dopasowania, tym razem [R : Prop],
      ktÃ³re pasuje. [idtac x] wypisuje na ekran "R", ale [fail] znÃ³w
      zawodzi.
    - PrÃ³bujemy kolejno dopasowaÅ„ [Q : Prop] i [P : Prop], w wyniku ktÃ³rych
      wypisane zostaje "Q" oraz "P", ale rÃ³wnieÅ¼ w tych dwÃ³ch przypadkach
      taktyka [fail] zawodzi.
    - Nie ma wiÄ™cej potencjalnych dopasowaÅ„, wiÄ™c caÅ‚y [match] zawodzi. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  Fail match reverse goal with
      | x : Prop |- _ => idtac x; fail
  end.
Abort.

(** Ten przykÅ‚ad jest podobny do ostatniego, ale [match reverse] przeszukuje
    kontekst w kolejnoÅ›ci od najstarszego do najnowszego. DziÄ™ki temu od razu
    natrafiamy na dopasowanie [P : Prop], potem na [Q : Prop] etc. Na samym
    koÅ„cu prÃ³bujemy do [x : Prop] dopasowaÄ‡ [H : P], [H0 : Q] i [H1 : R], co
    koÅ„czy siÄ™ niepowodzeniem.

    ZauwaÅ¼my, Å¼e w dwÃ³ch ostatnich przykÅ‚adach nie wystÄ…piÅ‚ backtracking â€”
    [match] nigdy nie wyzwala backtrackingu. Obserwowane dziaÅ‚anie [match]a
    wynika stÄ…d, Å¼e jeÅ¼eli taktyka po prawej od [=>] zawiedzie, to nastÄ™puje
    prÃ³ba znalezienia jakiegoÅ› innego dopasowania wzorca [x : Prop]. Dopiero
    gdy taktyka na prawo od [=>] zawiedzie dla wszystkich moÅ¼liwych takich
    dopasowaÅ„, caÅ‚y [match] zawodzi. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  Fail
  match goal with
      | x : Prop |- _ => idtac x
  end; fail.
Abort.

(** Ten przykÅ‚ad potwierdza naszÄ… powyÅ¼szÄ… obserwacjÄ™ dotyczÄ…cÄ… backtrackingu.
    Mamy tutaj identyczne dopasowanie jak w pierwszym przykÅ‚adzie â€” wypisuje
    ono [S] i koÅ„czy siÄ™ sukcesem, ale tuÅ¼ po nim nastÄ™puje taktyka [fail],
    przez co caÅ‚a taktyka [match ...; fail] zawodzi. Jak widaÄ‡, nie nastÄ™puje
    prÃ³ba ponownego dopasownia wzorca [x : Prop]. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  Fail
  lazymatch goal with
      | x : Prop |- _ => idtac x; fail
  end.
Abort.

(** Konstrukt [lazymatch] rÃ³Å¼ni siÄ™ od [match]a tym, Å¼e jeÅ¼eli taktyka na
    prawo od [=>] zawiedzie, to alternatywne dopasowania wzorca po lewej
    nie bÄ™dÄ… rozwaÅ¼ane i nastÄ…pi przejÅ›cie do kolejnej gaÅ‚Ä™zi dopasowania.
    W naszym przypadku nie ma kolejnych gaÅ‚Ä™zi, wiÄ™c po pierwszym dopasowaniu
    [x : Prop] do [S : Prop] i wypisaniu "S" caÅ‚y [lazymatch] zawodzi. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  Fail
  multimatch goal with
      | x : Prop |- _ => idtac x
  end; fail.
Abort.

(** [multimatch] to wariant [match]a, ktÃ³ry wyzwala backtracking. W powyÅ¼szym
    przykÅ‚adzie dziaÅ‚a on nastÄ™pujÄ…co:
    - do wzorca [x : Prop] dopasowujemy [H1 : R], a nastÄ™pnie [H0 : Q] i
      [H : P], co siÄ™ rzecz jasna nie udaje.
    - Znajdujemy dopasowanie [S : Prop] i caÅ‚y [multimatch] koÅ„czy siÄ™
      sukcesem.
    - Taktyka [fail] zawodzi i wobec tego caÅ‚a taktyka [multimatch ...; fail]
      taÅ¼e zawodzi.
    - NastÄ™puje nawrÃ³t i znÃ³w prÃ³bujemy znaleÅºÄ‡ dopasowanie wzorca [x : Prop].
      Znajdujemy [R : Prop], [multimatch] koÅ„czy siÄ™ sukcesem, ale [fail]
      zawodzi.
    - NastÄ™pujÄ… kolejne nawroty i dopasowania do wzorca. Ostatecznie po
      wyczerpaniu siÄ™ wszystkich moÅ¼liwoÅ›Ä‡ caÅ‚a taktyka zawodzi. *)

Goal
  forall P Q R S : Prop, P -> Q -> R -> S.
Proof.
  intros.
  match goal with
      | x : Prop |- _ => idtac x
  end.
  multimatch goal with
      | x : Prop |- _ => idtac x
  end.
  repeat match goal with
      | x : Prop |- _ => idtac x
  end.
  repeat multimatch goal with
      | x : Prop |- _ => idtac x
  end.
Abort.

(** Przyjrzyjmy siÄ™ jeszcze rÃ³Å¼nicy w zachowaniach [match]a i [multimatch]a
    w poÅ‚Ä…czeniu z kombinatorem [repeat]. Bez [repeat] oba dopasowania
    zachowujÄ… siÄ™ identycznie. UÅ¼ycie [repeat] przed [match] nie zmienia w
    tym konkretnym wypadku jego dziaÅ‚ania, ale w przypadku [multimatch]a
    uÅ¼ycie [repeat] ujawnia wszystkie jego sukcesy.

    Å¹rÃ³dÅ‚o rÃ³Å¼nego zachowania [match]a i [multimatch]a, jeÅ¼eli chodzi o
    backtracking, jest bardzo proste: tak naprawdÄ™ [match] jest jedynie
    skrÃ³tem dla [once multimatch]. [lazymatch], choÄ‡ nie pokazano tego
    na powyÅ¼szym przykÅ‚adzie, w obu wypadkach (z [repeat] i bez) zachowujÄ™
    siÄ™ tak jak [match].

    Przyjrzyjmy siÄ™ teraz dopasowaniom celu. *)

Goal
  forall (P Q R S : Prop) (a b c : nat),
    42 = 43 /\ (P -> Q).
Proof.
  intros. split;
  match goal with
      | X : Prop |- P -> Q => idtac X
      | n : nat |- 42 = 43 => idtac n
  end.
Abort.

(** Dopasowanie celu jest jeszcze prostsze niÅ¼ dopasowanie hipotezy, bo
    cel jest tylko jeden i wobec tego nie trzeba dawaÄ‡ mu Å¼adnej nazwy.
    PowyÅ¼sza taktyka [split; match ...] dziaÅ‚a nastÄ™pujÄ…co:
    - [split] generuje dwa podcele i wobec tego [match] dziaÅ‚a na
      kaÅ¼dym z nich z osobna
    - pierwszy wzorzec gÅ‚osi, Å¼e jeÅ¼eli w kontekÅ›cie jest jakieÅ› zdanie
      logiczne, ktÃ³re nazywamy [X], a cel jest postaci [P -> Q], to
      wypisujemy [X]
    - drugi wzorzec gÅ‚osi, Å¼e jeÅ¼eli w kontekÅ›cie jest jakaÅ› liczba
      naturalna, ktÃ³rÄ… nazywamy [n], a cel jest postaci [42 = 43], to
      wypisujemy [n]
    - nastÄ™puje prÃ³ba dopasowania pierwszego wzorca do pierwszego podcelu.
      Mimo, Å¼e w kontekÅ›cie sÄ… zdania logiczne, to cel nie jest postaci
      [P -> Q], a zatem dopasowanie zawodzi.
    - nastÄ™puje prÃ³ba dopasowania drugiego wzorca do pierwszego podcelu.
      W kontekÅ›cie jest liczba naturalna i cel jest postaci [42 = 43], a
      zatem dopasowanie udaje siÄ™. Do okna Messages zostaje wypisane "c",
      ktÃ³re zostaÅ‚o dopasowane jako pierwsze, gdyÅ¼ kontekst jest przeglÄ…dany
      w kolejnoÅ›ci od najstarszej hipotezy do najÅ›wieÅ¼szej.
    - pierwszy wzorzec zostaje z powodzeniem dopasowany do drugiego podcelu
      i do okna Messages zostaje wypisane "S". *)

Goal
  forall (P Q R S : Prop) (a b c : nat), P.
Proof.
  intros.
  match goal with
      | _ => idtac "-_-"
  end.
  match goal with
      | _ => fail
      | X : Prop |- _ => idtac X
  end.
Abort.

(** PozostaÅ‚o nam jedynie zademonstrowaÄ‡ dziaÅ‚anie wzorca [_]. Pierwsza z
    powyÅ¼szych taktyk z sukcesem dopasowuje wzorzec [_] (gdyÅ¼ pasuje on do
    kaÅ¼dego kontekstu i celu) i wobec tego do okna Messages zostaje wypisany
    napis "-_-".

    W drugim [match]u rÃ³wnieÅ¼ zostaje dopasowany wzorzec [_], ale taktyka
    [fail] zawodzi i nastÄ™puje przejÅ›cie do kolejnego wzorca, ktÃ³ry takÅ¼e
    pasuje. Wobec tego wypisane zostaje "S". Przypomina to nam o tym, Å¼e
    kolejnoÅ›Ä‡ wzorcÃ³w ma znaczenie i to nawet w przypadku, gdy ktÃ³ryÅ› z
    nich (tak jak [_]) pasuje do wszystkiego. *)

(** **** Ä†wiczenie (destr_and) *)

(** Napisz taktykÄ™ [destr_and], ktÃ³ra rozbija wszystkie koniunkcje, ktÃ³re
    znajdzie w kontekÅ›cie, a nastÄ™pnie udowodni cel, jeÅ¼eli zachodzi on na
    mocy zaÅ‚oÅ¼enia.

    Dla przykÅ‚adu, kontekst [H : P /\ Q /\ R |- _] powinien zostaÄ‡
    przeksztaÅ‚cony w [H : P, H0 : Q, H1 : R].

    JeÅ¼eli to moÅ¼liwe, nie uÅ¼ywaj kombinatora [;] *)

(* begin hide *)
Ltac destr_and := intros; repeat
match goal with
    | H : _ /\ _ |- _ => destruct H
    | _ => try assumption
end.
(* end hide *)

Section destr_and.

Hypotheses A B C D E F G H I J : Prop.

Theorem destruct_0 :
    A /\ B /\ C /\ D /\ E /\ F /\ G /\ H /\ I /\ J -> D.
Proof. destr_and. Qed.

Theorem destruct_1 :
    ((((((((A /\ B) /\ C) /\ D) /\ E) /\ F) /\ G) /\ H) /\ I) /\ J -> F.
Proof. destr_and. Qed.

Theorem destruct_2 :
    A /\ ~ B /\ (C \/ C \/ C \/ C) /\ ((((D /\ I) /\ I) /\ I) /\ J) -> I.
Proof. destr_and. Qed.

End destr_and.

(** **** Ä†wiczenie (solve_and_perm) *)

(** Napisz taktykÄ™ [solve_and_perm], ktÃ³ra bÄ™dzie potrafiÅ‚a rozwiÄ…zywaÄ‡
    cele postaci [P_1 /\ P_2 /\ ... /\ P_n -> P_i1 /\ P_i2 /\ ... /\ P_iN],
    gdzie prawa strona implikacji jest permutacjÄ… lewej strony, tzn. sÄ… w
    niej te same zdania, ale wystÄ™pujÄ…ce w innej kolejnoÅ›ci. *)

(* begin hide *)
Ltac solve_and_perm := intros; repeat
match goal with
    | H : _ /\ _ |- _ => destruct H
end; repeat split; try assumption.
(* end hide *)

Section solve_and_perm.

Hypotheses A B C D E F G H I J : Prop.

Theorem and_perm_0 :
  A /\ B /\ C /\ D /\ E /\ F /\ G /\ H /\ I /\ J ->
  J /\ I /\ H /\ G /\ F /\ E /\ D /\ C /\ B /\ A.
Proof. solve_and_perm. Qed.

Theorem and_perm_1 :
  A /\ B /\ C /\ D /\ E /\ F /\ G /\ H /\ I /\ J ->
  (((((((((A /\ B) /\ C) /\ D) /\ E) /\ F) /\ G) /\ H) /\ I) /\ J).
Proof. solve_and_perm. Qed.

Theorem and_perm_2 :
  (A /\ B) /\ (C /\ (D /\ E)) /\ (((F /\ G) /\ H) /\ I) /\ J ->
  (I /\ I /\ J) /\ ((A /\ B /\ (A /\ B)) /\ J) /\ (C /\ (E /\ (D /\ F /\ F))).
Proof. solve_and_perm. Qed.

End solve_and_perm.

(** **** Ä†wiczenie (solve_or_perm) *)

(** Napisz taktykÄ™ [solve_or_perm], ktÃ³ra bÄ™dzie potrafiÅ‚a rozwiÄ…zywaÄ‡
    cele postaci [P_1 \/ P_2 \/ ... \/ P_n -> P_i1 \/ P_i2 \/ ... \/ P_iN],
    gdzie prawa strona implikacji jest permutacjÄ… lewej strony, tzn. sÄ…
    w niej te same zdania, ale wystÄ™pujÄ…ce w innej kolejnoÅ›ci.

    WskazÃ³wka: wykorzystaj taktykÄ™ [search] z jednego z poprzednich
    Ä‡wiczeÅ„. *)

(* begin hide *)
Ltac solve_or_perm := intros; repeat
match goal with
    | H : _ \/ _ |- _ => destruct H
end; search.
(* end hide *)

Section solve_or_perm.

Hypotheses A B C D E F G H I J : Prop.

Theorem or_perm_0 :
  A \/ B \/ C \/ D \/ E \/ F \/ G \/ H \/ I \/ J ->
  J \/ I \/ H \/ G \/ F \/ E \/ D \/ C \/ B \/ A.
Proof. solve_or_perm. Qed.

Theorem or_perm_1 :
  A \/ B \/ C \/ D \/ E \/ F \/ G \/ H \/ I \/ J ->
  (((((((((A \/ B) \/ C) \/ D) \/ E) \/ F) \/ G) \/ H) \/ I) \/ J).
Proof. solve_or_perm. Qed.

Theorem or_perm_2 :
  (A \/ B) \/ (C \/ (D \/ E)) \/ (((F \/ G) \/ H) \/ I) \/ J ->
  (I \/ H \/ J) \/ ((A \/ B \/ (G \/ B)) \/ J) \/ (C \/ (E \/ (D \/ F \/ F))).
Proof. solve_or_perm. Qed.

Theorem or_perm_3 :
  A \/ B \/ C \/ D \/ E \/ F \/ G \/ H \/ I \/ J ->
  (((((((((A \/ B) \/ C) \/ D) \/ E) \/ F) \/ G) \/ H) \/ I) \/ J).
Proof. solve_or_perm. Qed.

End solve_or_perm.

(** **** Ä†wiczenie (negn) *)

Section negn.

Require Import Arith.

(** Napisz funkcjÄ™ [negn : nat -> Prop -> Prop], gdzie [negn n P] zwraca
    zdanie [P] zanegowane [n] razy. *)

(* begin hide *)
Fixpoint negn (n : nat) (P : Prop) : Prop :=
match n with
    | 0 => P
    | S n' => ~ negn n' P
end.
(* end hide *)

Eval cbn in negn 10 True.
(* ===> = ~ ~ ~ ~ ~ ~ ~ ~ ~ ~ True : Prop *)

(** Udowodnij poniÅ¼sze lematy. *)

Lemma dbl_neg :
  forall P : Prop, P -> ~ ~ P.
(* begin hide *)
Proof.
  unfold not. auto.
Qed.
(* end hide *)

Lemma double_n :
  forall n : nat, 2 * n = n + n.
(* begin hide *)
Proof.
  cbn. intro. rewrite <- plus_n_O. trivial.
Qed.
(* end hide *)

(** PrzydadzÄ… ci siÄ™ one do pokazania dwÃ³ch wÅ‚aÅ›ciwoÅ›ci fukncji [negn].
    Zanim przystÄ…pisz do dowodzenia drugiego z nich, sprÃ³buj zgadnÄ…Ä‡,
    po ktÃ³rym argumencie najproÅ›ciej bÄ™dzie przeprowadziÄ‡ indukcjÄ™. *)

Theorem even_neg :
  forall (n : nat) (P : Prop), P -> negn (2 * n) P.
(* begin hide *)
Proof.
  induction n as [| n']; cbn; intros.
    trivial.
    rewrite <- plus_n_O, Nat.add_comm. cbn. apply dbl_neg.
      rewrite <- double_n. apply IHn'. assumption.
Qed.
(* end hide *)

Theorem even_neg' :
  forall (n k : nat) (P : Prop),
    negn (2 * n) P -> negn (2 * (n + k)) P.
(* begin hide *)
Proof.
  induction k as [| k']; intros.
    rewrite <- plus_n_O. assumption.
    cbn. repeat (rewrite <- (Nat.add_comm (S _)); cbn).
      apply dbl_neg. replace _ with (negn (2 * (n + k')) P).
        apply IHk'. assumption.
        f_equal. cbn. rewrite <- !plus_n_O, (Nat.add_comm n). trivial.
Qed.
(* end hide *)

(** Napisz taktykÄ™ [negtac], ktÃ³ra bÄ™dzie potrafiÅ‚a udowadniaÄ‡ cele postaci
    [forall P : Prop, negn (2 * n) P -> negn (2 * (n + k)) P], gdzie
    [n] oraz [k] sÄ… staÅ‚ymi. Nie uÅ¼ywaj twierdzeÅ„, ktÃ³re udowodniÅ‚eÅ› wyÅ¼ej.

    WskazÃ³wka: przydatny moÅ¼e byc konstrukt [match reverse goal]. *)

(* begin hide *)
Ltac negtac := cbn; unfold not; intros;
match reverse goal with
    | H : _ -> False |- False => apply H; clear H; negtac
    | _ => try assumption
end.
(* end hide *)

Theorem neg_2_14 :
  forall P : Prop, negn 2 P -> negn 14 P.
Proof. negtac. Qed.

Theorem neg_100_200 :
  forall P : Prop, negn 100 P -> negn 200 P.
Proof. negtac. Qed.

Theorem neg_42_1000 :
  forall P : Prop, negn 42 P -> negn 200 P.
Proof. negtac. Qed.

End negn.

(** * Wzorce i unifikacja *)

(** Skoro wiemy juÅ¼ jak dziaÅ‚a dopasowywanie kontekstu do wzorca, czas
    nauczyÄ‡ siÄ™ jak dokÅ‚adnie dziaÅ‚ajÄ… wzorce oraz czym sÄ… zmienne
    unifikacyjne i sama unifikacja.

    Przede wszystkim, jak przekonaliÅ›my siÄ™ wyÅ¼ej, termy sÄ… wzorcami.
    Termy nie zawierajÄ… zmiennych unifikacyjnych, a wzorce bÄ™dÄ…ce
    termami dopasowujÄ… siÄ™ tylko do identycznych termÃ³w. Dopasowanie
    takie nie wiÄ…Å¼e Å¼adnych nowych zmiennych. Zobaczmy to na przykÅ‚adzie. *)

Goal
  forall P Q : Prop, P -> P \/ Q.
Proof.
  intros.
  match goal with
      | p : P |- P \/ Q => left; assumption
  end.
Qed.

(** PowyÅ¼szy [match] nie zawiera zmiennych unifikacyjnych i dziaÅ‚a w
    nastÄ™pujÄ…cy sposÃ³b:
    - szukamy w kontekÅ›cie obiektu [p], ktÃ³rego typ pasuje do wzorca [P].
      Obiekt, ktÃ³ry nazywamy [p] w rzeczywistoÅ›ci nie musi nazywaÄ‡ siÄ™ [p],
      ale jego typem rzeczywiÅ›cie musi byÄ‡ [P]. W szczegÃ³lnoÅ›ci, wzorzec
      [P] nie pasuje do [Q], gdyÅ¼ [P] i [Q] nie sÄ… konwertowalne.
    - jednoczeÅ›nie Å¼Ä…damy, by cel byÅ‚ postaci [P \/ Q], gdzie zarÃ³wno [P]
      jak i [Q] odnoszÄ… siÄ™ do obiektÃ³w z kontekstu, ktÃ³re rzeczywiÅ›cie tak
      siÄ™ nazywajÄ….
    - jeÅ¼eli powyÅ¼sze wzorce zostanÄ… dopasowane, to uÅ¼ywamy taktyki [left;
      assumption], ktÃ³ra rozwiÄ…zuje cel. *)

(** Zobaczmy, co siÄ™ stanie, jeÅ¼eli w powyÅ¼szym przykÅ‚adzie zmienimy nazwy
    hipotez. *)

Goal
  forall A B : Prop, A -> A \/ B.
Proof.
  intros.
  Fail match goal with
      | p : P |- P \/ Q => left; assumption
  end.
  match goal with
      | p : A |- A \/ B => left; assumption
  end.
Qed.

(** Tutaj zamiast [P] mamy [A], zaÅ› zamiast [Q] jest [B]. [match] identyczny
    jak poprzednio tym razem zawodzi. Dzieje siÄ™ tak, gdyÅ¼ [P] odnosi siÄ™
    tu do obiektu z kontekstu, ktÃ³ry nazywa siÄ™ [P]. Niestety, w kontekÅ›cie
    nie ma obiektu o takiej nazwie, o czym Coq skrzÄ™tnie nas informuje.

    W [match]u w celu oraz po prawej stronie od [:] w hipotezie nie moÅ¼emy
    za pomocÄ… nazwy [P] dopasowaÄ‡ obiektu, ktÃ³ry nazywa siÄ™ [A]. DopasowaÄ‡
    [A] moÅ¼emy jednak uÅ¼ywajÄ…c wzorca [A]. Ale co, gdybyÅ›my nie wiedzieli,
    jak dokÅ‚adnie nazywa siÄ™ poszukiwany obiekt? *)

Goal
  forall A B : Prop, A -> A \/ B.
Proof.
  intros.
  match goal with
      | p : ?P |- ?P \/ ?Q => idtac P; idtac Q; left; assumption
  end.
Qed.

(** JeÅ¼eli chcemy dopasowaÄ‡ term o nieznanej nam nazwie (lub term, ktÃ³rego
    podtermy majÄ… nieznane nazwy) musimy uÅ¼yÄ‡ zmiennych unifikacyjnych.
    Wizualnie moÅ¼na rozpoznaÄ‡ je po tym, Å¼e ich nazwy zaczynajÄ… siÄ™ od
    znaku [?]. Zmienna unifkacyjna [?x] pasuje do dowolnego termu, a
    udane dopasowanie sprawia, Å¼e po prawej stronie strzaÅ‚ki [=>] moÅ¼emy
    do dopasowanego termu odnosiÄ‡ siÄ™ za pomocÄ… nazwy [x].

    PowyÅ¼sze dopasowanie dziaÅ‚a w nastÄ™pujÄ…cy sposÃ³b:
    - prÃ³bujemy dopasowaÄ‡ wzorzec [p : ?P] do najÅ›wieÅ¼szej hipotezy w
      kontekÅ›cie, czyli [H : A]. [p] jest nazwÄ… tymczasowÄ… i wobec tego
      pasuje do [H], zaÅ› zmienna unifikacyjna [?P] pasuje do dowolnego
      termu, a zatem pasuje takÅ¼e do [A].
    - dopasowanie hipotezy koÅ„czy siÄ™ sukcesem i wskutek tego zmienna
      unifikacyjna [?P] zostaje zwiÄ…zana z termem [A]. Od teraz w dalszych
      wzorcach bÄ™dzie ona pasowaÄ‡ jedynie do termu [A].
    - nastÄ™puje prÃ³ba dopasowania celu do wzorca [?P \/ ?Q]. PoniewaÅ¼
      [?P] zostaÅ‚o zwiÄ…zane z [A], to wzorzec [?P \/ ?Q] oznacza tak
      naprawdÄ™ [A \/ ?Q]. Zmienna unifikacyjna [?Q] nie zostaÅ‚a wczeÅ›niej
      zwiÄ…zana i wobec tego pasuje do wszystkiego.
    - wobec tego [?Q] w szczegÃ³lnoÅ›ci pasuje do [B], a zatem wzorzec
      [?P \/ ?Q] pasuje do [A \/ B] i caÅ‚e dopasowanie koÅ„czy siÄ™
      sukcesem. W jego wyniku [?Q] zostaje zwiÄ…zane z [B].
    - zostaje wykonana taktyka [idtac P; idtac Q], ktÃ³ra potwierdza, Å¼e
      zmienna unifikacyjna [?P] zostaÅ‚a zwiÄ…zana z [A], a [?Q] z [B],
      wobec czego na prawo od [=>] faktycznie moÅ¼emy do [A] i [B] odwoÅ‚ywaÄ‡
      siÄ™ odpowiednio jako [P] i [Q].
    - taktyka [left; assumption] rozwiÄ…zuje cel. *)

(** PodkreÅ›lmy raz jeszcze, Å¼e zmienne unifikacyjne mogÄ… wystÄ™paÄ‡ tylko we
    wzorcach, a wiÄ™c w hipotezach po prawej stronie dwukropka [:] oraz w
    celu. BÅ‚Ä™dem byÅ‚oby napisanie w hipotezie [?p : ?P]. Podobnie bÅ‚Ä™dem
    byÅ‚oby uÅ¼ycie nazwy [?P] na prawo od strzaÅ‚ki [=>].

    ZauwaÅ¼my teÅ¼, Å¼e w danej gaÅ‚Ä™zi [match]a kaÅ¼da zmienna unifikacyjna moÅ¼e
    wystÄ…piÄ‡ wiÄ™cej niÅ¼ jeden raz. Wzorce, w ktÃ³rych zmienne unifikacyjne
    wystÄ™pujÄ… wiÄ™cej niÅ¼ raz to wzorce nieliniowe. MoÅ¼emy skontrastowaÄ‡ je
    ze wzorcami liniowymi, w ktÃ³rych kaÅ¼da zmienna moÅ¼e wystÄ…piÄ‡ co najwyÅ¼ej
    raz.

    Wzorcami liniowymi sÄ… wzorce, ktÃ³rych uÅ¼ywamy podczas definiowania
    zwykÅ‚ych funkcji przez dopasowanie do wzorca (zauwaÅ¼my jednak, Å¼e
    tamtejsze zmienne unifikacyjne nie zaczynajÄ… siÄ™ od [?]). Ograniczenie
    do wzorcÃ³w liniowych jest spowodowane faktem, Å¼e nie zawsze moÅ¼liwe
    jest stwierdzenie, czy dwa dowolne termy do siebie pasujÄ….

    JÄ™zyk termÃ³w Coqa w celu unikniÄ™cia sprzecznoÅ›ci musi byÄ‡ zupeÅ‚nie
    nieskazitelny i musi zakazywaÄ‡ uÅ¼ywania wzorcÃ³w nieliniowych. JÄ™zyk
    Ltac, ktÃ³ry nie moÅ¼e sam z siebie wyczarowaÄ‡ sprzecznoÅ›ci, moÅ¼e sobie
    pozwoliÄ‡ na wiÄ™cej i wobec tego wzorce nieliniowe sÄ… legalne. *)

Goal
  [2] = [].
Proof.
  match goal with
      | |- ?x = _ => idtac x
  end.
  match goal with
      | |- cons ?h _ = nil => idtac h
  end.
  match goal with
      | |- 2 :: _ = ?l => idtac l
  end.
  match goal with
      | |- [?x] = [] => idtac x
  end.
Abort.

(** ZauwaÅ¼my, Å¼e nie musimy uÅ¼ywaÄ‡ zmiennych unifikacyjnych do dopasowywania
    caÅ‚ych termÃ³w â€” w pierwszym z powyÅ¼szych przykÅ‚adÃ³w uÅ¼ywamy zmiennej [?x],
    aby dopasowaÄ‡ jedynie lewÄ… stronÄ™ rÃ³wnania, ktÃ³re jest celem.

    Ze zmiennych unifikacyjnych oraz staÅ‚ych, zmiennych i funkcji (a wiÄ™c
    takÅ¼e konstruktorÃ³w) moÅ¼emy budowaÄ‡ wzorce dopasowujÄ…ce termy o rÃ³Å¼nych
    fikuÅ›nych ksztaÅ‚tach.

    W drugim przykÅ‚adzie wzorzec [cons ?h _ = nil] dopasowuje rÃ³wnanie,
    ktÃ³rego lewa strona jest listÄ… niepustÄ… o dowolnej gÅ‚owie, do ktÃ³rej
    moÅ¼emy siÄ™ odnosiÄ‡ jako [h], oraz dowolnym ogonie, do ktÃ³rego nie
    chcemy mÃ³c siÄ™ odnosiÄ‡. Prawa strona tego rÃ³wnania jest listÄ… pustÄ….

    Wzorce radzÄ… sobie bez problemu takÅ¼e z notacjami. Wzorzec [2 :: _ = ?l]
    dopasowuje rÃ³wnanie, ktÃ³rego lewa strona jest listÄ…, ktÃ³rej gÅ‚owa to [2],
    zaÅ› ogon jest dowolny, a prawa strona jest dowolnÄ… listÄ…, do ktÃ³rej
    bÄ™dziemy siÄ™ mogli odwoÅ‚ywaÄ‡ po prawej stronie [=>] jako [l].

    Ostatni wzorzec pasuje do rÃ³wnania, ktÃ³rego lewa strona jest singletonem
    (listÄ… jednoelementowÄ…) zawierajÄ…cym wartoÅ›Ä‡, do ktÃ³rej bÄ™dziemy mogli
    odnosiÄ‡ siÄ™ za pomocÄ… nazwy [x], zaÅ› prawÄ… stronÄ… jest lista pusta. *)

(** **** Ä†wiczenie (my_assumption) *)

(** Napisz taktykÄ™ [my_assumption], ktÃ³ra dziaÅ‚a tak samo, jak [assumption].
    Nie uÅ¼ywaj [assumption] â€” uÅ¼yj [match]a. *)

(* begin hide *)
Ltac my_assumption :=
match goal with
    | x : ?P |- ?P => exact x
end.
(* end hide *)

Goal
  forall P : Prop, P -> P.
Proof.
  intros. my_assumption.
Qed.

(** **** Ä†wiczenie (forward) *)

(** Napisz taktykÄ™ [forward], ktÃ³ra wyspecjalizuje wszystkie znalezione w
    kontekÅ›cie implikacje, o ile oczywiÅ›cie ich przesÅ‚anki rÃ³wnieÅ¼ bÄ™dÄ…
    znajdowaÅ‚y siÄ™ w kontekÅ›cie, a nastÄ™pnie rozwiÄ…Å¼e cel, jeÅ¼eli jest on
    prawdziwy na mocy zaÅ‚oÅ¼enia.

    Dla przykÅ‚adu, kontekst [H : P -> Q, H0 : Q -> R, H1 : P |- _]
    powinien zostaÄ‡ przeksztaÅ‚cony w [H : Q, H0 : R, H1 : P |- _].

    WskazÃ³wka: przydatna bÄ™dzie taktyka [specialize]. *)

(* begin hide *)
Ltac forward := intros; repeat
match goal with
    | H : ?P -> ?Q, H' : ?P |- _ => specialize (H H')
    | H : ?P |- ?P => assumption
end.
(* end hide *)

Example forward_1 :
  forall P Q R : Prop, (P -> Q) -> (Q -> R) -> P -> R.
Proof. forward. Qed.

Example forward_2 :
  forall P Q R S : Prop, (P -> Q -> R) -> (S -> Q -> P -> R).
Proof. forward. Qed.

(** * NarzÄ™dzia przydatne przy dopasowywaniu *)

(** Poznawszy juÅ¼ konstrukt [match] i jego warianty oraz sposÃ³b dopasowywania
    wzorcÃ³w i rolÄ™ unifikacji oraz zmiennych unifikacyjnych w tym procesie,
    czas rzuciÄ‡ okiem na kilka niezwykle przydatnych narzÄ™dzi, ktÃ³re uczyniÄ…
    nasze Å¼ycie dopasowywacza Å‚atwiejszym. *)

(** ** Dopasowanie podtermu *)

(** Pierwszym z nich jest wyraÅ¼enie [context ident [term]], dziÄ™ki ktÃ³remu
    moÅ¼emy tworzyÄ‡ wzorce dopasowujÄ…ce podtermy danego termu. Zobaczmy jego
    dziaÅ‚anie na przykÅ‚adzie. *)

Goal
  forall a b c : nat, a = b -> b = c -> a = c.
Proof.
  intros a b c.
  match goal with
      | |- context G [?x = ?y] => idtac G x y
  end.
  repeat multimatch goal with
      | |- context G [?x = ?y] => idtac G x y
  end.
Abort.

(** W powyÅ¼szym przykÅ‚adzie naszym celem jest znalezienie wszystkich rÃ³wnaÅ„,
    ktÃ³re sÄ… podtermami naszego celu. Dopasowanie wzorca [context G [?x = ?y]]
    przebiega w nastÄ™pujÄ…cy sposÃ³b:
    - w celu wyszukiwae sÄ… wszystkie podtermy postaci [?x = ?y]. SÄ… trzy
      takie: [a = b], [b = c] oraz [a = c]
    - wzorzec [?x = ?y] zostaje zunifikowany z pierwszym pasujÄ…cym podtermem,
      czyli [a = b]. W wyniku dopasowania zmienna unifikacyjna [?x] zostaje
      zwiÄ…zana z [a], zaÅ› [?y] z [b]
    - caÅ‚y term, ktÃ³rego podterm zostaÅ‚ dopasowany do wzorca, zostaje
      zwiÄ…zany ze zmiennÄ… [G], przy czym jego dopasowany podterm zostaje
      specjalnie zaznaczony (po wypisaniu w jego miejscu widaÄ‡ napis "?M-1")
    - zostaje wykonana taktyka [idtac G x y] *)

(** Druga z powyÅ¼szych taktyk dziaÅ‚a podobnie, ale dziÄ™ki zastosowaniu
    [repeat multimatch] ujawnia nam ona wszystkie podtermy pasujÄ…ce do wzorca
    [?x = ?y]. *)

(** **** Ä†wiczenie (podtermy) *)

(** Oblicz ile podtermÃ³w ma term [42]. NastÄ™pnie napisz taktykÄ™ [nat_subterm],
    ktÃ³ra potrafi wypisaÄ‡ wszystkie podtermy dowolnej liczby naturalnej, ktÃ³ra
    znajduje siÄ™ w celu. WymyÅ›l odpowiedni cel i przetestuj na nim swoje
    obliczenia. *)

(* begin hide *)
(** OdpowiedÅº: term [42] ma 42 podtermy, czyli tyle, ile razy wystÄ™puje w nim
    konstruktor [S]. *)

Ltac nat_subterm :=
  repeat multimatch goal with
      | |- context [S ?x] => idtac x
  end.

Goal @length nat [] = 42.
Proof.
  nat_subterm.
Abort.
(* end hide *)

(** ** Generowanie nieuÅ¼ywanych nazw *)

(** Drugim przydatnym narzÄ™dziem jest konstrukt [fresh], ktÃ³ry pozwala nam
    wygenerowaÄ‡ nazwÄ™, ktÃ³rej nie nosi jeszcze Å¼adna zmienna. DziÄ™ki temu
    moÅ¼emy uniknÄ…Ä‡ konfliktÃ³w nazw, gdy uÅ¼ywamy taktyk takich jak [intros]
    czy [destruct], ktÃ³re pozwalajÄ… nam nazywaÄ‡ obiekty. Przyjrzyjmy siÄ™
    nastÄ™pujÄ…cemu przykÅ‚adowi. *)

Goal forall x y : nat, {x = y} + {x <> y}.
Proof.
  intro x. Fail intro x.
  let x := fresh in intro x.
Restart.
  intro x. let x := fresh "y" in intro x.
Restart.
  intro x. let x := fresh x in intro x.
Restart.
  intro x. let x := fresh y in intro x.
Abort.

(** Mamy w kontekÅ›cie liczbÄ™ naturalnÄ… [x : nat] i chcielibyÅ›my wprowadziÄ‡
    do niego kolejnÄ…. CÃ³Å¼, nie jest to Å¼aden problem â€” wystarczy nazwaÄ‡ go
    dowolnÄ… nazwÄ… rÃ³Å¼nÄ… od "x". Ale co, jeÅ¼eli nie wiemy, jak nazywajÄ… siÄ™
    obiekty znajdujÄ…ce siÄ™ w kontekÅ›cie?

    Przy intensywnym posÅ‚ugiwaniu siÄ™ taktykami i automatyzacjÄ… jest to nader
    czÄ™sta moÅ¼liwoÅ›Ä‡: gdy dopasujemy kontekst za pomocÄ… [match]a, nie znamy
    oryginalnych nazw dopasowanych termÃ³w â€” moÅ¼emy odwoÅ‚ywaÄ‡ siÄ™ do nich
    tylko za pomocÄ… nazw lokalnych, wprowadzonych na potrzeby danego wzorca.

    Z odsiÄ™czÄ… przychodzi nam generator Å›wieÅ¼ych nazw o wdziÄ™cznej nazwie
    [fresh]. Zazwyczaj bÄ™dziemy siÄ™ nim posÅ‚ugiwaÄ‡ w nastÄ™pujÄ…cy sposÃ³b:
    [let var := fresh arg_1 ... arg_N in t]. Tutaj [var] jest zmiennÄ…
    jÄ™zyka [Ltac], ktÃ³rej wartoÅ›ciÄ… jest Å›wieÅ¼o wygenerowana nazwa, a [t]
    to jakaÅ› taktyka, ktÃ³ra w dowolny sposÃ³b korzysta z [var].

    PowyÅ¼sze cztery taktyki dziaÅ‚ajÄ… tak:
    - [let x := fresh in intro x] â€” [fresh] generuje Å›wieÅ¼Ä… nazwÄ™, domyÅ›lnie
      jest niÄ… "H". Nazwa ta staje siÄ™ wartoÅ›ciÄ… Ltacowej zmiennej [x]. Owa
      zmienna jest argumentem taktyki [intro], dziÄ™ki czemu wprowadzony do
      kontekstu obiekt typu [nat] zostaje nazwany "H".
    - [let x := fresh "y" in intro x] â€” jeÅ¼eli [fresh] dostanie jako argument
      ciÄ…g znakÃ³w, to wygeneruje nazwÄ™ zaczynajÄ…cÄ… siÄ™ od tego ciÄ…gu, ktÃ³ra
      nie jest jeszcze zajÄ™ta. PoniewaÅ¼ nazwa "y" jest wolna, wÅ‚aÅ›nie tak
      zostaje nazwany wprowadzany obiekt.
    - [let x := fresh x in intro x] â€” tutaj mamy maÅ‚y zamÄ™t. Pierwszy i trzeci
      [x] jest zmiennÄ… Ltaca, zaÅ› drugi odnosi siÄ™ do obiektu z kontekstu.
      JeÅ¼eli [arg] jest obiektem z kontekstu, to [fresh arg] tworzy Å›wieÅ¼Ä…
      nazwÄ™ zaczynajÄ…cÄ… siÄ™ od nazwy, jakÄ… [arg] nosi w kontekÅ›cie. Tutaj
      nie  ma to znaczenia, gdyÅ¼ [x] nazywa siÄ™ po prostu "x" i wobec tego
      [fresh] generuje nazwÄ™ "x0", ale mechanizm ten dziaÅ‚a tak samo w
      przypadku zmiennych unifikacyjnych.
    - [let x := fresh y in intro x] â€” jak widaÄ‡, argumentem [fresh] moÅ¼e teÅ¼
      byÄ‡ nazwa zmiennej nie odnoszÄ…ca siÄ™ zupeÅ‚nie do niczego. W naszym
      przypadku nie ma w kontekÅ›cie zmiennej [y], a [fresh] generuje na jej
      podstawie Å›wieÅ¼Ä… nazwÄ™ "y". *)

(** ** [fail] (znowu) *)

(** TaktykÄ™ [fail] juÅ¼ poznaliÅ›my, ale nie w jej peÅ‚nej krasie. Czas wiÄ™c
    odkryÄ‡ resztÄ™ jej moÅ¼liwoÅ›ci. *)

Goal False.
Proof.
  Fail fail "Hoho, czego siÄ™ spodziewaÅ‚eÅ›?" 1.
Abort.

(** Pierwsza z nich nie jest zbyt spektakularna â€” moÅ¼emy do [fail] przekazaÄ‡
    jako argumenty ciÄ…gi znakÃ³w lub termy, co spowoduje wyÅ›wietlenie ich w
    oknie wiadomoÅ›ci.

    DrugÄ…, znacznie waÅ¼niejszÄ… moÅ¼liwoÅ›ciÄ…, jakÄ… daje nam taktyka [fail],
    jest kontrola "poziomu poraÅ¼ki". DziÄ™ki niemu zyskujemy wÅ‚adzÄ™ nad
    tym, jak "mocno" taktyka [fail] zawodzi. DomyÅ›nie wynosi on 0. UÅ¼ycie
    taktyki [fail] (ktÃ³ra wobec tego oznacza to samo, co [fail 0]) powouje
    przerwanie wykonywania obecnej gaÅ‚Ä™zi [match]a i przejÅ›cie do nastÄ™pnej.
    UÅ¼ycie taktyki [fail n], gdzie n nie jest rÃ³wne 0, powoduje opuszczenie
    caÅ‚ego obecnego [match]a (tj. wszystkich gaÅ‚Ä™zi) lub bloku [do]/[repeat]
    i wywoÅ‚anie [fail (n - 1)].

    Przyjrzyjmy siÄ™ temu zachowaniu na przykÅ‚adzie. *)

Goal False.
Proof.
  match goal with
      | _ => idtac "first branch"; fail
      | _ => idtac "second branch"
  end.
  Fail match goal with
      | _ => idtac "first branch"; fail 1
      | _ => idtac "second branch"
  end.
  try match goal with
      | _ => idtac "first branch"; fail 1
      | _ => idtac "second branch"
  end.
  Fail try match goal with
      | _ => idtac "first branch"; fail 2
      | _ => idtac "second branch"
  end.
Abort.

(** Cztery powyÅ¼sze dopasowania dziaÅ‚ajÄ… nastÄ™pujÄ…co:
    - W pierwszym dopasowana jest pierwsza gaÅ‚Ä…Åº. WyÅ›wietlona zostaje
      wiadomoÅ›Ä‡, po czym taktyka [fail] zawodzi i nastÄ™puje przejÅ›cie
      do kolejnej gaÅ‚Ä™zi. Tutaj teÅ¼ wypisana zostaje wiadomoÅ›Ä‡ i caÅ‚a
      taktyka [match ...] koÅ„czy siÄ™ sukcesem.
    - W drugim przypadku dopasowana jest pierwsza gaÅ‚Ä…Åº, ktÃ³ra wypisuje
      wiadomoÅ›Ä‡, ale taktyka [fail 1] powoduje, Å¼e caÅ‚y [match] zawodzi
      i druga gaÅ‚Ä…Åº nie jest w ogÃ³le dopasowywana.
    - Trzeci przypadek jest podobny do drugiego. [fail 1] powoduje, Å¼e
      caÅ‚y [match] zawodzi, ale dziÄ™ki kombinatorowi [try] caÅ‚a taktyka
      [try match ...] koÅ„czy siÄ™ sukcesem.
    - Czwarta taktyka jest podobna do trzeciej, ale tym razem po udanym
      dopasowaniu pierwszej gaÅ‚Ä™zi taktyka [fail 2] powoduje, Å¼e caÅ‚y
      [match] zawodzi. NastÄ™pnie ma miejsce wywoÅ‚anie taktyki [fail 1],
      ktÃ³re powoduje, Å¼e nawet mimo uÅ¼ycia kombinatora [try] caÅ‚a taktyka
      [try match ...] zawodzi. *)

(** * Inne (maÅ‚o) wesoÅ‚e rzeczy *)

(** Ten podrozdziaÅ‚ bÄ™dzie wesoÅ‚Ä… zbieraninkÄ… rÃ³Å¼nych niezbyt przydatnych
    (przynajmniej dla mnie) konstruktÃ³w jÄ™zyka Ltac, ktÃ³re nie zostaÅ‚y
    dotychczas omÃ³wione. *)

Goal False /\ False /\ False.
Proof.
  repeat split.
  let n := numgoals in idtac n.
  all: let n := numgoals in idtac n.
Abort.

(** IloÅ›Ä‡ celÃ³w moÅ¼emy policzyÄ‡ za pomocÄ… taktyki [numgoals]. Liczy ona
    wszystkie cele, na ktÃ³re dziaÅ‚a, wiÄ™c jeÅ¼eli nie uÅ¼yjemy Å¼adnego
    selektora, zwrÃ³ci ona 1. Nie jest ona zbyt uÅ¼yteczna (poza bardzo
    skomplikowanymi taktykami, ktÃ³re z jakichÅ› powodÃ³w nie operujÄ… tylko na
    jednym celu, lecz na wszystkich). *)

Goal False /\ False /\ False.
Proof.
  repeat split.
  all: let n := numgoals in guard n > 2.
  Fail all: let n := numgoals in guard n < 2.
Abort.

(** Taktyka [guard cond] pozwala nam dokonywaÄ‡ prostych testÃ³w na liczbach
    caÅ‚kowitych Ltaca. JeÅ¼eli warunek zachodzi, taktyka ta zachowuje siÄ™
    jak [idtac], czyli koÅ„czy siÄ™ sukcesem i nie robi nic wiÄ™cej. JeÅ¼eli
    warunek nie zachodzi, taktyka zawodzi.

    W powyÅ¼szym przykÅ‚adzie taktyka [guard n > 2] koÅ„czy siÄ™ sukcesem,
    gdyÅ¼ sÄ… 3 cele, a 3 > 2, zaÅ› taktyka [guard n < 2] zawodzi, bo sÄ…
    3 cele, a nie jest prawdÄ…, Å¼e 3 < 2. *)

Inductive even : nat -> Prop :=
    | even0 : even 0
    | evenSS : forall n : nat, even n -> even (S (S n)).

Goal even 42.
Proof.
  try timeout 1 repeat constructor.
Abort.

Goal even 1338.
Proof.
  try timeout 1 repeat constructor.
Abort.

(** Kombinator [timeout n t] pozwala nam sprawiÄ‡, Å¼eby taktyka t zawiodÅ‚a,
    jeÅ¼eli jej wykonanie bÄ™dzie zajmowaÅ‚o dÅ‚uÅ¼ej, niÅ¼ n sekund. Nie jest on
    zbyt przydatny, gdyÅ¼ szybkoÅ›Ä‡ wykonania danej taktyki jest kwestiÄ… mocno
    zaleÅ¼nÄ… on sprzÄ™tu. Jak moÅ¼na przeczytaÄ‡ w manualu, kombinator ten bywa
    przydatny gÅ‚Ã³wnie przy debugowaniu i nie zaleca siÄ™, Å¼eby wystÄ™powaÅ‚ w
    finalnych dowodach, gdyÅ¼ moÅ¼e powodowaÄ‡ problemy z przenoÅ›noÅ›ciÄ….

    W powyÅ¼szym przykÅ‚adzie taktyka [timeout 1 repeat constructor] koÅ„czy siÄ™
    sukcesem, gdyÅ¼ udowodnienie [even 42] zajmuje jej mniej, niÅ¼ 1 sekundÄ™
    (przynajmniej na moim komputerze; na twoim taktyka ta moÅ¼e zawieÅ›Ä‡), ale
    juÅ¼ udowodnienie [even 1338] trwa wiÄ™cej niÅ¼ jednÄ… sekundÄ™ i wobec tego
    taktyka [timeout 1 repeat constructor] dla tego celu zawodzi (przynajmniej
    u mnie; jeÅ¼eli masz mocny komputer, u ciebie moÅ¼e zadziaÅ‚aÄ‡).

    Co wiÄ™cej, kombinator [timeout] moÅ¼e zachowywaÄ‡ siÄ™ rÃ³Å¼nie dla tego samego
    celu nawet na tym samym komputerze. Na przykÅ‚ad przed chwilÄ… taktyka ta
    zakoÅ„czyÅ‚Ä… siÄ™ na moim komputerze sukcesem, mimo Å¼e dotychczas zawsze
    zawodziÅ‚a). *)

Goal even 666.
Proof.
  time repeat constructor.
Restart.
  Time repeat constructor.
Abort.

(** Kolejnym kombinatorem jest [time t], ktÃ³ry odpala taktykÄ™ [t], a nastÄ™pnie
    wyÅ›wietla informacjÄ™ o czasie, jaki zajÄ™Å‚o jej wykonanie. Czas ten jest
    czasem rzeczywistym, tzn. zaleÅ¼y od mocy twojego komputera. Nie jest zbyt
    staÅ‚y â€” zazwyczaj rÃ³Å¼ni siÄ™ od jednego mierzenia do drugiego, czasem
    nawet doÅ›Ä‡ znacznie.

    AlternatywÄ… dla taktyki [time] jest komenda [Time], ktÃ³ra robi dokÅ‚adnie
    to samo. JeÅ¼eli stoisz przed wyborem miÄ™dzy tymi dwoma â€” wybierz komendÄ™
    [Time], gdyÅ¼ komendy zachowujÄ… siÄ™ zazwyczaj w sposÃ³b znacznie bardziej
    przewidywalny od taktyk. *)

(** * Konkluzja *)

(** W niniejszym rozdziale zapoznaliÅ›my siÄ™ z potÄ™Å¼nÄ… maszyneriÄ…, dziÄ™ki
    ktÃ³rej moÅ¼emy zjeÅ›Ä‡ ciastko i mieÄ‡ ciastko: dziÄ™ki wÅ‚asnym taktykom
    jesteÅ›my w stanie poÅ‚Ä…czyÄ‡ CoqowÄ… peÅ‚niÄ™ formalnej poprawnoÅ›ci oraz
    typowy dla matematyki uprawianej nieformalnie luÅºny styl dowodzenia,
    w ktÃ³rym maÅ‚o interesujÄ…ce szczegÃ³Å‚y zostajÄ… pominiÄ™te. A wszystko to
    okraszone (wystarczajÄ…cÄ…, mam nadziejÄ™) szczyptÄ… zadaÅ„.

    Ale to jeszcze nie wszystko, gdyÅ¼ pÃ³ki co pominiÄ™te zostaÅ‚y konstrukty
    Ltaca pozwalajÄ…ce dopasowywaÄ‡ termy, dziÄ™ki ktÃ³rym jesteÅ›my w stanie
    np. napisaÄ‡ taktykÄ™, ktÃ³ra odrÃ³Å¼ni [2 + 2] od [4]. JeÅ¼eli odczuwasz
    niedosyt po przeczytaniu tego rozdziaÅ‚u, to uszy do gÃ³ry â€” zapoznamy
    siÄ™ z nimi juÅ¼ niedÅ‚ugo, przy omawianiu dowodu przez reflekcjÄ™. Zanim
    to jednak nastÄ…pi, zrobimy przeglÄ…d taktyk wbudowanych. *)