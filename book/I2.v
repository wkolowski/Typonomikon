(** * I2: Spis przydatnych taktyk *)

(** Stare powiedzenie gÅ‚osi: nie wymyÅ›laj koÅ‚a na nowo. Aby uczyniÄ‡ zadoÅ›Ä‡
    duchom przodkÃ³w, ktÃ³rzy je wymyÅ›lili (zarÃ³wno koÅ‚o, jak i powiedzenie),
    w niniejszym rozdziale zapoznamy siÄ™ z rÃ³Å¼nymi przydatnymi taktykami,
    ktÃ³re prÄ™dzej czy pÃ³Åºniej i tak sami byÅ›my wymyÅ›lili, gdyby zaszÅ‚a taka
    potrzeba.

    Aby jednak nie popaÅ›Ä‡ w inny grzech i nie posÅ‚ugiwaÄ‡ siÄ™ czarami, ktÃ³rych
    nie rozumiemy, czÄ™Å›Ä‡ z poniÅ¼ej omÃ³wionych taktyk zaimplementujemy jako
    Ä‡wiczenie.

    OmÃ³wimy kolejno:
    - taktykÄ™ [refine]
    - drobne taktyki sÅ‚uÅ¼Ä…ce gÅ‚Ã³wnie do kontrolowania tego, co dzieje siÄ™
      w kontekÅ›cie
    - "Å›rednie" taktyki, wcielajÄ…ce w Å¼ycie pewien konkretny sposÃ³b
      rozumowania
    - taktyki sÅ‚uÅ¼Ä…ce do rozumowania na temat relacji rÃ³wnowaÅ¼noÅ›ci
    - taktyki sÅ‚uÅ¼Ä…ce do przeprowadzania obliczeÅ„
    - procedury decyzyjne
    - ogÃ³lne taktyki sÅ‚uÅ¼Ä…ce do automatyzacji *)

(** Uwaga: przykÅ‚ady uÅ¼ycia taktyk, ktÃ³rych reimplementacja bÄ™dzie
    Ä‡wiczeniem, zostaÅ‚y poÅ‚Ä…czone z testami w Ä‡wiczeniach Å¼eby nie pisaÄ‡ dwa
    razy tego samego. *)

(** * [refine] â€” matka wszystkich taktyk *)

(** Fama gÅ‚osi, Å¼e w zamierzchÅ‚ych czasach, gdy nie byÅ‚o jeszcze taktyk,
    a Å›wiatem Coqa rzÄ…dziÅ‚ Chaos (objawiajÄ…cy siÄ™ dowodzeniem przez rÄ™czne
    wpisywanie termÃ³w), jeden z Coqowych bogÃ³w imieniem He-fait-le-stos, w
    przebÅ‚ysku kreatywnego geniuszu wymyÅ›liÅ‚ dedukcjÄ™ naturalnÄ… i stworzyÅ‚
    pierwszÄ… taktykÄ™, ktÃ³rej nadaÅ‚ imiÄ™ [refine]. PomysÅ‚ przyjaÅ‚ siÄ™ i od
    tej pory Coqowi bogowie poczÄ™li uÅ¼ywaÄ‡ jej do tworzenia coraz to innych
    taktyk. Tak [refine] staÅ‚a siÄ™ matkÄ… wszystkich taktyk.

    OczywiÅ›cie legenda ta jest nieprawdziwa â€” deduckcjÄ™ naturalnÄ… wymyÅ›liÅ‚
    Gerhard Gentzen, a podstawowe taktyki sÄ… zaimplementowane w Ocamlu. Nie
    umniejsza to jednak mocy taktyki [refine]. Jej dziaÅ‚anie podobne jest
    do taktyki [exact], z tym Å¼e term bÄ™dÄ…cy jej argumentem moÅ¼e teÅ¼ zawieraÄ‡
    dziury [_]. JeÅ¼eli naszym celem jest [G], to taktyka [refine g] rozwiÄ…zuje
    cel, jeÅ¼eli [g] jest termem typu [G], i generuje takÄ… iloÅ›Ä‡ podcelÃ³w, ile
    [g] zawiera dziur, albo zawodzi, jeÅ¼eli [g] nie jest typu [G].

    Zobaczmy dziaÅ‚anie taktyki [refine] na przykÅ‚adach. *)

Example refine_0 : 42 = 42.
Proof.
  refine eq_refl.
Qed.

(** W powyÅ¼szym przykÅ‚adzie uÅ¼ywamy [refine] tak jak uÅ¼ylibyÅ›my [exact]a.
    [eq_refl] jest typu [42 = 42], gdyÅ¼ Coq domyÅ›la siÄ™, Å¼e tak naprawdÄ™
    chodzi nam o [@eq_refl nat 42]. PoniewaÅ¼ [eq_refl] zawiera 0 dziur,
    [refine eq_refl] rozwiÄ…zuje cel i nie generuje podcelÃ³w. *)

Example refine_1 :
  forall P Q : Prop, P /\ Q -> Q /\ P.
Proof.
  refine (fun P Q : Prop => _).
  refine (fun H => match H with | conj p q => _ end).
  refine (conj _ _ ).
    refine q.
    refine p.
Restart.
  intros P Q. intro H. destruct H as [p q]. split.
    exact q.
    exact p.
Qed.

(** W tym przykÅ‚adzie chcemy pokazaÄ‡ przemiennoÅ›Ä‡ konunkcji. PoniewaÅ¼ nasz
    cel jest kwantyfikacjÄ… uniwersalnÄ…, jego dowodem musi byÄ‡ jakaÅ› funkcja
    zaleÅ¼na. FunkcjÄ™ tÄ™ konstruujemy taktykÄ… [refine (fun P Q : Prop => _)].
    Nie podajemy jednak ciaÅ‚a funkcji, zastÄ™pujÄ…c je dzurÄ… [_], bo chcemy
    podaÄ‡ je pÃ³Åºniej. W zwiÄ…zku z tym nasz obecny cel zostaje rozwiÄ…zany, a
    w zamian dostajemy nowy cel postaci [P /\ Q -> Q /\ P], gdyÅ¼ takiego
    typu jest ciaÅ‚o naszej funkcji. To jednak nie wszystko: w kontekÅ›cie
    pojawiajÄ… siÄ™ [P Q : Prop]. Wynika to z tego, Å¼e [P] i [Q] mogÄ… zostaÄ‡
    uÅ¼yte w definicji ciaÅ‚a naszej funkcji.

    Jako, Å¼e naszym celem jest implikacja, jej dowodem musi byÄ‡ funkcja.
    Taktyka [refine (fun H => match H with | conj p q => _ end)] pozwala
    nam tÄ™ funkcjÄ™ skonstruowaÄ‡. CiaÅ‚em naszej funkcji jest dopasowanie
    zawierajÄ…ce dziurÄ™. WypeÅ‚nienie jej bÄ™dzie naszym kolejnym celem. Przy
    jego rozwiÄ…zywaniu bÄ™dziemy mogli skorzystaÄ‡ z [H], [p] i [q]. Pierwsza
    z tych hipotez pochodzi o wiÄ…zania [fun H => ...], zaÅ› [p] i [q] znajdÄ…
    siÄ™ w kontekÅ›cie dziÄ™ki temu, Å¼e zostaÅ‚y zwiÄ…zane podczas dopasowania
    [conj p q].

    Teraz naszym celem jest [Q /\ P]. PoniewaÅ¼ dowody koniunkcji sÄ… postaci
    [conj l r], gdzie [l] jest dowodem pierwszego czÅ‚onu, a [r] drugiego,
    uÅ¼ywamy taktyki [refine (conj _ _)], by osobno skonstruowaÄ‡ oba czÅ‚ony.
    Tym razem nasz proofterm zawiera dwie dziury, wiÄ™c wygenerowane zostanÄ…
    dwa podcele. Obydwa zachodzÄ… na mocy zaÅ‚oÅ¼enia, a rozwiÄ…zujemy je takÅ¼e
    za pomocÄ… [refine].

    PowyÅ¼szy przykÅ‚ad pokazuje, Å¼e [refine] potrafi zastÄ…piÄ‡ caÅ‚a gamÄ™
    przerÃ³Å¼nych taktyk, ktÃ³re dotychczas uwaÅ¼aliÅ›my za podstawowe: [intros],
    [intro], [destruct], [split] oraz [exact]. OkreÅ›lenie "matka wszystkich
    taktyk" wydaje siÄ™ caÅ‚kiem uzasadnione. *)

(** **** Ä†wiczenie (my_exact) *)

(** Napisz taktykÄ™ [my_exact], ktÃ³ra dziaÅ‚a tak, jak [exact]. UÅ¼yj taktyki
    [refine]. *)

(* begin hide *)
Ltac my_exact t := refine t.
(* end hide *)

Example my_exact_0 :
  forall P : Prop, P -> P.
Proof.
  intros. my_exact H.
Qed.

(** **** Ä†wiczenie (my_intro) *)

(** Zaimplementuj taktykÄ™ [my_intro1], ktÃ³ra dziaÅ‚a tak, jak [intro], czyli
    prÃ³buje wprowadziÄ‡ do kontekstu zmiennÄ… o domyÅ›lnej nazwie. Zaimplementuj
    teÅ¼ taktykÄ™ [my_intro2 x], ktÃ³ra dziaÅ‚a tak jak [intro x], czyli prÃ³buje
    wprowadziÄ‡ do kontekstu zmiennÄ… o nazwie [x]. UÅ¼yj taktyki [refine].

    Bonus: przeczytaj dokumentacjÄ™ na temat notacji dla taktyk (komenda
    [Tactic Notation]) i napisz taktykÄ™ [my_intro], ktÃ³ra dziaÅ‚a tak jak
    [my_intro1], gdy nie dostanie argumentu, a tak jak [my_intro2], gdy
    dostanie argument. *)

(* begin hide *)
Ltac my_intro1 := refine (fun _ => _).
Ltac my_intro2 x := refine (fun x => _).

Tactic Notation "my_intro" := my_intro1.
Tactic Notation "my_intro" ident(x) := my_intro2 x.
(* end hide *)

Example my_intro_0 :
  forall P : Prop, P -> P.
Proof.
  my_intro1. my_intro2 H. my_exact H.
Restart.
  my_intro. my_intro H. my_exact H.
Qed.

(** **** Ä†wiczenie (my_apply) *)

(** Napisz taktykÄ™ [my_apply H], ktÃ³ra dziaÅ‚a tak jak [apply H]. UÅ¼yj taktyki
    [refine]. *)

(* begin hide *)
Ltac my_apply H := refine (H _).
(* end hide *)

Example my_apply_0 :
  forall P Q : Prop, P -> (P -> Q) -> Q.
Proof.
  my_intro P. my_intro Q. my_intro p. my_intro H.
  my_apply H. my_exact p.
Qed.

(** **** Ä†wiczenie (taktyki dla konstruktorÃ³w 1) *)

(** Napisz taktyki:
    - [my_split], ktÃ³ra dziaÅ‚a tak samo jak [split]
    - [my_left] i [my_right], ktÃ³re dziaÅ‚ajÄ… tak jak [left] i [right]
    - [my_exists], ktÃ³ra dziaÅ‚a tak samo jak [exists] *)

(** UÅ¼yj taktyki [refine]. *)

(* begin hide *)
Ltac my_split := refine (conj _ _).
Ltac my_left := refine (or_introl _).
Ltac my_right := refine (or_intror _).
Ltac my_exists n := refine (ex_intro _ n _).
(* end hide *)

Example my_split_0 :
  forall P Q : Prop, P -> Q -> P /\ Q.
Proof.
  my_intro P; my_intro Q; my_intro p; my_intro q.
  my_split.
    my_exact p.
    my_exact q.
Qed.

Example my_left_right_0 :
  forall P : Prop, P -> P \/ P.
Proof.
  my_intro P; my_intro p. my_left. my_exact p.
Restart.
  my_intro P; my_intro p. my_right. my_exact p.
Qed.

Example my_exists_0 :
  exists n : nat, n = 42.
Proof.
  my_exists 42. reflexivity.
Qed.

(** * Drobne taktyki *)

(** ** [clear] *)

Goal
  forall x y : nat, x = y -> y = x -> False.
Proof.
  intros. clear H H0.
Restart.
  intros. Fail clear x. Fail clear wut.
Restart.
  intros. clear dependent x.
Restart.
  intros. clear.
Restart.
  intros.
  pose (z := 42).
  clearbody z.
Abort.

(** [clear] to niesamowicie uÅ¼yteczna taktyka, dziÄ™ki ktÃ³rej moÅ¼emy zrobiÄ‡
    porzÄ…dek w kontekÅ›cie. MoÅ¼na uÅ¼ywaÄ‡ jej na nastepujÄ…ce sposoby:
    - [clear x] usuwa [x] z kontekstu. JeÅ¼eli [x] nie ma w kontekÅ›cie lub sÄ…
      w nim jakieÅ› rzeczy zaleÅ¼ne od [x], taktyka zawodzi. MoÅ¼na usunÄ…Ä‡ wiele
      rzeczy na raz: [clear x_1 ... x_N].
    - [clear -x] usuwa z kontekstu wszystko poza [x].
    - [clear dependent x] usuwa z kontekstu [x] i wszystkie rzeczy, ktÃ³re od
      niego zaleÅ¼Ä…. Taktyka ta zawodzi jedynie gdy [x] nie ma w kontekÅ›cie.
    - [clear] usuwa z kontekstu absolutnie wszystko. Serdecznie nie polecam.
    - [clearbody x] usuwa definicjÄ™ [x] (jeÅ¼eli [x] jakÄ…Å› posiada). *)

(** **** Ä†wiczenie (tru) *)

(** Napisz taktykÄ™ [tru], ktÃ³ra czyÅ›ci kontekst z dowodÃ³w na [True] oraz
    potrafi udowodniÄ‡ cel [True].

    Dla przykÅ‚adu, taktyka ta powinna przeksztaÅ‚caÄ‡ kontekst
    [a, b, c : True, p : P |- _] w [p : P |- _]. *)

(* begin hide *)
Ltac tru := intros; repeat
match goal with
    | H : True |- _ => clear H
end; trivial.
(* end hide *)

Section tru.

Example tru_0 :
  forall P : Prop, True -> True -> True -> P.
Proof.
  tru. (* Kontekst: [P : Prop |- P] *)
Abort.

Example tru_1 : True.
Proof. tru. Qed.

End tru.

(** **** Ä†wiczenie (satans_neighbour_not_even) *)

Inductive even : nat -> Prop :=
    | even0 : even 0
    | evenSS : forall n : nat, even n -> even (S (S n)).

(** Napisz taktykÄ™ [even], ktÃ³ra potrafi udowodniÄ‡ poniÅ¼szy cel. *)

(* begin hide *)
Ltac even := unfold not; intros; repeat
match goal with
    | H : even _ |- _ => inversion H; subst; clear H
end.
(* end hide *)

Theorem satans_neighbour_not_even : ~ even 667.
(* begin hide *)
(* Proof. even. Qed. *)
Abort.
(* end hide *)

(** **** Ä†wiczenie (my_destruct_and) *)

(** Napisz taktykÄ™ [my_destruct H p q], ktÃ³ra dziaÅ‚a jak [destruct H as [p q]],
    gdzie [H] jest dowodem koniunkcji. UÅ¼yj taktyk [refine] i [clear].

    Bonus 1: zaimplementuj taktykÄ™ [my_destruct_and H], ktÃ³ra dziaÅ‚a tak jak
    [destruct H], gdy [H] jest dowodem koniunkcji.

    Bonus 2: zastanÃ³w siÄ™, jak (albo czy) moÅ¼na zaimplementowaÄ‡ taktykÄ™
    [destruct x], gdzie [x] jest dowolnego typu induktywnego. *)

(* begin hide *)
Ltac my_destruct_and_named H p q := refine (
match H with
    | conj p q => _
end); clear H.

Ltac my_destruct_and_unnamed H :=
  let p := fresh in let q := fresh in my_destruct_and_named H p q.

Tactic Notation "my_destruct_and" ident(H) ident(p) ident(q) :=
  my_destruct_and_named H p q.
Tactic Notation "my_destruct_and" ident(H) :=
  my_destruct_and_unnamed H.
(* end hide *)

Example my_destruct_and_0 :
  forall P Q : Prop, P /\ Q -> P.
Proof.
  my_intro P; my_intro Q; my_intro H.
  my_destruct_and H p q. my_exact p.
Restart.
  my_intro P; my_intro Q; my_intro H.
  my_destruct_and H. my_exact H0.
Qed.

(** ** [fold] *)

(** [fold] to taktyka sÅ‚uÅ¼Ä…ca do zwijania definicji. Jej dziaÅ‚anie jest
    odwrotne do dziaÅ‚ania taktyki [unfold]. Niestety, z nieznanych mi
    bliÅ¼ej powodÃ³w bardzo czÄ™sto jest ona nieskuteczna. *)

(** **** Ä†wiczenie (my_fold) *)

(** Napisz taktykÄ™ [my_fold x], ktÃ³ra dziaÅ‚a tak jak [fold x], tj. zastÄ™puje
    we wszystkich miejscach w celu term powstaÅ‚y po rozwiniÄ™ciu [x] przez [x].

    WskazÃ³wka: zapoznaj siÄ™ z konstruktem [eval] â€” zajrzyj do 9 rozdziaÅ‚u
    manuala. *)

(* begin hide *)
Ltac my_fold x :=
  let body := eval unfold x in x in
match goal with
    | |- context [body] => change body with x
end.
(* end hide *)

Example fold_0 :
  forall n m : nat, n + m = m + n.
Proof.
  intros. unfold plus. fold plus.
Restart.
  intros. unfold plus. my_fold plus.
Abort.

(** ** [move] *)

Example move_0 :
  forall P Q R S T : Prop, P /\ Q /\ R /\ S /\ T -> T.
Proof.
  destruct 1 as [p [q [r [s t]]]].
  move p after t.
  move p before s.
  move p at top.
  move p at bottom.
Abort.

(** [move] to taktyka sÅ‚uÅ¼Ä…ca do zmieniania kolejnoÅ›ci obiektÃ³w w kontekÅ›cie.
    Jej dziaÅ‚anie jest tak ewidentnie oczywiste, Å¼ nie ma zbytniego sensu,
    aby je opisywaÄ‡. *)

(** **** Ä†wiczenie *)

(** Przeczytaj dokÅ‚adny opis dziaÅ‚ania taktyki [move] w manualu. *)

(** ** [pose] i [remember] *)

Goal 2 + 2 = 4.
Proof.
  intros.
  pose (a := 2 + 2).
  remember (2 + 2) as b.
Abort.

(** Taktyka [pose (x := t)] dodaje do kontekstu zmiennÄ… [x] (pod warunkiem,
    Å¼e nazwa ta nie jest zajÄ™ta), ktÃ³ra zostaje zdefiniowana za pomocÄ… termu
    [t].

    Taktyka [remember t as x] zastÄ™puje wszystkie wystÄ…pienia termu [t]
    w kontekÅ›cie zmiennÄ… [x] (pod warunkiem, Å¼e nazwa ta nie jest zajÄ™ta) i
    dodaje do kontekstu rÃ³wnanie postaci [x = t].

    W powyÅ¼szym przykÅ‚adzie dziaÅ‚ajÄ… one nastÄ™pujÄ…co: [pose (a := 2 + 2)]
    dodaje do kontekstu wiÄ…zanie [a := 2 + 2], zaÅ› [remember (2 + 2) as b]
    dodaje do kontekstu rÃ³wnanie [Heqb : b = 2 + 2] i zastÄ™puje przez [b]
    wszystkie wystÄ…pienia [2 + 2] â€” takÅ¼e to w definicji [a].

    Taktyki te przydajÄ… siÄ™ w tak wielu rÃ³Å¼nych sytuacjach, Å¼e nie ma co
    prÃ³bowaÄ‡ ich tu wymieniaÄ‡. UÅ¼yjesz ich jeszcze nie raz. *)

(** **** Ä†wiczenie (set) *)

(** Taktyki te sÄ… jedynie wariantami bardziej ogÃ³lnej taktyki [set].
    Przeczytaj jej dokumentacjÄ™ w manualu. *)

(** ** [rename] *)

Goal forall P : Prop, P -> P.
Proof.
  intros. rename H into wut.
Abort.

(** [rename x into y] zmienia nazwÄ™ [x] na [y] lub zawodzi, gdy [x] nie ma
    w kontekÅ›cie albo nazwa [y] jest juÅ¼ zajÄ™ta *)

(** **** Ä†wiczenie (satans_neighbour_not_even') *)

(** Napisz taktykÄ™ [even'], ktÃ³ra potrafi udowodniÄ‡ poniÅ¼szy cel. Nie uÅ¼ywaj
    [match]a, a jedynie kombinatora [repeat]. *)

Theorem satans_neighbour_not_even' : ~ even 667.
(* begin hide *)
(* Proof.
  intro.
  Time repeat (inversion H; clear H H0; rename H1 into H; try (clear n0)).
Qed. *)
Abort.
(* end hide *)

(** ** [admit] *)

Module admit.

Lemma forgery :
  forall P Q : Prop, P -> Q /\ P.
Proof.
  intros. split.
    admit.
    assumption.
Admitted.

Print forgery.
(* ===> *** [[ forgery : forall P : Prop, P -> ~ P /\ P ]] *)

End admit.

(** [admit] to taktyka-oszustwo, ktÃ³ra rozwiÄ…zuje dowolny cel. Nie jest ona
    rzecz jasna wszechwiedzÄ…ca i przez to rozwiÄ…zanego za jej pomocÄ… celu
    nie moÅ¼na zapisaÄ‡ za pomocÄ… komend [Qed] ani [Defined], a jedynie za
    pomocÄ… komendy [Admitted], ktÃ³ra oszukaÅ„czo udowodnione twierdzenie
    przeksztaÅ‚ca w aksjomat.

    W CoqIDE oszustwo jest dobrze widoczne, gdyÅ¼ zarÃ³wno taktyka [admit]
    jak i komenda [Admitted] podÅ›wietlajÄ… siÄ™ na Å¼Ã³Å‚to, a nie na zielono,
    tak jak prawdziwe dowody. WyÅ›wietlenie [Print]em dowodu zakoÅ„czonego
    komendÄ… [Admitted] rÃ³wnieÅ¼ pokazuje, Å¼e ma on status aksjomatu.

    Na koniec zauwaÅ¼my, Å¼e komendy [Admitted] moÅ¼emy uÅ¼yÄ‡ rÃ³wnieÅ¼ bez
    wczesniejszego uÅ¼ycia taktyki [admit]. RÃ³Å¼nica miÄ™dzy tymi dwoma bytami
    jest taka, Å¼e taktyka [admit] sÅ‚uÅ¼y do "udowodnienia" pojedynczego celu,
    a komenda [Admitted] â€” caÅ‚ego twierdzenia. *)

(** * Åšrednie taktyki *)

(** ** [case_eq] *)

(** [case_eq] to taktyka podobna do taktyki [destruct], ale nieco mÄ…drzejsza,
    gdyÅ¼ nie zdarza jej siÄ™ "zapominaÄ‡", jaka byÅ‚a struktura rozbitego przez
    niÄ… termu. *)

Goal
  forall n : nat, n + n = 42.
Proof.
  intros. destruct (n + n).
Restart.
  intros. case_eq (n + n); intro.
Abort.

(** RÃ³Å¼nice miÄ™dzy [destruct] i [case_eq] dobrze ilustruje powyÅ¼szy przykÅ‚ad.
    [destruct] nadaje siÄ™ jedynie do rozbijania termÃ³w, ktÃ³re sÄ… zmiennymi.
    JeÅ¼eli rozbijemy coÅ›, co nie jest zmiennÄ… (np. term [n + n]), to utracimy
    czÄ™Å›Ä‡ informacji na jego temat. [case_eq] potrafi rozbijaÄ‡ dowolne termy,
    gdyÅ¼ poza samym rozbiciem dodaje teÅ¼ do celu dodatkowÄ… hipotezÄ™, ktÃ³ra
    zawiera rÃ³wnanie "pamiÄ™tajÄ…ce" informacje o rozbitym termie, o ktÃ³rych
    zwykÅ‚y [destruct] zapomina. *)

(** **** Ä†wiczenie (my_case_eq) *)

(** Napisz taktykÄ™ [my_case_eq t Heq], ktÃ³ra dziaÅ‚a tak jak [case_eq t], ale
    nie dodaje rÃ³wnania jako hipotezÄ™ na poczÄ…tku celu, tylko bezpoÅ›rednio
    do kontekstu i nazywa je [Heq]. UÅ¼yj taktyk [remember] oraz [destruct]. *)

(* begin hide *)
Ltac my_case_eq t Heq :=
  let x := fresh "x" in remember t as x;
  match goal with
      | H : x = t |- _ => symmetry in H; rename H into Heq
  end;
  destruct x.
(* end hide *)

Goal
  forall n : nat, n + n = 42.
Proof.
  intros. destruct (n + n).
Restart.
  intros. case_eq (n + n); intro.
Restart.
  intros. my_case_eq (n + n) H.
Abort.

(** ** [contradiction] *)

(** [contradiction] to taktyka, ktÃ³ra wprowadza do kontekstu wszystko co siÄ™
    da, a potem prÃ³buje znaleÅºÄ‡ sprzecznoÅ›Ä‡. Potrafi rozpoznawaÄ‡ hipotezy
    takie jak [False], [x <> x], [~ True]. Potrafi teÅ¼ znaleÅºÄ‡ dwie hipotezy,
    ktÃ³re sÄ… ze sobÄ… ewidentnie sprzeczne, np. [P] oraz [~ P]. Nie potrafi
    jednak wykrywaÄ‡ lepiej ukrytych sprzecznoÅ›ci, np. nie jest w stanie
    odrÃ³Å¼niÄ‡ [true] od [false]. *)

(** **** Ä†wiczenie (my_contradiction) *)

(** Napisz taktykÄ™ [my_contradiction], ktÃ³ra dziaÅ‚a tak jak standardowa
    taktyka [contradiction], a do tego jest w stanie udowodniÄ‡ dowolny
    cel, jeÅ¼eli w kontekÅ›cie jest hipoteza postaci [true = false] lub
    [false = true]. *)

(* begin hide *)
Ltac my_contradiction := intros;
match goal with
    | H : False |- _ => destruct H
    | H : ~ True |- _ => destruct (H I)
    | H : ?x <> ?x |- _ => destruct (H eq_refl)
    | H : ~ ?P, H' : ?P |- _ => destruct (H H')
    | H : true = false |- _ => inversion H
    | H : false = true |- _ => inversion H
end.
(* end hide *)

Section my_contradiction.

Example my_contradiction_0 :
  forall P : Prop, False -> P.
Proof.
  contradiction.
Restart.
  my_contradiction.
Qed.

Example my_contradiction_1 :
  forall P : Prop, ~ True -> P.
Proof.
  contradiction.
Restart.
  my_contradiction.
Qed.

Example my_contradiction_2 :
  forall (P : Prop) (n : nat), n <> n -> P.
Proof.
  contradiction.
Restart.
  my_contradiction.
Qed.

Example my_contradiction_3 :
  forall P Q : Prop, P -> ~ P -> Q.
Proof.
  contradiction.
Restart.
  my_contradiction.
Qed.

Example my_contradiction_4 :
  forall P : Prop, true = false -> P.
Proof.
  try contradiction.
Restart.
  my_contradiction.
Qed.

Example my_contradiction_5 :
  forall P : Prop, false = true -> P.
Proof.
  try contradiction.
Restart.
  my_contradiction.
Qed.

End my_contradiction.

(** **** Ä†wiczenie (taktyki dla sprzecznoÅ›ci) *)

(** Innymi taktykami, ktÃ³re mogÄ… przydaÄ‡ siÄ™ przy rozumowaniach przez
    sprowadzenie do sprzecznoÅ›ci, sÄ… [absurd], [contradict] i [exfalso].
    Przeczytaj ich opisy w manualu i zbadaj ich dziaÅ‚anie. *)

(** ** [constructor] *)

Example constructor_0 :
  forall P Q : Prop, P -> Q \/ P.
Proof.
  intros. constructor 2. assumption.
Restart.
  intros. constructor.
Restart.
  intros. constructor; assumption.
Qed.

(** [constructor] to taktyka uÅ‚atwiajÄ…ca aplikowanie konstruktorÃ³w typÃ³w
    induktywnych. JeÅ¼eli aktualnym celem jest [T], to taktyka [constructor i]
    jest rÃ³wnowaÅ¼na wywoÅ‚aniu jego i-tego konstruktora, gdzie porzÄ…dek
    konstruktorÃ³w jest taki jak w definicji typu. *)

Print or.
(* ===> Inductive or (A B : Prop) : Prop :=
            or_introl : A -> A \/ B | or_intror : B -> A \/ B *)

(** W powyÅ¼szym przykÅ‚adzie [constructor 2] dziaÅ‚a tak jak [apply or_intror]
    (czyli tak samo jak taktyka [right]), gdyÅ¼ w definicji spÃ³jnika [or]
    konstruktor [or_intror] wystÄ™puje jako drugi (liczÄ…c od gÃ³ry).

    UÅ¼ycie taktyki [constructor] bez liczby oznacza zaaplikowanie pierwszego
    konstruktora, ktÃ³ry pasuje do celu, przy czym taktyka ta moÅ¼e wyzwalaÄ‡
    backtracking. W drugim przykÅ‚adzie powyÅ¼ej [constructor] dziaÅ‚a jak
    [apply or_intro] (czyli jak taktyka [left]), gdyÅ¼ zaaplikowanie tego
    konstruktora nie zawodzi.

    W trzecim przykÅ‚adzie [constructor; assumption] dziaÅ‚a tak: najpierw
    aplikowany jest konstruktor [or_introl], ale wtedy [assumption] zawodzi,
    wiÄ™c nastÄ™puje nawrÃ³t i aplikowany jest konstruktor [or_intror], a wtedy
    [assumption] rozwiÄ…zuje cel. *)

(** **** Ä†wiczenie (taktyki dla konstruktorÃ³w 2) *)

(** Jaki jest zwiÄ…zek taktyki [constructor] z taktykami [split], [left],
    [right] i [exists]? *)

(** ** [decompose] *)

Example decompose_0 :
  forall P Q R S : nat -> Prop,
    (exists n : nat, P n) /\ (exists n : nat, Q n) /\
    (exists n : nat, R n) /\ (exists n : nat, S n) ->
      exists n : nat, P n \/ Q n \/ R n \/ S n.
Proof.
  intros. decompose [and ex] H. clear H. exists x. left. assumption.
Qed.

(** [decompose] to bardzo uÅ¼yteczna taktyka, ktÃ³ra potrafi za jednym zamachem
    rozbiÄ‡ bardzo skomplikowane hipotezy. [decompose [t_1 ... t_n] H] rozbija
    rekurencyjnie hipotezÄ™ [H] tak dÅ‚ugo, jak jej typem jest jeden z typÃ³w
    [t_i]. W powyÅ¼szym przykÅ‚adzie [decompose [and ex] H] najpierw rozbija [H],
    gdyÅ¼ jest ona koniunkcjÄ…, a nastÄ™pnie rozbija powstaÅ‚e z niej hipotezy,
    gdyÅ¼ sÄ… one kwantyfikacjami egzystencjalnymi ("exists" jest notacjÄ… dla
    [ex]). [decompose] nie usuwa z kontekstu hipotezy, na ktÃ³rej dziaÅ‚a, wiÄ™c
    czÄ™sto nastÄ™puje po niej taktyka [clear]. *)

(** ** [intros] *)

(** Dotychczas uÅ¼ywaÅ‚eÅ› taktyk [intro] i [intros] jedynie z nazwami lub
    wzorcami do rozbijania elementÃ³w typÃ³w induktywnych. Taktyki te potrafiÄ…
    jednak duÅ¼o wiÄ™cej. *)

Example intros_0 :
  forall P Q R S : Prop, P /\ Q /\ R -> S.
Proof.
  intros P Q R S [p [q r]].
Restart.
  intros ? ?P Q R. intros (p, (p0, q)).
Restart.
  intros *.
Restart.
  intros A B **.
Restart.
  intros * _.
Restart.
  Fail intros _.
Abort.

(** Pierwszy przykÅ‚ad to standardowe uÅ¼ycie [intros] â€” wprowadzamy cztery
    zmienne, ktÃ³ra nazywamy kolejno [P], [Q], [R] i [S], po czym wprowadzamy
    bezimiennÄ… hipotezÄ™ typu [P /\ Q /\ R], ktÃ³ra natychmiast rozbijamy za
    pomocÄ… wzorca [p [q r]].

    W kolejnym przykÅ‚adzie mamy juÅ¼ nowoÅ›ci: wzorzec [?] sÅ‚uÅ¼y do nadania
    zmiennej domyÅ›lnej nazwy. W naszym przypadku wprowadzone do kontekstu
    zdanie zostaje nazwane [P], gdyÅ¼ takÄ… nazwÄ™ nosi w kwantyfikatorze,
    gdy jest jeszcze w celu.

    Wzorzec [?P] sÅ‚uÅ¼y do nadania zmiennej domyÅ›lnej nazwy zaczynajÄ…c siÄ™
    od tego, co nastÄ™puje po znaku [?]. W naszym przypadku do konteksu
    wprowadzona zostaje zmienna [P0], gdyÅ¼ Å¼Ä…damy nazwy zaczynajÄ…cej siÄ™
    od "P", ale samo "P" jest juÅ¼ zajÄ™te. Widzimy teÅ¼ wzorzec [(p, (p0, q))],
    ktÃ³ry sÅ‚uÅ¼y do rozbicia hipotezy. Wzorce tego rodzaju dziaÅ‚ajÄ… tak samo
    jak wzorce w kwadratowych nawiasach, ale moÅ¼emy uÅ¼ywaÄ‡ ich tylko na
    elementach typu induktywnego z jednym konstruktorem.

    Wzorzec [*] wprowadza do kontekstu wszystkie zmienne kwantyfikowane
    uniwersalnie i zatrzymuje sie na pierwszej nie-zaleÅ¼nej hipotezie. W
    naszym przykÅ‚adzie uniwersalnie kwantyfikowane sÄ… [P], [Q], [R] i [S],
    wiÄ™c zostajÄ… wprowadzane, ale [P /\ Q /\ R] nie jest juÅ¼ kwantyfikowane
    uniwersalnie â€” jest przesÅ‚ankÄ… implikacji â€” wiÄ™c nie zostaje wprowadzone.

    Wzorzec [**] wprowadza do kontekstu wszystko. Wobec tego [intros **] jest
    synonimem [intros]. Mimo tego nie jest on bezuÅ¼yteczny â€” moÅ¼emy uÅ¼yÄ‡ go
    po innych wzorcach, kiedy nie chcemy juÅ¼ wiÄ™cej nazywaÄ‡/rozbijaÄ‡ naszych
    zmiennych. Wtedy duÅ¼o szybciej napisaÄ‡ [**] niÅ¼ [; intros]. W naszym
    przypadku chcemy nazwaÄ‡ jedynie pierwsze dwie zmienne, a resztÄ™ wrzucamy
    do kontekstu jak leci.

    Wzorzec [_] pozwala pozbyÄ‡ siÄ™ zmiennej lub hipotezy. Taktyka [intros _]
    jest wobec tego rÃ³wnowaÅ¼na [intro H; clear H] (przy zaÅ‚oÅ¼eniu, Å¼e [H]
    jest wolne), ale duÅ¼o bardziej zwiÄ™zÅ‚a w zapisie. Nie moÅ¼emy jednak
    usunÄ…Ä‡ zmiennych lub hipotez, od ktÃ³rych zaleÅ¼Ä… inne zmienne lub hipotezy.
    W naszym przedostatnim przykÅ‚adzie bez problemu usuwamy hipotezÄ™ [P /\
    Q /\ R], gdyÅ¼ Å¼aden term od niej nie zaleÅ¼y. Jednak w ostatnim przykÅ‚adzie
    nie moÅ¼emy usunÄ…Ä‡ [P], gdyÅ¼ zaleÅ¼y od niego hipoteza [P /\ Q /\ R]. *)

Example intros_1 :
  forall P0 P1 P2 P3 P4 P5 : Prop,
    P0 /\ P1 /\ P2 /\ P3 /\ P4 /\ P5 -> P3.
Proof.
  intros * [p0 [p1 [p2 [p3 [p4 p5]]]]].
Restart.
  intros * (p0 & p1 & p2 & p3 & p4 & p5).
Abort.

(** Wzorce postaci [(p_1 & ... & p_n)] pozwalajÄ… rozbijaÄ‡ termy zagnieÅ¼dÅ¼onych
    typÃ³w induktywnych. Jak widaÄ‡ na przykÅ‚adzie, im bardziej zagnieÅ¼dÅ¼ony
    jest typ, tym bardziej opÅ‚aca siÄ™ uÅ¼yÄ‡ tego rodzaju wzorca. *)

Example intros_2 :
  forall x y : nat, x = y -> y = x.
Proof.
  intros * ->.
Restart.
  intros * <-.
Abort.

(** WzorcÃ³w [->] oraz [<-] moÅ¼emy uÅ¼yÄ‡, gdy chcemy wprowadziÄ‡ do kontekstu
    rÃ³wnanie, przepisaÄ‡ je i natychmiast siÄ™ go pozbyÄ‡. Wobec tego taktyka
    [intros ->] jest rÃ³wnowaÅ¼na czemuÅ› w stylu [intro H; rewrite H in *;
    clear H] (oczywiÅ›cie pod warunkiem, Å¼e nazwa [H] nie jest zajÄ™ta). *)

Example intros_3 :
  forall a b c d : nat, (a, b) = (c, d) -> a = c.
Proof.
  Fail intros * [p1 p2].
Restart.
  intros * [= p1 p2].
Abort.

(** Wzorzec postaci [= p_1 ... p_n] pozwala rozbiÄ‡ rÃ³wnanie miÄ™dzy parami
    (i nie tylko) na skÅ‚adowe. W naszym przypadu mamy rÃ³wnanie [(a, b) =
    (c, d)] â€” zauwaÅ¼my, Å¼e nie jest ono koniunkcjÄ… dwÃ³ch rÃ³wnoÅ›ci [a = c]
    oraz [b = d], co jasno widaÄ‡ na przykÅ‚adzie, ale moÅ¼na z niego owÄ…
    koniunkjcÄ™ wywnioskowaÄ‡. Taki wÅ‚aÅ›nie efekt ma wzorzec [= p1 p2] â€”
    dodaje on nam do kontekstu hipotezy [p1 : a = c] oraz [p2 : b = d]. *)

Example intros_4 :
  forall P Q R : Prop, (P -> Q) -> (Q -> R) -> P -> R.
Proof.
  intros until 2. intro p. apply H in p. apply H0 in p.
Restart.
  intros until 2. intros p %H %H0.
Abort.

(** Taktyka [intros until x] wprowadza do kontekstu wszystkie zmienne jak
    leci dopÃ³ki nie natknie siÄ™ na takÄ…, ktÃ³ra nazywa siÄ™ "x". Taktyka
    [intros until n], gdzie [n] jest liczbÄ…, wprowadza do kontekstu wszyskto
    jak leci aÅ¼ do n-tej nie-zaleÅ¼nej hipotezy (tj. przesÅ‚anki implikacji).
    W naszym przykÅ‚adzie mamy 3 przesÅ‚anki implikacji: [(P -> Q)], [(Q -> R)]
    i [P], wiÄ™c taktyka [intros until 2] wprowadza do kontekstu dwie pierwsze
    z nich oraz wszystko, co jest poprzedza.

    Wzorzec [x %H_1 ... %H_n] wprowadza do kontekstu zmiennÄ… [x], a nastÄ™pnie
    aplikuje do niej po kolei hipotezy [H_1], ..., [H_n]. Taki sam efekt moÅ¼na
    osiÄ…gnÄ…Ä‡ rÄ™cznie za pomocÄ… taktyki [intro x; apply H_1 in x; ... apply H_n
    in x]. *)

(** **** Ä†wiczenie (intros) *)

(** Taktyka [intros] ma jeszcze trochÄ™ rÃ³Å¼nych wariantÃ³w. Poczytaj o nich
    w manualu. *)

(** ** [fix] *)

(** [fix] to taktyka sÅ‚uÅ¼Ä…ca do dowodzenia bezpoÅ›rednio przez rekursjÄ™. W
    zwiÄ…zku z tym nadeszÅ‚a dobra pora, Å¼eby pokazaÄ‡ wszystkie moÅ¼liwe sposoby
    na uÅ¼ycie rekursji w Coqu. Å»eby duÅ¼o nie pisaÄ‡, przyjrzyjmy siÄ™ przykÅ‚adom:
    zdefiniujemy/udowodnimy reguÅ‚Ä™ indukcyjnÄ… dla liczb naturalnych, ktÃ³rÄ…
    powinieneÅ› znaÄ‡ jak wÅ‚asnÄ… kieszeÅ„ (a jeÅ¼eli nie, to marsz robiÄ‡ zadania
    z liczb naturalnych!). *)

Definition nat_ind_fix_term
  (P : nat -> Prop) (H0 : P 0)
  (HS : forall n : nat, P n -> P (S n))
    : forall n : nat, P n :=
      fix f (n : nat) : P n :=
      match n with
          | 0 => H0
          | S n' => HS n' (f n')
      end.

(** Pierwszy, najbardziej prymitywny sposÃ³b to uÅ¼ycie konstruktu [fix]. [fix]
    to podstawowy budulec Coqowej rekursji, ale ma tÄ™ wadÄ™, Å¼e trzeba siÄ™
    trochÄ™ napisaÄ‡: w powyÅ¼szym przykÅ‚adzie najpierw piszemy [forall n : nat,
    P n], a nastÄ™pnie powtarzamy niemal to samo, piszÄ…c
    [fix f (n : nat) : P n]. *)

Fixpoint nat_ind_Fixpoint_term
  (P : nat -> Prop) (H0 : P 0)
  (HS : forall n : nat, P n -> P (S n))
  (n : nat) : P n :=
match n with
    | 0 => H0
    | S n' => HS n' (nat_ind_Fixpoint_term P H0 HS n')
end.

(** RozwiÄ…zaniem powyÅ¼szej robnej niedogodnoÅ›ci jest komenda [Fixpoint],
    ktÃ³ra jest skrÃ³tem dla [fix]. OszczÄ™dza nam ona pisania dwa razy tego
    samego, dziÄ™ki czemu definicja jest o linijkÄ™ krÃ³tsza. *)

Fixpoint nat_ind_Fixpoint_tac
  (P : nat -> Prop) (H0 : P 0)
  (HS : forall n : nat, P n -> P (S n))
  (n : nat) : P n.
Proof.
  apply nat_ind_Fixpoint_tac; assumption.
  Fail Guarded.
  (* ===> DÅ‚ugi komunikat o bÅ‚Ä™dzie. *)
   Show Proof.
  (* ===> (fix nat_ind_Fixpoint_tac
                 (P : nat -> Prop) (H0 : P 0)
                 (HS : forall n : nat, P n -> P (S n))
                 (n : nat) {struct n} : P n :=
                   nat_ind_Fixpoint_tac P H0 HS n) *)
Restart.
  destruct n as [| n'].
    apply H0.
    apply HS. apply nat_ind_Fixpoint_tac; assumption.
  Guarded.
  (* ===> The condition holds up to here *)
Defined.

(** W trzecim podejÅ›ciu rÃ³wnieÅ¼ uÅ¼ywamy komendy [Fixpoint], ale tym razem,
    zamiast rÄ™cznie wpisywaÄ‡ term, definiujemy naszÄ… reguÅ‚Ä™ za pomocÄ… taktyk.
    SposÃ³b ten jest prawie zawsze (duÅ¼o) dÅ‚uÅ¼szy niÅ¼ poprzedni, ale jego
    zaletÄ… jest to, Å¼e przy skomplikowanych celach jest duÅ¼o Å‚awiejszy do
    ogarniÄ™cia dla czÅ‚owieka.

    KorzystajÄ…c z okazji rzuÄ‡my okiem na komendÄ™ [Guarded]. Jest ona przydatna
    gdy, tak jak wyÅ¼ej, dowodzimy lub definiujemy bezpoÅ›rednio przez rekursjÄ™.
    Sprawdza ona, czy wszystkie dotychczasowe wywoÅ‚ania rekurencyjne odbyÅ‚y
    siÄ™ na strukturalnie mniejszych podtermach. JeÅ¼eli nie, wyÅ›wietla ona
    wiadomoÅ›Ä‡, ktÃ³ra informuje nas, gdzie jest bÅ‚Ä…d. Niestety wiadomoÅ›ci te
    nie zawsze sÄ… czytelne.

    Tak wÅ‚aÅ›nie jest, gdy w powyÅ¼szym przykÅ‚adzie uÅ¼ywamy jej po raz pierwszy.
    Na szczÄ™Å›cie ratuje nas komenda [Show Proof], ktÃ³ra pokazuje, jak wyglÄ…da
    term, ktÃ³ra pÃ³ki co wygenerowaÅ‚y taktyki. Pokazuje on nam term postaci
    [nat_ind_Fixpoint_tac P H0 HS n := nat_ind_Fixpoint_tac P H0 HS n], ktÃ³ry
    prÃ³buje wywoÅ‚aÄ‡ siÄ™ rekurencyjnie na tym samym argumencie, na ktÃ³rym sam
    zostaÅ‚ wywoÅ‚any. Nie jest wiÄ™c legalny.

    JeÅ¼eli z wywoÅ‚aniami rekurencyjnymi jest wszystko ok, to komenda [Guarded]
    wyÅ›wietla przyjazny komunikat. Tak wÅ‚aÅ›nie jest, gdy uÅ¼ywamy jej po raz
    drugi â€” tym razem wywoÅ‚anie rekurencyjne odbywa siÄ™ na [n'], ktÃ³re jest
    podtermem [n]. *)

Definition nat_ind_fix_tac :
  forall (P : nat -> Prop) (H0 : P 0)
    (HS : forall n : nat, P n -> P (S n)) (n : nat), P n.
Proof.
  Show Proof.
  (* ===> ?Goal *)
  fix IH 4.
  Show Proof.
  (* ===> (fix nat_ind_fix_tac
               (P : nat -> Prop) (H0 : P 0)
               (HS : forall n : nat, P n -> P (S n))
               (n : nat) {struct n} : P n := ... *)
 destruct n as [| n'].
    apply H0.
    apply HS. apply IH; assumption.
Defined.

(** Taktyki [fix] moÅ¼emy uÅ¼yÄ‡ w dowolnym momencie, aby rozpoczÄ…Ä‡ dowodzenie/
    definiowanie bezpoÅ›rednio przez rekursjÄ™. Jej argumentami sÄ… nazwa, ktÃ³rÄ…
    chcemy nadaÄ‡ hipotezie indukcyjnej oraz numer argument gÅ‚Ã³wnego. W
    powyÅ¼szym przykÅ‚adzie chcemy robiÄ‡ rekursjÄ™ po [n], ktÃ³ry jest czwarty
    z kolei (po [P], [H0] i [HS]).

    Komenda [Show Proof] pozwala nam odkryÄ‡, Å¼e uÅ¼ycie taktyki [fix] w
    trybie dowodzenia odpowiada po prostu uÅ¼yciu konstruktu [fix] lub
    komendy [Fixpoint].

    Taktyka [fix] jest bardzo prymitywna i prawie nigdy nie jest uÅ¼ywana,
    tak samo jak konstrukt [fix] (najbardziej porÄ™czne sÄ… sposoby, ktÃ³re
    widzieliÅ›my w przykladach 2 i 3), ale byÅ‚a dobrym pretekstem, Å¼eby
    omÃ³wiÄ‡ wszystkie sposoby uÅ¼ycia rekursji w jednym miejscu. *)

(** ** [functional induction] i [functional inversion] *)

(** Taktyki [functional induction] i [functional inversion] sÄ… zwiÄ…zane z
    pojÄ™ciem indukcji funkcyjnej. DoÅ›Ä‡ szczegÃ³Å‚owy opis tej pierwszej jest
    w notatkach na seminarium: https://zeimer.github.io/Seminar.html##lab247

    DrugÄ… z nich pÃ³ki co pominiemy. KiedyÅ› z pewnoÅ›ciÄ… napiszÄ™ coÅ› wiÄ™cej
    o indukcji funkcyjnej lub chociaÅ¼ przetÅ‚umaczÄ™ zalinkowane notatki na
    polski. *)

(** ** [generalize dependent] *)

(** [generalize dependent] to taktyka bÄ™dÄ…ca przeciwieÅ„stwem [intro] â€” dziÄ™ki
    niej moÅ¼emy przerzuciÄ‡ rzeczy znajdujÄ…ce siÄ™ w kontekÅ›cie z powrotem do
    kontekstu. Nieformalnie odpowiada ona sposobowi rozumowania: aby pokazaÄ‡,
    Å¼e cel zachodzi dla pewnego konkretnego [x], wystarczy czy pokazaÄ‡, Å¼e
    zachodzi dla dowolnego [x].

    W rozumowaniu tym z twierdzenia bardziej ogÃ³lnego wyciÄ…gamy wniosek, Å¼e
    zachodzi twierdzenie bardziej szczegÃ³Å‚owe. Nazwa [generalize] bierze siÄ™
    stÄ…d, Å¼e w dedukcji naturalnej nasze rozumowania przeprowadzamy "od tyÅ‚u".
    CzÅ‚on "dependent" bierze siÄ™ stÄ…d, Å¼e Å¼eby zgeneralizowaÄ‡ [x], musimy
    najpierw zgeneralizowaÄ‡ wszystkie obiekty, ktÃ³re sÄ… od niego zaleÅ¼ne. Na
    szczÄ™Å›cie taktyka [generalize dependent] robi to za nas. *)

Example generalize_dependent_0 :
  forall n m : nat, n = m -> m = n.
Proof.
  intros. generalize dependent n.
Abort.

(** UÅ¼ycie [intros] wprowadza do kontekstu [n], [m] i [H]. [generalize
    dependent n] przenosi [n] z powrotem do celu, ale wymaga to, aby do
    celu przenieÅ›Ä‡ rÃ³wnieÅ¼ [H], gdyÅ¼ typ [H], czyli [n = m], zaleÅ¼y od [n]. *)

(** **** Ä†wiczenie (generalize i revert) *)

(** [generalize dependent] jest wariantem taktyki [generalize]. TaktykÄ… o
    niemal identycznym dziaÅ‚aniu jest [revert dependent], wariant taktyki
    [revert]. Przeczytaj dokumentacjÄ™ [generalize] i [revert] w manualu i
    sprawdÅº, jak dziaÅ‚ajÄ…. *)

(** **** Ä†wiczenie (my_rec) *)

(** Zaimplementuj taktykÄ™ [rec x], ktÃ³ra bÄ™dzie pomagaÅ‚a przy dowodzeniu
    bezpoÅ›rednio przez rekursjÄ™ po [x]. Taktyka [rec x] ma dziaÅ‚aÄ‡ jak
    [fix IH n; destruct x], gdzie [n] to pozycja argumentu [x] w celu. Twoja
    taktyka powinna dziaÅ‚aÄ‡ tak, Å¼eby poniÅ¼szy dowÃ³d zadziaÅ‚aÅ‚ bez potrzeby
    wprowadzania modyfikacji.

    WskazÃ³wka: poÅ‚Ä…cz taktyki [fix], [intros], [generalize dependent] i
    [destruct]. *)

(* begin hide *)
Ltac rec x :=
  intros until x; generalize dependent x; fix IH 1; destruct x.
(* end hide *)

Lemma plus_comm_rec :
  forall n : nat, n + 1 = S n.
Proof.
  rec n.
    reflexivity.
    cbn. f_equal. rewrite IH. reflexivity.
Qed.

(** * Taktyki dla rÃ³wnoÅ›ci i rÃ³wnowaÅ¼noÅ›ci *)

(** ** [reflexivity], [symmetry] i [transitivity] *)

Require Import Arith.

Example reflexivity_0 :
  forall n : nat, n <= n.
Proof. reflexivity. Qed.

(** Znasz juÅ¼ taktykÄ™ [reflexivity]. MogÅ‚oby siÄ™ wydawaÄ‡, Å¼e sÅ‚uÅ¼y ona do
    udowadniania celÃ³w postaci [x = x] i jest w zasadzie rÃ³wnowaÅ¼na taktyce
    [apply eq_refl], ale nie jest tak. Taktyka [reflexivity] potrafi rozwiÄ…zaÄ‡
    kaÅ¼dy cel postaci [R x y], gdzie [R] jest relacjÄ… zwrotnÄ…, a [x] i [y] sÄ…
    konwertowalne (oczywiÅ›cie pod warunkiem, Å¼e udowodnimy wczeÅ›niej, Å¼e [R]
    faktycznie jest zwrotna; w powyÅ¼szym przykÅ‚adzie odpowiedni fakt zostaÅ‚
    zaimportowany z moduÅ‚u [Arith]).

    Å»eby zilustrowaÄ‡ ten fakt, zdefiniujmy nowÄ… relacjÄ™ zwrotnÄ… i zobaczmy,
    jak uÅ¼yÄ‡ taktyki [reflexivity] do radzenia sobie z niÄ…. *)

Definition eq_ext {A B : Type} (f g : A -> B) : Prop :=
  forall x : A, f x = g x.

(** W tym celu definiujemy relacjÄ™ [eq_ext], ktÃ³ra gÅ‚osi, Å¼e funkcja
    [f : A -> B] jest w relacji z funkcjÄ… [g : A -> B], jeÅ¼eli [f x]
    jest rÃ³wne [g x] dla dowolnego [x : A]. *)

Require Import RelationClasses.

(** ModuÅ‚ [RelationClasses] zawiera definicjÄ™ zwrotnoÅ›ci [Reflexive], z ktÃ³rej
    korzysta taktyka [reflexivity]. JeÅ¼eli udowodnimy odpowiednie twierdzenie,
    bÄ™dziemy mogli uÅ¼ywaÄ‡ taktyki [reflexivity] z relacjÄ… [eq_ext]. *)

Instance Reflexive_eq_ext :
  forall A B : Type, Reflexive (@eq_ext A B).
Proof.
  unfold Reflexive, eq_ext. intros A B f x. reflexivity.
Defined.

(** A oto i rzeczone twierdzenie oraz jego dowÃ³d. ZauwaÅ¼my, Å¼e taktyki
    [reflexivity] nie uÅ¼ywamy tutaj z relacjÄ… [eq_ext], a z relacjÄ… [=],
    gdyÅ¼ uÅ¼ywamy jej na celu postaci [f x = f x].

    Uwaga: Å¼eby taktyka [reflexivity] "widziaÅ‚a" ten dowÃ³d, musimy skorzystaÄ‡
    ze sÅ‚owa kluczowego [#[refine]
Instance] zamiast z [Theorem] lub [Lemma]. *)

Example reflexivity_1 :
  eq_ext (fun _ : nat => 42) (fun _ : nat => 21 + 21).
Proof. reflexivity. Defined.

(** VoilÃ ! Od teraz moÅ¼emy uÅ¼ywaÄ‡ taktyki [reflexivity] z relacjÄ… [eq_ext].

    SÄ… jeszcze dwie taktyki, ktÃ³re czasem przydajÄ… siÄ™ przy dowodzeniu
    rÃ³wnoÅ›ci (oraz rÃ³wnowaÅ¼noÅ›ci). *)

Example symmetry_transitivity_0 :
  forall (A : Type) (x y z : nat), x = y -> y = z -> z = x.
Proof.
  intros. symmetry. transitivity y.
    assumption.
    assumption.
Qed.

(** MogÅ‚oby siÄ™ wydawaÄ‡, Å¼e taktyka [symmetry] zamienia cel postaci [x = y]
    na [y = x], zaÅ› taktyka [transitivity y] rozwiÄ…zuje cel postaci [x = z]
    i generuje w zamian dwa cele postaci [x = y] i [y = z]. RzeczywistoÅ›Ä‡
    jest jednak bardziej hojna: podobnie jak w przypadku [reflexivity],
    taktyki te dziaÅ‚ajÄ… z dowolnymi relacjami symetrycznymi i przechodnimi. *)

Instance Symmetric_eq_ext :
  forall A B : Type, Symmetric (@eq_ext A B).
Proof.
  unfold Symmetric, eq_ext. intros A B f g H x. symmetry. apply H.
Defined.

Instance Transitive_eq_ext :
  forall A B : Type, Transitive (@eq_ext A B).
Proof.
  unfold Transitive, eq_ext. intros A B f g h H H' x.
  transitivity (g x); [apply H | apply H'].
Defined.

(** UÅ¼ycie w dowodach taktyk [symmetry] i [transitivity] jest legalne, gdyÅ¼
    nie uÅ¼ywamy ich z relacjÄ… [eq_ext], a z relacjÄ… [=]. *)

Example symmetry_transitivity_1 :
  forall (A B : Type) (f g h : A -> B),
    eq_ext f g -> eq_ext g h -> eq_ext h f.
Proof.
  intros. symmetry. transitivity g.
    assumption.
    assumption.
Qed.

(** DziÄ™ki powyÅ¼szym twierdzeniom moÅ¼emy teraz posÅ‚ugiwaÄ‡ siÄ™ taktykami
    [symmetry] i [transitivity] dowodzÄ…c faktÃ³w na temat relacji [eq_ext].
    To jednak wciÄ…Å¼ nie wyczerpuje naszego arsenaÅ‚u taktyk do radzenia sobie
    z relacjami rÃ³wnowaÅ¼noÅ›ci. *)

(** ** [f_equal] *)

Check f_equal.
(* ===> f_equal : forall (A B : Type) (f : A -> B) (x y : A),
                    x = y -> f x = f y *)

(** [f_equal] to jedna z podstawowych wÅ‚aÅ›ciwoÅ›ci relacji [eq], ktÃ³ra gÅ‚osi,
    Å¼e wszystkie funkcje zachowujÄ… rÃ³wnoÅ›Ä‡. Innymi sÅ‚owy: aby pokazaÄ‡, Å¼e
    wartoÅ›ci zwracane przez funkcjÄ™ sÄ… rÃ³wne, wystarczy pokazaÄ‡, Å¼e argumenty
    sÄ… rÃ³wne. Ten sposÃ³b rozumowania, choÄ‡ nie jest ani jedyny, ani skuteczny
    na wszystkie cele postaci [f x = f y], jest wystarczajÄ…co czÄ™sty, aby mieÄ‡
    swojÄ… wÅ‚asnÄ… taktykÄ™, ktÃ³rÄ… zresztÄ… powinieneÅ› juÅ¼ dobrze znaÄ‡ â€” jest niÄ…
    [f_equal].

    Taktyka ta sprowadza siÄ™ w zasadzie do jak najsprytniejszego aplikowania
    faktu [f_equal]. Nie potrafi ona wprowadzaÄ‡ zmiennych do kontekstu, a z
    wygenerowanych przez siebie podcelÃ³w rozwiÄ…zuje jedynie te postaci [x = x],
    ale nie potrafi rozwiÄ…zaÄ‡ tych, ktÃ³re zachodzÄ… na mocy zaÅ‚oÅ¼enia. *)

(** **** Ä†wiczenie (my_f_equal) *)

(** Napisz taktykÄ™ [my_f_equal], ktÃ³ra dziaÅ‚a jak [f_equal] na sterydach, tj.
    poza standardowÄ… funkcjonalnoÅ›ciÄ… [f_equal] potrafi teÅ¼ wprowadzaÄ‡ zmienne
    do kontekstu oraz rozwiÄ…zywaÄ‡ cele prawdziwe na mocy zaÅ‚oÅ¼enia.

    UÅ¼yj tylko jednej klauzuli [match]a. Nie uÅ¼ywaj taktyki [subst]. Bonus:
    wykorzystaj kombinator [first], ale nie wciskaj go na siÅ‚Ä™. Z czego
    Å‚atwiej jest skorzystaÄ‡: rekursji czy iteracji? *)

(* begin hide *)
(* Odp: Å‚atwiejsza jest iteracja. *)
Ltac my_f_equal := intros; repeat (try
match goal with
    | |- ?f ?x = ?g ?y =>
        let H1 := fresh "H" in
        let H2 := fresh "H" in
          assert (H1 : f = g); assert (H2 : x = y);
          rewrite ?H1, ?H2
end; first [reflexivity | assumption | idtac]).
(* end hide *)

Example f_equal_0 :
  forall (A : Type) (x : A), x = x.
Proof.
  intros. f_equal.
  (* Nie dziaÅ‚a, bo [x = x] nie jest podcelem
     wygenerowanym przez [f_equal]. *)
Restart.
  my_f_equal.
Qed.

Example f_equal_1 :
  forall (A : Type) (x y : A), x = y -> x = y.
Proof.
  intros. f_equal.
Restart.
  my_f_equal.
Qed.

Example f_equal_2 :
  forall (A B C D E : Type) (f f' : A -> B -> C -> D -> E)
    (a a' : A) (b b' : B) (c c' : C) (d d' : D),
      f = f' -> a = a' -> b = b' -> c = c' -> d = d' ->
        f a b c d = f' a' b' c' d'.
Proof.
  intros. f_equal. all: assumption.
Restart.
  my_f_equal.
Qed.

(** **** Ä†wiczenie (wÅ‚aÅ›ciwoÅ›ci [f_equal]) *)

(** Przyjrzyj siÄ™ definicjom [f_equal], [id], [compose], [eq_sym], [eq_trans],
    a nastÄ™pnie udowodnij poniÅ¼sze lematy. Ich sens na razie niech pozostanie
    ukryty â€” kiedyÅ› byÄ‡ moÅ¼e napiszÄ™ coÅ› na ten temat. JeÅ¼eli intrygujÄ… ciÄ™
    one, przyjrzyj siÄ™ ksiÄ…Å¼ce https://homotopytypetheory.org/book/ *)

Require Import Coq.Program.Basics.

Print f_equal.
Print eq_sym.
Print eq_trans.
Print compose.

Section f_equal_properties.

Variables
  (A B C : Type)
  (f : A -> B) (g : B -> C)
  (x y z : A)
  (p : x = y) (q : y = z).

Lemma f_equal_refl :
  f_equal f (eq_refl x) = eq_refl (f x).
(* begin hide *)
Proof. reflexivity. Qed.
(* end hide *)

Lemma f_equal_id :
  f_equal id p = p.
(* begin hide *)
Proof. destruct p. cbn. trivial. Qed.
(* end hide *)

Lemma f_equal_compose :
  f_equal g (f_equal f p) = f_equal (compose g f) p.
(* begin hide *)
Proof. destruct p. cbn. trivial. Qed.
(* end hide *)

Lemma eq_sym_map_distr :
  f_equal f (eq_sym p) = eq_sym (f_equal f p).
(* begin hide *)
Proof. destruct p. cbn. trivial. Qed.
(* end hide *)

Lemma eq_trans_map_distr :
  f_equal f (eq_trans p q) = eq_trans (f_equal f p) (f_equal f q).
(* begin hide *)
Proof. destruct p, q. cbn. trivial. Qed.
(* end hide *)

End f_equal_properties.

(** OstatniÄ… taktykÄ…, ktÃ³rÄ… poznamy w tym podrozdziale, jest [f_equiv], czyli
    pewne uogÃ³lnienie taktyki [f_equal]. Niech nie zmyli ciÄ™ nazwa tej taktyki
    â€” bynajmniej nie przydaje siÄ™ ona jedynie do rozumowaÅ„ dotyczÄ…cych relacji
    rÃ³wnowaÅ¼noÅ›ci. *)

Require Import Classes.Morphisms.

(** Aby mÃ³c uÅ¼ywaÄ‡ tej taktyki, musimy najpierw zaimportowaÄ‡ moduÅ‚
    [Classes.Morphisms]. *)

Definition len_eq {A : Type} (l1 l2 : list A) : Prop :=
  length l1 = length l2.

(** W naszym przykÅ‚adzie posÅ‚uÅ¼ymy siÄ™ relacjÄ… [len_eq], ktÃ³ra gÅ‚osi, Å¼e
    dwie listy sÄ… w relacji gdy majÄ… takÄ… samÄ… dÅ‚ugoÅ›Ä‡. *)

Instance Proper_len_eq_map {A : Type} :
  Proper (@len_eq A ==> @len_eq A ==> @len_eq A) (@app A).
Proof.
  Locate "==>".
  unfold Proper, respectful, len_eq.
  induction x as [| x xs]; destruct y; inversion 1; cbn; intros.
    assumption.
    f_equal. apply IHxs; assumption.
Qed.

(** Taktyka [f_equal] dziaÅ‚a na celach postaci [f x = f y], gdzie [f] jest
    dowolne, albowiem wszystkie funkcje zachowujÄ… rÃ³wnoÅ›Ä‡. Analogicznie
    taktyka [f_equiv] dziaÅ‚a na celach postaci [R (f x) (f y)], gdzie [R]
    jest dowolnÄ… relacjÄ…, ale tylko pod warunkiem, Å¼e funkcja [f] zachowuje
    relacjÄ™ [R].

    Musi tak byÄ‡, bo gdyby [f] nie zachowywaÅ‚a [R], to mogÅ‚oby jednoczeÅ›nie
    zachodziÄ‡ [R x y] oraz [~ R (f x) (f y)], a wtedy sposÃ³b rozumowania
    analogiczny do tego z twierdzenia [f_equal] byÅ‚by niepoprawny.

    Aby taktyka [f_equiv] "widziaÅ‚a", Å¼e [f] zachowuje [R], musimy znÃ³w
    posÅ‚uÅ¼yÄ‡ siÄ™ komendÄ… [Instance] i uÅ¼yÄ‡ [Proper], ktÃ³re sÅ‚uÅ¼y do
    zwiÄ™zÅ‚ego wyraÅ¼ania, ktÃ³re konkretnie relacje i w jaki sposÃ³b zachowuje
    dana funkcja.

    W naszym przypadku bÄ™dziemy chcieli pokazaÄ‡, Å¼e jeÅ¼eli listy [l1] oraz
    [l1'] sÄ… w relacji [len_eq] (czyli majÄ… takÄ… samÄ… dÅ‚ugoÅ›Ä‡) i podobnie
    dla [l2] oraz [l2'], to wtedy konkatenacja [l1] i [l2] jest w relacji
    [len_eq] z konkatenacjÄ… [l1'] i [l2']. Ten wÅ‚aÅ›nie fakt jest wyraÅ¼any
    przez zapis [Proper (@len_eq A ==> @len_eq A ==> @len_eq A) (@app A)].

    NaleÅ¼y teÅ¼ zauwaÅ¼yÄ‡, Å¼e strzaÅ‚ka [==>] jest jedynie notacjÄ… dla tworu
    zwanego [respectful], co moÅ¼emy Å‚atwo sprawdziÄ‡ komendÄ… [Locate.] *)

Example f_equiv_0 :
  forall (A B : Type) (f : A -> B) (l1 l1' l2 l2' : list A),
    len_eq l1 l1' -> len_eq l2 l2' ->
      len_eq (l1 ++ l2) (l1' ++ l2').
Proof.
  intros. f_equiv.
    assumption.
    assumption.
Qed.

(** VoilÃ ! Teraz moÅ¼emy uÅ¼ywaÄ‡ taktyki [f_equiv] z relacjÄ… [len_eq] oraz
    funkcjÄ… [app] dokÅ‚adnie tak, jak taktyki [f_equal] z rÃ³wnoÅ›ciÄ… oraz
    dowolnÄ… funkcjÄ….

    Trzeba przyznaÄ‡, Å¼e prÃ³ba uÅ¼ycia [f_equiv] z rÃ³Å¼nymi kombinacjami
    relacji i funkcji moÅ¼e zakoÅ„czyÄ‡ siÄ™ nagÅ‚ym i niekontrolowanym
    rozmnoÅ¼eniem lematÃ³w mÃ³wiÄ…cych o tym, Å¼e funkcje zachowujÄ… relacje.
    Niestety, nie ma na to Å¼adnego sposobu â€” jak przekonaliÅ›my siÄ™ wyÅ¼ej,
    udowodnienie takiego lematu to jedyny sposÃ³b, aby upewniÄ‡ siÄ™, Å¼e nasz
    sposÃ³b rozumowania jest poprawny. *)

(** **** Ä†wiczenie (f_equiv_filter) *)

Require Import List.
Import ListNotations.

Definition stupid_id {A : Type} (l : list A) : list A :=
  filter (fun _ => true) l.

(** Oto niezbyt mÄ…dry sposÃ³b na zapisanie funkcji identycznoÅ›ciowej na
    listach typu [A]. PokaÅ¼, Å¼e [stupid_id] zachowuje relacjÄ™ [len_eq],
    tak aby poniÅ¼szy dowÃ³d zadziaÅ‚aÅ‚ bez wpowadzania zmian. *)

(* begin hide *)
Instance Proper_len_eq_stupid_id {A : Type} :
  Proper (@len_eq A ==> @len_eq A) (@stupid_id A).
Proof.
  unfold Proper, respectful, len_eq.
  induction x as [| x xs]; destruct y; inversion 1; cbn; intros.
    trivial.
    f_equal. apply (IHxs _ H1).
Qed.
(* end hide *)

Example f_equiv_1 :
  forall (A : Type) (l l' : list A),
    len_eq l l' -> len_eq (stupid_id l) (stupid_id l').
Proof.
  intros. f_equiv. assumption.
Qed.

(** ** [rewrite] *)

(** PowinieneÅ› byÄ‡ juÅ¼ nieÅºle wprawiony w uÅ¼ywaniu taktyki [rewrite]. Czas
    najwyÅ¼szy wiÄ™c opisaÄ‡ wszystkie jej moÅ¼liwoÅ›ci.

    Podstawowe wywoÅ‚anie tej taktyki ma postaÄ‡ [rewrite H], gdzie [H] jest
    typu [forall (x_1 : A_1) ... (x_n : A_n), R t_1 t_2], zaÅ› [R] to [eq]
    lub dowolna relacja rÃ³wnowaÅ¼noÅ›ci. Przypomnijmy, Å¼e relacja rÃ³wnowaÅ¼noÅ›ci
    to relacja, ktÃ³ra jest zwrotna, symetryczna i przechodnia.

    [rewrite H] znajduje pierwszy podterm celu, ktÃ³ry pasuje do [t_1] i
    zamienia go na [t_2], generujÄ…c podcele [A_1], ..., [A_n], z ktÃ³rych
    czÄ™Å›Ä‡ (a czÄ™sto caÅ‚oÅ›Ä‡) jest rozwiÄ…zywana automatycznie. *)

Check plus_n_Sm.
(* ===> plus_n_Sm :
          forall n m : nat, S (n + m) = n + S m *)

Goal 2 + 3 = 6 -> 4 + 4 = 42.
Proof.
  intro.
  rewrite <- plus_n_Sm.
  rewrite plus_n_Sm.
  rewrite <- plus_n_Sm.
  rewrite -> plus_n_Sm.
  rewrite <- !plus_n_Sm.
  Fail rewrite <- !plus_n_Sm.
  rewrite <- ?plus_n_Sm.
  rewrite 4!plus_n_Sm.
  rewrite <- 3?plus_n_Sm.
  rewrite 2 plus_n_Sm.
Abort.

(** PowyÅ¼szy skrajnie bezsensowny przykÅ‚ad ilustruje fakt, Å¼e dziaÅ‚anie
    taktyki [rewrite] moÅ¼emy zmieniaÄ‡, poprzedzajÄ…c hipotezÄ™ [H] nastÄ™pujÄ…cymi
    modyfikatorami:
    - [rewrite -> H] oznacza to samo, co [rewrite H]
    - [rewrite <- H] zamienia pierwsze wystÄ…pienie [t_2] na [t_1], czyli
      przepisuje z prawa na lewo
    - [rewrite ?H] przepisuje [H] 0 lub wiÄ™cej razy
    - [rewrite n?H] przepisuje [H] co najwyÅ¼ej n razy
    - [rewrite !H] przepisuje [H] 1 lub wiÄ™cej razy
    - [rewrite n!H] lub [rewrite n H] przepisuje [H] dokÅ‚adnie n razy *)

(** ZauwaÅ¼my, Å¼e modyfikator [<-] moÅ¼na Å‚Ä…czyÄ‡ z modyfikatorami okreÅ›lajÄ…cymi
    iloÅ›Ä‡ przepisaÅ„. *)

Lemma rewrite_ex_1 :
  forall n m : nat, 42 = 42 -> S (n + m) = n + S m.
Proof.
  intros. apply plus_n_Sm.
Qed.

Goal 2 + 3 = 6 -> 5 + 5 = 12 -> (4 + 4) + ((5 + 5) + (6 + 6)) = 42.
Proof.
  intros.
  rewrite <- plus_n_Sm, <- plus_n_Sm.
  rewrite <- plus_n_Sm in H.
  rewrite <- plus_n_Sm in * |-.
  rewrite !plus_n_Sm in *.
  rewrite <- rewrite_ex_1. 2: reflexivity.
  rewrite <- rewrite_ex_1 by reflexivity.
Abort.

(** PozostaÅ‚e warianty taktyki [rewrite] przedstawiajÄ… siÄ™ nastÄ™pujÄ…co:
    - [rewrite H_1, ..., H_n] przepisuje kolejno hipotezy [H_1], ..., [H_n].
      KaÅ¼dÄ… z hipotez moÅ¼emy poprzedziÄ‡ osobnym zestawem modyfikatorÃ³w.
    - [rewrite H in H'] przepisuje [H] nie w celu, ale w hipotezie [H']
    - [rewrite H in * |-] przepisuje [H] we wszystkich hipotezach
      rÃ³Å¼nych od [H]
    - [rewrite H in *] przepisuje [H] we wszystkich hipotezach rÃ³Å¼nych
      od [H] oraz w celu
    - [rewrite H by tac] dziaÅ‚a jak [rewrite H], ale uÅ¼ywa taktyki [tac] do
      rozwiÄ…zania tych podcelÃ³w, ktÃ³re nie mogÅ‚y zostaÄ‡ rozwiÄ…zane
      automatycznie *)

(** Jest jeszcze wariant [rewrite H at n] (wymagajÄ…cy zaimportowania moduÅ‚u
    [Setoid]), ktÃ³ry zamienia n-te (liczÄ…c od lewej) wystÄ…pienie [t_1] na
    [t_2]. ZauwaÅ¼my, Å¼e [rewrite H] znaczy to samo, co [rewrite H at 1]. *)

(** * Taktyki dla redukcji i obliczeÅ„ (TODO) *)

(** * Procedury decyzyjne *)

(** Procedury decyzyjne to taktyki, ktÃ³re potrafiÄ… zupeÅ‚nie same rozwiÄ…zywaÄ‡
    cele naleÅ¼Ä…ce do pewnej konkretnej klasy, np. cele dotyczÄ…ce funkcji
    boolowskich albo nierÃ³wnoÅ›ci liniowych na liczbach caÅ‚kowitych. W tym
    podrozdziale omÃ³wimy najprzydatniejsze z nich. *)

(** ** [btauto] *)

(** [btauto] to taktyka, ktÃ³ra potrafi rozwiÄ…zywaÄ‡ rÃ³wnania boolowskie, czyli
    cele postaci [x = y], gdzie [x] i [y] sÄ… wyraÅ¼eniami mogÄ…cymi zawieraÄ‡
    boolowskie koniunkcje, dysjunkcje, negacje i inne rzeczy (patrz manual).

    TaktykÄ™ moÅ¼na zaimportowaÄ‡ komendÄ… [Require Import Btauto]. Uwaga: nie
    potrafi ona wprowadzaÄ‡ zmiennych do kontekstu. *)

(** **** Ä†wiczenie (my_btauto) *)

(** Napisz nastÄ™pujÄ…ce taktyki:
    - [my_btauto] â€” taktyka podobna do [btauto]. Potrafi rozwiÄ…zywaÄ‡ cele,
      ktÃ³re sÄ… kwantyfikowanymi rÃ³wnaniami na wyraÅ¼eniach boolowskich,
      skÅ‚adajÄ…cymi siÄ™ z dowolnych funkcji boolowskich (np. [andb], [orb]).
      W przeciwieÅ„stwie do [btauto] powinna umieÄ‡ wprowadzaÄ‡ zmienne do
      kontekstu.
    - [my_btauto_rec] â€” tak samo jak [my_btauto], ale bez uÅ¼ywana
      kombinatora [repeat]. MoÅ¼esz uÅ¼ywaÄ‡ jedynie rekurencji.
    - [my_btauto_iter] â€” tak samo jak [my_btauto], ale bez uÅ¼ywania
      rekurencji. MoÅ¼esz uÅ¼ywaÄ‡ jedynie kombinatora [repeat].
    - [my_btauto_no_intros] â€” tak samo jak [my_btauto], ale bez uÅ¼ywania
      taktyk [intro] oraz [intros]. *)

(** Uwaga: twoja implementacja taktyki [my_btauto] bÄ™dzie diametralnie rÃ³Å¼niÄ‡
    siÄ™ od implementacji taktyki [btauto] z biblioteki standardowej. [btauto]
    jest zaimplementowana za pomocÄ… reflekcji. DowÃ³d przez reflekcjÄ™ omÃ³wimy
    pÃ³Åºniej. *)

(* begin hide *)
Ltac my_btauto := intros; repeat
match goal with
    | b : bool |- _ => destruct b
end; cbn; reflexivity.

Ltac my_btauto_rec := intros;
match goal with
    | b : bool |- _ => destruct b; my_btauto_rec
    | _ => cbn; reflexivity
end.

Ltac my_btauto_iter := my_btauto.

Ltac my_btauto_no_intros := repeat
match goal with
    | |- forall b : bool, _ => destruct b
end; cbn; reflexivity.
(* end hide *)

Section my_btauto.

Require Import Bool.
Require Import Btauto.

Theorem andb_dist_orb :
  forall b1 b2 b3 : bool,
    b1 && (b2 || b3) = (b1 && b2) || (b1 && b3).
Proof.
  intros. btauto.
Restart.
  my_btauto.
Restart.
  my_btauto_rec.
Restart.
  my_btauto_iter.
Restart.
  my_btauto_no_intros.
Qed.

Theorem negb_if :
  forall b1 b2 b3 : bool,
    negb (if b1 then b2 else b3) = if negb b1 then negb b3 else negb b2.
Proof.
  intros. btauto.
Restart.
  my_btauto.
Restart.
  my_btauto_rec.
Restart.
  my_btauto_iter.
Restart.
  my_btauto_no_intros.
Qed.

(** Przetestuj dziaÅ‚anie swoich taktyk na reszcie twierdzeÅ„ z rozdziaÅ‚u
    o logice boolowskiej. *)

End my_btauto.

(** ** [congruence] *)

Example congruence_0 :
  forall P : Prop, true <> false.
Proof. congruence. Qed.

Example congruence_1 :
  forall (A : Type) (f : A -> A) (g : A -> A -> A) (a b : A),
    a = f a -> g b (f a) = f (f a) -> g a b = f (g b a) ->
      g a b = a.
Proof.
(* begin hide *)
  intros. rewrite H1, H at 1. rewrite H0, <- !H. trivial.
Restart.
(* end hide *)
  congruence.
Qed.

Example congruence_2 :
  forall (A : Type) (f : A -> A * A) (a c d : A),
    f = pair a -> Some (f c) = Some (f d) -> c = d.
Proof.
(* begin hide *)
  intros. inversion H0. rewrite H in H2. inversion H2. trivial.
Restart.
(* end hide *)
  congruence.
Qed.

(** [congruece] to taktyka, ktÃ³ra potrafi rozwiÄ…zywaÄ‡ cele dotyczÄ…ce
    nieinterpretowanych rÃ³wnoÅ›ci, czyli takie, ktÃ³rych prawdziwoÅ›Ä‡ zaleÅ¼y
    jedynie od hipotez postaci [x = y] i ktÃ³re moÅ¼na udowodniÄ‡ rÄ™cznie za
    pomocÄ… mniejszej lub wiÄ™kszej iloÅ›ci [rewrite]'Ã³w. [congruence] potrafi
    teÅ¼ rozwiÄ…zywaÄ‡ cele dotyczÄ…ce konstruktorÃ³w. W szczegÃ³lnoÅ›ci wie ona,
    Å¼e konstruktory sÄ… injektywne i potrafi odrÃ³Å¼niÄ‡ [true] od [false]. *)

(** **** Ä†wiczenie (congruence) *)

(** Udowodnij przykÅ‚ady [congruence_1] i [congruence_2] rÄ™cznie. *)

(** **** Ä†wiczenie (discriminate) *)

(** InnÄ… taktykÄ…, ktÃ³ra potrafi rozrÃ³Å¼niaÄ‡ konstruktory, jest [discriminate].
    Zbadaj, jak dziaÅ‚a ta taktyka. ZnajdÅº przykÅ‚ad celu, ktÃ³ry [discriminate]
    rozwiÄ…zuje, a na ktÃ³rym [congruence] zawodzi. WskazÃ³wka: [congruence]
    niebardzo potrafi odwijaÄ‡ definicje. *)

(* begin hide *)
Definition mytrue := true.

Goal ~ (mytrue = false).
Proof.
  Fail congruence.
  discriminate.
Qed.
(* end hide *)

(** **** Ä†wiczenie (injection i simplify_eq) *)

(** Kolejne dwie taktyki do walki z konstruktorami typÃ³w induktywnych to
    [injection] i [simplify_eq]. Przeczytaj ich opisy w manualu. Zbadaj,
    czy sÄ… one w jakikolwiek sposÃ³b przydatne (wskazÃ³wka: porÃ³wnaj je z
    taktykami [inversion] i [congruence]. *)

(** ** [decide equality] *)

Inductive C : Type :=
    | c0 : C
    | c1 : C -> C
    | c2 : C -> C -> C
    | c3 : C -> C -> C -> C.

(** Przyjrzyjmy siÄ™ powyÅ¼szemu, dosÄ‡ enigmatycznemu typowi. Czy posiada on
    rozstrzygalnÄ… rÃ³wnoÅ›Ä‡? OdpowiedÅº jest twierdzÄ…ca: rozstrzygalnÄ… rÃ³wnoÅ›Ä‡
    posiada kaÅ¼dy typ induktywny, ktÃ³rego konstruktory nie biorÄ… argumentÃ³w
    bÄ™dÄ…cych dowodami, funkcjami ani termami typÃ³w zaleÅ¼nych. *)

Theorem C_eq_dec :
  forall x y : C, {x = y} + {x <> y}.
(* begin hide *)
Proof.
  induction x.
    destruct y. left; trivial. 1-3: right; inversion 1.
    destruct y.
      Focus 2. destruct (IHx y); subst; auto. right. congruence.
      1-3: right; inversion 1.
    destruct y.
      Focus 3. destruct (IHx1 y1), (IHx2 y2); subst; auto.
        1-3: right; congruence.
      1-3: right; congruence.
    destruct y.
      Focus 4. destruct (IHx1 y1), (IHx2 y2), (IHx3 y3); subst; auto.
        1-7: right; congruence.
      1-3: right; congruence.
Restart.
  induction x; destruct y; try (right; congruence).
    left; trivial.
    destruct (IHx y); firstorder congruence.
(*    repeat match goal with
        | H : forall _, {_} + {_} |- _ =>
            let H' := fresh "H" in edestruct H as [H' | H']; clear H;
              [rewrite H'; clear H' | idtac]
    end; firstorder (try congruence).*)
    destruct (IHx1 y1), (IHx2 y2); firstorder congruence.
    destruct (IHx1 y1), (IHx2 y2), (IHx3 y3); firstorder congruence.
Restart.
  induction x; destruct y;
  repeat match goal with
      | H : forall _, {_} + {_} |- _ => edestruct H; clear H
      | H : _ = _ |- _ => rewrite H in *; clear H
  end; firstorder congruence.
  Unshelve. all: auto.
Defined.
(* end hide *)

(** Zanim przejdziesz dalej, udowodnij rÄ™cznie powyÅ¼sze twierdzenie. Przyznasz,
    Å¼e dowÃ³d nie jest zbyt przyjemny, prawda? Na szczÄ™Å›cie nie musimy robiÄ‡ go
    rÄ™cznie. Na ratunek przychodzi nam taktyka [decide equality], ktÃ³ra umie
    udowadniaÄ‡ cele postaci [forall x y : T, {x = y} + {x <> y}], gdzie [T]
    speÅ‚nia warunki wymienione powyÅ¼ej. *)

Theorem C_eq_dec' :
  forall x y : C, {x = y} + {x <> y}.
Proof. decide equality. Defined.

(** **** Ä†wiczenie *)

(** PokrewnÄ… taktyce [decide equality] jest taktyka [compare]. Przeczytaj
    w manualu, co robi i jak dziaÅ‚a. *)

(** ** [omega] *)

(** [omega] to taktyka, ktÃ³ra potrafi rozwiÄ…zywaÄ‡ cele dotyczÄ…ce arytmetyki
    Presburgera. Jej szerszy opis moÅ¼na znaleÅºÄ‡ w manualu. Na nasze potrzeby
    przez arytmetykÄ™ Presburgera moÅ¼emy rozumieÄ‡ rÃ³wnania ([=]), nie-rÃ³wnania
    ([<>]) oraz nierÃ³wnoÅ›ci ([<], [<=], [>], [>=]) na typie [nat], ktÃ³re mogÄ…
    zawieraÄ‡ zmienne, [0], [S], dodawanie i mnoÅ¼enie przez staÅ‚Ä…. Dodatkowo
    zdania tej postaci mogÄ… byÄ‡ poÅ‚Ä…czone spÃ³jnikami [/\], [\/], [->] oraz
    [~], ale nie mogÄ… byÄ‡ kwantyfikowane â€” [omega] nie umie wprowadzaÄ‡
    zmiennych do kontekstu.

    Uwaga: ta taktyka jest przestarzaÅ‚a, a jej opis znajduje siÄ™ tutaj tylko
    dlatego, Å¼e jak go pisaÅ‚em, to jeszcze nie byÅ‚a. Nie uÅ¼ywaj jej! Zamiast
    [omega] uÅ¼ywaj [lia]! *)

Require Import Arith Omega.

Example omega_0 :
  forall n : nat, n + n = 2 * n.
Proof. intro. omega. Qed.

Example omega_1 :
  forall n m : nat, 2 * n + 1 <> 2 * m.
Proof. intros. omega. Qed.

Example omega_2 :
  forall n m : nat, n * m = m * n.
Proof. intros. try omega. Abort.

(** JednÄ… z wad taktyki [omega] jest rozmiar generowanych przez niÄ… termÃ³w.
    SÄ… tak wielkie, Å¼e wszelkie prÃ³by rozwiniÄ™cia definicji czy dowodÃ³w,
    ktÃ³re zostaÅ‚y przy jej pomocy skonstruowane, muszÄ… z koniecznoÅ›ci Åºle
    siÄ™ skoÅ„czyÄ‡. Zobaczmy to na przykÅ‚adzie. *)

Lemma filter_length :
  forall (A : Type) (f : A -> bool) (l : list A),
    length (filter f l) <= length l.
Proof.
  induction l; cbn; try destruct (f a); cbn; omega.
Qed.

Print filter_length.
(* ===> Proofterm o dÅ‚ugoÅ›ci 317 linijek. *)

(** Oto jedna ze standardowych wÅ‚aÅ›ciwoÅ›ci list: filtrowanie nie zwiÄ™ksza
    jej rozmiaru. Term skonstruowany powyÅ¼szym dowodem, bÄ™dÄ…cy Å›wiadkiem
    tego faktu, liczy sobie 317 linijek dÅ‚ugoÅ›ci (po wypisaniu, wklejeniu
    do CoqIDE i usuniÄ™ciu tego, co do termu nie naleÅ¼y). *)

Lemma filter_length' :
  forall (A : Type) (f : A -> bool) (l : list A),
    length (filter f l) <= length l.
Proof.
  induction l; cbn; try destruct (f a); cbn.
    trivial.
    apply le_n_S. assumption.
    apply le_trans with (length l).
      assumption.
      apply le_S. apply le_n.
Qed.

Print filter_length'.
(* ===> Proofterm o dÅ‚ugoÅ›ci 14 linijek. *)

(** Jak widaÄ‡, rÄ™czny dowÃ³d tego faktu daje w wyniku proofterm, ktÃ³ry jest
    o ponad 300 linijek krÃ³tszy niÅ¼ ten wyprodukowany przez taktykÄ™ [omega].
    MogÅ‚oby siÄ™ zdawaÄ‡, Å¼e jesteÅ›my w sytuacji bez wyjÅ›cia: albo dowodzimy
    rÄ™cznie, albo prooftermy bÄ™dÄ… tak wielkie, Å¼e nie bÄ™dziemy mogli ich
    odwijaÄ‡. *)

(** ** Procedury decyzyjne dla logiki *)

Example tauto_0 :
  forall A B C D : Prop,
    ~ A \/ ~ B \/ ~ C \/ ~ D -> ~ (A /\ B /\ C /\ D).
Proof. tauto. Qed.

Example tauto_1 :
  forall (P : nat -> Prop) (n : nat),
    n = 0 \/ P n -> n <> 0 -> P n.
Proof. auto. tauto. Qed.

(** [tauto] to taktyka, ktÃ³ra potrafi udowodniÄ‡ kaÅ¼dÄ… tautologiÄ™
    konstruktywnego rachunku zdaÅ„. Taktyka ta radzi sobie takÅ¼e z niektÃ³rymi
    nieco bardziej skomplikowanymi celami, w tym takimi, ktÃ³rych nie potrafi
    udowodniÄ‡ [auto]. [tauto] zawodzi, gdy nie potrafi udowodniÄ‡ celu. *)

Example intuition_0 :
  forall (A : Prop) (P : nat -> Prop),
    A \/ (forall n : nat, ~ A -> P n) -> forall n : nat, ~ ~ (A \/ P n).
Proof.
  Fail tauto. intuition.
Qed.

(** [intuition] to [tauto] na sterydach â€” potrafi rozwiÄ…zaÄ‡ nieco wiÄ™cej
    celÃ³w, a poza tym nigdy nie zawodzi. JeÅ¼eli nie potrafi rozwiÄ…zaÄ‡ celu,
    upraszcza go.

    MoÅ¼e teÅ¼ przyjmowaÄ‡ argument: [intuition t] najpierw upraszcza cel, a
    pÃ³Åºniej prÃ³buje go rozwiÄ…zaÄ‡ taktykÄ… [t]. Tak naprawdÄ™ [tauto] jest
    jedynie synonimem dla [intuition fail], zaÅ› samo [intuition] to synonim
    [intuition auto with *], co teÅ¼ tÅ‚umaczy, dlaczego [intuition] potrafi
    wiÄ™cej niÅ¼ [tauto]. *)

Record and3 (P Q R : Prop) : Prop :=
{
    left : P;
    mid : Q;
    right : R;
}.

Example firstorder_0 :
  forall (B : Prop) (P : nat -> Prop),
    and3 (forall x : nat, P x) B B ->
      and3 (forall y : nat, P y) (P 0) (P 0) \/ B /\ P 0.
Proof.
  Fail tauto.
  intuition.
Restart.
  firstorder.
Qed.

Example firstorder_1 :
  forall (A : Type) (P : A -> Prop),
    (exists x : A, ~ P x) -> ~ forall x : A, P x.
Proof.
  Fail tauto. intuition.
Restart.
  firstorder.
Qed.

(** Jednak nawet [intuition] nie jest w stanie sprostaÄ‡ niektÃ³rym prostym
    dla czÅ‚owieka celom â€” powyÅ¼szy przykÅ‚ad pokazuje, Å¼e nie potrafi ona
    posÅ‚ugiwaÄ‡ siÄ™ niestandardowymi spÃ³jnikami logicznymi, takimi jak
    potrÃ³jna koniunkcja [and3].

    NajpotÄ™Å¼niejszÄ… taktykÄ… potrafiÄ…cÄ… dowodziÄ‡ tautologii jest [firstorder].
    Nie tylko rozumie ona niestandardowe spÃ³jniki (co i tak nie ma wiÄ™kszego
    praktycznego znaczenia), ale teÅ¼ Å›wietnie radzi sobie z kwantyfikatorami.
    Drugi z powyÅ¼szych przykÅ‚adÃ³w pokazuje, Å¼e potrafi ona dowodziÄ‡ tautologii
    konstruktywnego rachunku predykatÃ³w, z ktÃ³rymi problem ma [intuition]. *)

(** **** Ä†wiczenie (my_tauto) *)

(** Napisz taktykÄ™ [my_tauto], ktÃ³ra bÄ™dzie potrafiÅ‚a rozwiÄ…zaÄ‡ jak najwiÄ™cej
    tautologii konstruktywnego rachunku zdaÅ„.

    WskazÃ³wka: poÅ‚Ä…cz taktyki z poprzednich Ä‡wiczeÅ„. Przetestuj swojÄ… taktykÄ™
    na Ä‡wiczeniach z rozdziaÅ‚u pierwszego â€” byÄ‡ moÅ¼e ujawni to problemy, o
    ktÃ³rych nie pomyÅ›laÅ‚eÅ›.

    Nie uÅ¼ywaj Å¼adnej zaawansowanej automatyzacji. UÅ¼yj jedynie [unfold],
    [intro], [repeat], [match], [destruct], [clear], [exact], [split],
    [specialize] i [apply]. *)

(* begin hide *)
Ltac my_tauto :=
  unfold not in *; repeat
multimatch goal with
    | H : ?P |- ?P => exact H
    | H : False |- _ => destruct H
    | H : True |- _ => clear H
    | |- True => exact I
    | H : _ /\ _ |- _ => destruct H
    | |- _ /\ _ => split
    | H : _ <-> _ |- _ => destruct H
    | |- _ <-> _ => split
    | H : _ \/ _ |- _ => destruct H
    | |- _ \/ _ => (left + right); my_tauto; fail
    | |- _ -> _ => intro
    | H : ?P -> ?Q, H' : ?P |- _ => specialize (H H')
    | H : _ -> _ |- _ => apply H
end.

Section my_tauto.

Hypotheses P Q R S : Prop.

Theorem and_comm : P /\ Q -> Q /\ P.
Proof. my_tauto. Qed.

Theorem or_comm : P \/ Q -> Q \/ P.
Proof. my_tauto. Qed.

Theorem and_assoc : P /\ (Q /\ R) <-> (P /\ Q) /\ R.
Proof. my_tauto. Qed.

Theorem or_assoc : P \/ (Q \/ R) <-> (P \/ Q) \/ R.
Proof. my_tauto. Qed.

Theorem and_dist_or : P /\ (Q \/ R) <-> (P /\ Q) \/ (P /\ R).
Proof. my_tauto. Qed.

Theorem or_dist_and : P \/ (Q /\ R) <-> (P \/ Q) /\ (P \/ R).
Proof. my_tauto. Qed.

Theorem imp_dist_imp : (P -> Q -> R) <-> ((P -> Q) -> (P -> R)).
Proof. my_tauto. Qed.

Theorem curry : (P /\ Q -> R) -> (P -> Q -> R).
Proof. intros. assert (P /\ Q). my_tauto. my_tauto. Qed.

Theorem uncurry : (P -> Q -> R) -> (P /\ Q -> R).
Proof. my_tauto. Qed.

Theorem deMorgan_1 : ~(P \/ Q) <-> ~P /\ ~Q.
Proof. my_tauto. Qed.

Theorem deMorgan_2 : ~P \/ ~Q -> ~(P /\ Q).
Proof. my_tauto. Qed.

Theorem noncontradiction' : ~(P /\ ~P).
Proof. my_tauto. Qed.

Theorem noncontradiction_v2 : ~(P <-> ~P).
Proof. my_tauto. Qed.

Theorem em_irrefutable : ~~(P \/ ~P).
Proof. my_tauto. Qed.

Theorem and_false_annihilation : P /\ False <-> False.
Proof. my_tauto. Qed.

Theorem or_false_neutral : P \/ False <-> P.
Proof. my_tauto. Qed.

Theorem and_true_neutral : P /\ True <-> P.
Proof. my_tauto. Qed.

Theorem or_true_annihilation : P \/ True <-> True.
Proof. my_tauto. Qed.

Theorem or_imp_and : (P \/ Q -> R) <-> (P -> R) /\ (Q -> R).
Proof. my_tauto. Qed.

Theorem and_not_imp : P /\ ~Q -> ~(P -> Q).
Proof. my_tauto. Qed.

Theorem or_not_imp : ~P \/ Q -> (P -> Q).
Proof. my_tauto. Qed.

Theorem contraposition : (P -> Q) -> (~Q -> ~P).
Proof. my_tauto. Qed.

Theorem absurd : ~P -> P -> Q.
Proof. my_tauto. Qed.

Theorem impl_and : (P -> Q /\ R) -> ((P -> Q) /\ (P -> R)).
Proof. my_tauto. Qed.

End my_tauto.
(* end hide *)

(** * OgÃ³lne taktyki automatyzacyjne *)

(** W tym podrozdziale omÃ³wimy pozostaÅ‚e taktyki przydajÄ…ce siÄ™ przy
    automatyzacji. Ich cechÄ… wspÃ³lnÄ… jest rozszerzalnoÅ›Ä‡ â€” za pomocÄ…
    specjalnych baz podpowiedzi bÄ™dziemy mogli nauczyÄ‡ je radziÄ‡ sobie
    z kaÅ¼dym celem. *)

(** ** [auto] i [trivial] *)

(** [auto] jest najbardziej ogÃ³lnÄ… taktykÄ… sÅ‚uÅ¼Ä…cÄ… do automatyzacji. *)

Example auto_ex0 :
  forall (P : Prop), P -> P.
Proof. auto. Qed.

Example auto_ex1 :
  forall A B C D E : Prop,
    (A -> B) -> (B -> C) -> (C -> D) -> (D -> E) -> A -> E.
Proof. auto. Qed.

Example auto_ex2 :
  forall (A : Type) (x : A), x = x.
Proof. auto. Qed.

Example auto_ex3 :
  forall (A : Type) (x y : A), x = y -> y = x.
Proof. auto. Qed.

(** [auto] potrafi uÅ¼ywaÄ‡ zaÅ‚oÅ¼eÅ„, aplikowaÄ‡ hipotezy i zna podstawowe
    wÅ‚asnoÅ›ci rÃ³wnoÅ›ci â€” caÅ‚kiem nieÅºle. Wprawdzie nie wystarczy to do
    udowodnienia Å¼adnego nietrywialnego twierdzenia, ale przyda siÄ™ z
    pewnoÅ›ciÄ… do rozwiÄ…zywania prostych podcelÃ³w generowanych przez
    inne taktyki. CzÄ™sto spotykanym idiomem jest [t; auto] â€” "uÅ¼yj
    taktyki [t] i pozbÄ…dÅº siÄ™ prostych podcelÃ³w za pomocÄ… [auto]". *)

Section auto_ex4.

Parameter P : Prop.
Parameter p : P.

Example auto_ex4 : P.
Proof.
  auto.
Restart.
  auto using p.
Qed.

(** Jak widaÄ‡ na powyÅ¼szym przykÅ‚adzie, [auto] nie widzi aksjomatÃ³w (ani
    definicji/lematÃ³w/twierdzeÅ„ etc.), nawet jeÅ¼eli zostaÅ‚y zadeklarowane
    dwie linijki wyÅ¼ej. Tej przykrej sytuacji moÅ¼emy jednak Å‚atwo zaradziÄ‡,
    piszÄ…c [auto using t_1, ..., t_n]. Ten wariant taktyki [auto]
    widzi definicje termÃ³w [t_1], ..., [t_n].

    Co jednak w sytuacji, gdy bÄ™dziemy wielokrotnie chcieli, Å¼eby [auto]
    widziaÅ‚o pewne definicje? Nietrudno wyobraziÄ‡ sobie ogrom pisaniny,
    ktÃ³rÄ… mogÅ‚oby spowodowaÄ‡ uÅ¼ycie do tego celu klauzuli [using]. Na
    szczÄ™Å›cie moÅ¼emy temu zaradziÄ‡ za pomocÄ… podpowiedzi, ktÃ³re bytujÄ…
    w specjalnych bazach. *)

Hint Resolve p : my_hint_db.

Example auto_ex4' : P.
Proof. auto with my_hint_db. Qed.

(** Komenda [Hint Resolve ident : db_name] dodaje lemat o nazwie [ident]
    do bazy podpowiedzi o nazwie [db_name]. DziÄ™ki temu taktyka [auto with
    db_1 ... db_n] widzi wszystkie lematy dodane do baz [db_1], ..., [db_n].
    JeÅ¼eli to dla ciebie wciÄ…Å¼ zbyt wiele pisania, uszy do gÃ³ry! *)

Example auto_ex4'' : P.
Proof. auto with *. Qed.

(** Taktyka [auto with *] widzi wszystkie moÅ¼liwe bazy podpowiedzi. *)

Hint Resolve p.

Example auto_ex4''' : P.
Proof. auto. Qed.

(** Komenda [Hint Resolve ident] dodaje lemat o nazwie [ident] do bazy
    podpowiedzi o nazwie [core]. Taktyka [auto] jest zaÅ› rÃ³wnowaÅ¼na
    taktyce [auto with core]. DziÄ™ki temu nie musimy pisaÄ‡ juÅ¼ nic ponad
    zwykÅ‚e [auto]. *)

End auto_ex4.

(** Tym oto sposobem, uÅ¼ywajÄ…c komendy [Hint Resolve], jesteÅ›my w stanie
    zaznajomiÄ‡ [auto] z rÃ³Å¼nej maÅ›ci lematami i twierdzeniami, ktÃ³re
    udowodniliÅ›my. Komendy tej moÅ¼emy uÅ¼ywaÄ‡ po kaÅ¼dym lemacie, dziÄ™ki
    czemu taktyka [auto] roÅ›nie w siÅ‚Ä™ w miarÄ™ rozwoju naszej teorii. *)

Example auto_ex5 : even 8.
Proof.
  auto.
Restart.
  auto using even0, evenSS.
Qed.

(** KolejnÄ… sÅ‚aboÅ›ciÄ… [auto] jest fakt, Å¼e taktyka ta nie potrafi budowaÄ‡
    wartoÅ›ci typÃ³w induktywnych. Na szczÄ™Å›cie moÅ¼emy temu zaradziÄ‡ uÅ¼ywajÄ…c
    klauzuli [using c_1 ... c_n], gdzie [c_1], ..., [c_n] sÄ… konstruktorami
    naszego typu, lub dodajÄ…c je jako podpowiedzi za pomocÄ… komendy [Hint
    Resolve c_1 ... c_n : db_name]. *)

Hint Constructors even.

Example auto_ex5' : even 8.
Proof. auto. Qed.

(** Å»eby jednak za duÅ¼o nie pisaÄ‡ (wypisanie nazw wszystkich konstruktorÃ³w
    mogÅ‚oby byÄ‡ bolesne), moÅ¼emy posÅ‚uÅ¼yÄ‡ siÄ™ komendÄ… [Hint Constructors
    I : db_name], ktÃ³ra dodaje konstruktory typu induktywnego [I] do bazy
    podpowiedzi [db_name]. *)

Example auto_ex6 : even 10.
Proof.
  auto.
Restart.
  auto 6.
Qed.

(** Kolejnym celem, wobec ktÃ³rego [auto] jest bezsilne, jest [even 10].
    Jak widaÄ‡, nie wystarczy dodaÄ‡ konstruktorÃ³w typu induktywnego jako
    podpowiedzi, Å¼eby wszystko byÅ‚o cacy. Niemoc [auto] wynika ze sposobu
    dziaÅ‚ania tej taktyki. Wykonuje ona przeszukiwanie w gÅ‚Ä…b z nawrotami,
    ktÃ³re dziaÅ‚a mniej wiÄ™cej tak:
    - zrÃ³b pierwszy lepszy moÅ¼liwy krok dowodu
    - jeÅ¼eli nie da siÄ™ nic wiÄ™cej zrobiÄ‡, a cel nie zostaÅ‚ udowodniony,
      wykonaj nawrÃ³t i sprÃ³buj czegoÅ› innego
    - w przeciwnym wypadku wykonaj nastÄ™pny krok dowodu i powtarzaj
      caÅ‚Ä… procedurÄ™ *)

(** Å»eby ograniczyÄ‡ czas poÅ›wiÄ™cony na szukanie dowodu, ktÃ³ry moÅ¼e byÄ‡
    potencjalnie bardzo dÅ‚ugi, [auto] ogranicza siÄ™ do wykonania jedynie
    kilku krokÃ³w w gÅ‚Ä…b (domyÅ›lnie jest to 5). *)

Print auto_ex5'.
(* ===> evenSS 6 (evenSS 4 (evenSS 2 (evenSS 0 even0)))
        : even 8 *)

Print auto_ex6.
(* ===> evenSS 8 (evenSS 6 (evenSS 4 (evenSS 2 (evenSS 0 even0))))
        : even 10 *)

(** [auto] jest w stanie udowodniÄ‡ [even 8], gdyÅ¼ dowÃ³d tego faktu wymaga
    jedynie 5 krokÃ³w, mianowicie czeterokrotnego zaaplikowania konstruktora
    [evenSS] oraz jednokrotnego zaaplikowania [even0]. Jednak 5 krokÃ³w nie
    wystarcza juÅ¼, by udowodniÄ‡ [even 10], gdyÅ¼ tutaj dowÃ³d liczy sobie 6
    krokÃ³w: 5 uÅ¼yÄ‡ [evenSS] oraz 1 uÅ¼ycie [even0].

    Nie wszystko jednak stracone â€” moÅ¼emy kontrolowaÄ‡ gÅ‚Ä™bokoÅ›Ä‡, na jakÄ…
    [auto] zapuszcza siÄ™, poszukujÄ…c dowodu, pizÄ…c [auto n]. ZauwaÅ¼my, Å¼e
    [auto] jest rÃ³wnowaÅ¼ne taktyce [auto 5]. *)

Example auto_ex7 :
  forall (A : Type) (x y z : A), x = y -> y = z -> x = z.
Proof.
  auto.
Restart.
  Fail auto using eq_trans.
Abort.

(** Kolejnym problemem taktyki [auto] jest udowodnienie, Å¼e rÃ³wnoÅ›Ä‡ jest
    relacjÄ… przechodniÄ…. Tym razem jednak problem jest powaÅ¼niejszy, gdyÅ¼
    nie pomaga nawet prÃ³ba uÅ¼ycia klauzuli [using eq_trans], czyli wskazanie
    [auto] dokÅ‚adnie tego samego twierdzenia, ktÃ³rego prÃ³bujemy dowieÅ›Ä‡!

    PowÃ³d znÃ³w jest doÅ›Ä‡ prozaiczny i wynika ze sposobu dziaÅ‚ania taktyki
    [auto] oraz postaci naszego celu. OtÃ³Å¼ konkluzja celu jest postaci
    [x = z], czyli wystÄ™pujÄ… w niej zmienne [x] i [z], zaÅ› kwantyfikujemy
    nie tylko po [x] i [z], ale takÅ¼e po [A] i [y].

    Wywnioskowanie, co wstawiÄ‡ za [A] nie stanowi problemu, gdyÅ¼ musi to
    byÄ‡ typ [x] i [z]. Problemem jest jednak zgadniÄ™cie, co wstawiÄ‡ za [y],
    gdyÅ¼ w ogÃ³lnoÅ›ci moÅ¼liwoÅ›ci moÅ¼e byÄ‡ wiele (nawet nieskoÅ„czenie wiele).
    Taktyka [auto] dziaÅ‚a w ten sposÃ³b, Å¼e nawet nie prÃ³buje tego zgadywaÄ‡. *)

Hint Extern 0 =>
match goal with
    | H : ?x = ?y, H' : ?y = ?z |- ?x = ?z => apply (@eq_trans _ x y z)
end : extern_db.

Example auto_ex7 :
  forall (A : Type) (x y z : A), x = y -> y = z -> x = z.
Proof. auto with extern_db. Qed.

(** Jest jednak sposÃ³b, Å¼eby uporaÄ‡ siÄ™ i z tym problemem: jest nim komenda
    [Hint Extern]. Jej ogÃ³lna postaÄ‡ to [Hint Extern n pattern => tactic : db].
    W jej wyniku do bazy podpowiedzi [db] zostanie dodana podpowiedÅº, ktÃ³ra
    sprawi, Å¼e w dowolnym momencie dowodu taktyka [auto], jeÅ¼eli wyprÃ³bowaÅ‚a
    juÅ¼ wszystkie podpowiedzi o koszcie mniejszym niÅ¼ [n] i cel pasuje do
    wzorca [pattern], to sprÃ³buje uÅ¼yÄ‡ taktyki [tac].

    W naszym przypadku koszt podpowiedzi wynosi 0, a wiÄ™c podpowiedÅº bÄ™dzie
    odpalana niemal na samym poczÄ…tku dowodu. Wzorzec [pattern] zostaÅ‚
    pominiÄ™ty, a wiÄ™c [auto] uÅ¼yje naszej podpowiedzi niezaleÅ¼nie od tego,
    jak wyglÄ…da cel. Ostatecznie jeÅ¼eli w konktekÅ›cie bÄ™dÄ… odpowiednie
    rÃ³wnania, to zaaplikowany zostanie lemat [@eq_trans _ x y z], wobec
    czego wygenerowane zostanÄ… dwa podcele, [x = y] oraz [y = z], ktÃ³re
    [auto] bÄ™dzie potrafiÅ‚o rozwiÄ…zaÄ‡ juÅ¼ bez naszej pomocy. *)

Hint Extern 0 (?x = ?z) =>
match goal with
    | H : ?x = ?y, H' : ?y = ?z |- _ => apply (@eq_trans _ x y z)
end.

Example auto_ex7' :
  forall (A : Type) (x y z : A), x = y -> y = z -> x = z.
Proof. auto. Qed.

(** A tak wyglÄ…da wersja [Hint Extern], w ktÃ³rej nie pominiÄ™to wzorca
    [pattern]. Jest ona rzecz jasna rÃ³wnowaÅ¼na z poprzedniÄ….

    Jest to dobry moment, by opisaÄ‡ dokÅ‚adniej dziaÅ‚anie taktyki [auto].
    [auto] najpierw prÃ³buje rozwiÄ…zaÄ‡ cel za pomocÄ… taktyki [assumption].
    JeÅ¼eli siÄ™ to nie powiedzie, to [auto] uÅ¼ywa taktyki [intros], a
    nastÄ™pnie dodaje do tymczasowej bazy podpowiedzi wszystkie hipotezy.
    NastÄ™pnie przeszukuje ona bazÄ™ podpowiedzi dopasowujÄ…c cel do wzorca
    stowarzyszonego z kaÅ¼dÄ… podpowiedziÄ…, zaczynajÄ…c od podpowiedzi o
    najmniejszym koszcie (podpowiedzi pochodzÄ…ce od komend [Hint Resolve]
    oraz [Hint Constructors] sÄ… skojarzone z pewnymi domyÅ›lnymi kosztami
    i wzorcami). NastÄ™pnie [auto] rekurencyjnie wywoÅ‚uje siÄ™ na podcelach
    (chyba, Å¼e przekroczona zostaÅ‚a maksymalna gÅ‚Ä™bokoÅ›Ä‡ przeszukiwania â€”
    wtedy nastÄ™puje nawrÃ³t). *)

Example trivial_ex0 :
  forall (P : Prop), P -> P.
Proof. trivial. Qed.

Example trivial_ex1 :
  forall A B C D E : Prop,
    (A -> B) -> (B -> C) -> (C -> D) -> (D -> E) -> A -> E.
Proof. trivial. Abort.

Example trivial_ex2 :
  forall (A : Type) (x : A), x = x.
Proof. trivial. Qed.

Example trivial_ex3 :
  forall (A : Type) (x y : A), x = y -> y = x.
Proof. trivial. Abort.

Example trivial_ex5 : even 0.
Proof. trivial. Qed.

Example trivial_ex5' : even 8.
Proof. trivial. Abort.

(** Taktyka [trivial], ktÃ³rÄ… juÅ¼ znasz, dziaÅ‚a dokÅ‚adnie tak samo jak [auto],
    ale jest nierekurencyjna. To tÅ‚umaczy, dlaczego potrafi ona posÅ‚ugiwaÄ‡
    siÄ™ zaÅ‚oÅ¼eniami i zna wÅ‚aciwoÅ›ci rÃ³wnoÅ›ci, ale nie umie uÅ¼ywaÄ‡ implikacji
    i nie radzi sobie z celami pokroju [even 8], mimo Å¼e potrafi udowodniÄ‡
    [even 0]. *)

(** **** Ä†wiczenie (auto i trivial) *)

(** Przeczytaj w manualu dokÅ‚adny opis dziaÅ‚ania taktyk [auto] oraz [trivial]:
    https://coq.inria.fr/refman/proof-engine/tactics.html#coq:tacn.auto *)

(** ** [autorewrite] i [autounfold] *)

(** [autorewrite] to bardzo poÅ¼yteczna taktyka umoÅ¼liwiajÄ…ca zautomatyzowanie
    czÄ™Å›ci dowodÃ³w opierajÄ…cych siÄ™ na przepisywaniu.

    Dlaczego tylko czÄ™Å›ci? ZastanÃ³wmy siÄ™, jak zazwyczaj przebiegajÄ… dowody
    przez przepisywanie. W moim odczuciu sÄ… dwa rodzaje takich dowodÃ³w:
    - dowody pierwszego rodzaju to te, w ktÃ³rych wszystkie przepisania majÄ…
      charakter upraszczajÄ…cy i dziÄ™ki temu moÅ¼emy przepisywaÄ‡ zupeÅ‚nie
      bezmyÅ›lnie
    - dowody drugiego rodzaju to te, w ktÃ³rych niektÃ³re przepisania nie majÄ…
      charakteru upraszczajÄ…cego albo muszÄ… zostaÄ‡ wykonane bardzo precyzyjnie.
      W takich przypadkach nie moÅ¼emy przepisywaÄ‡ bezmyÅ›lnie, bo grozi to
      zapÄ™tleniem taktyki [rewrite] lub po prostu poraÅ¼kÄ… *)

(** Dowody pierwszego rodzaju ze wzglÄ™du na swojÄ… bezmyÅ›lnoÅ›Ä‡ sÄ… dobrymi
    kandydatami do automatyzacji. WÅ‚aÅ›nie tutaj do gry wkracza taktyka
    [autorewrite]. *)

Section autorewrite_ex.

Variable A : Type.
Variable l1 l2 l3 l4 l5 : list A.

(** Zacznijmy od przykÅ‚adu (a raczej Ä‡wiczenia): udowodnij poniÅ¼sze
    twierdzenie. NastÄ™pnie udowodnij je w jednej linijce. *)

Example autorewrite_intro :
  rev (rev (l1 ++ rev (rev l2 ++ rev l3) ++ rev l4) ++ rev (rev l5)) =
  (rev (rev (rev l5 ++ l1)) ++ (l3 ++ rev (rev l2))) ++ rev l4.
(* begin hide *)
Proof.
  rewrite ?rev_involutive.
  rewrite <- ?app_assoc.
  rewrite ?rev_app_distr.
  rewrite ?rev_involutive.
  rewrite <- ?app_assoc.
  reflexivity.
Restart.
  rewrite ?rev_app_distr, ?rev_involutive, <- ?app_assoc. reflexivity.
Qed.
(* end hide *)

(** Ten dowÃ³d nie byÅ‚ zbyt twÃ³rczy ani przyjemny, prawda? WyobraÅº sobie
    teraz, co by byÅ‚o, gdybyÅ› musiaÅ‚ udowodniÄ‡ 100 takich twierdzeÅ„ (i
    to w czasach, gdy jeszcze nie moÅ¼na byÅ‚o pisaÄ‡ [rewrite ?t_0, ..., ?t_n]).
    Jest to doÅ›Ä‡ ponura wizja. *)

Hint Rewrite rev_app_distr rev_involutive : list_rw.
Hint Rewrite <- app_assoc : list_rw.

Example autorewrite_ex :
  rev (rev (l1 ++ rev (rev l2 ++ rev l3) ++ rev l4) ++ rev (rev l5)) =
  (rev (rev (rev l5 ++ l1)) ++ (l3 ++ rev (rev l2))) ++ rev l4.
Proof.
  autorewrite with list_rw. reflexivity.
Qed.

End autorewrite_ex.

(** Komenda [Hint Rewrite [<-] ident_0 ... ident_n : db_name] dodaje
    podpowiedzi [ident_0], ..., [ident_n] do bazy podpowidzi [db_nam].
    DomyÅ›lnie bÄ™dÄ… one przepisywane z lewa na prawo, chyba Å¼e dodamy
    przeÅ‚Ä…cznik [<-] â€” wtedy wszystkie bÄ™dÄ… przepisywane z prawa na
    lewo. W szczegÃ³lnoÅ›ci znaczy to, Å¼e jeÅ¼eli chcemy niektÃ³re lematy
    przepisywaÄ‡ w jednÄ… stronÄ™, a inne w drugÄ…, to musimy komendy
    [Hint Rewrite] uÅ¼yÄ‡ dwukrotnie.

    Sama taktyka [autorewrite with db_0 ... db_n] przepisuje lematy ze
    wszystkich baz podpowiedzi [db_0], ..., [db_n] tak dÅ‚ugo, jak to
    tylko moÅ¼liwe (czyli tak dÅ‚ugo, jak przepisywanie skutkuje dokonaniem
    postÄ™pu).

    Jest kilka waÅ¼nych cech, ktÃ³re powinna posiadaÄ‡ baza podpowiedzi:
    - przede wszystkim nie moÅ¼e zawieraÄ‡ tego samego twierdzenia do
      przepisywania w obydwie strony. JeÅ¼eli tak siÄ™ stanie, taktyka
      [autorewrite] siÄ™ zapÄ™tli, gdyÅ¼ przepisanie tego twierdzenia w
      jednÄ… lub drugÄ… stronÄ™ zawsze bÄ™dzie moÅ¼liwe
    - w ogÃ³lnoÅ›ci, nie moÅ¼e zawieraÄ‡ Å¼adnego zbioru twierdzeÅ„, ktÃ³rych
      przepisywanie powoduje zapÄ™tlenie
    - baza powinna byÄ‡ deterministyczna, tzn. jedne przepisania nie
      powinny blokowaÄ‡ kolejnych
    - wszystkie przepisywania powinny byÄ‡ upraszczajÄ…ce *)

(** OczywiÅ›cie dwa ostatnie kryteria nie sÄ… zbyt Å›cisÅ‚e â€” ciÄ™Å¼ko sprawdziÄ‡
    determinizm systemu przepisywania, zaÅ› samo pojÄ™cie "uproszczenia" jest
    bardzo zwodnicze i niejasne. *)

(** **** Ä†wiczenie (autorewrite) *)

(** Przeczytaj opis taktyki [autorewrite] w manualu:
    coq.inria.fr/refman/proof-engine/tactics.html#coq:tacn.autorewrite *)

Section autounfold_ex.

Definition wut : nat := 1.
Definition wut' : nat := 1.

Hint Unfold wut wut' : wut_db.

Example autounfold_ex : wut = wut'.
Proof.
  autounfold.
  autounfold with wut_db.
Restart.
  auto.
Qed.

(** Na koniec omÃ³wimy taktykÄ™ [autounfold]. DziaÅ‚a ona na podobnej zasadzie
    jak [autorewrite]. Za pomocÄ… komendy [Hint Unfold] dodajemy definicje do
    do bazy podpowiedzi, dziÄ™ki czemu taktyka [autounfold with db_0, ..., db_n]
    potrafi odwinÄ…Ä‡ wszystkie definicje z baz [db_0], ..., [db_n].

    Jak pokazuje nasz gÅ‚upi przykÅ‚ad, jest ona Å›rednio uÅ¼yteczna, gdyÅ¼ taktyka
    [auto] potrafi (przynajmniej do pewnego stopnia) odwijaÄ‡ definicje. Moim
    zdaniem najlepiej sprawdza siÄ™ ona w zestawieniu z taktykÄ… [autorewrite]
    i kombinatorem [repeat], gdy potrzebujemy na przemian przepisywaÄ‡ lematy
    i odwijaÄ‡ definicje. *)

End autounfold_ex.

(** **** Ä†wiczenie (autounfold) *)

(** Przeczytaj w manualu opis taktyki [autounfold]:
    coq.inria.fr/refman/proof-engine/tactics.html#coq:tacn.autounfold *)

(** **** Ä†wiczenie (bazy podpowiedzi) *)

(** Przeczytaj w manualu dokÅ‚adny opis dziaÅ‚ania systemu baz podpowiedzi
    oraz komend pozwalajÄ…cych go kontrolowaÄ‡:
    coq.inria.fr/refman/proof-engine/tactics.html#controlling-automation *)

(** * PierÅ›cienie, ciaÅ‚a i arytmetyka *)

(** PierÅ›cieÅ„ (ang. ring) to struktura algebraiczna skÅ‚adajÄ…ca siÄ™ z pewnego
    typu A oraz dziaÅ‚aÅ„ + i *, ktÃ³re zachowujÄ… siÄ™ mniej wiÄ™cej tak, jak
    dodawanie i mnoÅ¼enie liczb caÅ‚kowitych. PrzykÅ‚adÃ³w jest sporo: liczby
    wymierne i rzeczywiste z dodawaniem i mnoÅ¼eniem, wartoÅ›ci boolowskie z
    dysjunkcjÄ… i koniunkcjÄ… oraz wiele innych, ktÃ³rych na razie nie wymieniÄ™.

    KiedyÅ› z pewnoÅ›ciÄ… napiszÄ™ coÅ› na temat algebry oraz pierÅ›cieni, ale z
    taktykami do radzenia sobie z nimi moÅ¼emy zapoznaÄ‡ siÄ™ juÅ¼ teraz. W Coqu
    dostÄ™pne sÄ… dwie taktyki do radzenia sobie z pierÅ›cieniami: taktyka
    [ring_simplify] potrafi upraszczaÄ‡ wyraÅ¼enia w pierÅ›cieniach, zaÅ› taktyka
    [ring] potrafi rozwiÄ…zywaÄ‡ rÃ³wnania wielomianowe w pierÅ›cieniach.

    CiaÅ‚o (ang. field) to pierÅ›cieÅ„ na sterydach, w ktÃ³rym poza dodawaniem,
    odejmowaniem i mnoÅ¼eniem jest takÅ¼e dzielenie. PrzykÅ‚adami ciaÅ‚ sÄ…
    liczby wymierne oraz liczby rzeczywiste, ale nie liczby naturalne ani
    caÅ‚kowite (bo dzielenie naturalne/caÅ‚kowitoliczbowe nie jest odwrotnoÅ›ciÄ…
    mnoÅ¼enia). Je teÅ¼ kiedyÅ› pewnie opiszÄ™.

    W Coqu sÄ… 3 taktyki pomagajÄ…ce w walce z ciaÅ‚ami: [field_simplify]
    upraszcza wyraÅ¼enia w ciaÅ‚ach, [field_simplify_eq] upraszcza cele,
    ktÃ³re sÄ… rÃ³wnaniami w ciaÅ‚ach, zaÅ› [field] rozwiÄ…zuje rÃ³wnania w
    ciaÅ‚ach. *)

(** **** Ä†wiczenie (pierÅ›cienie i ciaÅ‚a) *)

(** Przyczytaj w manualu opis 5 wymienionych wyÅ¼ej taktyk:
    https://coq.inria.fr/refman/addendum/ring.html *)

(** * Zmienne egzystencjalne i ich taktyki (TODO) *)

(** NapisaÄ‡ o co chodzi ze zmiennymi egzystencjalnymi. OpisaÄ‡ taktykÄ™
    [evar] i wspomnieÄ‡ o taktykach takich jak [eauto], [econstructor],
    [eexists], [edestruct], [erewrite] etc., a takÅ¼e taktykÄ™ [shelve]
    i komendÄ™ [Unshelve]. *)

(** * Taktyki do radzenia sobie z typami zaleÅ¼nymi (TODO) *)

(** OpisaÄ‡ taktyki [dependent induction], [dependent inversion],
    [dependent destruction], [dependent rewrite] etc. *)

(** * Dodatkowe Ä‡wiczenia *)

(** **** Ä†wiczenie (assert) *)

(** Znasz juÅ¼ taktyki [assert], [cut] i [specialize]. Okazuje siÄ™, Å¼e dwie
    ostatnie sÄ… jedynie wariantami taktyki [assert]. Przeczytaj w manualu
    opis taktyki [assert] i wszystkich jej wariantÃ³w. *)

(** **** Ä†wiczenie (easy i now) *)

(** Taktykami, ktÃ³rych nie miaÅ‚em nigdy okazji uÅ¼yÄ‡, sÄ… [easy] i jej
    wariant [now]. Przeczytaj ich opisy w manualu. Zbadaj, czy sÄ… do
    czegokolwiek przydatne oraz czy sÄ… wygodne w porÃ³wnaniu z innymi
    taktykami sÅ‚uÅ¼Ä…cymi do podobnych celÃ³w. *)

(** **** Ä†wiczenie (inversion_sigma) *)

(** Przeczytaj w manualu o wariantach taktyki [inversion]. SzczegÃ³lnie
    interesujÄ…ca wydaje siÄ™ taktyka [inversion_sigma], ktÃ³ra pojawiÅ‚a
    siÄ™ w wersji 8.7 Coqa. Zbadaj jÄ…. WymyÅ›l jakiÅ› przykÅ‚ad jej uÅ¼ycia. *)

(** **** Ä†wiczenie (pattern) *)

(** Przypomnijmy, Å¼e podstawÄ… wszelkich obliczeÅ„ w Coqu jest redkucja
    beta. Redukuje ona aplikacjÄ™ funkcji, np. [(fun n : nat => 2 * n) 42]
    betaredukuje siÄ™ do [2 * 42]. Jej wykonywanie jest jednym z gÅ‚Ã³wnych
    zadaÅ„ taktyk obliczeniowych.

    PrzeciwieÅ„stwem redukcji beta jest ekspansja beta. Pozwala ona zamieniÄ‡
    dowolny term na aplikacjÄ™ jakiejÅ› funkcji do jakiegoÅ› argumentu, np.
    term [2 * 42] moÅ¼na betaekspandowaÄ‡ do [(fun n : nat => 2 * n) 42].

    O ile redukcja beta jest trywialna do automatycznego wykonania, o tyle
    ekspansja beta juÅ¼ nie, gdyÅ¼ wystÄ™puje tu duÅ¼a dowolnoÅ›Ä‡. Dla przykÅ‚adu,
    term [2 * 42] moÅ¼na teÅ¼ betaekspandowaÄ‡ do [(fun n : nat => n * 42) 2].

    EkspansjÄ™ beta implementuje taktyka [pattern]. Rozumowanie za jej pomocÄ…
    nie jest zbyt czÄ™stne, ale niemniej jednak kilka razy mi siÄ™ przydaÅ‚o.
    Przeczytaj opis taktyki [pattern] w manuaulu.

    TODO: byÄ‡ moÅ¼e Ä‡wiczenie to warto byÅ‚oby rozszerzyÄ‡ do peÅ‚noprawnego
    podrozdziaÅ‚u. *)

(** **** Ä†wiczenie (arytmetyka) *)

(** Poza taktykami radzÄ…cymi sobie z pierÅ›cieniami i ciaÅ‚ami jest teÅ¼ wiele
    taktyk do walki z arytmetykÄ…. Poza omÃ³wionÄ… juÅ¼ taktykÄ… [omega] sÄ… to
    [lia], [nia], [lra], [nra]. Nazwy taktyk moÅ¼na zdekodowaÄ‡ w nastÄ™pujÄ…cy
    sposÃ³b:
    - l â€” linear
    - n â€” nonlinar
    - i â€” integer
    - r â€” real/rational
    - a â€” arithmetic *)

(** SprÃ³buj ogarnÄ…Ä‡, co one robiÄ…:
    https://coq.inria.fr/refman/addendum/micromega.html *)

(** **** Ä†wiczenie (wyÅ¼sza magia) *)

(** SprÃ³buj ogarnÄ…Ä‡, co robiÄ… taktyki [nsatz], [psatz] i [fourier]. *)

(** * Inne jÄ™zyki taktyk *)

(** Ltac w pewnym sensie nie jest jedynym jÄ™zykiem taktyk, jakiego moÅ¼emy
    uÅ¼yÄ‡ do dowodzenia w Coqu â€” sÄ… inne. GÅ‚Ã³wnymi konkurentami Ltaca sÄ…:
    - Rtac: gmalecha.github.io/reflections/2016/rtac-technical-overview
    - Mtac: plv.mpi-sws.org/mtac/
    - ssreflect:
      https://coq.inria.fr/refman/proof-engine/ssreflect-proof-language.html
      oraz https://math-comp.github.io/math-comp/ *)

(** Pierwsze dwa, [Rtac] i [Mtac], faktycznie sÄ… osobnymi jÄ™zykami taktyk,
    znacznie rÃ³Å¼niÄ…cymi siÄ™ od Ltaca. Nie bÄ™dziemy siÄ™ nimi zajmowaÄ‡,
    gdyÅ¼ ich droga do praktycznej uÅ¼ytecznoÅ›ci jest jeszcze doÅ›Ä‡ dÅ‚uga.

    ssreflect to nieco inna bajka. Nie jest on w zasadzie osobnym jÄ™zykiem
    taktyk, lecz jest oparty na Ltacu. RÃ³Å¼ni siÄ™ on od niego filozofiÄ…,
    podstawowym zestawem taktyk i stylem dowodzenia. Od wersji 8.7 Coqa
    jÄ™zyk ten jet dostÄ™pny w bibliotece standardowej, mimo Å¼e nie jest z
    niÄ… w peÅ‚ni kompatybilny. *)

(** **** Ä†wiczenie (ssreflect) *)

(** Najbardziej wartoÅ›ciowym moim zdaniem elementem jÄ™zyka ssreflect jest
    taktyka [rewrite], duÅ¼o potÄ™Å¼niejsza od tej opisanej w tym rozdziale.
    Jest ona warta uwagi, gdyÅ¼:
    - daje jeszcze wiÄ™kszÄ… kontrolÄ™ nad przepisywaniem, niÅ¼ standardowa
      taktyka [rewrite]
    - pozwala Å‚Ä…czyÄ‡ kroki przepisywania z odwijaniem definicji i wykonywaniem
      obliczeÅ„, a wiÄ™c zastÄ™puje taktyki [unfold], [fold], [change], [replace],
      [cbn], [cbn] etc.
    - daje wiÄ™ksze moÅ¼liwoÅ›ci radzenia sobie z generowanymi przez siebie
      podcelami *)

(** Przeczytaj rozdziaÅ‚ manuala opisujÄ…cy jÄ™zyk ssreflect. JeÅ¼eli nie
    chce ci siÄ™ tego robiÄ‡, zapoznaj siÄ™ chociaÅ¼ z jego taktykÄ… [rewrite]. *)

(** * Konkluzja *)

(** W niniejszym rozdziale przyjrzeliÅ›my siÄ™ bliÅ¼ej znacznej czÄ™Å›ci Coqowych
    taktyk. Moje ich opisanie nie jest aÅ¼ tak kompletne i szczegÃ³Å‚owe jak to
    z manuala, ale nadrabia (mam nadziejÄ™) wplecionymi w tekst przykÅ‚adami i
    zadaniami. JeÅ¼eli jednak uwaÅ¼asz je za upoÅ›ledzone, nie jesteÅ› jeszcze
    stracony! Alternatywne opisy niektÃ³rych taktyk dostÄ™pne sÄ… teÅ¼ tu:
    - pjreddie.com/coq-tactics/
    - cs.cornell.edu/courses/cs3110/2017fa/a5/coq-tactics-cheatsheet.html
    - typesofnote.com/posts/coq-cheat-sheet.html *)

(** Poznawszy podstawy Ltaca oraz caÅ‚e zoo przerÃ³Å¼nych taktyk, do zostania
    peÅ‚noprawnym inÅ¼ynierem dowodu (ang. proof engineer, ukute przez analogiÄ™
    do software engineer) brakuje ci jeszcze tylko umiejÄ™tnoÅ›ci dowodzenia
    przez reflekcjÄ™, ktÃ³rÄ… zajmiemy siÄ™ juÅ¼ niedÅ‚ugo. *)